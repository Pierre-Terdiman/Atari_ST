;MODE=1 depack data from a0 to a0 
;MODE=0 depack data from a0 to a1 (RESTORE SPACE a 1 inutile! si MODE=0)
;PIC_ALGO = 0 decrunch file not encoded with special picture algorythm.
;PIC_ALGO = 1 decrunch all files with or without picture algorythm.
;DEC_SPACE = (lesser decrunch space is gived after packing by atomik v3.5)
;RESTORE_SPACE = 1 the allocated decrunch space will be restored .
;RESTORE_SPACE = 0 the allocated decrunch space will not be restored.

MODE:	EQU	0
PIC_ALGO:	EQU	0
DEC_SPACE:	EQU	$80	 ;MAX IS $7FFE (no odd value!)
RESTORE_SPACE:	EQU	0


; RESTE A INTEGRER LES 4 EFFACAGES AU BAS DE LA VBL
;UN CYCLE SE COMPOSE DE 40 VBLs
LARG = 230
EFFACE	MACRO
N	SET	-70*230
	REPT	18
	MOVE.W	D0,N(A1)
	MOVE.W	D0,N+8(A1)
	MOVE.W	D0,N+16(A1)
	MOVE.W	D0,N+24(A1)
	MOVE.W	D0,N+32(A1)
	MOVE.W	D0,N+40(A1)
	MOVE.W	D0,N+48(A1)
	MOVE.W	D0,N+56(A1)
	MOVE.W	D0,N+64(A1)
	MOVE.W	D0,N+72(A1)
	MOVE.W	D0,N+80(A1)
	MOVE.W	D0,N+88(A1)
N	SET	N+230
	ENDR
	ENDM

EFFACE2	MACRO
N	SET	-(70+18)*230
	REPT	18
	MOVE.W	D0,N(A1)
	MOVE.W	D0,N+8(A1)
	MOVE.W	D0,N+16(A1)
	MOVE.W	D0,N+24(A1)
	MOVE.W	D0,N+32(A1)
	MOVE.W	D0,N+40(A1)
	MOVE.W	D0,N+48(A1)
	MOVE.W	D0,N+56(A1)
	MOVE.W	D0,N+64(A1)
	MOVE.W	D0,N+72(A1)
	MOVE.W	D0,N+80(A1)
	MOVE.W	D0,N+88(A1)
N	SET	N+230
	ENDR
	ENDM

MAIN	
;	CLR.L	-(SP)
;	MOVE.W	#$20,-(SP)
;	TRAP	#1
;	ADDQ.W	#6,SP

;	MOVEQ	#6,D7
;	MOVE.W	#$2100,SR
;.LOW	STOP	#$2100
;	CLR.B	$FFFF8260.W
;	DBRA	D7,.LOW

;	MOVEQ	#1,D0
;	JSR	ZIK
;	LEA	ZIK+8,A0

	MOVE.L	A0,ADR_ZIK1
	MOVE.L	A0,ADR_ZIK2
	MOVE.L	A0,ADR_ZIK3

	MOVE.W	#$2700,SR
	LEA	PILE,SP
	MOVE.L	#VBLR_ZIK_TROU_DU_CUL,$70.W
	MOVE.W	#$2300,SR

	LEA	DEB_BSS,A0
	LEA	FIN_BSS,A1
.EFF	CLR.W	(A0)+
	CMPA.L	A1,A0
	BLT.S	.EFF

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W

	LEA	IMG,A0
	BSR	PUT_IMG_SCREEN
	LEA	BUF_GFX,A0
	LEA	56*480(A0),A1
	LEA	56*480(A1),A2
	LEA	56*480(A2),A3
	MOVE.L	#BUFFER+64000+16000,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	MOVE.L	D0,A4
	MOVE.W	#200-1,D6
.AFF_Y	MOVEQ	#20-1,D7
.AFF_X	MOVE.W	(A0)+,(A4)+
	MOVE.W	(A1)+,(A4)+
	MOVE.W	(A2)+,(A4)+
	MOVE.W	(A3)+,(A4)+
	DBRA	D7,.AFF_X
	LEA	16(A0),A0
	LEA	16(A1),A1
	LEA	16(A2),A2
	LEA	16(A3),A3
	DBRA	D6,.AFF_Y

	MOVE.L	#BUF_PLUS,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	MOVE.W	#$2700,SR
	SF	FLAG_IMG_1

	CLR.W	FLAG_FINI
	MOVE.W	#5,CNTFAD
	CLR.W	COMPTEUR

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR_ZIK,$70.W
	MOVE.W	#$2300,SR

	LEA	IMG2,A0
	LEA	BUF_GFX,A1
	BSR	depack
	LEA	BUF_GFX,A0
	MOVE.L	SCREEN2,A4
	LEA	32(A4),A4
	LEA	56*480(A0),A1
	LEA	56*480(A1),A2
	LEA	56*480(A2),A3
	MOVE.W	#200-1,D6
.F_Y	MOVEQ	#20-1,D7
.F_X	MOVE.W	(A0)+,(A4)+
	MOVE.W	(A1)+,(A4)+
	MOVE.W	(A2)+,(A4)+
	MOVE.W	(A3)+,(A4)+
	DBRA	D7,.F_X
	LEA	16(A0),A0
	LEA	16(A1),A1
	LEA	16(A2),A2
	LEA	16(A3),A3
	DBRA	D6,.F_Y

	LEA	BUF_GFX,A0
	MOVE.W	#110400/4-1,D7
.EFFA	CLR.L	(A0)+
	DBRA	D7,.EFFA

	BSR	PREP_CODE
******
	CLR.W	NB_VBL
	MOVE.W	#1,FLAG_FINI
	MOVE.W	#5,CNTFAD
.WAIT	CMPI.W	#50,NB_VBL
	BLT.S	.WAIT

	BSR	PREP_ECR

	LEA	IMG4,A0
	MOVE.L	SCREEN1,A1
	JSR	depack	
	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A2
	LEA	160-24(A2),A2
	MOVE.W	#270-1,D6
.COPY_Y_ARALE	MOVEQ	#28-1,D7
.COPY_X_ARALE	MOVE.W	(A0)+,D0
	NOT.W	D0
	MOVE.W	D0,6(A2)
	ADDQ.W	#8,A2
	DBRA	D7,.COPY_X_ARALE
	ADDQ.W	#6,A2
	DBRA	D6,.COPY_Y_ARALE

	MOVE.L	SCREEN2,A0
	MOVE.L	SCREEN1,A1
	LEA	160(A0),A0
	LEA	160(A1),A1
	MOVE.W	#270-1,D6
.RECOPY_Y	MOVEQ	#28-1,D7
.RECOPY_X	CLR.L	(A1)
	CLR.W	4(A1)
	MOVE.W	6(A0),6(A1)
	ADDQ.W	#8,A0
	ADDQ.W	#8,A1
	DBRA	D7,.RECOPY_X
	ADDQ.W	#6,A0
	ADDQ.W	#6,A1
	DBRA	D6,.RECOPY_Y

	MOVE.W	#$2700,SR
	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	MOVE.L	#VBLR,$70.W
	CLR.W	NB_VBL
	MOVE.W	#$2300,SR

NOP_DE_FIN = *
VBL	BRA.S	VBL
	MOVE.L	$4.W,A0
	JMP	(A0)

FLAG_IMG_1	DS.B	1
	EVEN

VBLR_ZIK	ADDQ.W	#1,NB_VBL
	ADDQ.W	#1,COMPTEUR
	MOVEM.L	D0-A6,-(SP)
	CMPI.W	#400,COMPTEUR
	BNE.S	.FUCK
	CLR.W	NB_VBL
	MOVE.W	#1,FLAG_FINI
	MOVE.W	#5,CNTFAD
	ST	FLAG_IMG_1
.FUCK
ADR_ZIK1= *+2
	JSR	$12345678
	BSR	MK_FADE
	MOVEM.L	(A0),D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	TST.B	FLAG_IMG_1
	BEQ.S	.ON_PASSE
	CMPI.W	#50,NB_VBL
	BLT.S	.ON_PASSE
	MOVE.B	SCREEN2+1,$FFFF8201.W
	MOVE.B	SCREEN2+2,$FFFF8203.W
	MOVE	#-32,MOD2+4
	MOVE	#-32,MOD2B+2
	MOVE	#-32,MOD1+4
	MOVE	#-32,MOD1B+2
	CLR.W	MODDEG1+2
	CLR.W	MODDEG2+2
	CLR.W	FLAG_FINI
	MOVE.W	#5,CNTFAD
	SF	FLAG_IMG_1
.ON_PASSE	MOVEM.L	(SP)+,D0-A6
	RTE

VBLR_ZIK_TROU_DU_CUL
	MOVEM.L	D0-A6,-(SP)
ADR_ZIK3 = *+2
	JSR	$12345678
	MOVEM.L	(SP)+,D0-A6
	RTE

BUF_ATTEND	DS.L	3*3

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W       ; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W
	;ICI 33 LIGNES DE LIBRES
	MOVE.W	#$8240,A0

	MOVE	#$667,(A0)+
	MOVE	#$556,(A0)+	42 0001
	MOVE	#$445,(A0)+	44 0010
	MOVE	#$344,(A0)+	46 0011
	MOVE	#$334,(A0)+	48 0100
	MOVE	#$455,(A0)+	4A 0101
	MOVE	#$566,(A0)+	4C 0110
	MOVE	#$777,(A0)+	4E 0111

	MOVE.L	ADR_EFF,A0
	LEA	16(A0),A0
	MOVEQ	#0,D0
	MOVE.L	(A0)+,A1
	EFFACE
	MOVE.L	(A0),A1
	EFFACE2

	MOVE.W	CNT_CYCLE,D0
	ADDQ.W	#1,D0
	CMPI.W	#40,D0
	BLT.S	.OK_CYCLE
	MOVE.L	PT_SCENAR,A0
	MOVE.W	(A0)+,D0	;SCENARIO CERCLE 1
	MOVE.W	(A0)+,D1	;SCENARIO CERCLE 2
	MOVE.W	(A0)+,D2	;SCENARIO CERCLE 3
	CMPI.W	#$1234,(A0)
	BNE.S	.OK_SCENAR
	LEA	SCENARIO,A0
.OK_SCENAR	MOVE.L	A0,PT_SCENAR
	LEA	TABLE_CORES_SCENAR,A0
	LEA	BUF_ADR_ACTIF,A2
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADD.W	D1,D1
	ADD.W	D1,D1
	ADD.W	D2,D2
	ADD.W	D2,D2
	MOVE.L	(A0,D0.W),A1
	MOVE.L	A1,(A2)+
	MOVE.L	(A0,D1.W),A1
	MOVE.L	A1,(A2)+
	MOVE.L	(A0,D2.W),A1
	MOVE.L	A1,(A2)+
	MOVEQ	#0,D0
.OK_CYCLE	MOVE.W	D0,CNT_CYCLE

	LEA	BUF_ATTEND,A6
	MOVE.L	PT_MOUVEMENT,A3
	LEA	BUF_ADR_ACTIF,A4
 	MOVEQ	#0,D6	;ON COMMENCE SUR LE PLAN 0
	MOVEQ	#0,D4
	MOVEQ	#2,D7
.REPEAT_CERCLES	MOVE.W	(A3)+,D0	;X
	ADDI.W	#185,D0
	MOVE.W	(A3)+,D1	;Y
	
	MOVE.L	(A4),A5
	MOVE.W	(A5)+,D2	;No D'ETAPE
	MOVE.L	A5,(A4)+
	LEA	BUF_OFFSETS,A5
	ADD.W	D2,D2
	ADD.W	D2,D2
	ADD.W	(A5,D2.W),D0	;REAL X
	ADD.W	2(A5,D2.W),D1	;REAL Y
	MOVE.W	D0,D3
	ANDI.W	#$FFF0,D3
	LSR.W	#1,D3
	SUBI.W	#50,D1
	MULS.W	#LARG,D1
	ADD.W	D3,D1
	MOVE.L	SCREEN2,A0
	LEA	32160(A0),A0
	ADD.W	D6,D1
	ADDA.L	D1,A0
	LEA	112+LARG*70(A0),A0
	ANDI.W	#15,D0
	LSR.W	#1,D0
	MOVE.W	D2,D1
	LSL.W	#3,D1
	LEA	ADR_GFX,A2
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADD.W	D0,D1
	MOVE.L	(A2,D1.W),A1 ;ADRESSE DES GRAFS COMPLEMENTAIRES
	LEA	ADR_COD,A5
	MOVE.L	(A5,D2.W),A5
	MOVE.L	A0,(A6)+	;ADRESSE ECRAN
	MOVE.L	A1,(A6)+	;ADRESSE DES GFX COMPLEMENTAIRES
	MOVE.L	A5,(A6)+	;ADRESSE DE LA ROUTINE
	ADDQ.W	#2,D6	;PROCHAIN PLAN
	MOVE.L	ADR_EFF,A1
	MOVE.L	A0,(A1,D4.W)
	LEA	TABLE_HAUTEUR,A5
	ADDA.L	(A5,D2.W),A0
	MOVE.L	A0,4(A1,D4.W)
	ADDQ.W	#8,D4
	DBRA	D7,.REPEAT_CERCLES
	CMPI.W	#$1234,(A3)
	BNE.S	.FUCK_1
	LEA	MOUVEMENT(PC),A3
.FUCK_1	MOVE.L	A3,PT_MOUVEMENT
INTER_RTE	RTE

INTER_TMA:	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR
full
	MOVEQ	#16,D0
	LEA	$FFFF8209.W,A0
	LEA	$FFFF820A.W,A6
	LEA	$FFFF8260.W,A3

	MOVEQ	#2,D3
	MOVEQ	#0,D4

	STOP	#$2100

	MOVE	#$2700,SR
	MOVE	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROA	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D4,(A6)	;820A
	REPT	6
	NOP
	ENDR
	MOVE.B	D3,(A6)	;820A

	;SYNCHRO ICI...
.SYNCHRO	MOVE.B	(A0),D2
	BEQ.S	.SYNCHRO
	SUB.B	D2,D0
	LSL.W	D0,D0

	DCB.W	97-20,$4E71

	MOVEQ	#0,D6	;1
	MOVEQ	#-1,D7	;1
	MOVEQ	#2,D3	;1
	MOVEQ	#0,D5	;1
	LEA	BUF_ATTEND,A4	;3
	MOVE.L	(A4)+,A0	;3 ADRESSE ECRAN
	MOVE.L	(A4)+,A1	;3 GFX COMPLEMENTAIRES
	MOVE.L	(A4)+,A5	;3 ROUTINE
	JSR	(A5)	;4

	REPT	2
	DC.L	$16835245
	DC.W	$1686
	DCB.W	89,$4E71
	DC.L	$1C861C83
	MOVE.L	(A4)+,A0	;3 ADRESSE ECRAN
	MOVE.L	(A4)+,A1	;3 GFX COMPLEMENTAIRES
	MOVE.L	(A4)+,A5	;3 ROUTINE
	DCB.W	13-9,$4E71
	DC.L	$16834E71
	DC.W	$1686
	DCB.W	12-4,$4E71
	JSR	(A5)	;4
	ENDR

	DC.L	$16835245
	DC.W	$1686
	MOVE.W	#226,D0
	SUB.W	D5,D0	;NB DE LIGNES OVERSCANS RESTANT A EXECUTER
	DCB.W	89-2-1,$4E71
	DC.L	$1C861C83
	DCB.W	13,$4E71
	DC.L	$16834E71
	DC.W	$1686
	DCB.W	12,$4E71

	DC.L	$16834E71
	DC.W	$1686
	DCB.W	89-1,$4E71
.MAK_FULL	NOP
	DC.L	$1C861C83
	DCB.W	13,$4E71
	DC.L	$16834E71
	DC.W	$1686
	DCB.W	12,$4E71
	DC.L	$16834E71
	DC.W	$1686
	DCB.W	89-3-1,$4E71
	DBRA	D0,.MAK_FULL

	DC.L	$1C861C83
	DCB.W	13,$4E71
	DC.L	$16834E71
	DC.W	$1686
	CLR.B	$FFFF820A.W 
	DCB.W	12-4,$4E71
	DC.L	$16835245
	DC.W	$1686
	MOVE.B	D3,(A6)
	DCB.W	89-2,$4E71

	DC.L	$1C861C83
	DCB.W	13,$4E71
	DC.L	$16834E71
	DC.W	$1686
	MOVEQ	#48-32,D7
	DCB.W	12-1,$4E71
.FUCK_FULL	DC.L	$16835245
	DC.W	$1686
	DCB.W	89,$4E71
	DC.L	$1C861C83
	DCB.W	13,$4E71
	DC.L	$16834E71
	DC.W	$1686
	DCB.W	12-3,$4E71
	DBRA	D7,.FUCK_FULL

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W
	BSR	SWAPEC
	LEA	ADR_EFF,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)

	MOVE.L	(A0),A0
	MOVEQ	#0,D0
	MOVE.L	(A0)+,A1
	EFFACE
	MOVE.L	(A0)+,A1
	EFFACE2
	MOVE.L	(A0)+,A1
	EFFACE
	MOVE.L	(A0)+,A1
	EFFACE2

	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	.NO_NO
	MOVE.W	#$4E71,NOP_DE_FIN
.NO_NO	
ADR_ZIK2 = *+2
	JSR	$12345678
	RTE

PREP_CODE	LEA	ROTATIONS(PC),A0
	LEA	BUF_COD,A6
	LEA	ADR_GFX,A1
	LEA	BUF_GFX,A2
	CLR.W	MOD_2
	MOVEQ	#20,D7
REPEAT_ETAPE	MOVE.W	D7,MOD_1
	ADDQ.W	#4,A0	;ON SAUTE OFF_X ET OFF_Y
	MOVE.W	(A0)+,D7	;HAUTEUR...
	MOVE.W	D7,D0
	MULS.W	#230,D0
	MOVE.W	MOD_2,D1
	ADD.W	D1,D1
	ADD.W	D1,D1
	LEA	TABLE_HAUTEUR,A4
	MOVE.L	D0,(A4,D1.W)
	SUBQ.W	#1,D7	;...-1 (DBRA)
	MOVE.W	D7,MOD_P
	LEA	BUF_CERCLE_TEST,A4
ALWAYS_ON_THE_RUN
	MOVE.W	(A0)+,D0	;X0
	MOVE.W	(A0)+,D1	;DISTANCE
	BSR	REMP_LINE
	LEA	24(A4),A4
	DBRA	D7,ALWAYS_ON_THE_RUN
	BSR	COD_CERCLE
	BSR	EFF_CERCLE
MOD_1 = *+2
	MOVE.W	#$1234,D7
	ADDQ.W	#1,MOD_2
	DBRA	D7,REPEAT_ETAPE

	LEA	ROTATIONS(PC),A4
	LEA	BUF_OFFSETS,A5
	MOVEQ	#20,D7
.PUT_OFFSETS	MOVE.W	(A4)+,(A5)+	;OFFSET X
	MOVE.W	(A4)+,(A5)+	;OFFSET Y
	MOVE.W	(A4)+,D0	;HAUTEUR
	ADD.W	D0,D0
	ADD.W	D0,D0	;*4
	ADDA.W	D0,A4
	DBRA	D7,.PUT_OFFSETS

	ADDQ.W	#4,A0
	MOVE.W	(A0)+,D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	ADDA.W	D0,A0
	MOVE.L	A0,MOD_41
	MOVEQ	#19,D7
REPEAT_ETAPEY	MOVE.W	D7,MOD_5
	ADDQ.W	#4,A0	;ON SAUTE OFF_X ET OFF_Y
	MOVE.W	(A0)+,D7	;HAUTEUR...
	MOVE.W	D7,D0
	MULS.W	#230,D0
	MOVE.W	MOD_2,D1
	ADD.W	D1,D1
	ADD.W	D1,D1
	LEA	TABLE_HAUTEUR,A4
	MOVE.L	D0,(A4,D1.W)
	SUBQ.W	#1,D7	;...-1 (DBRA)
	MOVE.W	D7,MOD_P
	LEA	BUF_CERCLE_TEST,A4
ALWAYS_ON_THE_RUNY
	MOVE.W	(A0)+,D0	;X0
	MOVE.W	(A0)+,D1	;DISTANCE
	BSR	REMP_LINE
	LEA	24(A4),A4
	DBRA	D7,ALWAYS_ON_THE_RUNY
	BSR	COD_CERCLE
	BSR	EFF_CERCLE
MOD_5 = *+2
	MOVE.W	#$1234,D7
	ADDQ.W	#1,MOD_2
	DBRA	D7,REPEAT_ETAPEY

MOD_41 = *+2
	LEA	$12345678,A4
	LEA	BUF_OFFSETS+4*21,A5
	MOVEQ	#19,D7
.PUT_OFFSETS	MOVE.W	#-85,(A5)+
	MOVE.W	#-85,(A5)+
;	MOVE.W	(A4)+,(A5)+	;OFFSET X
;	MOVE.W	(A4)+,(A5)+	;OFFSET Y
	MOVE.W	(A4)+,D0	;HAUTEUR
	ADD.W	D0,D0
	ADD.W	D0,D0	;*4
	ADDA.W	D0,A4
	DBRA	D7,.PUT_OFFSETS

	MOVE.L	#MOUVEMENT,PT_MOUVEMENT
	MOVE.L	#SCENARIO,PT_SCENAR
	MOVE.W	#39,CNT_CYCLE

	LEA	ADR_EFF,A0
	LEA	BUF_EFF_1,A1
	LEA	BUF_EFF_2,A2
	MOVE.L	A1,(A0)+
	MOVE.L	A2,(A0)+
	LEA	BUFFER,A3
	MOVE.L	A3,(A1)+
	MOVE.L	A3,(A1)+
	MOVE.L	A3,(A1)+
	MOVE.L	A3,(A1)+
	MOVE.L	A3,(A1)+
	MOVE.L	A3,(A1)+
	MOVE.L	A3,(A2)+
	MOVE.L	A3,(A2)+
	MOVE.L	A3,(A2)+
	MOVE.L	A3,(A2)+
	MOVE.L	A3,(A2)+
	MOVE.L	A3,(A2)+
	RTS

REMP_LINE	MOVE.L	A4,A5
	MOVE.W	D0,D2
	ANDI.W	#-16,D2
	LSR.W	#3,D2
	ADD.W	D2,A5
	ANDI.W	#15,D0	;DECALAGE
	MOVE.W	D0,D2
	LSR.W	#3,D2
	ADD.W	D2,A5
	BCLR	#3,D0
	MOVEQ	#7,D2
	SUB.W	D0,D2
.CONT_PLOT	BSET	D2,(A5)
	SUBQ.W	#1,D2
	BGE.S	.MEME_QUE_D_ABORD
	MOVEQ	#7,D2
	ADDQ.W	#1,A5
.MEME_QUE_D_ABORD
	DBRA	D1,.CONT_PLOT
	RTS

COD_CERCLE	
MOD_P = *+2
	MOVE.W	#$1234,D5
	MOVE.W	D5,D7
	MOVE.W	D5,MOD_HAUT
	LEA	BUF_CERCLE_TEST,A4
	LEA	BUF_FLAGS,A5
	ADDQ.W	#1,D7
	MULU.W	#12,D7
	MOVE.W	D7,D4
	ADD.W	D4,D4
	MOVE.W	D4,DECAL_CERCLE+2
	NEG.W	D4
	MOVE.W	D4,.COMPLETE_TEST+2
	SUBQ.W	#1,D7
	MOVE.W	D7,D4
	MOVE.W	D7,MODING
.TEST_WORDS	MOVE.W	(A4)+,D0
	BNE.S	.TEST_2
	SF	(A5)+
	BRA.S	.CONT
.TEST_2	CMPI.W	#-1,D0
	BNE.S	.TEST_3
	ST	(A5)+
	BRA.S	.CONT
.TEST_3	MOVE.B	#$40,(A5)+
.CONT	DBRA	D7,.TEST_WORDS
	MOVEQ	#7-1,D3
.COMPLETE_TEST	LEA	$1234(A4),A4
	BSR	DECAL_CERCLE
	LEA	BUF_FLAGS,A5
	MOVE.W	D4,D7
.TEST_ALL	TST.W	(A4)
	BEQ.S	.MOT_NUL
	CMPI.W	#-1,(A4)
	BEQ.S	.MOT_PLEIN
	MOVE.B	#$40,(A5)
.MOT_PLEIN
.MOT_NUL	ADDQ.W	#1,A5
	ADDQ.W	#2,A4
	DBRA	D7,.TEST_ALL
	DBRA	D3,.COMPLETE_TEST

	LEA	BUF_FLAGS,A5
	LEA	BUF_COD_ANNEX,A4
	MOVE.W	#-70*LARG,D4	;OFFSET ECRAN
	MOVE.W	D5,D6
	MOVEQ	#0,D5	;TEMPS MACHINE
.COD_ALL	MOVEQ	#0,D2
	MOVEQ	#11,D7
.COD_LINE	MOVE.B	(A5)+,D0
	BEQ.S	.NIB
	BLT.S	.MOVE_D7
	TST.W	D2
	BNE.S	.FUCK_1
	MOVEQ	#-1,D2
	MOVE.W	#$3146,(A4)+
	MOVE.W	D4,(A4)
	SUBI.W	#16,(A4)+
	MOVE.W	#$3146,(A4)+
	MOVE.W	D4,(A4)
	SUBQ.W	#8,(A4)+
	ADDQ.W	#6,D5	;+6 NOPS
.FUCK_1	MOVE.W	#$3159,(A4)+	;MOVE.W (A1)+,d16(a0)
	MOVE.W	D4,(A4)+
	ADDQ.W	#4,D5	;+4 NOPS
	BRA.S	.NEXT_WORD
.MOVE_D7	TST.W	D2
	BNE.S	.FUCK_2
	MOVEQ	#-1,D2
	MOVE.W	#$3146,(A4)+
	MOVE.W	D4,(A4)
	SUBI.W	#16,(A4)+
	MOVE.W	#$3146,(A4)+
	MOVE.W	D4,(A4)
	SUBQ.W	#8,(A4)+
	ADDQ.W	#6,D5	;+6 NOPS	
.FUCK_2	MOVE.W	#$3147,(A4)+	;MOVE.W D7,d16(a0)
	MOVE.W	D4,(A4)+
	ADDQ.W	#3,D5	;+3 NOPS
	BRA.S	.NEXT_WORD
.NIB	TST.W	D2
	BEQ.S	.NEXT_WORD
	CMPI.W	#$1234,D2
	BEQ.S	.NEXT_WORD
	MOVE.W	#$1234,D2
	MOVE.W	#$3146,(A4)+
	MOVE.W	D4,(A4)+
	MOVE.W	#$3146,(A4)+
	MOVE.W	D4,(A4)
	ADDQ.W	#8,(A4)+
	ADDQ.W	#6,D5	;+6 NOPS
.NEXT_WORD	ADDQ.W	#8,D4
	DBRA	D7,.COD_LINE
	CMPI.W	#$1234,D2
	BEQ.S	.FUCK_3
	MOVE.W	#$3146,(A4)+
	MOVE.W	D4,(A4)+
	MOVE.W	#$3146,(A4)+
	MOVE.W	D4,(A4)
	ADDQ.W	#8,(A4)+
	ADDQ.W	#6,D5	;+6 NOPS
.FUCK_3	ADDI.W	#LARG-8*12,D4
	DBRA	D6,.COD_ALL
	BSR	INTEGRATION
	BSR	REMPLI_GFX
	RTS

REMPLI_GFX	LEA	BUF_CERCLE_TEST,A4
	MOVEQ	#8-1,D6
.REMPLI_DEC	LEA	BUF_FLAGS,A5
	MOVE.L	A2,(A1)+
MODING = *+2
	MOVE.W	#$1234,D7
.REMPLI_1	TST.B	(A5)+
	BLE.S	.NOTHING
	MOVE.W	(A4),(A2)+
.NOTHING	ADDQ.W	#2,A4
	DBRA	D7,.REMPLI_1

	DBRA	D6,.REMPLI_DEC
	RTS

DECAL_CERCLE	LEA	$1234(A4),A3
MOD_HAUT = *+2
	MOVE.W	#$1234,D7
.DECAL_ALL	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.W	#6-1,D6
.DECAL_LINE	MOVE.W	(A4)+,D0
	ROR.L	#2,D0
	OR.W	D1,D0
	MOVE.W	D0,(A3)+
	CLR.W	D0
	SWAP	D0
	MOVE.W	(A4)+,D1
	ROR.L	#2,D1
	OR.W	D0,D1
	MOVE.W	D1,(A3)+
	CLR.W	D1
	SWAP	D1
	DBRA	D6,.DECAL_LINE
	DBRA	D7,.DECAL_ALL
	RTS

EFF_CERCLE	LEA	BUF_CERCLE_TEST,A4
	LEA	BUF_FLAGS,A3
	MOVEQ	#0,D0
	MOVE.W	#8*170-1,D1
.EFF_BUF	MOVE.L	D0,(A4)+
	MOVE.L	D0,(A4)+
	MOVE.L	D0,(A4)+
	MOVE.L	D0,(A4)+
	MOVE.L	D0,(A4)+
	MOVE.L	D0,(A4)+
	DBRA	D1,.EFF_BUF
	MOVE.W	#170-1,D1
.EFF_BUF2	MOVE.L	D0,(A3)+
	MOVE.L	D0,(A3)+
	MOVE.L	D0,(A3)+
	DBRA	D1,.EFF_BUF2
	RTS

	;ENTRêE = D5:NB DE NOPS DU CODE A INTEGRER
INTEGRATION	LEA	BUF_COD_ANNEX,A4
	LEA	ADR_COD,A5
MOD_2 = *+2
	MOVE.W	#$1234,D1
	ADD.W	D1,D1
	ADD.W	D1,D1
	MOVE.L	A6,(A5,D1.W)	;ON PLACE L'ADRESSE DE LA ROUTINE
	MOVEQ	#0,D1	;COMPTEUR DE LIGNE OVR UTILISêES
.ON_CONTINUE	ADDQ.W	#1,D1
	;OVR GAUCHE
;	MOVE.L	#$4E714E71,(A6)+
;	MOVE.L	#$4E714E71,(A6)+
;	MOVE.W	#$4E71,(A6)+
	MOVE.L	#$16835245,(A6)+
	MOVE.W	#$1686,(A6)+
	;
	MOVEQ	#89,D7
	BSR	SEARCH
	;OVR DROIT
;	MOVE.L	#$4E714E71,(A6)+
;	MOVE.L	#$4E714E71,(A6)+
	MOVE.L	#$1C861C83,(A6)+
	;
	MOVEQ	#13,D7
	BSR	SEARCH
	;STABILISATEUR
;	MOVE.L	#$4E714E71,(A6)+
;	MOVE.L	#$4E714E71,(A6)+
;	MOVE.W	#$4E71,(A6)+
	MOVE.L	#$16834E71,(A6)+
	MOVE.W	#$1686,(A6)+
	;
	MOVEQ	#12,D7
	BSR	SEARCH
	TST.W	D5
	BGT.S	.ON_CONTINUE
	MOVEQ	#3,D7
	MOVEQ	#0,D2
.RETURN	SUBQ.W	#2,D2
	CMPI.W	#$4E71,(A6,D2.W)
	BNE.S	.ON_CONTINUE
	DBRA	D7,.RETURN
	SUBQ.W	#8,A6
	MOVE.W	#$4E75,(A6)+
	RTS

SEARCH	TST.W	D5
	BEQ.S	.COMPLETING
	MOVE.L	A4,A5
	MOVEQ	#0,D2  ;FLAG:IL RESTE ASSEZ DE TEMPS POUR N'IMPORTE QUELLE INSTRUCTION
.NEXT_INSTRUCTION
	LEA	INSTRUCTIONS(PC),A3
	MOVE.W	(A5),D0  ;ON PREND LE CODE DE L'INSTRUCTION
	MOVEQ	#2,D3
.NEXT_CODE	CMP.W	(A3)+,D0
	BNE.S	.NEXT_TRY
	CMP.W	4(A3),D7	;YA T'IL ASSEZ DE TEMPS?
	BGE.S	.OK_POSE
	BRA.S	.COMPLETING
	ADD.W	(A3),A5
	MOVEQ	#-1,D2	;FLAG:ON CHERCHE UNE INSTRUCTION PLUS RAPIDE
	BRA.S	.NEXT_INSTRUCTION
.OK_POSE	ADDQ.W	#2,A3
	MOVE.W	(A3)+,D4	;LONGUEUR EN MOTS-1
	MOVE.W	D4,D3
.RECOPY	MOVE.W	(A5),(A6)+	;ON POSE LE CODE
	MOVE.W	#$4E71,(A5)+	;ON LE REMPLACE PAR $4E71
	DBRA	D3,.RECOPY
	SUB.W	(A3),D7	;ON RETRANCHE LE TEMPS DE L'INSTRUCTION INTêGRêE DU TEMPS TOTAL A INTêGRER SUR LA LIGNE
	SUB.W	(A3),D5	;ON RETRANCHE LE TEMPS DE L'INSTRUCTION INTêGRêE DU TEMPS TOTAL A INTêGRER
	BEQ.S	.COMPLETING
	TST.W	D2
	BNE.S	.PAS_ADDIT
	MOVE.L	A5,A4
.PAS_ADDIT	BRA.S	.TEST_END
.NEXT_TRY	ADDQ.W	#6,A3
	DBRA	D3,.NEXT_CODE
	ILLEGAL
.TEST_END	CMPI.W	#2,D7
	BGT.S	.NEXT_INSTRUCTION
.COMPLETING	SUBQ.W	#1,D7
	BGE.S	.PROUT
	RTS
.PROUT	MOVE.W	#$4E71,(A6)+	;ON COMPLETE LE TEMPS AVEC DES NOPS
	DBRA	D7,.PROUT
.ENDING	RTS

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,A0
	MOVEQ	#0,D1
	MOVE.W	#32000-1,D7
.EFF_ECR	MOVE.L	D1,(A0)+
	DBRA	D7,.EFF_ECR
	MOVE.L	D0,SCREEN1
	ADDI.L	#64000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

depack:	movem.l	d0-a6,-(a7)
	cmp.l	#"ATM5",(a0)+
	bne	not_packed
	link	a2,#-28
	move.l	(a0)+,d0
	ifne	MODE
	lea	4(a0,d0.l),a5
	move.l	d0,-(a7)
	elseif
	move.l	a1,a5
	add.l	d0,a5
	endc
	move.l	a5,a4
	ifne	MODE
	ifne	DEC_SPACE
	lea	DEC_SPACE(a4),a5
	endc
	endc
	lea	-$c(a4),a4
	move.l	(a0)+,d0
	move.l	a0,a6
	add.l	d0,a6
	ifne	PIC_ALGO
	moveq	#0,d0
	move.b	-(a6),d0
	move	d0,-2(a2)
	ifne	RESTORE_SPACE
	lsl	#2,d0
	sub	d0,a4
	endc
	elseif
	ifne	RESTORE_SPACE
	clr	-2(a2)
	endc
	subq	#1,a6
	endc
	ifne	RESTORE_SPACE
	lea	buff_marg(pc),a3
	move	-2(a2),d0
	lsl	#2,d0
	add	#DEC_SPACE+$C,d0
	bra.s	.save
.save_m:	move.b	(a4)+,(a3)+
	subq	#1,d0
.save:	bne.s	.save_m
	movem.l	a3-a4,-(a7)
	endc
	ifne	PIC_ALGO
	pea	(a5)
	endc
	move.b	-(a6),d7
	bra	take_type
decrunch:	move	d3,d5
take_lenght:	add.b	d7,d7
.cont_take:	dbcs	d5,take_lenght
	beq.s	.empty1
	bcc.s	.next_cod
	sub	d3,d5
	neg	d5
	bra.s	.do_copy1
.next_cod:	moveq	#3,d6
	bsr.s	get_bit2
	beq.s	.next_cod1
	bra.s	.do_copy
.next_cod1:	moveq	#7,d6
	bsr.s	get_bit2
	beq.s	.next_cod2
	add	#15,d5
	bra.s	.do_copy
.empty1:	move.b	-(a6),d7
	addx.b	d7,d7
	bra.s	.cont_take
.next_cod2:	moveq	#13,d6
	bsr.s	get_bit2
	add	#255+15,d5
.do_copy:	add	d3,d5
.do_copy1:	lea	decrun_table(pc),a4
	move	d5,d2
	bne.s	bigger
	add.b	d7,d7
	bne.s	.not_empty
	move.b	-(a6),d7
	addx.b	d7,d7
.not_empty:	bcs.s	.ho_kesako
	moveq	#1,d6
	bra.s	word
.ho_kesako:	moveq	#3,d6
	bsr.s	get_bit2
	tst.b	-28(a2)
	beq.s	.ho_kesako1
	move.b	10-28(a2,d5.w),-(a5)
	bra	tst_end
.ho_kesako1:	move.b	(a5),d0
	btst	#3,d5
	bne.s	.ho_kesako2
	bra.s	.ho_kesako3
.ho_kesako2:	add.b	#$f0,d5
.ho_kesako3:	sub.b	d5,d0
	move.b	d0,-(a5)
	bra	tst_end
get_bit2:	clr	d5
.get_bits:	add.b	d7,d7
	beq.s	.empty
.cont:	addx	d5,d5
	dbf	d6,.get_bits
	tst	d5
	rts
.empty:	move.b	-(a6),d7
	addx.b	d7,d7
	bra.s	.cont
bigger:	moveq	#2,d6
word:	bsr.s	get_bit2
contus:	move	d5,d4
	move.b	14(a4,d4.w),d6
	ext	d6
	tst.b	1-28(a2)
	bne.s	.spe_ofcod1
	addq	#4,d6
	bra.s	.nospe_ofcod1
.spe_ofcod1:	bsr.s	get_bit2
	move	d5,d1
	lsl	#4,d1
	moveq	#2,d6
	bsr.s	get_bit2
	cmp.b	#7,d5
	blt.s	.take_orof
	moveq	#0,d6
	bsr.s	get_bit2
	beq.s	.its_little
	moveq	#2,d6
	bsr.s	get_bit2
	add	d5,d5
	or	d1,d5
	bra.s	.spe_ofcod2
.its_little:	or.b	2-28(a2),d1
	bra.s	.spe_ofcod3
.take_orof:	or.b	3-28(a2,d5.w),d1
.spe_ofcod3:	move	d1,d5
	bra.s	.spe_ofcod2
.nospe_ofcod1:	bsr.s	get_bit2
.spe_ofcod2:	add	d4,d4
	beq.s	.first
	add	-2(a4,d4.w),d5
.first:	lea	1(a5,d5.w),a4
	move.b	-(a4),-(a5)
.copy_same:	move.b	-(a4),-(a5)
	dbf	d2,.copy_same
	bra.s	tst_end
make_jnk:	add.b	d7,d7
	bne.s	.not_empty
	move.b	-(a6),d7
	addx.b	d7,d7
.not_empty:	bcs.s	string
	move.b	-(a6),-(a5)
tst_end:	cmp.l	a5,a3
	bne.s	make_jnk
	cmp.l	a6,a0
	beq.s	work_done
take_type:	moveq	#0,d6
	bsr	get_bit2
	beq.s	.nospe_ofcod
	move.b	-(a6),d0
	lea	2-28(a2),a1
	move.b	d0,(a1)+
	moveq	#1,d1
	moveq	#6,d2
.next:	cmp.b	d0,d1
	bne.s	.no_off_4b
	addq	#2,d1
.no_off_4b:	move.b	d1,(a1)+
	addq	#2,d1
	dbf	d2,.next
	st	1-28(a2)
	bra.s	.spe_ofcod
.nospe_ofcod:	sf	1-28(a2)
.spe_ofcod:	moveq	#0,d6
	bsr	get_bit2
	beq.s	.relatif
	lea	10-28(a2),a1
	moveq	#15,d0
.next_f:	move.b	-(a6),(a1)+
	dbf	d0,.next_f
	st	-28(a2)
	bra.s	.freq
.relatif:	sf	-28(a2)
.freq:	clr	d3
	move.b	-(a6),d3
	move.b	-(a6),d0
	lsl	#8,d0
	move.b	-(a6),d0
	move.l	a5,a3
	sub	d0,a3
	bra.s	make_jnk
string:	bra	decrunch
work_done:
	ifne	PIC_ALGO
	move.l	(a7)+,a0
	pea	(a2)
	bsr.s	decod_picture
	move.l	(a7)+,a2
	endc
	ifne	RESTORE_SPACE
	movem.l	(a7)+,a3-a4
	endc
	ifne	MODE
	move.l	(a7)+,d0
	bsr	copy_decrun
	endc
	ifne	RESTORE_SPACE
	move	-2(a2),d0
	lsl	#2,d0
	add	#DEC_SPACE+$C,d0
	bra.s	.restore
.restore_m:	move.b	-(a3),-(a4)
	subq	#1,d0
.restore:	bne.s	.restore_m
	endc
	unlk	a2
not_packed:	movem.l	(a7)+,d0-a6
 	rts
decrun_table:	dc.w	32,32+64,32+64+256,32+64+256+512,32+64+256+512+1024
	dc.w	32+64+256+512+1024+2048,32+64+256+512+1024+2048+4096
	dc.b	0,1,3,4,5,6,7,8
	ifne	PIC_ALGO
decod_picture:	move	-2(a2),d7
.next_picture:	dbf	d7,.decod_algo
	rts
.decod_algo:	move.l	-(a0),d0
	lea	0(a5,d0.l),a1
.no_odd:	lea	$7d00(a1),a2
.next_planes:	moveq	#3,d6
.next_word:	move	(a1)+,d0
	moveq	#3,d5
.next_bits:	add	d0,d0
	addx	d1,d1
	add	d0,d0
	addx	d2,d2
	add	d0,d0
	addx	d3,d3
	add	d0,d0
	addx	d4,d4
	dbf	d5,.next_bits
	dbf	d6,.next_word
	movem	d1-d4,-8(a1)
	cmp.l	a1,a2
	bne.s	.next_planes
	bra.s	.next_picture
	endc
	ifne	MODE
copy_decrun:	lsr.l	#4,d0
	lea	-12(a6),a6
.copy_decrun:	rept	4
	move.l	(a5)+,(a6)+
	endr
	dbf	d0,.copy_decrun
	rts
	endc
	ifne	RESTORE_SPACE
buff_marg:	dcb.b	$90+DEC_SPACE+$C
	endc

	INCLUDE	VIEWER.S

	SECTION	DATA
	;CODE MACHINE
	;LONGUEUR EN OCTETS
	;LONGUEUR EN MOTS-1
	;NB DE NOPS
INSTRUCTIONS	DC.W	$3147,4,2-1,3
	DC.W	$3159,4,2-1,4
	DC.W	$3146,4,2-1,3

ROTATIONS	INCBIN	A:\INTERPAR.T\ROT_X_Y.DAT

MOUVEMENT	INCBIN	MOVE_CER.DAT
	DC.W	$1234

ROTATE_X	DC.W	0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20
	DC.W	19,18,17,16,15,14,13,12,11,10,9,8,7,6,5,4,3,2,1

ROTATE_Y	DC.W	0,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40
	DC.W	39,38,37,36,35,34,33,32,31,30,29,28,27,26,25,24,23,22,21

RIEN_DU_TOUT	DCB.W	40,0

TABLE_CORES_SCENAR
	DC.L	RIEN_DU_TOUT
	DC.L	ROTATE_X
	DC.L	ROTATE_Y

	;0:NE RIEN FAIRE PENDANT UN CYCLE
	;1:ROTATION X PENDANT UN CYCLE
	;2:ROTATION Y PENDANT UN CYCLE
SCENARIO	DC.W	1,0,0
	DC.W	0,1,0
	DC.W	0,0,1
	DC.W	1,2,1
	DC.W	1,2,0
	DC.W	2,1,2
	DC.W	1,0,1
	DC.W	1,1,0
	DC.W	$1234

ZIK	INCBIN	MUSIX.MUS

IMG	INCBIN	VGA11.PAK
IMG2	INCBIN	VGA12.PAK
IMG4	INCBIN	ARALE2.PAK

	SECTION	BSS
DEB_BSS	DS.B	500
PILE	DS.B	500
ADR_EFF	DS.L	2
BUF_EFF_1	DS.L	6
BUF_EFF_2	DS.L	6
TABLE_HAUTEUR	DS.L	50
NB_VBL	DS.W	1
PT_MOUVEMENT	DS.L	1
PT_SCENAR	DS.L	1
CNT_CYCLE	DS.W	1
BUF_ADR_ACTIF	DS.L	3
SCREEN1	DS.L	1
SCREEN2	DS.L	1
COMPTEUR	DS.W	1
BUF_FLAGS	DS.B	12*170
	DS.B	1000
ADR_GFX	DS.L	8*50
ADR_COD	DS.L	42
BUF_GFX	DS.B	315000
BUF_COD	DS.B	332000
BUF_OFFSETS	DS.W	2*42
	DS.B	256+23000
	DS.B	256
BUFFER	
BUF_CERCLE_TEST	DS.B	65280 ;CE BUFFER SERA REMPLACê PAR UN êCRAN AU COURS DE LA DêMO
BUF_COD_ANNEX	DS.B	10000
	DS.B	53000
BUF	EQU	BUFFER+64000+5000+32066
	DS.B	256
BUF_PLUS	DS.B	160*200
FIN_BSS