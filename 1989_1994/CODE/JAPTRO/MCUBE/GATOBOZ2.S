SPEED = 7
TEMPO_INTER_LET = 2

X1	EQU	0
Y1	EQU	2
X2	EQU	4
Y2	EQU	6
X3	EQU	8
Y3	EQU	10
X4	EQU	12
Y4	EQU	14

Y_CLIP=199
MAX=130
nb_pts=64

main
	clr.l	-(sp)
	move.w	#$20,-(sp)
	trap	#1
	addq.l	#6,sp

	lea.l	8,a0
	move.l	#fini,d0
	move.l	#fini,d1
	move.l	#fini,d2
	move.l	#fini,d3
	move.l	#fini,d4
	move.l	#fini,d5
	move.l	#fini,d6
	move.l	#fini,d7
	movem.l	d0-d7,(a0)

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	move.w	#$5,-(sp)
	trap	#14
	lea.l	12(sp),sp
	MOVEQ	#1,D0
	JSR	ZIK
	LEA	ZIK+8,A0
*******
	MOVE.L	A0,MOD_ZIK
	MOVE.L	A1,MODRET

	LEA	DEB_BSS,A0
	LEA	END_BSS,A1
KILL	CLR.L	(A0)+
	CMP.L	A1,A0
	BLT.S	KILL

	LEA	NEWPILE,A7
	move.l	#BUFECR,d0	deff ecr
	CLR.B	D0
	move.l	d0,SCREEN1
	move.l	#BUFECR2,d0	deff ecr
	CLR.B	D0
	move.l	d0,SCREEN2
	move.l	#BUFECR3,d0	deff ecr
	CLR.B	D0
	move.l	d0,SCREEN3

	MOVE.L	#$00010001,D0
	MOVE.L	D0,$FFFF8240.W

	MOVE.B	SCREEN3+1,$FFFF8201.W
	MOVE.B	SCREEN3+2,$FFFF8203.W

	CLR.B	COLOR
	CLR.B	FLAG1
	CLR.B	FLAG2
	CLR.B	FLAG3
	CLR.B	FLAG4
	MOVE.L	#TEXT1,PT_TXT

	LEA	TEXT1,A0
	JSR	PREP_TEXT
	MOVE.L	#VBL_DEBUT,$70.W

*****	
	LEA	IMG+32,A0
;	LEA	80-32+160*40(A0),A0
	LEA	DATAS_IMG,A1
	
	MOVE	#(nb_pts*7)-1,D4
DO_A_LINE
	MOVE	#8-1,D7	8*16=128 PIX
DO_16_PIX
	MOVE	(A0),D0
	MOVE	2(A0),D1
	MOVE	4(A0),D2
	MOVE	6(A0),D3

	MOVE	#15,D5
	MOVE.B	#15,D6
DO_A_WORD
;	LEA	KAT+2,A2
	BTST	D6,D0
	BEQ.S	VIDE1
	MOVE.B	#1,FLAG1
VIDE1	BTST	D6,D1
	BEQ.S	VIDE2
	MOVE.B	#1,FLAG2
VIDE2	BTST	D6,D2
	BEQ.S	VIDE3
	MOVE.B	#1,FLAG3
VIDE3	BTST	D6,D3
	BEQ.S	VIDE4
	MOVE.B	#1,FLAG4
VIDE4	JSR	TROUVE_COLOR

;	MOVE	D4,-(SP)
;	MOVEQ	#0,D4
;	MOVE.B	COLOR,D4
;	LSL	#1,D4
;	ADDA.L	D4,A2
;	MOVE	(SP)+,D4
;	MOVE	(A2),(A1)+

	MOVE.L	D0,-(SP)
	MOVEQ	#0,D0
	MOVE.B	COLOR,D0
	ADD	D0,D0
	ADD	D0,D0
	MOVE.B	D0,(A1)+
;	MOVE.B	COLOR,(A1)+
	MOVE.L	(SP)+,D0

	CLR.B	COLOR
	CLR.B	FLAG1
	CLR.B	FLAG2
	CLR.B	FLAG3
	CLR.B	FLAG4
	SUBQ.B	#1,D6
	DBF	D5,DO_A_WORD
	ADDQ	#8,A0
	DBF	D7,DO_16_PIX
;	LEA	128(A0),A0
	DBF	D4,DO_A_LINE

	
	move.b	#$12,$fffffc02.w	

**

;	move.l	#newvbl,$70.w	

	clr.b	$fffffa07.w	
	clr.b	$fffffa09.w	

	;MOVE.L	#VBL,$70.W
	jmp	newvbl

TROUVE_COLOR
	CMPI.B	#1,FLAG4
	BNE.S	FLAG4_OFF
	JSR	FLAG4_ON
FLAG4_OFF	CMPI.B	#1,FLAG3
	BNE.S	FLAG3_OFF
	JSR	FLAG3_ON
FLAG3_OFF	CMPI.B	#1,FLAG2
	BNE.S	FLAG2_OFF
	JSR	FLAG2_ON
FLAG2_OFF	CMPI.B	#1,FLAG1
	BNE.S	FLAG1_OFF
	JSR	FLAG1_ON
FLAG1_OFF	RTS
FLAG4_ON	ADDQ.B	#8,COLOR
	RTS
FLAG3_ON	ADDQ.B	#4,COLOR
	RTS
FLAG2_ON	ADDQ.B	#2,COLOR
	RTS
FLAG1_ON	ADDQ.B	#1,COLOR
	RTS

;PNT_SYNC	DS.L	1
COLOR	DS.B	1
FLAG1	DS.B	1
FLAG2	DS.B	1
FLAG3	DS.B	1
FLAG4	DS.B	1
	EVEN		
	
fini	MOVE.L	4.W,A0
	JMP	(A0)

DONNEE	DC	$1c
DONNEE2	DC	-$1c
newvbl        
	JSR	JUMP

	;SF	$FFFF8240.W
	;MOVEM.L	IMG,D0-D7
	;MOVEM.L	D0-D7,$FFFF8240.W
	CMPI.B	#$3B,$FFFFFC02.W
	BNE.S	NO_F1
	CMPI	#MAX,DONNEE
	BGT.S	NO_F1
	ADDQ	#1,DONNEE
	SUBQ	#1,DONNEE2
NO_F1	CMPI.B	#$3C,$FFFFFC02.W
	BNE.S	NO_F2
	CMPI	#MAX,DONNEE2
	BGT.S	NO_F2
	SUBQ	#1,DONNEE
	ADDQ	#1,DONNEE2
NO_F2
	LEA	tpoitesp1,A0
	LEA	2(A0),A0
	MOVEQ	#0,D0
	MOVE	DONNEE,D0
	SWAP	D0
	MOVE	DONNEE,D0
	MOVEQ	#0,D1
	MOVE	DONNEE2,D1
	SWAP	D1
	MOVE	DONNEE2,D1

	MOVE.L	D1,(A0)+
	MOVE	D1,(A0)+
;	dc.w	-50,-50,-50		1

	MOVE	D1,(A0)+
	MOVE	D0,(A0)+
	MOVE	D1,(A0)+
;	dc.w	-50,50,-50		2

	MOVE.L	D0,(A0)+
	MOVE	D1,(A0)+
;	dc.w	50,50,-50		3	

	MOVE	D0,(A0)+
	MOVE.L	D1,(A0)+
;	dc.w	50,-50,-50		4

	MOVE.L	D1,(A0)+
	MOVE	D0,(A0)+
;	dc.w	-50,-50,50		5	

	MOVE	D1,(A0)+
	MOVE.L	D0,(A0)+
;	dc.w	-50,50,50		6	

	MOVE.L	D0,(A0)+
	MOVE	D0,(A0)+
;	dc.w	50,50,50		7

	MOVE	D0,(A0)+
	MOVE	D1,(A0)+
	MOVE	D0,(A0)+
;	dc.w	50,-50,50		8


	MOVE	DONNEE,$FFFFC

	CMPI.B	#$3D,$FFFFFC02.W
	BNE.S	NO_F3
	ADDQ.L	#2,MOD_IMG
NO_F3

	CMPI.B	#$3E,$FFFFFC02.W
	BNE.S	NO_F4
	SUBQ.L	#2,MOD_IMG
NO_F4
	CMPI.B	#$3F,$FFFFFC02.W
	BNE.S	NO_F5
	ADD.L	#320,MOD_IMG
NO_F5

	CMPI.B	#$40,$FFFFFC02.W
	BNE.S	NO_F6
	SUB.L	#320,MOD_IMG
NO_F6
	;MOVE.L	MOD_IMG,$E0000
	;MOVE	DONNEE,$E0008

	bsr	efecr

	move.w	$fffffc02.w,key
	bsr	calcpoint
	bsr	calcrot  

*

	LEA	DATAS_FACES,A6

	lea	t_points,a0
	lea	face_en_cours,a1
	move.l	(a0)+,(a1)+
	move.l	(a0)+,(a1)+
	move.l	(a0)+,(a1)+
	move.l	(a0)+,(a1)+
;	CLR	MOD_PLAN
	MOVE.L	(A6)+,PNT_DATA
	MOVE.L	A6,-(SP)
	JSR	GERE_FACE
*	
	MOVE.L	(SP)+,A6

;	BRA	STOP
	lea	t_points,a0
	lea	face_en_cours,a1
	move.l	28(a0),(a1)+
	move.l	24(a0),(a1)+
	move.l	20(a0),(a1)+
	move.l	16(a0),(a1)+
;	CLR	MOD_PLAN
	MOVE.L	(A6)+,PNT_DATA
	MOVE.L	A6,-(SP)
	JSR	GERE_FACE
*
	MOVE.L	(SP)+,A6

	lea	t_points,a0
	lea	face_en_cours,a1
	move.l	12(a0),(a1)+
	move.l	8(a0),(a1)+
	move.l	24(a0),(a1)+
	move.l	28(a0),(a1)+
;	MOVE	#2,MOD_PLAN
	MOVE.L	(A6)+,PNT_DATA
	MOVE.L	A6,-(SP)
	JSR	GERE_FACE
*
	MOVE.L	(SP)+,A6
	lea	t_points,a0
	lea	face_en_cours,a1
	move.l	16(a0),(a1)+
	move.l	20(a0),(a1)+
	move.l	4(a0),(a1)+
	move.l	(a0),(a1)+
;	MOVE	#2,MOD_PLAN
	MOVE.L	(A6)+,PNT_DATA
	MOVE.L	A6,-(SP)
	JSR	GERE_FACE
*
	MOVE.L	(SP)+,A6
	lea	t_points,a0
	lea	face_en_cours,a1
	move.l	16(a0),(a1)+
	move.l	(a0),(a1)+
	move.l	12(a0),(a1)+
	move.l	28(A0),(a1)+
;	MOVE	#4,MOD_PLAN
	MOVE.L	(A6)+,PNT_DATA
	MOVE.L	A6,-(SP)
	JSR	GERE_FACE
*
	MOVE.L	(SP)+,A6
	lea	t_points,a0
	lea	face_en_cours,a1
	move.l	4(a0),(a1)+
	move.l	20(a0),(a1)+
	move.l	24(a0),(a1)+
	move.l	8(a0),(a1)+
;	MOVE	#4,MOD_PLAN
	MOVE.L	(A6)+,PNT_DATA
	MOVE.L	A6,-(SP)
	JSR	GERE_FACE
*
	MOVE.L	(SP)+,A6
STOP	

	MOVE.L	SCREEN2,A1
	LEA	160*(100-25)+48(A1),A1
	MOVEQ	#50-1,D6
	MOVE.L	A1,A2
.CONT3	MOVE.L	A2,A1
	MOVEQ	#15,D0
	MOVEQ	#0,D7
.CONT2	MOVE	(A1)+,D1	P1
	MOVE	(A1)+,D2	..
	MOVE	(A1)+,D3
	MOVE	(A1)+,D4	P4
	
.CONT	BTST	D0,D1
	BEQ.S	.P1_NUL
	BRA.S	.SUITE
.P1_NUL
	BTST	D0,D2
	BEQ.S	.P2_NUL
	BRA.S	.SUITE
.P2_NUL
	BTST	D0,D3
	BEQ.S	.P3_NUL
	BRA.S	.SUITE
.P3_NUL
	BTST	D0,D4
	BEQ.S	.P4_NUL
	BRA.S	.SUITE
.P4_NUL
	MOVE	D0,D5
	SUBQ	#1,D5
	CMPI	#-1,D5
	BNE.S	.TRAVAIL
***
	ADDQ	#2,D5

***
.TRAVAIL
	BTST	D5,D1
	BEQ.S	.P1_NULB
	BSET	D0,D1
	;BRA.S	.SUITEB
.P1_NULB
	BTST	D5,D2
	BEQ.S	.P2_NULB
	BSET	D0,D2
	;BRA.S	.SUITEB
.P2_NULB
	BTST	D5,D3
	BEQ.S	.P3_NULB
	BSET	D0,D3
	;BRA.S	.SUITEB
.P3_NULB
	BTST	D5,D4
	BEQ.S	.P4_NULB
	BSET	D0,D4
	BRA.S	.SUITEB
.P4_NULB
	BRA.S	.SUITE
.SUITEB	SUBQ	#8,A1
	MOVE	D1,(A1)+
	MOVE	D2,(A1)+
	MOVE	D3,(A1)+
	MOVE	D4,(A1)+

.SUITE	SUBQ	#1,D0
	CMPI	#-1,D0
	BNE.S	.CONT
	MOVEQ	#15,D0
	ADDQ	#1,D7
	CMPI	#5,D7
	BNE.S	.CONT2
	LEA	160(A2),A2
	DBF	D6,.CONT3

***
	LEA	BUF_DAT,A0
MODB	EQU	*+2
	ADDA.L	#0,A0

	MOVE.L	SCREEN2,A1
	LEA	160*(100-25)+48(A1),A1

	MOVE	#50-1,D0
.COP	
	MOVEM.L	(A1),D1-D7/A2-A4
	MOVEM.L	D1-D7/A2-A4,(A0)
	LEA	160(A1),A1
	LEA	40(A0),A0
	ADD.L	#40,MODB
	DBF	D0,.COP
	ADDQ	#1,TIME2
	CMPI	#128,TIME2
	BNE	GOGO
	move.l	SCREEN1,A0
	move.l	SCREEN2,A1
	MOVE	#7999,D0
	moveq	#0,d1
GLOGLO	MOVE.L	D1,(A0)+
	MOVE.L	D1,(A1)+
	DBF	D0,GLOGLO
** PRP ZOOM
	LEA	TABLE_256,A0
	LEA	BUF_COD,A1
	MOVE.L	A1,(A0)+
	MOVEQ	#0,D0

CONTINUE	MOVEQ	#0,D7	OFFSET
	MOVEQ	#0,D6
	;%00000001
	;        =BIT 0, BNE SAUTE
*TEST 1
TEST1	BTST	#7,D0
	BNE.S	.BIT_A_1
	;%0x
	BTST	#6,D0
	BNE.S	.BIT_A_1B
	;%00
	JMP	TEST2
.BIT_A_1B	;%01
	OR.B	#%00001111,D6
	JMP	TEST2
.BIT_A_1	;%1X
	BTST	#6,D0
	BNE.S	.BIT_A_1C
	;%10
	OR.B	#%11110000,D6
	JMP	TEST2
.BIT_A_1C	;%11
	OR.B	#%11111111,D6
*TEST2
TEST2	LSL	#8,D6
	BTST	#5,D0
	BNE.S	.BIT_A_1
	;%0x
	BTST	#4,D0
	BNE.S	.BIT_A_1B
	;%00
	JMP	TEST3
.BIT_A_1B	;%01
	OR.B	#%00001111,D6
	JMP	TEST3
.BIT_A_1	;%1X
	BTST	#4,D0
	BNE.S	.BIT_A_1C
	;%10
	OR.B	#%11110000,D6
	JMP	TEST3
.BIT_A_1C	;%11
	OR.B	#%11111111,D6
*TEST3
TEST3	;TST	D6
	;BEQ.S	NON
	MOVE	#$3D7C,(A1)+
	MOVE	D6,(A1)+
	MOVE	D7,(A1)+
NON	ADDQ	#8,D7
	MOVEQ	#0,D6
****
	BTST	#3,D0
	BNE.S	.BIT_A_1
	;%0x
	BTST	#2,D0
	BNE.S	.BIT_A_1B
	;%00
	JMP	TEST4
.BIT_A_1B	;%01
	OR.B	#%00001111,D6
	JMP	TEST4
.BIT_A_1	;%1X
	BTST	#2,D0
	BNE.S	.BIT_A_1C
	;%10
	OR.B	#%11110000,D6
	JMP	TEST4
.BIT_A_1C	;%11
	OR.B	#%11111111,D6
*TEST4
TEST4	LSL	#8,D6
	BTST	#1,D0
	BNE.S	.BIT_A_1
	;%0x
	BTST	#0,D0
	BNE.S	.BIT_A_1B
	;%00
	JMP	TEST5
.BIT_A_1B	;%01
	OR.B	#%00001111,D6
	JMP	TEST5
.BIT_A_1	;%1X
	BTST	#0,D0
	BNE.S	.BIT_A_1C
	;%10
	OR.B	#%11110000,D6
	JMP	TEST5
.BIT_A_1C	;%11
	OR.B	#%11111111,D6
*TEST5
TEST5	;TST	D6
	;BEQ.S	NON2
	MOVE	#$3D7C,(A1)+
	MOVE	D6,(A1)+
	MOVE	D7,(A1)+
NON2	ADDQ	#8,D7
	MOVEQ	#0,D6
	MOVE	#$4E75,(A1)+
	MOVE.L	A1,(A0)+
	ADDQ	#1,D0
	CMPI	#256,D0
	BNE	CONTINUE
FINI	NOP

	JMP	NEWVBL

GOGO	
	JSR	CACHE
	move.l	SCREEN1,d0		flip SCREEN
	move.l	SCREEN2,d1
	;move.b	SCREEN2+1,$ffff8201.w
	;move.b	SCREEN2+2,$ffff8203.w
	move.l	d0,SCREEN2
	move.l	d1,SCREEN1

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	NO_TIME2
	ST	$FFFF8240.W
NO_TIME2
	cmp.b	#$39,key
	beq	fini
POPO	TST.L	VBL_FLAG
	BEQ.S	POPO
	CLR.L	VBL_FLAG

	CMPI.B	#$10,$FFFFFC02.W
	BEQ.S	JUMPED
POPO2	TST.L	VBL_FLAG
	BEQ.S	POPO2
	CLR.L	VBL_FLAG
JUMPED
	jmp	newvbl
	rte
TIME2	DC	0

CACHE
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVE	#100-25-1,D1
.E	MOVE	#20-1,D2
.R	
	MOVE	#$FFFF,(A0)
	ADDQ	#8,A0
	DBF	D2,.R
	DBF	D1,.E
***
	MOVE.L	SCREEN2,A0
	LEA	160*(100-25)(A0),A0
	MOVEQ	#0,D0
	MOVE	#50-1,D1
.EU	MOVE	#$FFFF,(A0)
	MOVE	#$FFFF,8(A0)
	MOVE	#$FFFF,16(A0)
	MOVE	#$FFFF,24(A0)
	MOVE	#$FFFF,32(A0)
	MOVE	#$FFFF,40(A0)

	MOVE	#$FFFF,88(A0)
	MOVE	#$FFFF,96(A0)
	MOVE	#$FFFF,104(A0)
	MOVE	#$FFFF,112(A0)
	MOVE	#$FFFF,120(A0)
	MOVE	#$FFFF,128(A0)
	MOVE	#$FFFF,136(A0)
	MOVE	#$FFFF,144(A0)
	MOVE	#$FFFF,152(A0)
	LEA	160(A0),A0
	DBF	D1,.EU

***
	MOVE.L	SCREEN2,A0
	LEA	160*125(A0),A0
	MOVEQ	#0,D0
	MOVE	#100-25-1,D1
.EE	MOVE	#20-1,D2
.RE	
	
	MOVE	#$FFFF,(A0)
	ADDQ	#8,A0
	DBF	D2,.RE
	DBF	D1,.EE
	RTS
**** LES CHOSES SERIEUSES COMMENCENT -ICI- *****
VBL	ADDQ.L	#1,VBL_FLAG
	MOVEM.L	D0-A6,-(SP)
	JSR	RT_ZIK
	MOVEM.L	(SP)+,D0-A6
	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	.ZZ
MODRET	EQU	*+2
	MOVE.L	#0,MODRET2
.ZZ	RTE
NEWVBL	MOVE.L	#VBL,$70.W
	MOVEM.L	IMG,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

* AFFICHAGE DU CUBE EN RUBBER DANS UN BUFFER
	LEA	RUBBER,A2
MODRUB	EQU	*+2
	LEA	0(A2),A2
	CMPI.L	#$99999999,200*4(A2)
	BNE.S	.OK
	LEA	RUBBER,A2
	CLR	MODRUB
.OK	ADDQ	#4,MODRUB

	LEA	BUF_DAT,A3
MODB2	EQU	*+2
	ADDA.L	#0,A3
	MOVE.L	SCREEN3,A1
	;LEA	160*(100-25)+48(A1),A1
	MOVE	#50-1,D0
.COPR
	MOVE.L	(A2)+,D1
	MOVE.L	A3,A0
	ADDA.L	D1,A0

	CMP.L	#BUF_DAT,A0
	BGE.S	.ROOL1
	ADD.L	#128*50*40,A0
.ROOL1	CMP.L	#BUF_END,A0
	BLT.S	.ROOL2
	SUB.L	#128*40*50,A0
.ROOL2
	MOVEM.L	(A0),D1-D6
	MOVEM.L	D1-D6,(A1)
	MOVEM.L	4*6(A0),D1-D4
	MOVEM.L	D1-D4,4*6(A1)
	LEA	160(A1),A1
	LEA	40(A3),A3
	ADD.L	#40,MODB2
	DBF	D0,.COPR
	CMPI.L	#128*40*50,MODB2
	BNE.S	.GOGOR
	CLR.L	MODB2
.GOGOR
*** ZOOM
	MOVE.L	SCREEN3,A0
	MOVE.L	SCREEN2,A6
	JSR	ZOOM_ROUTY

	MOVE.L	SCREEN3,A0
	ADDQ	#2,A0
	MOVE.L	SCREEN2,A6
	ADDQ	#2,A6
	JSR	ZOOM_ROUTY

	MOVE.L	SCREEN3,A0
	ADDQ	#4,A0
	MOVE.L	SCREEN2,A6
	ADDQ	#4,A6
	JSR	ZOOM_ROUTY

	MOVE.L	SCREEN3,A0
	ADDQ	#6,A0
	MOVE.L	SCREEN2,A6
	ADDQ	#6,A6
	JSR	ZOOM_ROUTY

*** RECOPIE
	MOVE.L	SCREEN2,A0
N	SET	0
	REPT	50
	MOVEM.L	N(A0),D0-D7/A1-A6
	MOVEM.L	D0-D7/A1-A6,160+N(A0)
	MOVEM.L	D0-D7/A1-A6,320+N(A0)
	MOVEM.L	D0-D7/A1-A6,480+N(A0)

	MOVEM.L	N+14*4(A0),D0-D7/A1-A6
	MOVEM.L	D0-D7/A1-A6,160+14*4+N(A0)
	MOVEM.L	D0-D7/A1-A6,320+14*4+N(A0)
	MOVEM.L	D0-D7/A1-A6,480+14*4+N(A0)

	MOVEM.L	N+14*4*2(A0),D0-D7/A1-A4
	MOVEM.L	D0-D7/A1-A4,160+14*4*2+N(A0)
	MOVEM.L	D0-D7/A1-A4,320+14*4*2+N(A0)
	MOVEM.L	D0-D7/A1-A4,480+14*4*2+N(A0)
N	SET	N+640
	ENDR
***
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.Q
	ST	$FFFF8240.W
.Q	
 
***
	move.l	SCREEN1,d0		flip SCREEN
	move.l	SCREEN2,d1
	move.b	SCREEN2+1,$ffff8201.w
	move.b	SCREEN2+2,$ffff8203.w
	move.l	d0,SCREEN2
	move.l	d1,SCREEN1

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.POPO
	ST	$FFFF8240.W
.POPO	TST.L	VBL_FLAG
	BEQ.S	.POPO
	CLR.L	VBL_FLAG
.POPO2	TST.L	VBL_FLAG
	BEQ.S	.POPO2
	CLR.L	VBL_FLAG
MODRET2	EQU	*+2
	jmp	NEWVBL
ZOOM_ROUTY

	LEA	TABLE_256,A1
	MOVE	#50-1,D7
.DO_A_LINE
	MOVE.L	A0,A3
	REPT	10/2
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0	2
	ADD	D0,D0	1
	ADD	D0,D0	1
	MOVE.L	(A1,D0.W),A2
	JSR	(A2)	5?
	LEA	16(A6),A6

	MOVEQ	#0,D0
	MOVE.B	(A0),D0	2
	ADD	D0,D0	1
	ADD	D0,D0	1
	MOVE.L	(A1,D0.W),A2
	JSR	(A2)	5?
	LEA	16(A6),A6
	ADDQ	#7,A0
	ENDR
	MOVE.L	A3,A0
	LEA	160(A0),A0
	LEA	160*3(A6),A6
	DBF	D7,.DO_A_LINE

	RTS
GERE_FACE
	CLR	CNT
;	st	$ffff8240.w
;1) Teste si la face est visible
  	lea.l	face_en_cours,a1
	move	(a1),d3
	sub	4(a1),d3
	move	2(a1),d4
	sub	6(a1),d4
	move	4*2(a1),d0
	sub	4(a1),d0
	move	10(a1),d5
	sub	6(a1),d5
	muls	d3,d5	(X1-X2)*(Y3-Y2)
	muls	d0,d4	(X3-X2)*(Y1-Y2)
	sub.l	d4,d5	produit vectoriel
;(X1-X2)*(Y3-Y2)-(X3-X2)*(Y1-Y2)
;=(X1Y3-X1Y2-X2Y3+X2Y2)-(X3Y1-X3Y2-X2Y1+X2Y2)
;=X1Y3 - X1Y2 - X2Y3 +X2Y1 - X3Y1 + X3Y2
	bgt.s	visible
cachee	rts
COL	DC	0
visible
;3721
	CLR.L	MOD_IMG
	;JMP	PART2
	CMPI	#600,D5
	BGT.S	.COO1
	ADD.L	#nb_pts*nb_pts,MOD_IMG
.COO1
	CMPI	#600*2,D5
	BGT.S	.COO2
	ADD.L	#nb_pts*nb_pts,MOD_IMG
.COO2
	CMPI	#600*3,D5
	BGT.S	.COO3
	ADD.L	#nb_pts*nb_pts,MOD_IMG
.COO3	CMPI	#600*4,D5
	BGT.S	.COO4
	ADD.L	#nb_pts*nb_pts,MOD_IMG
.COO4	CMPI	#600*5,D5
	BGT.S	.COO5
	ADD.L	#nb_pts*nb_pts,MOD_IMG
.COO5	


PART2
;2) Efface les tableaux
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	SUBA.L	A1,A1
	SUBA.L	A2,A2
	SUBA.L	A3,A3
	SUBA.L	A4,A4
	SUBA.L	A5,A5
	SUBA.L	A6,A6
	LEA	TAB,A0
N	SET	0
	REPT	7
	MOVEM.L	D0-D7/A1-A6,N(A0)
N	SET	N+14*4
	ENDR
	MOVEM.L	D0-D1,N(A0)
	LEA	TAB2,A0
N	SET	0
	REPT	7
	MOVEM.L	D0-D7/A1-A6,N(A0)
N	SET	N+14*4
	ENDR
	MOVEM.L	D0-D1,N(A0)

;3) CrÇe les nouveaux tableaux
	LEA	ADR_RTS,A4

	LEA	face_en_cours,a0
	LEA	DATAS_X,A3
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE	(A0),D0	x1
	MOVE	4(A0),D2	x2
;	MOVE	2(A0),D1
	MOVE	#nb_pts,D3
;	MOVE	D1,D4
;	ADD	D4,D4
;	ADD	D4,A3
	JSR	CALCUL

	LEA	face_en_cours,a0
	LEA	DATAS_Y,A3
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE	2(A0),D0	Y1
	MOVE	4+2(A0),D2	Y2
;	MOVE	2(A0),D1
	MOVE	#nb_pts,D3
;	MOVE	D1,D4
;	ADD	D4,D4
;	ADD	D4,A3
	JSR	CALCUL

	LEA	face_en_cours,a0
	LEA	DATAS_X2,A3
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE	12(A0),D0	x3
	MOVE	8(A0),D2	x4
;	MOVE	2(A0),D1
	MOVE	#nb_pts,D3
;	MOVE	D1,D4
;	ADD	D4,D4
;	ADD	D4,A3
	JSR	CALCUL

	LEA	face_en_cours,a0
	LEA	DATAS_Y2,A3
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE	12+2(A0),D0	Y3
	MOVE	8+2(A0),D2	Y4
;	MOVE	2(A0),D1
	MOVE	#nb_pts,D3
;	MOVE	D1,D4
;	ADD	D4,D4
;	ADD	D4,A3
	JSR	CALCUL
*******************************************************
;	LEA	DATAS_IMG,A6
	MOVE.L	PNT_DATA,A6
MOD_IMG	EQU	*+2
	ADDA.L	#0,A6

	MOVE	#nb_pts-1,D6
DO_A_SEG
	LEA	DATAS_X,a0
	ADD	CNT,A0
	LEA	DATAS_X2,a1
	ADD	CNT,A1

	LEA	TRAVAIL_X,A3
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE	(A0)+,D0	x3
	MOVE	(A1)+,D2	x4
;	MOVE	2(A0),D1
	MOVE	#nb_pts,D3
;	MOVE	D1,D4
;	ADD	D4,D4
;	ADD	D4,A3
	JSR	CALCUL

	LEA	DATAS_Y,a0
	ADD	CNT,A0
	LEA	DATAS_Y2,a1
	ADD	CNT,A1
	LEA	TRAVAIL_Y,A3
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE	(A0)+,D0	x3
;	MULU	#160,D0
	MOVE	(A1)+,D2	x4
;	MULU	#160,D2
;	MOVE	2(A0),D1
	MOVE	#nb_pts,D3
;	MOVE	D1,D4
;	ADD	D4,D4
;	ADD	D4,A3
	JSR	CALCUL

	LEA	TRAVAIL_X,A0
	LEA	TRAVAIL_Y,A1
	MOVE	#nb_pts-1,D7
	moveq	#0,d3
	MOVE.L	SCREEN2,A5
	lea	-16(a5),a5
	LEA	TABLO_Y,A2
DO_A_DOT	REPT	nb_pts
	LEA	TABLO_X,A4
	MOVE.L	A5,A3
	MOVE	(A0)+,D0
	MOVE	(A1)+,D1

	ADD	D1,D1
	ADD	(A2,D1.W),A3

	ADD	D0,D0
	ADD	D0,D0
	MOVE	(A4,D0.W),D2
	ADD	2(A4,D0.W),A3
	MOVE.B	(A6)+,D3
	LEA	TABLE,A4
	MOVE.L	(A4,D3.W),A4
	JSR	(A4)
;	LEA	160(A3),A3
;	JSR	(A4)
	ENDR
;	DBF	D7,DO_A_DOT

	ADDQ	#2,CNT
;	LEA	(320-nb_pts)(A6),A6
	DBF	D6,DO_A_SEG

DONE


	LEA	face_en_cours,a0
	MOVE	2(A0),D0	Y1
	MOVE	2+4(A0),D1	Y2
	MOVE	2+4*2(A0),D2	Y3
	MOVE	2+4*3(A0),D3	Y4
	JSR	TRIE_MIN
**
;tri Ö faire avant pour Jumper+tot en cas de clip, mais tab a modifier
;apres le calcul...
	CMP	#Y_CLIP,D4
	BLT.S	OK_OK
	BRA	cachee
OK_OK	TST	D4
	BGE.S	NO_CLIP
	MOVEQ	#0,D4
NO_CLIP	ADD	D4,D4
;	MOVE	D4,MOD_DEP_TAB
	MULU	#80,D4
;	MOVE	D4,MODY

	LEA	face_en_cours,a0
	MOVE	2(A0),D0	Y1
	MOVE	2+4(A0),D1	Y2
	MOVE	2+4*2(A0),D2	Y3
	MOVE	2+4*3(A0),D3	Y4
	JSR	TRIE_MAX
	TST	D4
	BGE.S	OK_OK_OK
	BRA	cachee
OK_OK_OK	LEA	TAB,A3
	LEA	TAB2,A4
	CMPI	#Y_CLIP,D4
	BLE.S	NO_CLIP2
	MOVE	#Y_CLIP,D4
NO_CLIP2	ADD	D4,D4
	ADD	D4,A3
	ADD	D4,A4
	MOVE	#0,(A3)
	MOVE	#320,(A4)

***
;	sf	$ffff8240.w
	RTS
CNT	DC	0
TABLE
	DC.L	COL0
	DC.L	COL1
	DC.L	COL2
	DC.L	COL3
	DC.L	COL4
	DC.L	COL5
	DC.L	COL6
	DC.L	COL7
	DC.L	COL8
	DC.L	COL9
	DC.L	COL10
	DC.L	COL11
	DC.L	COL12
	DC.L	COL13
	DC.L	COL14
	DC.L	COL15

COL0
	RTS
COL1	OR	D2,(A3)
	NOT	D2
	AND	D2,2(A3)
	AND	D2,4(A3)
	AND	D2,6(A3)
	RTS
COL2	OR	D2,2(A3)
	NOT	D2
	AND	D2,(A3)
	AND	D2,4(A3)
	AND	D2,6(A3)
	RTS
COL3	OR	D2,(A3)
	OR	D2,2(A3)
	NOT	D2
	AND	D2,4(A3)
	AND	D2,6(A3)
	RTS
COL4	OR	D2,4(A3)
	NOT	D2
	AND	D2,(A3)
	AND	D2,2(A3)
	AND	D2,6(A3)
	RTS
COL5	OR	D2,4(A3)
	OR	D2,(A3)
	NOT	D2
	AND	D2,6(A3)
	AND	D2,2(A3)
	RTS
COL6	OR	D2,4(A3)
	OR	D2,2(A3)
	NOT	D2
	AND	D2,(A3)
	AND	D2,6(A3)
	RTS
COL7
	OR	D2,(A3)
	OR	D2,2(A3)
	OR	D2,4(A3)
	NOT	D2
	AND	D2,6(A3)
	RTS
COL8	OR	D2,6(A3)
	NOT	D2
	AND	D2,(A3)
	AND	D2,2(A3)
	AND	D2,4(A3)
	RTS
COL9	OR	D2,6(A3)
	OR	D2,(A3)
	NOT	D2
	AND	D2,2(A3)
	AND	D2,4(A3)
	RTS
COL10	OR	D2,6(A3)
	OR	D2,2(A3)
	NOT	D2
	AND	D2,(A3)
	AND	D2,4(A3)
	RTS
COL11	OR	D2,6(A3)
	OR	D2,2(A3)
	OR	D2,(A3)
	NOT	D2
	AND	D2,4(A3)
	RTS
COL12	OR	D2,6(A3)
	OR	D2,4(A3)
	NOT	D2
	AND	D2,2(A3)
	AND	D2,(A3)
	RTS
COL13	OR	D2,6(A3)
	OR	D2,4(A3)
	OR	D2,(A3)
	NOT	D2
	AND	D2,2(A3)
	RTS
COL14	OR	D2,6(A3)
	OR	D2,4(A3)
	OR	D2,2(A3)
	NOT	D2
	AND	D2,(A3)
	RTS
COL15	OR	D2,6(A3)
	OR	D2,4(A3)
	OR	D2,2(A3)
	OR	D2,(A3)
	RTS

	even
key	dc.b	0
	even 

;FLAG	DC	0
TRIE_MAX	CMP	D0,D1
	BGT.S	AD1_WIN_A
;	BLT.S	AD0_WIN_A
;	ADDQ	#1,FLAG1
AD0_WIN_A	CMP	D2,D0
	BGT.S	AD0_WIN_B
;	BLT.S	AD2_WIN_A
;	ADDQ	#1,FLAG2
AD2_WIN_A	CMP	D3,D2
	BGT.S	AD2_WIN_B
;	BLT.S	AD3_WIN_A
;	ADDQ	#1,FLAG3
AD3_WIN_A	MOVE	D3,D4
;	TST	FLAG3
;	BNE.S	OK0
;	TST	FLAG2
;	BNE.S	OK1
;	TST	FLAG1
;	BNE.S	OK2
;	;4 Y EGAUX=> PROB
;	MOVE	#$700,$FFFF8240.W
;	JMP	FIN
;OK2	;D3=D2=D1
;OK1	;D3=D2
;	MOVE	4*3(A0),D6
;	SUB	4*2(A0),D6
;	B
OK0	MOVE	4*3(A0),D5
	RTS
AD2_WIN_B	MOVE	D2,D4
	MOVE	4*2(A0),D5
	RTS
AD0_WIN_B	CMP	D3,D0
	BGT.S	AD0_WIN_C
AD3_WIN_B	MOVE	D3,D4
	MOVE	4*3(A0),D5
	RTS
AD0_WIN_C	MOVE	D0,D4
	MOVE	(A0),D5
	RTS
AD1_WIN_A	CMP	D2,D1
	BGT.S	AD1_WIN_B
AD2_WIN_C	CMP	D3,D2
	BGT.S	AD2_WIN_B
	MOVE	D3,D4
	MOVE	4*3(A0),D5
	RTS
AD1_WIN_B	CMP	D3,D1
	BGT.S	AD1_WIN_C
	MOVE	D3,D4
	MOVE	4*3(A0),D5
	RTS
AD1_WIN_C	MOVE	D1,D4
	MOVE	4(A0),D5
	RTS


TRIE_MIN	CMP	D0,D1
	BLT.S	D1_WIN_A
D0_WIN_A	CMP	D2,D0
	BLT.S	D0_WIN_B
D2_WIN_A	CMP	D3,D2
	BLT.S	D2_WIN_B
D3_WIN_A	MOVE	D3,D4
	MOVE	4*3(A0),D5
	RTS
D2_WIN_B	MOVE	D2,D4
	MOVE	4*2(A0),D5
	RTS
D0_WIN_B	CMP	D3,D0
	BLT.S	D0_WIN_C
D3_WIN_B	MOVE	D3,D4
	MOVE	4*3(A0),D5
	RTS
D0_WIN_C	MOVE	D0,D4
	MOVE	(A0),D5
	RTS
D1_WIN_A	CMP	D2,D1
	BLT.S	D1_WIN_B
D2_WIN_C	CMP	D3,D2
	BLT.S	D2_WIN_B
	MOVE	D3,D4
	MOVE	4*3(A0),D5
	RTS
D1_WIN_B	CMP	D3,D1
	BLT.S	D1_WIN_C
	MOVE	D3,D4
	MOVE	4*3(A0),D5
	RTS
D1_WIN_C	MOVE	D1,D4
	MOVE	4(A0),D5
	RTS

CALCUL	LEA	ADR_RTS,A4
	MOVEQ	#0,D4
	CMP	D0,D2
	BGT.S	D2_P_G
	EXG.L	D0,D2
	BRA	OTHER
D2_P_G	SUB.W	D0,D2	DELTA X
	SUB.W	D1,D3	DELTA Y
	BNE.S	NON_NUL
	RTS
NON_NUL
	MOVE	D2,D4

	ASL	#7,D4	*128
;	EXT.L	D4
	DIVS.W	D3,D4	*X(+1)=(DX/DY)+X
	MOVE	#9,D2
	EXT.L	d4
	ASL.L	D2,D4
	
	ADD	D3,D3
	ADD	D3,D3
	NEG	D3
	SWAP	D4
	
;	ADD	D0,D0
;	ADD	D0,D0
	SUB	D1,D1
	JMP	(A4,D3)

	REPT	401*2
	MOVE	D0,(A3)+
	ADDX.L	D4,D0
	ENDR
ADR_RTS
	RTS

OTHER	LEA	ADR_RTS2,A4
	SUB.W	D0,D2	DELTA X
	SUB.W	D1,D3	DELTA Y
	BNE.S	NON_NUL2
	RTS
NON_NUL2
	MOVE	D3,D5
	ADD	D5,D5
	ADD	D5,A3
	MOVE	D2,D4
	
	ASL	#7,D4	*128
;	EXT.L	D4
	DIVS.W	D3,D4	*X(+1)=(DX/DY)+X
	MOVE	#9,D2
	EXT.L	d4
	ASL.L	D2,D4
	
	ADD	D3,D3
	ADD	D3,D3
	NEG	D3
	SWAP	D4
	
;	ADD	D0,D0
;	ADD	D0,D0
	SUB	D1,D1
	JMP	(A4,D3)

	REPT	401*2
	MOVE	D0,-(A3)
	ADDX.L	D4,D0
	ENDR
ADR_RTS2
	RTS

MINI_TABLO	DS.L	8
	DS	200*2
TAB	DS	200
	DS	200*2
TAB2	DS	200
	DS	200*2
TABE	DC.L	$99999999,0,0,0


**
ROUTINE	MOVEM.L	D0-D2/A0-A1,-(SP)
	MOVE.L	SCREEN2,a1
	MOVEQ	#0,D2
	SWAP	D0
	SWAP	D1
	CLR	D0
	CLR	D1
	SWAP	D0
	SWAP	D1

;	MOVE	xp,D0
;	MOVE	yp,d1

	TST	D0
	BGE.S	TOK1
	MOVE	#-1,D1
	MOVEQ	#0,D0
TOK1	CMPI	#320,D0
	BLT.S	TOK2
	MOVE	#-1,D1
	MOVEQ	#0,D0
TOK2
	CMPI	#-1,D1
	BGE.S	TOK3
	MOVE	#-1,D1
	MOVEQ	#0,D0
TOK3	CMPI	#199,D1
	BLE.S	TOK4
	MOVEQ	#0,D1
	MOVE	#-1,D1
TOK4	MULU	#160,D1
	DIVS	#16,D0
	MOVE	D0,D2
	MULU	#8,D2
	ADD.L	D2,D1
	SWAP	D0
	CMPI	#$7,D0
	BGT.S	PLUS_UN
	BRA.S	PLUS_LOIN
PLUS_UN	ADD.L	#1,D1
	SUB.L	#8,D0
PLUS_LOIN
	LEA	KK,A0
	LSL	#3,D0
	JSR	(A0,D0.W)

;	CMPI	#0,ZA
;	BGT.S	PLD
;	BRA.S	NOPLD
;PLD	ADD	#2,D1
;NOPLD	;MOVE	D1,(A2)+

	OR.B	D0,(A1,D1.W)

	MOVEM.L	(SP)+,D0-D2/A0-A1
	RTS

KK	MOVE.B	#%10000000,D0
	NOP
	RTS
	MOVE.B	#%01000000,D0
	NOP
	RTS
	MOVE.B	#%00100000,D0
	NOP
	RTS	
	MOVE.B	#%00010000,D0
	NOP
	RTS	
	MOVE.B	#%00001000,D0
	NOP
	RTS	
	MOVE.B	#%00000100,D0
	NOP
	RTS
	MOVE.B	#%00000010,D0
	NOP
	RTS
	MOVE.B	#%00000001,D0
	NOP
	RTS

	*-----sous programe------*	
calcrot
;	sub.w	#1,attvbl
;	beq.s	calcrots	
	bra	finacal
calcrots
	move.w	#5,attvbl
	moveq	#0,d0
	move.b	key,d0
	
	cmp.w	#7,d0
	bgt.s	finacal
	beq.s	changobj
	subq	#1,d0
	
	lea.l	adag1,a0
	moveq.l	#1,d1
	and.w	d0,d1
	bne.s	suitadcal
	
	sub.w	d1,d0
	add.w	d0,a0
	sub.w	#2,(a0)
	bra.s	finacal
suitadcal
	sub.w	d1,d0
	add.w	d0,a0
	add.w	#2,(a0)
	bra.s	finacal
changobj	
	add.w	#8,numobj
	lea.l	paobj,a0
	add.w	numobj,a0
	move.l	(a0)+,d0	
	bgt.s	schangobj	
	move.w	#-8,numobj
	bra.s	changobj
schangobj	
;	move.l	d0,ptchang2+2
	addq.l	#2,d0
;	move.l	d0,ptchang1+2
	move.l	(a0),ptchang3+2	
	clr.l	adag1
	clr.w	adag3
	clr.w	ang1
	clr.w	ang2
	clr.w	ang3

finacal	
	move.w	ang1,d0
	add.w	adag1,d0
	and.w	#$1ff,d0
	move.w	d0,ang1
	
	move.w	ang2,d0
	add.w	adag2,d0
	and.w	#$1ff,d0
	move.w	d0,ang2
	
	move.w	ang3,d0
	add.w	adag3,d0
	and.w	#$1ff,d0
	move.w	d0,ang3

	rts
adag1	dc.w	4
adag2	dc.w	4
adag3	dc.w	4
attvbl	dc.w	10
numobj	dc.w	0	

efecr	
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	SUBA.L	A1,A1
	SUBA.L	A2,A2
	SUBA.L	A3,A3
	SUBA.L	A4,A4
	SUBA.L	A5,A5
	SUBA.L	A6,A6
N	SET	0
	REPT	200
	MOVEM.L	D0-D7/A1-A6,N(A0)
	MOVEM.L	D0-D7/A1-A6,N+4*14(A0)
	MOVEM.L	D0-D7/A1-A4,N+4*14*2(A0)
N	SET	N+160
	ENDR
	RTS
	
;	MOVE	#199,D1
;JK
;N	SET	0
;	REPT	20
;	MOVE	D0,N(A0)
;N	SET	N+8
;	ENDR
;	LEA	160(A0),A0
;	DBF	D1,JK
;	RTS
	



	******************
	*matrix 3axe(zyx)*
	******************
calcpoint	
	lea.l	tsin,a0
	lea.l	128(a0),a1
ang1	equ	*+2
	move.w	#0,d0
ang2	equ	*+2
	move.w	#0,d1
ang3	equ	*+2	
	move.w	#0,d2

	move.w	(a0,d1.w),d3	-sinb
	neg	d3
	move.w	d3,modf3+2	

	move.w	d0,d3		d3=a-b
	sub.w	d1,d3		
	add.w	d0,d1		d1=a+b

	move.w	(a1,d1.w),d4	1/2(cos(a+b)+cos(a-b))
	add.w	(a1,d3.w),d4	cosacosb
	asr	d4
	move.w	d4,modf1+2		
	
	move.w	(a0,d1.w),d4	-1:2(sin(a+b)+sin((a-b))
	add.w	(a0,d3.w),d4	-sinacosb
	asr	d4
	neg	d4
	move.w	d4,modf2+2		

	sub.w	d0,d1
	move.w	d1,d3		
	sub.w	d2,d3		d3=b-c
	add.w	d1,d2		d2=b+c
	
	move.w	(a0,d2.w),d4	-1:2(sin(b+c)-sin((b-c))
	sub.w	(a0,d3.w),d4	-sinccosb
	asr	d4
	neg	d4
	move.w	d4,modf6+2		
	
	move.w	(a1,d2.w),d4	1/2(cos(b+c)+cos(b-c))
	add.w	(a1,d3.w),d4	cosbcosc
	asr	d4
	move.w	d4,modf9+2		

	sub.w	d1,d2
	move.w	d0,d3
	sub.w	d2,d3		d3=a-c
	add.w	d0,d2		d2=a+c
	
	move.w	(a0,d2.w),d4	1:2(sin(a+c)+sin(a-c))
	add.w	(a0,d3.w),d4	sinacosc
	asr	d4
	move.w	d4,addmodf1
	
	move.w	(a1,d2.w),d4	1:2(cos(a+c)+cos(a-c))
	add.w	(a1,d3.w),d4	cosacosc
	asr	d4
	move.w	d4,addmodf2
	
	move.w	(a1,d3.w),d4	1:2(cos(a-c)-cos(a+c))
	sub.w	(a1,d2.w),d4	sinasinc
	asr	d4
	move.w	d4,addmodf3
	
	move.w	(a0,d2.w),d4	1:2(sin(a+c)-sin(a-c))
	sub.w	(a0,d3.w),d4	cosasinc
	asr	d4
	move.w	d4,addmodf4

	sub.w	d0,d2
	move.w	#14,d3

	move.w	(a1,d0.w),d4	-cosasinbsinc+sinacosc
	muls.w	(a0,d1.w),d4
	muls.w	(a0,d2.w),d4
	asr.l	d3,d4
	neg	d4
addmodf1	equ	*+2
	add.w	#0,d4
	move.w	d4,modf4+2
	
	move.w	(a0,d0.w),d4	sinasinbsinc+cosacosc
	muls.w	(a0,d1.w),d4
	muls.w	(a0,d2.w),d4
	asr.l	d3,d4
addmodf2	equ	*+2
	add.w	#0,d4
	move.w	d4,modf5+2
	
	move.w	(a1,d0.w),d4	cosasinbcosc+sinasinc
	muls.w	(a0,d1.w),d4
	muls.w	(a1,d2.w),d4
	asr.l	d3,d4
addmodf3	equ	*+2
	add.w	#0,d4
	move.w	d4,modf7+2
	
	move.w	(a0,d0.w),d4	-sinasinbcosc+cosasinc
	muls.w	(a0,d1.w),d4
	muls.w	(a1,d2.w),d4
	asr.l	d3,d4
	neg	d4
addmodf4	equ	*+2
	add.w	#0,d4
	move.w	d4,modf8+2
	
	lea.l	t_points,a0
ptchang3	lea.l	tpoitesp1,a1
	lea.l	tz+255*2,a2

	move.w	#168,d5
	move.w	#100,d6
	
	move.w	(a1)+,d7

bmatrix
	move.w	(a1)+,a3
	move.w	(a1)+,a4
	move.w	(a1)+,d4

	move.w	a3,d0
	move.w	a4,d1
	move.w	d4,d2

modf1	muls	#1<<7,d0
modf2	muls	#0,d1
modf3	muls	#0,d2
	add.l	d1,d0
	add.l	d2,d0
	
	move.w	a3,d1
	move.w	a4,d2
	move.w	d4,d3

modf4	muls	#0,d1
modf5	muls	#1<<7,d2
modf6	muls	#0,d3
	add.l	d2,d1
	add.l	d3,d1
	
	move.w	a3,d2
	move.w	a4,d3
	
modf7	muls	#0,d2
modf8	muls	#0,d3
modf9	muls	#1<<7,d4
	add.l	d3,d2
	add.l	d4,d2
	
	asr.w	#7,d2
	add.w	d2,d2
	
	add.w	#20,d2
	
	muls	(a2,d2.w),d0
	muls	(a2,d2.w),d1
	
	swap	d0
	swap	d1
	
	add.w	d5,d0
	add.w	d6,d1
	
	move.w	d0,(a0)+
	move.w	d1,(a0)+
;	JSR	ROUTINE
	dbra.w	d7,bmatrix
	rts
**********
ON_CHANGE
	MOVE.L	#$00010001,D0
	MOVE.L	D0,$FFFF8240.W
	MOVE.L	D0,$FFFF8244.W
	MOVE.L	D0,$FFFF8248.W
	MOVE.L	D0,$FFFF824C.W
	MOVE.L	D0,$FFFF8250.W
	MOVE.L	D0,$FFFF8254.W
	MOVE.L	D0,$FFFF8258.W
	MOVE.L	D0,$FFFF825C.W
	
	LEA	MSG,A0
	LEA	8000(A0),A1
	LEA	8000(A1),A2
	LEA	8000(A2),A3
	MOVE.L	SCREEN3,A4
	MOVE	#200-1,D0
AFY2	MOVE	#20-1,D1
AFX2	MOVE	(A0)+,(A4)+
	MOVE	(A1)+,(A4)+
	MOVE	(A2)+,(A4)+
	MOVE	(A3)+,(A4)+
	DBF	D1,AFX2
	DBF	D0,AFY2
	MOVE.L	#VBL_AI,$70.W
	RTS
VBL_AI	ADDQ.L	#1,VBL_FLAG
	MOVEM.L	D0-A6,-(SP)
	LEA	FAD_DEB,A6
MODFDEND	EQU	*+2
	LEA	0(A6),A6
	CMPI	#-1,(A6)
	BEQ.S	.OUT2
	SUBQ	#1,BAZOUNGA
	BNE.S	.OUT
	MOVE	#5,BAZOUNGA
	ADDQ	#2,MODFDEND
	MOVE	(A6),$FFFF8242.W
	MOVE	(A6),$FFFF8246.W
	MOVE	(A6),$FFFF824A.W
	MOVE	(A6),$FFFF824E.W
	MOVE	(A6),$FFFF8252.W
	MOVE	(A6),$FFFF8256.W
	MOVE	(A6),$FFFF825A.W
	MOVE	(A6),$FFFF825E.W
.OUT	
	JSR	RT_ZIK
	JSR	TSTKEY
	MOVEM.L	(SP)+,D0-A6
	RTE
.OUT2
VBL2	LEA	FAD_MIL,A6
MODFDMIL	EQU	*+2
	LEA	0(A6),A6
	CMPI	#-1,(A6)
	BEQ.S	.OUT2
	SUBQ	#1,BAZOUNGA
	BNE.S	.OUT
	MOVE	#5,BAZOUNGA
	ADDQ	#2*3,MODFDMIL
	MOVE	(A6),$FFFF8242.W
	MOVE	(A6),$FFFF824A.W
	MOVE	(A6),$FFFF8252.W
	MOVE	(A6),$FFFF825A.W
	MOVE	2(A6),$FFFF8244.W
	MOVE	2(A6),$FFFF824C.W
	MOVE	4(A6),$FFFF8246.W
	MOVE	4(A6),$FFFF824E.W
	MOVE	2(A6),$FFFF8254.W
	MOVE	2(A6),$FFFF825C.W
	MOVE	4(A6),$FFFF8256.W
	MOVE	4(A6),$FFFF825E.W
.OUT	
	JSR	RT_ZIK
	JSR	TSTKEY
	MOVEM.L	(SP)+,D0-A6
	RTE
.OUT2	
VBL3	LEA	FAD_MIL2,A6
MODFDMIL2	EQU	*+2
	LEA	0(A6),A6
	CMPI	#-1,(A6)
	BEQ.S	.OUT2
	SUBQ	#1,BAZOUNGA
	BNE.S	.OUT
	MOVE	#5,BAZOUNGA
	ADDQ	#2*3,MODFDMIL2
	MOVE	(A6),$FFFF8244.W
	MOVE	(A6),$FFFF8246.W
	MOVE	2(A6),$FFFF8248.W
	MOVE	2(A6),$FFFF824A.W
	MOVE	4(A6),$FFFF824C.W
	MOVE	4(A6),$FFFF824E.W
	MOVE	(A6),$FFFF8254.W
	MOVE	(A6),$FFFF8256.W
	MOVE	2(A6),$FFFF8258.W
	MOVE	2(A6),$FFFF825A.W
	MOVE	4(A6),$FFFF825C.W
	MOVE	4(A6),$FFFF825E.W
.OUT	
	JSR	RT_ZIK
	JSR	TSTKEY
	MOVEM.L	(SP)+,D0-A6
	RTE
.OUT2
VBL4	LEA	FAD_MIL3,A6
MODFDMIL3	EQU	*+2
	LEA	0(A6),A6
	CMPI	#-1,(A6)
	BEQ.S	.OUT2
	SUBQ	#1,BAZOUNGA
	BNE.S	.OUT
	MOVE	#5,BAZOUNGA
	ADDQ	#2*3,MODFDMIL3
	MOVE	(A6),$FFFF8248.W
	MOVE	(A6),$FFFF824A.W
	MOVE	(A6),$FFFF824C.W
	MOVE	(A6),$FFFF824E.W
	MOVE	2(A6),$FFFF8250.W
	MOVE	2(A6),$FFFF8252.W
	MOVE	2(A6),$FFFF8254.W
	MOVE	2(A6),$FFFF8256.W
	MOVE	4(A6),$FFFF8258.W
	MOVE	4(A6),$FFFF825A.W
	MOVE	4(A6),$FFFF825C.W
	MOVE	4(A6),$FFFF825E.W
.OUT	
	JSR	RT_ZIK
	JSR	TSTKEY
	MOVEM.L	(SP)+,D0-A6
	RTE
.OUT2	
VBL5
	LEA	FAD_END,A6
MODFDEND2	EQU	*+2
	LEA	0(A6),A6
	CMPI	#-1,(A6)
	BEQ.S	.OUT2
	SUBQ	#1,BAZOUNGA
	BNE.S	.OUT
	MOVE	#5,BAZOUNGA
	ADDQ	#2,MODFDEND2
	MOVE	(A6),$FFFF8250.W
	MOVE	(A6),$FFFF8252.W
	MOVE	(A6),$FFFF8254.W
	MOVE	(A6),$FFFF8256.W
	MOVE	(A6),$FFFF8258.W
	MOVE	(A6),$FFFF825A.W
	MOVE	(A6),$FFFF825C.W
	MOVE	(A6),$FFFF825E.W
.OUT
	JSR	RT_ZIK
	JSR	TSTKEY
	MOVEM.L	(SP)+,D0-A6
	RTE
.OUT2	
	;LEA	TEXT4,A0
	;MOVE.L	A0,PT_TXT
	;MOVE.L	#1,MODIF
	;MOVE.L	#VBL_TRANSIT,$70.W
	JSR	RT_ZIK
	MOVE.L	#VBLEF1,$70.W
	JSR	TSTKEY
	MOVEM.L	(SP)+,D0-A6
	RTE
VBLEF1	MOVEM.L	D0-A6,-(SP)
	MOVE.L	SCREEN3,A0
	JSR	EFSPE
	JSR	RT_ZIK
	MOVEM.L	(SP)+,D0-A6
	ADDQ.L	#1,VBL_FLAG
	JSR	TSTKEY
	MOVE.L	#VBLEF2,$70.W
	RTE
VBLEF2	MOVEM.L	D0-A6,-(SP)
	MOVE.L	SCREEN3,A0
	ADDQ	#4,A0
	JSR	EFSPE
	JSR	RT_ZIK
	ADDQ.L	#1,VBL_FLAG
	LEA	TEXT4,A0
	MOVE.L	A0,PT_TXT
	MOVE.L	#1,MODIF
	JSR	TSTKEY
	MOVE.L	#VBL_TRANSIT,$70.W
	MOVEM.L	(SP)+,D0-A6
	RTE
EFSPE	MOVEQ	#0,D0
	MOVE	#200-1,D1
.F
N	SET	0
	REPT	20
	MOVE.L	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBF	D1,.F
	RTS
BAZOUNGA	DC	5
	*0001=42	1001=52
	*0010=44	1010=54
	*0011=46	1011=56
	*0100=48	1100=58
	*0101=4A	1101=5A
	*0110=4C	1110=5C
	*0111=4E	1111=5E
	*1000=50

JUMP	TST.L	MODIF
	BNE.S	AH
	RTS
AH
	CLR.L	MODIF
	MOVE.L	PT_TXT,A0
	LEA	202(A0),A0
	CMPI	#$9999,(A0)
	BNE.S	OI
	JMP	ON_CHANGE
OI	MOVE.L	A0,PT_TXT
	JSR	PREP_TEXT
	LEA	FAD,A0
KKK	TST.L	VBL_FLAG
	BEQ.S	KKK
	CLR.L	VBL_FLAG
	ADDQ.L	#1,CNT_FAD
	CMPI.L	#3,CNT_FAD
	BNE.S	KKK
	CMPI	#$9999,(A0)
	BEQ.S	OK_GO
	MOVE	(A0)+,$FFFF8244.W
	CLR.L	CNT_FAD
	BRA.S	KKK
OK_GO	JSR	EFFACE_PLAN3
	MOVE.L	#VBL_PART1,$70.W
	CLR.L	CNT_FAD
	CLR.L	VBL_FLAG
	RTS
CNT_FAD	DC.L	0
FAD	DC	$444,$333,$222,$111,$001,$9999
EFFACE_PLAN3	MOVE.L	SCREEN3,A0
	MOVE	#3999,D0
.EFF	CLR.L	(A0)
	ADDQ	#8,A0
	DBF	D0,.EFF
	RTS
CHG_RETURN	EQU	*+2
FIN	
	BRA.S	FIN
PT_TXT	DC.L	0
MODIF	DC.L	0
TIME	DC.L	0
VBL_FLAG	DC.L	0
RT_ZIK
MOD_ZIK	EQU	*+2
	JSR	$12345678
	RTS
VBL_DEBUT	
VBL_PART1	MOVEM.L	D0-A6,-(SP)
NO_MUS2	CLR.B	$FFFFFA1B.W
	JSR	RT_ZIK
	JSR	TSTKEY
	MOVE	#$001,$FFFF8240.W
	MOVE.L	#TB0,$120.W
;	CMPI.B	#1,$FFFFFC02.W
;	BEQ.S	SAUT
	JSR	AFF_ROUTY
SAUT	JSR	SWAPEC
	ADDQ.L	#1,TIME
	CMPI.L	#50*7,TIME
	BNE.S	NO_TIME
	MOVE.L	#VBL_TRANSIT,$70.W
	ADDQ.L	#1,MODIF
NO_TIME	MOVEM.L	(SP)+,D0-A6
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	IOIO
	ST	$FFFF8240.W
IOIO	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	ADDQ.L	#1,VBL_FLAG
	RTE
VBL_TRANSIT	MOVEM.L	D0-A6,-(SP)
	CLR.B	$FFFFFA1B.W
	JSR	RT_ZIK
	JSR	TSTKEY
	ADDQ.L	#1,VBL_FLAG
	JSR	SWAPEC
	CLR.L	TIME
	MOVEM.L	(SP)+,D0-A6
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE

;TIME	DC.L	0
RTE	RTE

TB0	
	CLR.B	$FFFFFA1B.W
	BCLR	#0,$FFFFFA0F.W
	RTE

TA0	RTE
TSTKEY	CMPI.B	#$39,$FFFFFC02.W
	BNE.S	.E
	MOVE.L	4.W,A0
	JMP	(A0)
.E	RTS

SWAPEC	;MOVE.L	SCREEN3,D0
	;MOVE.L	SCREEN4,SCREEN3
	;MOVE.L	D0,SCREEN4
	MOVE.B	SCREEN3+1,$FFFF8201.W
	MOVE.B	SCREEN3+2,$FFFF8203.W
	RTS
**
AFF_ROUTY
	MOVE.W	#$222,$FFFF8242.W
	MOVE.W	#$444,$FFFF8244.W
	MOVE.W	#$444,$FFFF8246.W
	MOVE.L	#$07770777,$FFFF8250.W
	MOVE.L	#$07770777,$FFFF8254.W
	MOVE.L	#$07770777,$FFFF8258.W
	MOVE.L	#$07770777,$FFFF825C.W
	BSR	AFF_TEXT
	RTS
AFF_TEXT	TST.B	FLAG_FIN_PRISE
	BNE	.AFF_RIEN

	SUBQ.W	#1,TEMPO_LET
	BGT	.PAS_ENCORE
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE.W	#TEMPO_INTER_LET,TEMPO_LET
	MOVE.L	PT_SRC,A0
	MOVE.W	(A0)+,D3	;X_DEP
	MOVE.W	(A0)+,D4	;Y_DEP
	CMPI.W	#$1234,(A0)
	BNE.S	.PAS_LOOP_SRC
	;MOVE.L	#VBL_ENDING,$70.W	
	LEA	CRB_SRC,A0
.PAS_LOOP_SRC	MOVE.L	A0,PT_SRC
	MOVE.L	PT_LET,A0
	MOVE.W	(A0)+,D5	;X_ARR
	MOVE.W	(A0)+,D6	;Y_ARR
	MOVE.W	(A0)+,D7	;No DU SPRITE ( Z=0 )
	CMPI.W	#$1234,(A0)
	BNE.S	.PAS_LOOP_LET
	ST	FLAG_FIN_PRISE
.PAS_LOOP_LET	MOVE.L	A0,PT_LET
	MOVE.W	D5,-(SP)
	MOVE.W	D6,-(SP)
	SUB.W	D3,D5	;DX
	SUB.W	D4,D6	;DY
	EXT.L	D5
	EXT.L	D6
	ASL.L	#8,D5
	ASL.L	#8,D6
	DIVS.W	#(150/SPEED),D5 ;PAS_X
	DIVS.W	#(150/SPEED),D6 ;PAS_Y
	EXT.L	D3
	EXT.L	D4
	ASL.L	#8,D3
	ASL.L	#8,D4
	LEA	BUF_TIRS,A0
.CONT_S	TST.W	14(A0)
	BGE.S	.PAS_BON
	MOVE.L	D3,(A0)+	;X_DEP
	MOVE.L	D4,(A0)+	;Y_DEP
	MOVE.W	#150,(A0)+	;Z_DEP
	MOVE.W	D5,(A0)+	;PAS_X
	MOVE.W	D6,(A0)+	;PAS_Y
	CLR.W	(A0)+	;FLAG=0-->TIR EN COURS !!!
	MOVE.W	D7,(A0)+	;No DE LA LETTRE ( Z=0 )
	MOVE.W	(SP)+,D6
	MOVE.W	(SP)+,D5
	MOVE.W	D5,(A0)+
	MOVE.W	D6,(A0)

	BRA.S	.FIN_S
.PAS_BON	LEA	22(A0),A0
	BRA.S	.CONT_S
.FIN_S
.PAS_ENCORE
.AFF_RIEN
	MOVE.L	BUF_ADR_CARRê,A0
.CONT_RECOP	CMPI.W	#$1234,(A0)
	BEQ.S	.STOP_RECOP
	MOVE.L	SCREEN3,A1
	LEA	-30*160(A1),A1
	ADDA.W	(A0)+,A1	;A1=ADRESSE ECRAN
	MOVE.W	(A0)+,D7
	MOVEQ	#0,D3
.RECOP_SCREEN	ADDQ.W	#4,A0
	MOVE.W	D3,(A1)
	MOVE.W	D3,8(A1)
	LEA	160(A1),A1
	DBRA	D7,.RECOP_SCREEN
	BRA.S	.CONT_RECOP

.STOP_RECOP	MOVE.L	BUF_ADR_CARRê,A5
	LEA	BUF_TIRS,A0
.RECOM_AFF	CMPA.L	#FIN_BUF_TIRS,A0
	BGE	.FIN_AFF
	TST.W	14(A0)
	BLT	.PAS_AFF
	SF	FLAG
	MOVE.L	(A0),D3	;X
	MOVE.L	4(A0),D4	;Y
	MOVE.W	8(A0),D5	;Z
	BGT.S	.Z_DIFF_0
	ST	FLAG
.Z_DIFF_0	ADD.W	D5,D5
	MOVE.W	D5,D6
	LEA	BUF_COEFF,A1
	MOVE.W	(A1,D5.W),D5	;COEFF 3D
	LEA	CORES_ZOOM,A1
	MOVE.W	(A1,D6.W),D6	;No DU ZOOM
	ASR.L	#8,D3	;X*2
	ASR.L	#8,D4	;Y*2
	ASL.L	#1,D3
	ASL.L	#1,D4
	MULS.W	D5,D3	;X PROJETê
	MULS.W	D5,D4	;Y PROJETê
	SWAP	D3
	SWAP	D4
	ADDI.W	#160,D3
	ADDI.W	#100,D4
	MOVE.W	16(A0),D7	;No DE LA LETTRE
	MULU.W	#39,D6
	ADD.W	D7,D6	;D6=No DU SPRITE DANS LA BANQUE
	LEA	FONTES_I,A1
	MULU.W	#6,D6
	ADDA.W	D6,A1
	LEA	FONTES_S,A2
	ADDA.W	(A1)+,A2	;A2=ADRESSE DE LA FONTE
	MOVE.W	2(A1),D7	;D7=HAUTEUR
	MOVE.W	D3,D5
	ANDI.W	#15,D3	;D3=DECALAGE
	ANDI.W	#$FFF0,D5
	LSR	#1,D5
	MOVE.L	SCREEN3,A3
	LEA	-160*30(A3),A3
	MULU.W	#160,D4
	ADD.W	D5,D4
	ADDA.W	D4,A3	;A3=ADRESSE DESTINATION ECRAN
	
	TST.B	FLAG
	BEQ.S	.OK_Z
	ADDQ.W	#2,A3
	BRA.S	.AFF_DEC

.OK_Z	MOVE.L	A3,A4
	MOVE.W	D4,(A5)+
	MOVE.W	D7,(A5)+
	MOVE.W	D7,-(SP)
.RECOP_BUF	MOVE.W	(A4),(A5)+	;ON SAUVE LE CARRê
	MOVE.W	8(A4),(A5)+
	LEA	160(A4),A4
	DBRA	D7,.RECOP_BUF
	MOVE.W	(SP)+,D7

.AFF_DEC	MOVEQ	#0,D4
	MOVE.W	(A2)+,D4
	ROR.L	D3,D4
	OR.W	D4,(A3)
	SWAP	D4
	OR.W	D4,8(A3)
	LEA	160(A3),A3
	DBRA	D7,.AFF_DEC
.PAS_AFF	LEA	22(A0),A0
	BRA	.RECOM_AFF

.FIN_AFF	MOVE.W	#$1234,(A5)

	MOVEQ	#0,D6
	MOVEQ	#0,D7
	LEA	BUF_TIRS,A0
.RECOM_INC	CMPA.L	#FIN_BUF_TIRS,A0
	BGE.S	.FIN_INC

	TST.W	14(A0)
	BLT.S	.PAS_INC
	BEQ.S	.ON_INC
	ADDQ.B	#1,14(A0)	;L'OCTET DE POIDS FORT DU FLAG EST LE NB DE VBL DURANT LESQUELLES LA LETTRES VA ETRE FIXE
	CMPI.B	#3,14(A0)
	BLT.S	.PAS_INC
	MOVE.W	#-1,14(A0)
	BRA.S	.PAS_INC
.ON_INC	MOVE.L	(A0),D3	;X
	MOVE.L	4(A0),D4	;Y
	MOVE.W	8(A0),D5	;Z
	MOVE.W	10(A0),D6	;PAS_X
	MOVE.W	12(A0),D7	;PAS_Y
	SUBQ.W	#SPEED,D5	;NEW Z
	BGT.S	.OK
	;LA LETTRE EST ARRIVêê !!
	MOVE.W	#1,14(A0)	;FLAG=1
	MOVEQ	#0,D5
	MOVEQ	#0,D6	;PAS_X=0
	MOVEQ	#0,D7	;PAS_Y=0
	MOVEQ	#0,D3
	MOVE.W	18(A0),D3
	ASL.L	#8,D3
	MOVEQ	#0,D4
	MOVE.W	20(A0),D4
	ASL.L	#8,D4
.OK	EXT.L	D6
	EXT.L	D7
	ADD.L	D6,D3	;NEW X
	ADD.L	D7,D4	;NEW Y
	MOVE.L	D3,(A0)
	MOVE.L	D4,4(A0)
	MOVE.W	D5,8(A0)
.PAS_INC	LEA	22(A0),A0
	BRA.S	.RECOM_INC
.FIN_INC
	LEA	BUF_ADR_CARRê,A0
	MOVE.L	(A0)+,D3
	MOVE.L	(A0),-(A0)
	MOVE.L	D3,4(A0)
	RTS
PREP_TEXT	;LEA	TEXT(PC),A0
	LEA	CORES_LET,A1
	LEA	BUF_LET,A2
	MOVE.W	#-60,D1	;D1=Y
	MOVEQ	#9,D6
.A2	MOVE.W	#-160,D2	;D2=X
	MOVEQ	#19,D7
.A	MOVEQ	#0,D0
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVE.B	(A0)+,D0
	SUBI.B	#32,D0
	BEQ.S	.PASSE
	CMPI.B	#"I"-32,D0
	BNE.S	.L1
	MOVEQ	#5,D4
.L1	CMPI.B	#"!"-32,D0
	BNE.S	.L2
	MOVEQ	#5,D4
.L2	CMPI.B	#"-"-32,D0
	BNE.S	.L3
	MOVEQ	#6,D5
.L3	ADD.W	D0,D0
	MOVE.W	(A1,D0.W),D0	;D0=No DU SPRITE DANS LA BANQUE
	MOVE.W	D2,(A2)
	ADD.W	D4,(A2)+
	MOVE.W	D1,(A2)
	ADD.W	D5,(A2)+
	MOVE.W	D0,(A2)+
.PASSE	ADDI.W	#16,D2
	DBRA	D7,.A
	ADDI.W	#16,D1
	DBRA	D6,.A2
	MOVE.W	#1,TEMPO_LET
	MOVE.L	#CRB_SRC,PT_SRC
	MOVE.L	#BUF_LET,PT_LET
	MOVE.W	#$1234,(A2)
	LEA	BUF_TIRS,A0
	MOVEQ	#29,D7
.A3	MOVE.W	#-1,14(A0)
	LEA	22(A0),A0
	DBRA	D7,.A3
	SF	FLAG
	SF	FLAG_FIN_PRISE
	MOVE.L	#BUF_CARRê,BUF_ADR_CARRê
	MOVE.L	#BUF_CARRê+(68*30),BUF_ADR_CARRê+4
	MOVE.W	#$1234,BUF_CARRê
	MOVE.W	#$1234,BUF_CARRê+(68*30)
	RTS

	DATA
	
IMG	INCBIN	DATAS.GFX
PNT_DATA	DC.L	0
DATAS_FACES
	DC.L	DATAS_IMG
	DC.L	DATAS_IMG
	DC.L	DATAS_IMG
	DC.L	DATAS_IMG
	DC.L	DATAS_IMG
	DC.L	DATAS_IMG

;	DC.L	DATAS_IMG
;	DC.L	DATAS_IMG+64
;	DC.L	DATAS_IMG+64*2
;	DC.L	DATAS_IMG+64*3
;	DC.L	DATAS_IMG+64*4
;	DC.L	DATAS_IMG+320*64

	incbin	tsin7.aid
tsin	incbin	tsin7.aid
	incbin	tsin7.aid

tz	incbin	tzoom.aid

paobj	dc.l	tpoly1,tpoitesp1,-1

tpoly1	dc.w	6-1
	dc.l	face11+2,face12+2,face13+2,face14+2,face15+2,face16+2 	

face11	dc.w	0,$5488,4,0,4,8,12
face12	dc.w	0,$5888,4,16,0,12,28
face13	dc.w	0,$5c88,4,12,8,24,28
face14	dc.w	0,$5888,4,4,20,24,8
face15	dc.w	0,$5c88,4,16,20,4,0 
face16	dc.w	0,$4e71,4,28,24,20,16

tpoitesp1
	dc.w	8-1
	dc.w	-40,-40,-40		1
	dc.w	-40,40,-40		2
	dc.w	40,40,-40		3	
	dc.w	40,-40,-40		4
	dc.w	-40,-40,40		5
	dc.w	-40,40,40		6
	dc.w	40,40,40		7
	dc.w	40,-40,40		8



	DCB	200,-320
TABLO_Y
N	SET	0
	REPT	200
	DC	N*160
N	SET	N+1
	ENDR

	DCB	200,-320
	DCB	200,0
TABLO_X

N	SET	0
	REPT	20
	DC	%1000000000000000,N
	DC	%0100000000000000,N
	DC	%0010000000000000,N
	DC	%0001000000000000,N
	DC	%0000100000000000,N
	DC	%0000010000000000,N
	DC	%0000001000000000,N
	DC	%0000000100000000,N
	DC	%0000000010000000,N
	DC	%0000000001000000,N
	DC	%0000000000100000,N
	DC	%0000000000010000,N
	DC	%0000000000001000,N
	DC	%0000000000000100,N
	DC	%0000000000000010,N
	DC	%0000000000000001,N
N	SET	N+8
	ENDR
	DCB	200,0

TABLO_X2
N	SET	0
	REPT	20
	DC	%1100000000000000,N
	DC	%0110000000000000,N
	DC	%0011000000000000,N
	DC	%0001100000000000,N
	DC	%0000110000000000,N
	DC	%0000011000000000,N
	DC	%0000001100000000,N
	DC	%0000000110000000,N
	DC	%0000000011000000,N
	DC	%0000000001100000,N
	DC	%0000000000110000,N
	DC	%0000000000011000,N
	DC	%0000000000001100,N
	DC	%0000000000000110,N
	DC	%0000000000000011,N
	DC	%0000000000000001,N
N	SET	N+8
	ENDR
	DCB	200,0
RUBBER
	INCLUDE	RUBBER.DAT
***DATAS AI
	
FAD_DEB	
	DC	$001,$112,$223,$334,$445,$556,$667,$777
	DCB	60,$777
	DC	-1
FAD_MIL
	DC	$777,$001,$777
	DC	$667,$002,$777
	DC	$556,$122,$777
	DC	$445,$233,$777
	DC	$334,$344,$777
	DC	$223,$455,$777
	DC	$112,$566,$777
	DC	$001,$677,$777
	DC	$001,$777,$777
	REPT	60
	DC	$001,$777,$777
	ENDR
	DC	-1
FAD_MIL2
	DC	$777,$001,$777
	DC	$766,$101,$777
	DC	$655,$212,$777
	DC	$543,$323,$777
	DC	$432,$434,$777
	DC	$321,$545,$777
	DC	$210,$656,$777
	DC	$100,$767,$777
	DC	$001,$777,$777
	REPT	60
	DC	$001,$777,$777
	ENDR
	DC	-1
FAD_MIL3
	DC	$777,$001,$777
	DC	$676,$002,$777
	DC	$565,$013,$777
	DC	$454,$024,$777
	DC	$343,$135,$777
	DC	$232,$246,$777
	DC	$121,$357,$777
	DC	$010,$567,$777
	DC	$001,$777,$777
	REPT	60
	DC	$001,$777,$777
	ENDR
	DC	-1
FAD_END
	DC	$777,$667,$556,$445,$334,$223,$112,$001
	DC	-1
	
MSG	INCBIN	MSG7.DAT
	INCBIN	ZAP31.GFX
	INCBIN	ZAP44.GFX
	INCBIN	MSG4.DAT

***DATAS ZOOMTEXT
	;DC.B	"                    "
TEXT1	DC.B	"--------------------"	1
	DC.B	"THE GIRL  AT THE END"	2
	DC.B	"OF DISK ONE IS TAKEN"	3
	DC.B	" FROM THE VERY-VERY "	4
	DC.B	" VERY-VERY-VERY-VERY"	5
	DC.B	" AND EVEN MORE VERY "	6
	DC.B	"    - - COOL - -    "	7
	DC.B	" --VIDEO GIRL AI--  "	8
	DC.B	"                    "	9
	DC.B	"--------------------"	10
	DC.W	$1234

TEXT2	DC.B	"       WE HAVE      "
	DC.B	" -THIRTY NINE MEGAS "
	DC.B	"  OF IMAGES FOR     "
	DC.B	"  VIDEO GIRL AI     "
	DC.B	" -ELEVEN FOR        "
	DC.B	"  CITY HUNTER       "
	DC.B	" -TWENTY FOUR       "
	DC.B	"FOR -M A D O K A-!!!"
	DC.B	"MOST OF THEM ARE IN "
	DC.B	"     TRUE COLOR!    "
	DC.W	$1234
TEXT3
	DC.B	"                    "
	DC.B	"                    "
	DC.B	"                    "
	DC.B	"                    "
	DC.B	"NOT ON ST OF COURSE!"
	DC.B	"                    "
	DC.B	"                    "
	DC.B	"                    "
	DC.B	"                    "
	DC.B	"                    "
	DC.W	$1234
	DC	$9999
TEXT4
	DC.B	" AND NOW HERE COMES "
	DC.B	" SOMETHING YOU HAVE "
	DC.B	" NEVER SEEN ON ST!  "
	DC.B	"                    "
	DC.B	"  AND I DOUBT EVEN  "
	DC.B	"ON AN OTHER MACHINE!"
	DC.B	"                    "
	DC.B	"                    "
	DC.B	"   IT IS SO UGLY!   "
	DC.B	"                    "
	DC.W	$1234
TEXT4B
	DC.B	" AND NOW HERE COMES "
	DC.B	" SOMETHING YOU HAVE "
	DC.B	" NEVER SEEN ON ST!  "
	DC.B	"                    "
	DC.B	"  AND I DOUBT EVEN  "
	DC.B	"ON AN OTHER MACHINE!"
	DC.B	"                    "
	DC.B	"                    "
	DC.B	"   IT IS SO UGLY!   "
	DC.B	"                    "
	DC.W	$1234
TEXT5
	DC.B	"   PREPARE FOR A    "
	DC.B	"      -ZOOMED-      "
	DC.B	"      -MAPPED-      "
	DC.B	"  -FOUR BITPLANES-  "
	DC.B	"  -SOURCE LIGHTED-  "
	DC.B	"-THREE AXIS ROTATED-"
	DC.B	"      -RUBBER-      "
	DC.B	"                    "
	DC.B	" X-TRA BIG CUBE ON  "
	DC.B	"THE WHOLE SCREEN !!!"
	DC.W	$1234
TEXT6
	DC.B	"--------------------"	10
	DC.B	"--------------------"	10
	DC.B	"--------------------"	10
	DC.B	"--------------------"	10
	DC.B	"--------------------"	10
	DC.B	"--------------------"	10
	DC.B	"--------------------"	10
	DC.B	"--------------------"	10
	DC.B	"--------------------"	10
	DC.B	"--------------------"	10
	DC.W	$1234

	DC	$9999
CORES_ZOOM	INCBIN	CORE2ZOM.3D
BUF_COEFF	INCBIN	COEFF.3D

CORES_LET	DC.W	0,11,0,0,0,0,0,0,0,0,0,0,0
	DC.W	12,0,0,0,1,2,3,4,5,6,7,8,9
	DC.W	0,0,0,0,0,10,0
	DC.W	13,14,15,16,17,18,19,20,21,22
	DC.W	23,24,25,26,27,28,29,30,31,32
	DC.W	33,34,35,36,37,38,39

FONTES_I	INCBIN	BANK_FNT.INF
FONTES_S	INCBIN	BANK_FNT.SPR

CRB_SRC	
	DC	0,90
	DC.W	-80,-25
	DC.W	80,-25
	DC.W	80,50
	DC.W	-80,50
	INCBIN	SRC_TIRS.SRC
	DC.W	$1234

TABLE_160
N	SET	0
	REPT	200
	DC.W	N*160
N	SET	N+1
	ENDR
ZIK	INCBIN	MUSIX.MUS

	BSS
DEB_BSS
	DS.L	256
NEWPILE	DS.L	1

SCREEN1	DS.L	1
SCREEN2	DS.L	1
SCREEN3	DS.L	1
TABLE_256	DS.L	256+2
BUF_COD	DS.B	10000
t_points	ds.w	20*2	
face_en_cours	ds.l	4
	DS.B	256
BUFECR	DS.B	32000
	DS.B	256
BUFECR2	DS.B	32000
	DS.B	256
BUFECR3	DS.B	32000

DATAS_IMG	DS.B	nb_pts*nb_pts*7

DATAS_X	DS	nb_pts
DATAS_Y	DS	nb_pts
DATAS_X2	DS	nb_pts
DATAS_Y2	DS	nb_pts
TRAVAIL_X	DS	nb_pts
TRAVAIL_Y	DS	nb_pts
BUF_DAT
	DS.B	128*50*40
BUF_END
*** BSS ZOOMTEXT
TEMPO_LET	DS.W	1
PT_SRC	DS.L	1
PT_LET	DS.L	1
FLAG	DS.B	1
FLAG_FIN_PRISE	DS.B	1
;FLAG_TRANSFORM	DS.B	1
FLAG_FINI	DS.B	1
PASSE	DS.W	1
BUF_ADR_CARRê	DS.L	2
BUF_CARRê	DS.W	((1+1)+(2*16))*30*2
	;X(.L),Y(.L),Z(.W),PAS_X(.W),PAS_Y(.W),FLAG(.W),No(.W),X_DEF(.W),Y_DEF(.W)
BUF_TIRS	DS.W	(2+2+1+1+1+1+1+1+1)*30
FIN_BUF_TIRS
BUF_LET	DS.W	(3*200)+1
	;X(.L),Y(.L),0(.W),PAS_X(.W),0(.W),PAS_Y(.W),X_DEF(.W),Y_DEF(.W),OFFSET(.W),INC(.W)
END_BSS