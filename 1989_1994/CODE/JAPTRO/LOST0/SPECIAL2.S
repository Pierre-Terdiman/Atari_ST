;TAB=16 !!! (TRES IMPORTANT POUR LES COMMENTAIRES !!)
;REPRISE DE CETTE SOURCE: LE 9-3-94. 

LARG = 230
LIMITE = 448
NB_BALLS = 27
RAYON = 17
LIMITE_CARRE = 130*2+1

BORD_GAUCHE	MACRO
	MOVE.W	A7,(A7)	;$8260
	MOVE.B	#0,(A7)
	ENDM

BORD_DROIT	MACRO
	MOVE.B	#0,(A6)	;$820A
	MOVE.W	A7,(A6)
	ENDM

STAB	MACRO
	MOVE.W	A7,(A7)	;$8260
	MOVE.B	#0,(A7)
	ENDM

NP	MACRO
	DCB.W	\1,$4E71
	ENDM

;   BOULOT-BOULOT :    CODAGE DES ROND (FAIT)
;                      DECALAGES DES RONDS (FAIT)
;                      CODAGE DU CODE POUR L'AFFICHAGE DES POINTS (FAIT)
;	       DECALAGES DES POINTS (FAIT)
;                      INTEGRATION DES RONDS (FAIT)
;	       INTEGRATION DES POINTS (FAIT)

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	MOVEQ	#7,D7
	MOVE.W	#$2100,SR
.LOW	STOP	#$2100
	CLR.B	$FFFF8260.W
	DBRA	D7,.LOW

	BSR	PREP_ECR

	MOVEQ	#1,D0
	JSR	ZIK

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W

	LEA	IMG_0,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D7
	BSR	PUT_IMG_SCREEN
	TST.L	$5A0.W
	BEQ.S	.STF
	MOVE.L	#PALETTE2,MODPAL1+2
	MOVE.L	#PALETTE1,MODPAL2+2
.STF
	CLR.W	FLAG_FINI
	MOVE.W	#5,CNTFAD

	MOVE.L	SCREEN1,A1
	LEA	BUFPAK,A0
	LEA	56*480(A0),A2
	LEA	56*480(A2),A3
	LEA	56*480(A3),A6
	MOVE	#200-1,D1
.G2	MOVE.L	A1,A5
	MOVE	#20-1,D0
.H2Z	MOVE	(A0)+,(A1)+
	MOVE	(A2)+,(A1)+
	MOVE	(A3)+,(A1)+
	MOVE	(A6)+,(A1)+
	DBF	D0,.H2Z
	LEA	16(A0),A0
	LEA	16(A2),A2
	LEA	16(A3),A3
	LEA	16(A6),A6
	LEA	160(A5),A1
	DBF	D1,.G2

	MOVE.L	#VBLR_ZIK,$70.W
	MOVE.W	#$2300,SR

	MOVE.L	SCREEN2,A1
	MOVE.W	#40*200-1,D7
.EFF_2ND	CLR.L	(A1)+
	DBRA	D7,.EFF_2ND
	MOVE.L	#MOUV,PT_MOUV
	BSR	PREP_BALLS
	BSR	PREP_DOTS
	LEA	ROTATE_CUBE_1,A3
	BSR	PREP_CUBE_OBJ
	BSR	PREP_CUBE
	BSR	PREP_POINTS
	BSR	MK_ROUT_EFF

	LEA	IMG_1,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D7
	JSR	PUT_IMG_SCREEN

	CLR.W	NB_VBL
	MOVE.W	#1,FLAG_FINI
	MOVE.W	#5,CNTFAD
.ON_GLANDE	CMPI.W	#50,NB_VBL
	BNE.S	.ON_GLANDE
.ON_SE_BARRE
	MOVE.L	SCREEN1,A1
	LEA	1600+16(A1),A1
	LEA	BUFPAK,A0
	LEA	56*480(A0),A2
	LEA	56*480(A2),A3
	LEA	56*480(A3),A6
	MOVE	#480-1,D1
G4	MOVE.L	A1,A5
	MOVE	#28-1,D0
H4Z	MOVE	(A0)+,(A1)+
	MOVE	(A2)+,(A1)+
	MOVE	(A3)+,(A1)+
	MOVE	(A6)+,(A1)+
	DBF	D0,H4Z
	MOVE.L	#-1,(A1)+
	MOVE.W	#-1,(A1)
	LEA	230(A5),A1
	DBF	D1,G4

	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	LEA	1600(A0),A0
	LEA	1600(A1),A1
	MOVE.W	#115*480-1,D7
.COPY	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.COPY

	MOVE.L	#CRB_IMG,PT_CRB_HARD
	CLR.L	Y_VALUE
	MOVE.L	#DATA_HS,POIT
	MOVE.W	#50*30,COMPTEUR	;30 SECONDES

	LEA	ADR_POS,A0
	LEA	BUF_POS,A1
	LEA	32+32*NB_BALLS(A1),A2
	MOVE.L	A1,(A0)+
	MOVE.L	A2,(A0)

	MOVE.W	#$2700,SR

	BSR	HELP_CALC

	CLR.W	FLAG_FINI
	MOVE.W	#5,CNTFAD
	MOVE	#-32,MOD2+4
	MOVE	#-32,MOD2B+2
	MOVE	#-32,MOD1+4
	MOVE	#-32,MOD1B+2
	CLR.W	MODDEG1+2
	CLR.W	MODDEG2+2

	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

J	BRA.S	J

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W

	MOVE.L	#VBLR_ZIK,$70.W
	MOVE.W	#$2300,SR

	MOVE.L	#MOUV2,PT_MOUV
	MOVE.L	#MOUV2,ADR_CRB
	MOVE.L	#J2,ADR_POZ_NOP

	LEA	IMG_2,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D7
	JSR	PUT_IMG_SCREEN

	CLR.W	NB_VBL
	MOVE.W	#1,FLAG_FINI
	MOVE.W	#5,CNTFAD
.ON_GLANDE	CMPI.W	#50,NB_VBL
	BNE.S	.ON_GLANDE
.ON_SE_BARRE
	MOVE.L	SCREEN1,A1
	LEA	1600(A1),A1
	LEA	BUFPAK,A0
	LEA	56*480(A0),A2
	LEA	56*480(A2),A3
	LEA	56*480(A3),A6
	MOVE	#480-1,D1
G5	MOVE.L	A1,A5
	MOVE	#28-1,D0
H5Z	MOVE	(A0)+,(A1)+
	MOVE	(A2)+,(A1)+
	MOVE	(A3)+,(A1)+
	MOVE	(A6)+,(A1)+
	DBF	D0,H5Z
	MOVE.L	#-1,(A1)+
	MOVE.W	#-1,(A1)
	LEA	230(A5),A1
	DBF	D1,G5

	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	LEA	1600(A0),A0
	LEA	1600(A1),A1
	MOVE.W	#115*480-1,D7
.COPY	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.COPY

	MOVE.L	#CRB_IMG,PT_CRB_HARD
	CLR.L	Y_VALUE
	MOVE.L	#DATA_HS,POIT
	MOVE.W	#50*30,COMPTEUR	;30 SECONDES

	LEA	ADR_POS,A0
	LEA	BUF_POS,A1
	LEA	32+32*NB_BALLS(A1),A2
	MOVE.L	A1,(A0)+
	MOVE.L	A2,(A0)

	CLR.L	TAB_Y
	CLR.L	TAB_Y+4
	CLR.L	TAB_Y+8
	CLR.L	Y_VALUE

	MOVE.W	#$2700,SR

	BSR	HELP_CALC

	CLR.W	FLAG_FINI
	MOVE.W	#5,CNTFAD
	MOVE	#-32,MOD2+4
	MOVE	#-32,MOD2B+2
	MOVE	#-32,MOD1+4
	MOVE	#-32,MOD1B+2
	CLR.W	MODDEG1+2
	CLR.W	MODDEG2+2

	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

J2	BRA.S	J2

	MOVE.L	#VBLR_ZIK,$70.W
	MOVE.W	#$2300,SR

	MOVE.L	#MOUV,PT_MOUV
	LEA	ROTATE_CUBE_2,A3
	BSR	PREP_SPHERE_OBJ
	BSR	PREP_CUBE

	LEA	IMG_3,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D7
	JSR	PUT_IMG_SCREEN

	CLR.W	NB_VBL
	MOVE.W	#1,FLAG_FINI
	MOVE.W	#5,CNTFAD
.ON_GLANDE	CMPI.W	#50,NB_VBL
	BNE.S	.ON_GLANDE
.ON_SE_BARRE
	MOVE.L	SCREEN1,A1
	LEA	1600+16(A1),A1
	LEA	BUFPAK,A0
	LEA	56*480(A0),A2
	LEA	56*480(A2),A3
	LEA	56*480(A3),A6
	MOVE	#480-1,D1
.G4	MOVE.L	A1,A5
	MOVE	#28-1,D0
.H4Z	MOVE	(A0)+,(A1)+
	MOVE	(A2)+,(A1)+
	MOVE	(A3)+,(A1)+
	MOVE	(A6)+,(A1)+
	DBF	D0,.H4Z
	MOVE.L	#-1,(A1)+
	MOVE.W	#-1,(A1)
	LEA	230(A5),A1
	DBF	D1,.G4

	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	LEA	1600(A0),A0
	LEA	1600(A1),A1
	MOVE.W	#115*480-1,D7
.COPY	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.COPY

	MOVE.L	#CRB_IMG,PT_CRB_HARD
	CLR.L	Y_VALUE
	MOVE.L	#DATA_HS,POIT
	MOVE.W	#50*30,COMPTEUR	;30 SECONDES

	LEA	ADR_POS,A0
	LEA	BUF_POS,A1
	LEA	32+32*NB_BALLS(A1),A2
	MOVE.L	A1,(A0)+
	MOVE.L	A2,(A0)

	MOVE.W	#$2700,SR

	CLR.L	TAB_Y
	CLR.L	TAB_Y+4
	CLR.L	TAB_Y+8
	CLR.L	Y_VALUE

	BSR	HELP_CALC

	CLR.W	FLAG_FINI
	MOVE.W	#5,CNTFAD
	MOVE	#-32,MOD2+4
	MOVE	#-32,MOD2B+2
	MOVE	#-32,MOD1+4
	MOVE	#-32,MOD1B+2
	CLR.W	MODDEG1+2
	CLR.W	MODDEG2+2

	MOVE.L	#MOUV2,ADR_CRB
	MOVE.L	#J3,ADR_POZ_NOP

	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

J3	BRA.S	J3

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W

	MOVE.L	#VBLR_ZIK,$70.W
	MOVE.W	#$2300,SR

	MOVE.L	#MOUV2,PT_MOUV
	MOVE.L	#MOUV2,ADR_CRB
	MOVE.L	#J4,ADR_POZ_NOP

	LEA	IMG_4,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D7
	JSR	PUT_IMG_SCREEN

	CLR.W	NB_VBL
	MOVE.W	#1,FLAG_FINI
	MOVE.W	#5,CNTFAD
.ON_GLANDE	CMPI.W	#50,NB_VBL
	BNE.S	.ON_GLANDE
.ON_SE_BARRE
	MOVE.L	SCREEN1,A1
	LEA	1600(A1),A1
	LEA	BUFPAK,A0
	LEA	56*480(A0),A2
	LEA	56*480(A2),A3
	LEA	56*480(A3),A6
	MOVE	#480-1,D1
.G5	MOVE.L	A1,A5
	MOVE	#28-1,D0
.H5Z	MOVE	(A0)+,(A1)+
	MOVE	(A2)+,(A1)+
	MOVE	(A3)+,(A1)+
	MOVE	(A6)+,(A1)+
	DBF	D0,.H5Z
	MOVE.L	#-1,(A1)+
	MOVE.W	#-1,(A1)
	LEA	230(A5),A1
	DBF	D1,.G5

	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	LEA	1600(A0),A0
	LEA	1600(A1),A1
	MOVE.W	#115*480-1,D7
.COPY	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.COPY

	MOVE.L	#CRB_IMG,PT_CRB_HARD
	CLR.L	Y_VALUE
	MOVE.L	#DATA_HS,POIT
	MOVE.W	#50*30,COMPTEUR	;30 SECONDES

	LEA	ADR_POS,A0
	LEA	BUF_POS,A1
	LEA	32+32*NB_BALLS(A1),A2
	MOVE.L	A1,(A0)+
	MOVE.L	A2,(A0)

	MOVE.W	#$2700,SR

	CLR.L	TAB_Y
	CLR.L	TAB_Y+4
	CLR.L	TAB_Y+8
	CLR.L	Y_VALUE

	BSR	HELP_CALC

	CLR.W	FLAG_FINI
	MOVE.W	#5,CNTFAD
	MOVE	#-32,MOD2+4
	MOVE	#-32,MOD2B+2
	MOVE	#-32,MOD1+4
	MOVE	#-32,MOD1B+2
	CLR.W	MODDEG1+2
	CLR.W	MODDEG2+2

	MOVE.L	#INTER_RTE,$68.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

J4	BRA.S	J4
	
	MOVE.L	$4.W,A0
	JMP	(A0)

VBLR_ZIK	ADDQ.W	#1,NB_VBL
	MOVEM.L	D0-A6,-(SP)
	JSR	ZIK+8
	BSR	MK_FADE
	MOVEM.L	(A0),D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	MOVEM.L	(SP)+,D0-A6
	RTE

RIEN	RTS

ADR_REGS	DC.L	REG_DEC_1
	DC.L	REG_DEC_1_BIS
	DC.L	REG_DEC_2
	DC.L	REG_DEC_2_BIS
	DC.L	REG_DEC_3
	DC.L	REG_DEC_3_BIS
	DC.L	REG_DEC_4
	DC.L	REG_DEC_4_BIS

	DC.L	REG_DEC_11
	DC.L	REG_DEC_11_BIS
	DC.L	REG_DEC_12
	DC.L	REG_DEC_12_BIS
	DC.L	REG_DEC_13
	DC.L	REG_DEC_13_BIS
	DC.L	REG_DEC_14
	DC.L	REG_DEC_14_BIS

REG_DEC_1	DC.W	$80,$40,$20,$10,8,4,2,1 ;VALEURS DE Dn
	DC.W	0,0,1,1	        ;INCREMENTS DES An
REG_DEC_1_BIS	DC.W	$20,$10,8,4,2,1,$80,$40
	DC.W	0,0,1,1
REG_DEC_2	DC.W	8,4,2,1,$80,$40,$20,$10
	DC.W	0,1,1,8
REG_DEC_2_BIS	DC.W	2,1,$80,$40,$20,$10,8,4
	DC.W	0,1,1,8
REG_DEC_3	DC.W	$80,$40,$20,$10,8,4,2,1
	DC.W	1,1,8,8
REG_DEC_3_BIS	DC.W	$20,$10,8,4,2,1,$80,$40
	DC.W	1,1,8,8
REG_DEC_4	DC.W	8,4,2,1,$80,$40,$20,$10
	DC.W	1,8,8,9
REG_DEC_4_BIS	DC.W	2,1,$80,$40,$20,$10,8,4
	DC.W	1,8,8,9

REG_DEC_11	DC.W	0,0,0,0,0,0,0,0 ;VALEURS DE Dn
	DC.W	0,0,1,1	        ;INCREMENTS DES An
REG_DEC_11_BIS	DC.W	0,0,0,0,0,0,0,0 ;VALEURS DE Dn
	DC.W	0,0,1,1	        ;INCREMENTS DES An
REG_DEC_12	DC.W	0,0,0,0,0,0,0,0 ;VALEURS DE Dn
	DC.W	0,1,1,8
REG_DEC_12_BIS	DC.W	0,0,0,0,0,0,0,0 ;VALEURS DE Dn
	DC.W	0,1,1,8
REG_DEC_13	DC.W	0,0,0,0,0,0,0,0 ;VALEURS DE Dn
	DC.W	1,1,8,8
REG_DEC_13_BIS	DC.W	0,0,0,0,0,0,0,0 ;VALEURS DE Dn
	DC.W	1,1,8,8
REG_DEC_14	DC.W	0,0,0,0,0,0,0,0 ;VALEURS DE Dn
	DC.W	1,8,8,9
REG_DEC_14_BIS	DC.W	0,0,0,0,0,0,0,0 ;VALEURS DE Dn
	DC.W	1,8,8,9

ADR_POS	DS.L	2
	;SCHEMA D'UNE STRUCTURE <POSITION> :
	;          - 1.L : ADRESSE ECRAN
	;          - 1.L : ADRESSE DU CODE POUR LE ROND
	;          - 1.L : ADRESSE ECRAN
	;          - 1.L : ADRESSE POUR LES REG Dn
	;          - 5.W : OFFSET POUR LES An
	;          - 1.L : ADRESSE DU CODE POUR LES POINTS
BUF_POS	DS.W	16*(NB_BALLS+10)*2+50

PAL_IMG	DC	$000,$888,$111,$999,$222,$AAA,$333,$BBB
	DC	$444,$CCC,$555,$DDD,$666,$EEE,$777,$FFF

VBLR	ADDQ.W	#1,NB_VBL
	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W       ; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W
	;ICI 33 LIGNES DE LIBRES
	BSR	MK_FADE
	MOVE.L	A0,ADR_FADE

	BSR	FINI_CALC

INTER_RTE	RTE

FINI_CALC	LEA	POINTEURS,A0
	MOVE.L	SCREEN2,D6
	ADDI.L	#1606-48+230*130,D6
	ADD.L	Y_VALUE,D6
MOD_A1 = *+2
	LEA	$12345678,A1
MOD_A4 = *+2
	LEA	$12345678,A4
	LEA	ADR_REGS,A3
	LEA	TABLE_ADR_ROND,A5

	MOVEQ	#NB_BALLS-3-1,D7
CALC_ALL	CMPI.W	#216,COUNT
	BLT.S	.PAS_FULL_BAS
	CLR.L	(A4)+
	MOVE.L	#FULL_BAS,(A4)+
	MOVE.W	#225,D0
	SUB.W	COUNT,D0
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_LIG_FULL
	CLR.W	COUNT
.PAS_FULL_BAS	MOVEQ	#0,D1
	MOVE.W	(A1)+,D0	;D0=X
	MOVE.W	(A1)+,D1	;D1=Y
MMD = *+2
	ADDI.W	#$1234,D0
MME = *+2
	ADDI.W	#$1234,D1
	ADDI.W	#160,D0
	ADDI.W	#100,D1
	SUBI.W	#130,D1
	EXT.L	D1
;	CMPI.L	#-130-50,D1
;	BGE.S	.OPK
;	MOVE.L	#-130-50,D1
;.OPK	CMPI.L	#120,D1
;	BLT.S	.OPK2
;	MOVE.L	#-130-50,D1
.OPK2	MULS.W	#LARG,D1
	MOVE.W	D0,D2
	ADD.W	D2,D2
	ADD.W	D2,D2
	LEA	CORES_X,A6
	ADD.L	(A6,D2.W),D1	;D1=OFFSET ECRAN
	ADD.L	D6,D1
	MOVE.L	D1,(A4)+	;ADRESSE ECRAN
	ANDI.W	#14,D0
	ADD.W	D0,D0
	MOVE.L	(A5,D0.W),(A4)+	;ADRESSE DU CODE POUR LE ROND
	MOVE.W	2+8*4(A5,D0.W),D4
	ADD.W	D4,COUNT
	MOVE.L	(A3,D0.W),D4	;ADRESSE POUR D1-D7,A0-A3

	MOVE.W	D0,D3
	MOVE.L	D4,(A4)+

	MOVE.L	D1,(A4)+   ;ADRESSE ECRAN
	MOVE.L	D4,A6
	MOVE.L	16(A6),D2	;LES OFFSETS POUR LES An
	MOVE.L	20(A6),D4
	MOVE.L	D2,(A4)+
	MOVE.L	D4,(A4)+

	MOVE.W	(A1)+,D0
	ADD.W	D0,D0
	MOVE.W	(A0,D0.W),D0	;D0=No DE L'ETAPE DE ROTATION
	BTST	#2,D3
	BEQ.S	DEC_0_4_8_12
DEC_2_6_10_14	LEA	TABLE_ADR3,A6
	BRA.S	CONT
DEC_0_4_8_12	LEA	TABLE_ADR,A6
CONT	MOVE.L	(A6,D0.W),(A4)+	;ADRESSE DU CODE POUR LES POINTS
	LEA	TABLE_ADR,A6
	ADDI.W	#140*4,D0
	MOVE.W	2(A6,D0.W),D0
	ADD.W	D0,COUNT
	DBRA	D7,CALC_ALL
	CMPI.W	#150,COUNT
	BLT.S	.PAS_2_FOIS
	CLR.L	(A4)+
	MOVE.L	#FULL_BAS,(A4)+
	MOVE.W	#225,D0
	SUB.W	COUNT,D0
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_LIG_FULL
	CLR.W	COUNT
.PAS_2_FOIS	MOVE.W	#29,D0
	SUB.W	COUNT,D0
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_LIG_FULL2

	CMPI.W	#$1234,(A1)
	BNE.S	.NAN_NAN
	LEA	CUBE_ANIMS,A1
.NAN_NAN	
	MOVE.L	A1,PT_CUBE_ANIM
	CLR.L	(A4)+
	MOVE.L	#LIGNE_STOP,(A4)+
	RTS

;INTER_TMA2	RTE

N	SET	-90*8
	REPT	90
	DCB.L	16,N
N	SET	N+8
	ENDR	
N	SET	0
CORES_X	REPT	90
	DCB.L	16,N
N	SET	N+8
	ENDR

ADR_APPEL	DS.L	1
COUNT	DS.W	1

INTER_TMA:	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR
full
	MOVEQ	#16,D0
	LEA	$FFFF8209.W,A0
	LEA	$FFFF820A.W,A6
	LEA	$FFFF8260.W,A3

	MOVEQ	#2,D3
	MOVEQ	#0,D4

	STOP	#$2100

	MOVE	#$2700,SR
	MOVE	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D4,(A6)	;820A
	REPT	6
	NOP
	ENDR
	MOVE.B	D3,(A6)	;820A

	;SYNCHRO ICI...
.SYNCHRO	MOVE.B	(A0),D2
	BEQ.S	.SYNCHRO
	SUB.B	D2,D0
	LSL.W	D0,D0

	DCB.W	97-5-7-5-4,$4E71
	MOVE.L	A7,AUTO_MOD1	;5
	MOVEA.L	POIT,A2	;5
	MOVE.W	#$8260,A4	;2
	MOVE.W	#$820A,A3	;2
	REPT	9
	MOVE.L	(A2)+,A1	*3
	JSR	(A1)	*4
	ENDR

	MOVE.W	#$8260,A7	;2
	MOVE.W	#$820A,A6	;2
	NP	3
	BORD_GAUCHE
ADR_FADE = *+4
	MOVEM.L	$12345678,D0-D7
	NP	88-15-21-19
	LEA	LIG_FIN_EFF,A5	;3
	MOVE.L	SCREEN2,A0	;5
	LEA	1606+230*132(A0),A0	;2
POS_OFFSET = *+2
	ADDA.L	#$12345678,A0	;4
	MOVEM.L	D0-D7,$FFFF8240.W
	MOVEQ	#-1,D0	;1
	BORD_DROIT
	NP	13
	STAB
	NP	12-3
	JMP	EFFAC_SCREEN	;3
LIG_FIN_EFF
	BORD_GAUCHE
	NP	88-12
	MOVE.L	ADR_POS,A4	;5
	MOVEQ	#0,D0	;1
	MOVE.L	(A4)+,A0	;3
	MOVE.L	(A4)+,A5	;3
	BORD_DROIT
	NP	13
	STAB
	NP	12-2
	JMP	(A5)	;2

FULL_BAS	
	BORD_GAUCHE
	NP	88-2
NB_LIG_FULL = *+2
	MOVE.W	#$1234,D7
	BORD_DROIT
	NP	12
.FULL	NOP
	STAB
	NP	12

	BORD_GAUCHE
	NP	88
	BORD_DROIT
	NP	13-4
	DBRA	D7,.FULL
	STAB
	MOVE.B	#0,$FFFF820A.W
	NP	4
	NP	12-4-4

	BORD_GAUCHE
	MOVE.B	#2,$FFFF820A.W
	MOVE.W	#$707,$FFFF8240.W
	NP	88-7-8-4
	MOVE.W	#$000,$FFFF8240.W
	MOVEQ	#0,D0	;1
	MOVE.L	(A4)+,A0	;3
	MOVE.L	(A4)+,A5	;3
	BORD_DROIT
	NP	13
	STAB
	NP	12-2
	JMP	(A5)	;2

LIGNE_STOP
	BORD_GAUCHE
	NP	88-2
NB_LIG_FULL2 = *+2
	MOVE.W	#$1234,D7
	BORD_DROIT
	NP	12
.FULL	NOP
	STAB
	NP	12

	BORD_GAUCHE
	NP	88
	BORD_DROIT
	NP	13-4
	DBRA	D7,.FULL
	STAB
	NP	12

AUTO_MOD1 = *+2
 	LEA	$12345678,A7

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.L	PT_CRB_HARD,A0
	MOVE.W	(A0)+,D0
	MULS.W	#230,D0
	MOVE.L	D0,Y_VALUE
	CMPI.W	#$1234,(A0)
	BNE.S	.PAS_LOOP
	LEA	CRB_IMG,A0
.PAS_LOOP	MOVE.L	A0,PT_CRB_HARD

	MOVEQ	#0,D0	
	MOVEQ	#0,D1
	MOVE.L	Y_VALUE,D0
	
	LSR.L	#8,D0
	MOVE.L	D0,D1
	LSL.L	#8,D1

	MOVE.L	Y_VALUE,D0
	SUB.L	D1,D0

	LEA	DATA_HS,A0

	TST.L	D0
	BNE.S	.DEJ
	LEA	PUDUKU,A0
	MOVE.L	A0,POIT
	BRA.S	.OKAY
.DEJ	SUBQ.L	#2,D0
	MULU	#18,D0
	ADDA.L	D0,A0
	MOVE.L	A0,POIT

.OKAY	MOVE.L	Y_VALUE,TAB_Y+8
	move.l	SCREEN2,d0
	MOVE.L	SCREEN1,SCREEN2
	MOVE.L	D0,SCREEN1
	ADD.L	D1,D0
	lsr.l	#8,d0
	LEA	$FFFF8201.W,A0
	movep	d0,(a0)

	BSR	HELP_CALC

	MOVE.L	TAB_Y+4,TAB_Y
	MOVE.L	TAB_Y+8,TAB_Y+4
	
	JSR	ZIK+8

	TST.W	FLAG_FINI
	BEQ.S	.PAS_FADE_DOWN
	CMPI.W	#50,NB_VBL
	BLT.S	.PAS_SI_PROCHE
ADR_POZ_NOP = *+4
	MOVE.W	#$4E71,J
	BRA.S	.PAS_SI_PROCHE
.PAS_FADE_DOWN	CMPI.B	#$39,$FFFFFC02.W
	BEQ.S	.LA_FIN_EST_PROCHE
	SUBQ.W	#1,COMPTEUR
	BNE.S	.PAS_SI_PROCHE
.LA_FIN_EST_PROCHE
	MOVE.W	#1,FLAG_FINI
	MOVE.W	#5,CNTFAD
	CLR.W	NB_VBL
.PAS_SI_PROCHE
	RTE

TAB_Y	DS.L	3

HELP_CALC	MOVE.L	PT_MOUV,A0
	MOVE.W	(A0),MMD
	MOVE.W	(A0),MMD2
	MOVE.W	(A0)+,D0
	MOVE.W	(A0),MME
	MOVE.W	(A0),MME2
	MOVE.W	(A0)+,D1
	CMPI.W	#$1234,(A0)
	BNE.S	.OKK
ADR_CRB = *+2
	LEA	MOUV,A0
.OKK	MOVE.L	A0,PT_MOUV
	ADD.W	D0,D0
	ADD.W	D0,D0
	LEA	CORES_X,A0
	MOVE.L	(A0,D0.W),D0
	MULS.W	#230,D1
	ADD.L	D0,D1
	ADD.L	TAB_Y,D1
	MOVE.L	D1,POS_OFFSET

	LEA	POINTEURS,A0
	MOVE.L	SCREEN2,D6
	ADDI.L	#1606-48+230*130,D6
	ADD.L	Y_VALUE,D6
	MOVE.L	PT_CUBE_ANIM,A1
	MOVE.L	ADR_POS,A4
	LEA	ADR_REGS,A3
	LEA	TABLE_ADR_ROND,A5

	LEA	POINTEURS,A2
	MOVEQ	#27-1,D1
.REACT	MOVE.W	(A2),D0
	ADDQ.W	#4,D0
	CMPI.W	#140*4,D0
	BLT.S	.OK
	MOVEQ	#0,D0
.OK	MOVE.W	D0,(A2)+
	DBRA	D1,.REACT

CPU_EFF = *+2
	MOVE.W	#$1234,COUNT

	MOVEQ	#3-1,D7
CALC_ALL2	MOVEQ	#0,D1
	MOVE.W	(A1)+,D0	;D0=X
	MOVE.W	(A1)+,D1	;D1=Y
MMD2 = *+2
	ADDI.W	#$1234,D0
MME2 = *+2
	ADDI.W	#$1234,D1
	ADDI.W	#160,D0
	ADDI.W	#100,D1
	SUBI.W	#130,D1
	EXT.L	D1
;	CMPI.L	#-130-50,D1
;	BGE.S	.OPK
;	MOVE.L	#-130-50,D1
;.OPK	CMPI.L	#120,D1
;	BLT.S	.OPK2
;	MOVE.L	#-130-50,D1
.OPK2	MULS.W	#LARG,D1
	MOVE.W	D0,D2
	ADD.W	D2,D2
	ADD.W	D2,D2
	LEA	CORES_X,A6
	ADD.L	(A6,D2.W),D1	;D1=OFFSET ECRAN
	ADD.L	D6,D1
	MOVE.L	D1,(A4)+	;ADRESSE ECRAN
	ANDI.W	#14,D0
	ADD.W	D0,D0
	MOVE.L	(A5,D0.W),(A4)+	;ADRESSE DU CODE POUR LE ROND
	MOVE.W	2+8*4(A5,D0.W),D4
	ADD.W	D4,COUNT
	MOVE.L	(A3,D0.W),D4	;ADRESSE POUR D1-D7,A0-A3

	MOVE.W	D0,D3
	MOVE.L	D4,(A4)+

	MOVE.L	D1,(A4)+   ;ADRESSE ECRAN
	MOVE.L	D4,A6
	MOVE.L	16(A6),D2	;LES OFFSETS POUR LES An
	MOVE.L	20(A6),D4
	MOVE.L	D2,(A4)+
	MOVE.L	D4,(A4)+

	MOVE.W	(A1)+,D0
	ADD.W	D0,D0
	MOVE.W	(A0,D0.W),D0	;D0=No DE L'ETAPE DE ROTATION
	BTST	#2,D3
	BEQ.S	DEC_0_4_8_122
DEC_2_6_10_142	LEA	TABLE_ADR3,A6
	BRA.S	CONT2
DEC_0_4_8_122	LEA	TABLE_ADR,A6
CONT2	MOVE.L	(A6,D0.W),(A4)+	;ADRESSE DU CODE POUR LES POINTS
	LEA	TABLE_ADR,A6
	ADDI.W	#140*4,D0
	MOVE.W	2(A6,D0.W),D0
	ADD.W	D0,COUNT
	DBRA	D7,CALC_ALL2

	MOVE.L	A1,MOD_A1
	MOVE.L	A4,MOD_A4
	RTS

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#128000+9*1280,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_CUBE_OBJ	LEA	COOR_CUBE,A0
	MOVE.W	#-2*RAYON,D0
	MOVEQ	#2,D5
.ADD_Z	MOVE.W	#-2*RAYON,D1
	MOVEQ	#2,D6
.ADD_Y	MOVE.W	#-2*RAYON,D2
	MOVEQ	#2,D7
.ADD_X	MOVE.W	D0,(A0)+
	MOVE.W	D1,(A0)+
	MOVE.W	D2,(A0)+
	ADDI.W	#2*RAYON,D2
	DBRA	D7,.ADD_X
	ADDI.W	#2*RAYON,D1
	DBRA	D6,.ADD_Y
	ADDI.W	#2*RAYON,D0
	DBRA	D5,.ADD_Z
	RTS

PREP_SPHERE_OBJ	LEA	COOR_CUBE,A0
	LEA	SPHERE_OBJ,A1
	MOVEQ	#27-1,D7
.COPY	MOVE.W	(A1)+,(A0)+
	MOVE.W	(A1)+,(A0)+
	MOVE.W	(A1)+,(A0)+
	DBRA	D7,.COPY
	RTS

PREP_CUBE	;ROTATION
	LEA	BUF_COS,A1
	MOVEQ	#2,D7
.RECOP_3_FOIS	LEA	TABLE_COS2,A0
	MOVE.W	#LONG_TB2,D6
.RECOP_TB	MOVE.W	(A0)+,(A1)+
	DBRA	D6,.RECOP_TB
	DBRA	D7,.RECOP_3_FOIS

	MOVE.W	SUIT_0,FINITO_GLOU_1
	MOVE.W	SUIT_1,FINITO_GLOU_2
	MOVE.W	SUIT_2,FINITO_GLOU_3

	LEA	BUF_COS,A0	;PT_COS
	LEA	300(A0),A1	;PT_SIN
	MOVE.L	A0,PT_COS
	MOVE.L	A1,PT_SIN

	LEA	CUBE_ANIMS,A6

CONTINUE_ROTATION
	MOVE.W	(A3)+,D0
	BNE.S	.PASSE_1
	MOVE.W	#$4E71,SUIT_0
.PASSE_1	MOVE.W	(A3)+,D0
	BNE.S	.PASSE_2
	MOVE.W	#$4E71,SUIT_1
.PASSE_2	MOVE.W	(A3)+,D0
	BNE.S	.PASSE_3
	MOVE.W	#$4E71,SUIT_2
.PASSE_3	MOVE.W	#200-1,D4
.ROTATE_ALL	MOVE.W	D4,CONNERIE
	BSR	MK_ROTATE_CUBE
	;ON VA CLASSER LES POINTS DU CUBE PAR Z DECROISSANTS
	LEA	COEFF_CUBE+255*2,A2  ;RECENTRAGE AU 0
	MOVEQ	#27-1,D6
.TRIE_TOUT	MOVE.L	SCREEN2,A0
	LEA	160*150(A0),A0
	MOVE.W	#-1000,D5	;MAXIMUM DE DEPART
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#27-1,D7
.TEST_PAR_Z	CMP.W	4(A0),D5
	BGE.S	.NOTHIN_MORE
	MOVE.W	4(A0),D5	;NOUVEAU MAXIMUM
	MOVE.L	A0,A1
	MOVE.W	D3,D4
.NOTHIN_MORE	ADDQ.W	#6,A0
	ADDQ.W	#1,D3
	DBRA	D7,.TEST_PAR_Z
	;D4 CONTIENT LE No DE LA BOULE TRIêE
	ADD.W	D5,D5
	MOVE.W	(A2,D5.W),D5
	MOVE.W	(A1),D0	;X 3D
	MOVE.W	2(A1),D1	;Y 3D
	MULS.W	D5,D0
	MULS.W	D5,D1
	ASR.L	#7,D0
	ASR.L	#7,D1
	MOVE.W	D0,(A6)+
	MOVE.W	D1,(A6)+
	MOVE.W	D4,(A6)+
	MOVE.W	#-1000,4(A1)	;COMME CA, AUCUN RISQUE...
	DBRA	D6,.TRIE_TOUT
CONNERIE = *+2
	MOVE.W	#$1234,D4
	DBRA	D4,.ROTATE_ALL

FINITO_GLOU_1 = *+2
	MOVE.W	#$1234,SUIT_0
FINITO_GLOU_2 = *+2
	MOVE.W	#$1234,SUIT_1
FINITO_GLOU_3 = *+2
	MOVE.W	#$1234,SUIT_2
	CMPI.W	#$1234,(A3)
	BNE	CONTINUE_ROTATION

	MOVE.W	#$1234,(A6)	;SENTINELLE DE FIN
	MOVE.L	#CUBE_ANIMS,PT_CUBE_ANIM
	RTS

MK_ROTATE_CUBE	MOVE.L	PT_COS,A0
	MOVE.L	PT_SIN,A1
	MOVE.W	(A0)+,D5	;COS
	MOVE.W	(A1)+,D6	;SIN
	MOVE.L	A0,PT_COS
	MOVE.L	A1,PT_SIN
	LEA	COOR_CUBE,A2
	MOVEQ	#0,D7
	MOVE.L	SCREEN2,A4
	LEA	160*150(A4),A4
CONT_ROT	CMPA.L	#COOR_CUBE+6*27,A2
	BEQ.S	FIN_ROT
	MOVE.W	(A2)+,D0	;X
	MOVE.W	(A2)+,D1	;Y
	MOVE.W	(A2)+,D2	;Z
	;ROTATION Z
SUIT_0	BRA.S	SUIT_1
	MOVE.W	D0,D3	;D3=X
	MOVE.W	D1,D4	;D4=Y
	MULS.W	D5,D0	;D0=X*COS T
	MULS.W	D6,D3	;D3=X*SIN T
	MULS.W	D5,D1	;D1=Y*COS T
	MULS.W	D6,D4	;D4=Y*SIN T
	SUB.L	D4,D0	;D0=X*COS T-Y*SIN T
	ADD.L	D3,D1	;D1=Y*COS T+X*SIN T
	ASR.L	#8,D0
	ASR.L	#8,D1
	;ROTATION X
SUIT_1	BRA.S	SUIT_2
	MOVE.W	D1,D3	;D3=Y
	MOVE.W	D2,D4	;D4=Z
	MULS.W	D5,D1	;D1=Y*COS T
	MULS.W	D6,D3	;D3=Y*SIN T
	MULS.W	D5,D2	;D2=Z*COS T
	MULS.W	D6,D4	;D4=Z*SIN T
	SUB.L	D4,D1
	ADD.L	D3,D2
	ASR.L	#8,D1
	ASR.L	#8,D2
	;ROTATION Y
SUIT_2	BRA.S	SUIT_3
	MOVE.W	D2,D3	;D3=Z
	MOVE.W	D0,D4	;D4=X
	MULS.W	D5,D2	;D2=Z*COS T
	MULS.W	D6,D3	;D3=Z*SIN T
	MULS.W	D5,D0	;D0=X*COS T
	MULS.W	D6,D4	;D4=X*SIN T
	SUB.L	D4,D2
	ADD.L	D3,D0
	ASR.L	#8,D2
	ASR.L	#8,D0
	;
SUIT_3	MOVE.W	D0,(A4)+
	MOVE.W	D1,(A4)+
	MOVE.W	D2,(A4)+
	BRA.S	CONT_ROT
FIN_ROT	RTS

PREP_POINTS	;ON GENERE LES POINTEURS D'ANIMATION ET D'EMPLACEMENT
	;DES SPHERES
	LEA	POINTEURS,A0
	LEA	RANDOM,A1
	LEA	PERSO,A2
	MOVEQ	#27-1,D7
.SORT_TOUT	MOVE.W	#140,D0
	ADD.W	D0,D0
	MULS.W	(A1)+,D0
	SWAP	D0
	ADD.W	D0,D0
	ADD.W	D0,D0
	MOVE.W	D0,(A0)+  ;No D'ANIMATION DES SPHERES AU HASARD
	DBRA	D7,.SORT_TOUT
	RTS

PREP_DOTS	LEA	BUF_COS,A1
	MOVEQ	#2,D7
.RECOP_3_FOIS	LEA	TABLE_COS,A0
	MOVE.W	#LONG_TB,D6
.RECOP_TB	MOVE.W	(A0)+,(A1)+
	DBRA	D6,.RECOP_TB
	DBRA	D7,.RECOP_3_FOIS

	MOVE.L	#BUF_P,MOD

	LEA	BUF_COS,A0	;PT_COS
	LEA	210(A0),A1	;PT_SIN
	MOVE.L	A0,PT_COS
	MOVE.L	A1,PT_SIN

	LEA	BUF_COD_DOTS,A3
	LEA	TABLE_ADR,A5
	MOVE.W	#140-1,D7
ROTATIONS	MOVE.W	D7,A6
	BSR	MK_ROTATE

	LEA	BUF_ROTATê,A0
	LEA	COEFF_3D,A1
;	MOVE.L	A3,A4
;	ADDQ.W	#2,A3
	MOVE.L	A3,(A5)
	MOVE.L	A3,140*4*2(A5)
	ADDI.L	#BUF_COD_DOTS3-BUF_COD_DOTS,140*4*2(A5)

	MOVE.L	SCREEN2,A3
	LEA	160*100(A3),A4
	MOVE.W	(A0)+,D7
	SUBQ.W	#1,D7
	MOVEQ	#0,D6	;NB DE POINTS A AFFICHER
.NEXT_POINT	MOVE.W	(A0)+,D0	;X
	MOVE.W	(A0)+,D1	;Y
	MOVE.W	(A0)+,D2	;Z
	BGE.S	.PAS
	ADD.W	D2,D2
	MOVE.W	(A1,D2.W),D2	;COEFF 3D
	MULS.W	D2,D0
	MULS.W	D2,D1
	ASR.L	#8,D0
	ASR.L	#8,D1
	ADDI.W	#15,D0
	ADDI.W	#15,D1
	TST.W	D0
	BLT.S	.PAS
	MULS.W	#LARG,D1
	MOVE.W	D0,D2
	ANDI.W	#$FFF0,D2
	LSR.W	#1,D2
	ADD.W	D2,D1
	ANDI.W	#15,D0
	ADD.W	D0,D0
	MOVE.W	D0,ABC	;ON SAUVE...
	LEA	TABLE_COD_EOR,A2
	MOVE.W	(A2,D0.W),D0	;CODE DU EOR
	MOVE.W	D0,(A3)+
	MOVE.W	D1,(A3)+

	LEA	TABLE_COD_EOR2,A2
ABC = *+2
	MOVE.W	#$1234,D0      ;ON RECUPERE LE (X AND 15)*2
	ADD.W	D0,D0
	ADD.W	2(A2,D0.W),D1	;INCREMENT A L'OFFSET (0,8)
	MOVE.W	(A2,D0.W),D0	;CODE DU EOR
	MOVE.W	D0,(A4)+
	MOVE.W	D1,(A4)+

	ADDQ.W	#1,D6
.PAS	DBRA	D7,.NEXT_POINT
MOD = *+2
	MOVE.W	D6,$12345678
	ADDQ.L	#2,MOD

	LEA	ROUT_TYPE_2(PC),A2
	MOVE.W	#LONG_ROUT2,D7
.RECOP_PRE_ROUT	MOVE.W	(A2),(A3)+
	MOVE.W	(A2)+,(A4)+
	DBRA	D7,.RECOP_PRE_ROUT
	MOVE.W	#$4E75,(A3)+
	MOVE.L	SCREEN2,A0
	MOVE.L	(A5),A1
	LEA	160*100(A0),A2
	MOVE.L	A1,A4
	ADDA.L	#BUF_COD_DOTS3-BUF_COD_DOTS,A4
	MOVEQ	#0,D6
	BSR	INTEGRE_BIS
	MOVE.L	D6,140*4(A5)
	ADDQ.W	#4,A5
	MOVE.L	A1,MOD_2

MOD_2 = *+2
	LEA	$12345678,A3

	MOVE.W	A6,D7
;	ADDI.W	#$123,$FFFF8240.W
	DBRA	D7,ROTATIONS
;	MOVE.W	#$1234,(A5)+

	RTS

TABLE_COD_EOR	DC.W	$8128,$8328,$8528,$8728
	DC.W	$8929,$8B29,$8D29,$8F29
	DC.W	$812A,$832A,$852A,$872A
	DC.W	$892B,$8B2B,$8D2B,$8F2B

TABLE_COD_EOR2	DC.W	$8128,0,$8328,0,$8529,0,$8729,0
	DC.W	$8929,0,$8B29,0,$8D2A,0,$8F2A,0
	DC.W	$812A,0,$832A,0,$852B,0,$872B,0
	DC.W	$892B,0,$8B2B,0,$8D28,8,$8F28,8

MK_ROTATE	MOVE.L	A3,-(SP)
	MOVE.L	PT_COS,A0
	MOVE.L	PT_SIN,A1
	MOVE.W	(A0)+,D5	;COS
	MOVE.W	(A1)+,D6	;SIN
	MOVE.L	A0,PT_COS
	MOVE.L	A1,PT_SIN
	LEA	COOR_SPHERES,A2
	MOVEQ	#0,D7
	LEA	BUF_ROTATê,A4
	MOVE.L	A4,A3
	ADDQ.W	#2,A3
.CONT_ROT	CMPA.L	#FIN_COOR,A2
	BEQ.S	.FIN_ROT
	MOVE.W	(A2)+,D0	;X
	MOVE.W	(A2)+,D1	;Y
	MOVE.W	(A2)+,D2	;Z
	;ROTATION Z
	MOVE.W	D0,D3	;D3=X
	MOVE.W	D1,D4	;D4=Y
	MULS.W	D5,D0	;D0=X*COS T
	MULS.W	D6,D3	;D3=X*SIN T
	MULS.W	D5,D1	;D1=Y*COS T
	MULS.W	D6,D4	;D4=Y*SIN T
	SUB.L	D4,D0	;D0=X*COS T-Y*SIN T
	ADD.L	D3,D1	;D1=Y*COS T+X*SIN T
	ASR.L	#8,D0
	ASR.L	#8,D1
	;ROTATION X
	MOVE.W	D1,D3	;D3=Y
	MOVE.W	D2,D4	;D4=Z
	MULS.W	D5,D1	;D1=Y*COS T
	MULS.W	D6,D3	;D3=Y*SIN T
	MULS.W	D5,D2	;D2=Z*COS T
	MULS.W	D6,D4	;D4=Z*SIN T
	SUB.L	D4,D1
	ADD.L	D3,D2
	ASR.L	#8,D1
	ASR.L	#8,D2
	;ROTATION Y
	MOVE.W	D2,D3	;D3=Z
	MOVE.W	D0,D4	;D4=X
	MULS.W	D5,D2	;D2=Z*COS T
	MULS.W	D6,D3	;D3=Z*SIN T
	MULS.W	D5,D0	;D0=X*COS T
	MULS.W	D6,D4	;D4=X*SIN T
	SUB.L	D4,D2
	ADD.L	D3,D0
	ASR.L	#8,D2
	ASR.L	#8,D0
	;
.PROUT	MOVE.W	D0,(A3)+
	MOVE.W	D1,(A3)+
	MOVE.W	D2,(A3)+
	ADDQ.W	#1,D7
	BRA.S	.CONT_ROT
.FIN_ROT	MOVE.W	D7,(A4)
	MOVE.L	(SP)+,A3
	RTS

PREP_BALLS	LEA	SPRITE,A0
	MOVE.L	SCREEN2,A1
	LEA	160*100(A1),A1
	LEA	TABLE_UTIL(PC),A3
	MOVE.W	#30-1,D7
.TRAC_ALL	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;X2
	SUB.W	D0,D1
	MOVE.W	D0,D2
	ANDI.W	#$FFF0,D2
	LSR.W	#1,D2
	MOVE.L	A1,A2
	ADDA.W	D2,A2
	ANDI.W	#15,D0
	ADD.W	D0,D0
	MOVE.W	(A3,D0.W),D0	;MOTIF DE DEPART
.TRAC_LIGNE	OR.W	D0,(A2)
	ROR.W	#1,D0
	CMPI.W	#$8000,D0
	BNE.S	.LAB1
	ADDQ.W	#8,A2	
.LAB1	DBRA	D1,.TRAC_LIGNE
	MOVE.W	#$5555,16(A2)
	LEA	160(A1),A1
	DBRA	D7,.TRAC_ALL

	LEA	BUF_COD_ROND,A1
	LEA	TABLE_ADR_ROND,A6
	BSR	COD_ROND
	BSR	DECAL_ROND
	BSR	COD_ROND
	BSR	DECAL_ROND
	BSR	COD_ROND
	BSR	DECAL_ROND
	BSR	COD_ROND
	BSR	DECAL_ROND
	BSR	COD_ROND
	BSR	DECAL_ROND
	BSR	COD_ROND
	BSR	DECAL_ROND
	BSR	COD_ROND
	BSR	DECAL_ROND
	BSR	COD_ROND
	RTS

COD_ROND	BSR	SEARCH
	MOVE.L	A1,(A6)
	MOVE.L	A1,-(SP)
	MOVE.L	A1,A5
	MOVE.L	SCREEN2,A1	;BUFFER DE CODE PROVISOIRE
	MOVE.W	#$4CBA,(A1)	;MOVEM PROVISOIRE...
			;...POUR L'INTEGRATION
	ADDQ.W	#6,A1	;DE LA PLACE POUR LE MOVEM		
	MOVE.L	SCREEN2,A0
	LEA	160*100(A0),A0
	MOVEQ	#0,D5	;OFFSET DE DEPART
	MOVEQ	#30-1,D7
CODE_ALL_ONE	MOVE.L	D5,D4
	MOVE.L	A0,A2
NEXT_WORD	TST.W	(A2)
	BEQ.S	RIEN_DU_TOUT
	CMPI.W	#-1,(A2)
	BEQ.S	MOVE_MOINS_1
	MOVE.W	(A2),D0
	NOT.W	D0	;MOTIF
	;ON TESTE SI LA VALEUR N'EST PAS CONTENUE DANS TABLE_MAX
	LEA	TABLE_MAX,A4
	MOVE.L	#$268C368,D3
	MOVEQ	#7-1,D6
.ESSAIE_ENCORE	CMP.W	(A4),D0
	BEQ.S	.TROUVê
	ADDI.W	#$200,D3
	ADDQ.W	#2,A4
	DBRA	D6,.ESSAIE_ENCORE
	;ON POSE UN ANDI.W
	SWAP	D3
	MOVE.W	D3,(A1)+    ;CODE DU ANDI.W #Imm,d16(A0)
	MOVE.W	D0,(A1)+
	MOVE.W	D4,(A1)+	;OFFSET
	BRA.S	RIEN_DU_TOUT
.TROUVê	MOVE.W	D3,(A1)+
	MOVE.W	D4,(A1)+
	BRA.S	RIEN_DU_TOUT
	;ON POSE UN MOVE D0
MOVE_MOINS_1	MOVE.W	#$3140,(A1)+	;CODE DU MOVE.W D0,d16(A0)
	MOVE.W	D4,(A1)+
RIEN_DU_TOUT	ADDQ.W	#8,A2
	ADDQ.W	#8,D4
	CMPI.W	#$5555,(A2)
	BNE.S	NEXT_WORD
	LEA	160(A0),A0
	ADDI.W	#LARG,D5
	DBRA	D7,CODE_ALL_ONE
	LEA	ROUT_TYPE_1(PC),A2
	MOVE.W	#LONG_ROUT,D7
.RECOP_PRE_ROUT	MOVE.W	(A2)+,(A1)+
	DBRA	D7,.RECOP_PRE_ROUT
	MOVE.W	#$4E75,(A1)+
	MOVE.L	(SP)+,A1	;ADRESSE DESTINATION
	MOVE.L	SCREEN2,A0	;ADRESSE DU CODE A INTEGRER
	MOVEQ	#0,D6
	BSR	INTEGRE
	MOVE.L	D6,8*4(A6)
	ADDQ.W	#4,A6
	LEA	2*3(A5),A5	;ESPACE DE 5 NOPS=3 MOTS
	MOVE.L	#$4CBA00FE,(A5)+
	MOVE.L	A1,D4
	LEA	TABLE_MAX,A0
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.W	(A0)+,(A1)+
	SUB.L	A5,D4
	MOVE.W	D4,(A5)
	RTS

SEARCH	MOVE.L	SCREEN2,A2
	LEA	160*100(A2),A2
	LEA	TABLE_IT,A4
	MOVEQ	#30-1,D7
.SEARCH_ALL	MOVE.L	A2,A0
.TOO	TST.W	(A0)
	BEQ.S	.NEXT_WORD
	CMPI.W	#$5555,(A0)
	BEQ.S	.NEXT_LINE
	MOVE.W	(A0),D0
	NOT.W	D0	;IL FAUT UN MASQUE...TSS...
	LEA	TABLE_IT,A3
.TRY_NEXT	CMPA.L	A4,A3
	BEQ.S	.NEW_VALUE
	CMP.W	(A3),D0
	BEQ.S	.PLUS_1
	ADDQ.W	#4,A3
	BRA.S	.TRY_NEXT
.PLUS_1	ADDQ.W	#1,2(A3)
	BRA.S	.NEXT_WORD
.NEW_VALUE	MOVE.W	D0,(A4)+
	MOVE.W	#1,(A4)+
.NEXT_WORD	ADDQ.W	#8,A0
	BRA.S	.TOO
.NEXT_LINE	LEA	160(A2),A2
	DBRA	D7,.SEARCH_ALL
	;SECTION: PROCEDURE DE TRI
	LEA	TABLE_MAX,A2
	MOVEQ	#0,D4	;GAIN TOTAL EN NOPS
	MOVEQ	#7-1,D7
.ROLL_AND_ROLL	LEA	TABLE_IT,A0
	MOVEQ	#0,D6	;MAXI DE DEPART...
.CONT_F	CMPA.L	A4,A0
	BEQ.S	.FINI_F
	CMP.W	2(A0),D6
	BGT.S	.NOT_DIS_ONE
	TST.W	(A0)
	BEQ.S	.NOT_DIS_ONE
	MOVE.L	A0,A3	;ON SAUVE L'ADRESSE DU MAXI
	MOVE.W	2(A0),D6	;NOUVEAU MAXI
.NOT_DIS_ONE	ADDQ.W	#4,A0
	BRA.S	.CONT_F
.FINI_F	MOVE.W	(A3),(A2)+
	ADD.W	D6,D4
	CLR.W	2(A3)
	DBRA	D7,.ROLL_AND_ROLL
	SUBI.W	#11,D4	;TEMPS DU MOVEM POUR 7 .W
	RTS

DECAL_ROND	MOVE.L	SCREEN2,A2
	LEA	160*100(A2),A2
	MOVEQ	#30-1,D7
DEC_ALL	MOVE.L	A2,A0
	MOVEQ	#0,D1
DEC_LINE	MOVEQ	#0,D0
	CMPI.W	#$5555,(A0)
	BEQ.S	NEXT_LINE
	MOVE.W	(A0),D0
	ROR.L	#2,D0
	OR.W	D1,D0
	MOVE.W	D0,(A0)
	SWAP	D0
	MOVE.W	D0,D1
	ADDQ.W	#8,A0
	BRA.S	DEC_LINE
NEXT_LINE	LEA	160(A2),A2
	DBRA	D7,DEC_ALL
	RTS

TABLE_UTIL	DC.W	$8000,$4000,$2000,$1000,$800,$400,$200,$100,$80,$40,$20,$10,8,4,2,1

	;A0=ADRESSE DU CODE A INTEGRER
	;A1=ADRESSE DU BUFFER RECEVANT LE CODE INTEGRê
INTEGRE	MOVE.L	A5,-(SP)
	LEA	INTEGRE_SUIT(PC),A3
	SF	FLAG_FINI_INT
.CONT_INT_1	
;	MOVE.L	#$4E714E71,(A1)+
;	MOVE.L	#$4E714E71,(A1)+
;	MOVE.W	#$4E71,(A1)+
	MOVE.L	#$3E8F1EBC,(A1)+
	MOVE.W	#$0000,(A1)+
	MOVEQ	#88,D2
	BSR	DDDF
;	MOVE.L	#$4E714E71,(A1)+
;	MOVE.L	#$4E714E71,(A1)+
;	MOVE.W	#$4E71,(A1)+
	MOVE.L	#$1CBC0000,(A1)+
	MOVE.W	#$3C8F,(A1)+
	MOVEQ	#13,D2
	BSR	DDDF
;	MOVE.L	#$4E714E71,(A1)+
;	MOVE.L	#$4E714E71,(A1)+
;	MOVE.W	#$4E71,(A1)+
	MOVE.L	#$3E8F1EBC,(A1)+
	MOVE.W	#$0000,(A1)+
	MOVEQ	#12,D2
	BSR	DDDF
	ADDQ.W	#1,D6
	TST.B	FLAG_FINI_INT
	BEQ.S	.CONT_INT_1
	SUBQ.W	#4,A1	;ON RECULE DE 2 NOPS POUR...
	CMPI.L	#$4E714E71,(A1)
	BEQ.S	.OK
	MOVE.W	#$700,$FFFF8240.W
	ILLEGAL
.OK	MOVE.W	#$4ED5,(A1)+	;...POSER UN 'JMP (A5)'.
	MOVE.L	(SP)+,A5
	RTS

DDDF	TST.B	FLAG_FINI_INT
	BNE.S	COMPLETE
	JMP	(A3)
INTEGRE_SUIT	MOVE.W	(A0),D0
	LEA	INSTRUCTIONS_INT,A5
	MOVE.W	#NB_INSTRUCT,D7
.GOTO	CMP.W	(A5)+,D0
	BEQ.S	.FIND
	ADDQ.W	#6,A5
	DBRA	D7,.GOTO
	MOVE.W	#$700,$FFFF8240.W
	ILLEGAL
.FIND	ADDQ.W	#2,A5
	CMP.W	(A5),D2
	BGE.S	.CONTINUE
	LEA	.CONTINUE(PC),A3
	BRA	COMPLETE
.CONTINUE	LEA	INTEGRE_SUIT(PC),A3
	CMPI.W	#$4E75,D0
	BNE.S	.NO
.RETOUR_RTS	ST	FLAG_FINI_INT
	BRA.S	COMPLETE
.NO	SUB.W	(A5)+,D2
	MOVE.W	(A5)+,D3
.COPY_2	MOVE.W	(A0)+,(A1)+
	DBRA	D3,.COPY_2
	TST.B	FLAG_FINI_INT
	BEQ.S	INTEGRE_SUIT
COMPLETE	SUBQ.W	#1,D2
	BGE.S	.OK
	RTS
.OK	MOVE.W	#$4E71,(A1)+
	DBRA	D2,.OK
	RTS

	;A0=ADRESSE DU CODE A INTEGRER
	;A1=ADRESSE DU BUFFER RECEVANT LE CODE INTEGRê
	;A2=ADRESSE DU CODE A INTEGRER EN PARALLELLE
               ;A4=ADRESSE DU BUFFER RECEVANT LE CODE INTEGRê EN PARALLELLE
INTEGRE_BIS	MOVE.L	A5,-(SP)
	LEA	INTEGRE_SUIT_BIS(PC),A3
	SF	FLAG_FINI_INT
.CONT_INT_1	
	MOVE.L	#$3E8F1EBC,(A1)+
	MOVE.W	#$0000,(A1)+
	MOVE.L	#$3E8F1EBC,(A4)+
	MOVE.W	#$0000,(A4)+
	MOVEQ	#88,D2
	BSR	DDDF_BIS
	MOVE.L	#$1CBC0000,(A1)+
	MOVE.W	#$3C8F,(A1)+
	MOVE.L	#$1CBC0000,(A4)+
	MOVE.W	#$3C8F,(A4)+
	MOVEQ	#13,D2
	BSR	DDDF_BIS
	MOVE.L	#$3E8F1EBC,(A1)+
	MOVE.W	#$0000,(A1)+
	MOVE.L	#$3E8F1EBC,(A4)+
	MOVE.W	#$0000,(A4)+
	MOVEQ	#12,D2
	BSR	DDDF_BIS
	ADDQ.W	#1,D6
	TST.B	FLAG_FINI_INT
	BEQ.S	.CONT_INT_1
	SUBQ.W	#4,A1	;ON RECULE DE 2 NOPS POUR...
	SUBQ.W	#4,A4	;ON RECULE DE 2 NOPS POUR...
	CMPI.L	#$4E714E71,(A1)
	BEQ.S	.OK
	MOVE.W	#$700,$FFFF8240.W
	ILLEGAL
.OK	MOVE.W	#$4ED5,(A1)+	;...POSER UN 'JMP (A5)'.
	MOVE.W	#$4ED5,(A4)+	;...POSER UN 'JMP (A5)'.
	MOVE.L	(SP)+,A5
	RTS

DDDF_BIS	TST.B	FLAG_FINI_INT
	BNE.S	COMPLETE_BIS
	JMP	(A3)
INTEGRE_SUIT_BIS
	MOVE.W	(A0),D0
	LEA	INSTRUCTIONS_INT,A5
	MOVE.W	#NB_INSTRUCT,D7
.GOTO	CMP.W	(A5)+,D0
	BEQ.S	.FIND
	ADDQ.W	#6,A5
	DBRA	D7,.GOTO
	MOVE.W	#$700,$FFFF8240.W
	ILLEGAL
.FIND	ADDQ.W	#2,A5
	CMP.W	(A5),D2
	BGE.S	.CONTINUE
	LEA	.CONTINUE(PC),A3
	BRA	COMPLETE_BIS
.CONTINUE	LEA	INTEGRE_SUIT_BIS(PC),A3
	CMPI.W	#$4E75,D0
	BNE.S	.NO
.RETOUR_RTS	ST	FLAG_FINI_INT
	BRA.S	COMPLETE_BIS
.NO	SUB.W	(A5)+,D2
	MOVE.W	(A5)+,D3
.COPY_2	MOVE.W	(A0)+,(A1)+
	MOVE.W	(A2)+,(A4)+
	DBRA	D3,.COPY_2
	TST.B	FLAG_FINI_INT
	BEQ.S	INTEGRE_SUIT_BIS
COMPLETE_BIS	SUBQ.W	#1,D2
	BGE.S	.OK
	RTS
.OK	MOVE.W	#$4E71,(A1)+
	MOVE.W	#$4E71,(A4)+
	DBRA	D2,.OK
	RTS

MK_ROUT_EFF	MOVE.L	SCREEN2,A0
	MOVE.W	#-230*100,D0
	MOVE.W	#162,D6
.EFF_Y	MOVEQ	#11-1,D7
.EFF_X	MOVE.W	#$3140,(A0)+
	MOVE.W	D0,(A0)+
	ADDI.W	#8,D0
	DBRA	D7,.EFF_X
	ADDI.W	#230-8*11,D0
	DBRA	D6,.EFF_Y
	MOVE.W	#$4E75,(A0)
	;
	MOVE.L	SCREEN2,A0
	LEA	EFFAC_SCREEN,A1
	MOVEQ	#0,D6
	BSR	INTEGRE
	ADDI.W	#9,D6
	MOVE.W	D6,CPU_EFF
	RTS

PERSO	INCBIN	EXEMPLE.DAT

INSTRUCTIONS_INT
	DC.W	$8128,0,4,1	;EOR.B Dn,d16(A0)
	DC.W	$8328,0,4,1	;0<=n<=7
	DC.W	$8528,0,4,1
	DC.W	$8728,0,4,1
	DC.W	$8929,0,4,1
	DC.W	$8B29,0,4,1
	DC.W	$8D29,0,4,1
	DC.W	$8F29,0,4,1
	DC.W	$812A,0,4,1
	DC.W	$832A,0,4,1
	DC.W	$852A,0,4,1
	DC.W	$872A,0,4,1
	DC.W	$892B,0,4,1
	DC.W	$8B2B,0,4,1
	DC.W	$8D2B,0,4,1
	DC.W	$8F2B,0,4,1

;	MOVE.L	USP,A5
;	DC.W	0,1,0
;	MOVE.L	A5,USP
;	DC.W	0,1,0

	DC.W	$1140,0,3,1
	DC.W	$1141,0,3,1
	DC.W	$1142,0,3,1
	DC.W	$1143,0,3,1
	DC.W	$1344,0,3,1
	DC.W	$1345,0,3,1
	DC.W	$1346,0,3,1
	DC.W	$1347,0,3,1
	DC.W	$1540,0,3,1
	DC.W	$1541,0,3,1
	DC.W	$1542,0,3,1
	DC.W	$1543,0,3,1
	DC.W	$1744,0,3,1
	DC.W	$1745,0,3,1
	DC.W	$1746,0,3,1
	DC.W	$1747,0,3,1

	DC.W	$C368,0,4,1	;AND.W Dn,d16(A0)
	DC.W	$C568,0,4,1	;1<=n<=7
	DC.W	$C768,0,4,1
	DC.W	$C968,0,4,1
	DC.W	$CB68,0,4,1
	DC.W	$CD68,0,4,1
	DC.W	$CF68,0,4,1

	MOVE.L	(A4)+,A0	;3
	DC.W	0,3,0
	MOVE.L	A0,A1	;1
	DC.W	0,1,0
	MOVE.L	A0,A2	;1
	DC.W	0,1,0
	MOVE.L	A0,A3	;1
	DC.W	0,1,0
;	MOVE.L	A0,A5	;1
;	DC.W	0,1,0
	MOVE.L	(A4)+,A5	;3
	DC.W	0,3,0
	DC.W	$4C9D,0,11,1
	ADDA.W	(A4)+,A0	;3
	DC.W	0,3,0
	ADDA.W	(A4)+,A1	;3
	DC.W	0,3,0
	ADDA.W	(A4)+,A2	;3
	DC.W	0,3,0
	ADDA.W	(A4)+,A3	;3
	DC.W	0,3,0
;	ADDA.W	(A4)+,A5	;3
;	DC.W	0,3,0
	MOVE.L	(A4)+,A5   ;3;A5=ADRESSE DU CODE DES POINTS
	DC.W	0,3,0
	MOVEQ	#0,D0
	DC.W	0,1,0

	DC.W	$3140,0,3,1	;MOVE.W D0,d16(A0)

	DC.W	$268,0,5,2	;ANDI.W #$Imm,d16(A0)

	DC.W	$4E75,0,4,0	;RTS

	DC.W	$4CBA,0,11,2	;MOVEM...
NB_INSTRUCT = ((*-INSTRUCTIONS_INT)/8)-1

	;SCHEMA DES SOUS-ROUTINES A INCLURE AU DEBUT
ROUT_TYPE_1	;...AU DEBUT DES POINTS
	MOVE.L	(A4)+,A5	;3
	MOVEM.W	(A5)+,D0-D7	;11
	MOVE.L	(A4)+,A0	;3
	MOVE.L	A0,A1	;1
	MOVE.L	A0,A2	;1
	MOVE.L	A0,A3	;1
	ADDA.W	(A4)+,A0	;3
	ADDA.W	(A4)+,A1	;3
	ADDA.W	(A4)+,A2	;3
	ADDA.W	(A4)+,A3	;3
	MOVE.L	(A4)+,A5   ;3;A5=ADRESSE DU CODE DES POINTS
	;TOTAL = 35 NOPS
LONG_ROUT = ((*-ROUT_TYPE_1)/2)-1

ROUT_TYPE_2	;...AU DEBUT DES RONDS
	MOVEQ	#0,D0	;1
	MOVE.L	(A4)+,A0    ;3;ADRESSE ECRAN
	MOVE.L	(A4)+,A5    ;3;ADRESSE DU CODE POUR LE ROND
LONG_ROUT2 = ((*-ROUT_TYPE_2)/2)-1

	INCLUDE	VIEWER.S
;	INCLUDE	TEXTER.S

ROTATE_CUBE_1	DC.W	0,0,0,0,1,0,$1234
ROTATE_CUBE_2	DC.W	0,0,1,0,0,0,$1234

CRB_IMG	INCBIN	SINUS_I1.CRB
	DC.W	$1234

MOUV	INCBIN	SINUSKK2.CRB
	DC.W	$1234

MOUV2	INCBIN	SINUSKK3.CRB
	DC.W	$1234

IMG_0	INCBIN	DISK3_10.PAK
IMG_1	INCBIN	DISK3_11.PAK	;CUBE
IMG_2	INCBIN	VGA14.PAK	;CUBE
IMG_3	INCBIN	VGA9.PAK	;SPHERE
IMG_4	INCBIN	DISK3_17.PAK	;SPHERE

ZIK	INCBIN	MUSIX.MUS
	EVEN

COEFF_CUBE	INCBIN	COEFCUBE.3D

RANDOM	INCBIN	RND.RND

SPRITE	INCBIN	MOTIF3.GFX

TABLE_COS	INCBIN	TB_SIN.SIN
LONG_TB = ((*-TABLE_COS)/2)-1

TABLE_COS2	INCBIN	TB_SIN2.SIN
LONG_TB2 = ((*-TABLE_COS2)/2)-1

SPHERE_OBJ	INCBIN	BIDULBUK.COR

COOR_CUBE	DS.W	3*27

COOR_SPHERES	INCBIN	COOR3SPH.COR
FIN_COOR

	INCBIN	COEFF.SPE
COEFF_3D

BUF_P	DS.W	200

	SECTION	BSS
FLAG_FINI_INT	DS.B	1
	EVEN
NB_VBL	DS.W	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256+10000
BUFFER	DS.B	128000+9*1280+13000
BUFPAK	DS.B	110400
TABLE_ADR	DS.L	140    ;140 ROUTINES EOR, DECAL. 0,4,8,12
	DS.L	140    ;140 ROUTINES MOVE, DECAL. 0,4,8,12
TABLE_ADR3	DS.L	140    ;140 ROUTINES EOR, DECAL. 2,6,10,14
	DS.L	140    ;140 ROUTINES MOVE, DECAL. 2,6,10,14
TABLE_IT	DS.W	2*50
TABLE_MAX	DS.W	7
BUF_COS	DS.W	(LONG_TB2+1)*3
TABLE_ADR_ROND	DS.L	8
TEMPS	DS.L	8
COMPTEUR	DS.W	1
PT_MOUV	DS.L	1
PT_SIN	DS.L	1
PT_COS	DS.L	1
Y_CARRE	DS.W	1
PT_CUBE_ANIM	DS.L	1
POINTEURS	DS.W	4*27
CUBE_ANIMS	DS.B	6*27*400+2
BUF_ROTATê	DS.W	3*400
BUF_COD_ROND	DS.B	6000
BUF_COD_DOTS	DS.B	67000	;EOR, DECALAGES 0,4,8,12
BUF_COD_DOTS3	DS.B	67000	;EOR, DECALAGES 2,6,10,14
EFFAC_SCREEN	DS.B	10000