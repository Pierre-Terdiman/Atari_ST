NB_PTS_=30

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
*************************************************************************

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	LEA	DEB_BSS,A0
	LEA	END_BSS,A1
.KILL_IT	CLR.L	(A0)+
	CMP.L	A1,A0
	BLE.S	.KILL_IT

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	MOVE	#$011,$FFFF8240.W
	MOVE	#$334,$FFFF8242.W
	MOVE	#$344,$FFFF8244.W
	MOVE	#$344,$FFFF8246.W
	MOVE	#$434,$FFFF8248.W
	MOVE	#$434,$FFFF824A.W
	MOVE	#$434,$FFFF824C.W
	MOVE	#$434,$FFFF824E.W

	JSR	PREPA_LOG_EXP

	LEA	SINUS,A0
	LEA	SINUS+LONG_SINUS,A1
	MOVE.W	#(LONG_SINUS/8)-1,D7
.RECOPY	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.RECOPY

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

MAIN_LOOP	BRA.S	MAIN_LOOP
VBLR
	MOVE	#$011,$FFFF8240.W

	LEA	POINTS,A0	Pts du cube
	LEA	ANGLES,A1
	LEA	TRANS,A6
	LEA	BUF_PTS,A2
	JSR	ROTATE

	JSR	AFFICH

	JSR	SWAPEC

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME
	MOVE.B	$FFFFFC02.W,D7
	LEA	ANGLES,A0

	MOVE.W	(A0),D0
	ADD	ADX,D0
	CMPI.B	#$1,D7
	BNE.S	.NO_F1
	ADDQ.W	#2,D0
.NO_F1
	CMPI.B	#$2,D7
	BNE.S	.NO_F2
	SUBQ.W	#2,D0
.NO_F2
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+
	MOVE.W	(A0),D0
	ADD	ADY,D0
	CMPI.B	#$3,D7
	BNE.S	.NO_F3
	ADDQ.W	#8,D0
.NO_F3
	CMPI.B	#$4,D7
	BNE.S	.NO_F4
	SUBQ.W	#8,D0
.NO_F4
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+
	MOVE.W	(A0),D0
	ADD	ADZ,D0
	CMPI.B	#$5,D7
	BNE.S	.NO_F5
	ADDQ.W	#2,D0
.NO_F5
	CMPI.B	#$6,D7
	BNE.S	.NO_F6
	SUBQ.W	#2,D0
.NO_F6
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	CMPI.B	#7,D7
	BNE.S	.NO7
	ADDQ.W	#1,Z_BASE
.NO7
	CMPI.B	#8,D7
	BNE.S	.NO8
	SUBQ.W	#1,Z_BASE
.NO8

	LEA	$FFFF8209.W,A0
	LEA	$FFFF8260.W,A4
	LEA	$FFFF820A.W,A3
	LEA	TABLO_COLORS,A6
	LEA	$FFFF8240.W,A5
	MOVEQ	#0,D1
	MOVEQ	#0,D7
	MOVEQ	#$10,D6
.SYNCHR	MOVE.B	(A0),D7
	BEQ.S	.SYNCHR
	SUB.W	D7,D6		
	LSL.W	D6,D1		
	DCB	97-3,$4E71	3?
	REPT	200
	MOVE	(A6)+,(A5)
	DCB	128-3,$4E71
	ENDR
	MOVE	#$011,$FFFF8240.W
	JSR	GERE_TABLO
	MOVE	#$700,$FFFF8240.W
	RTE

TIME	DC	0

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

FIN	MOVE.L	4.W,A0
	JMP	(A0)

EFFACE
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVE.L	D0,A1
	MOVE.L	D0,A2
	MOVE.L	D0,A3
	MOVE.L	D0,A4
	MOVE.L	D0,A5
	MOVE.L	D0,A6
	MOVE	#200-1,D7
	LEA	16+8(A0),A0
.EFF	MOVEM.L	D0-D6/A1-A6,(A0)
	MOVEM.L	D0-D6/A1-A6,4*13(A0)
	;MOVEM.L	D0-D4,4*13*2(A0)
	LEA	160(A0),A0
	DBF	D7,.EFF
	RTS
	;194    (DONNEE)
EFFACR
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVE.L	D0,A1
	MOVE.L	D0,A2
	MOVE.L	D0,A3
	MOVE.L	D0,A4
	MOVE.L	D0,A5
	MOVE.L	D0,A6
	MOVE	#200-1,D7
.EFF	MOVEM.L	D0-D6/A1-A6,(A0)
	MOVEM.L	D0-D6/A1-A6,4*13(A0)
	MOVEM.L	D0-D6/A1-A6,4*13*2(A0)
	MOVE.L	D0,4*13*3(A0)
	LEA	160(A0),A0
	DBF	D7,.EFF
	RTS
	;194    (DONNEE)

EFFAC
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVE.W	#200-1,D7
.ALL
N	SET	0
	REPT	20
	MOVE.L	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBRA	D7,.ALL
	RTS
RT_1	MOVE	(A6),(A2)+
	RTS
RT_2	MOVE	(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS

RT_15	
	MOVE	(A6),(A2)+
	MOVE	2(A6),(A2)+
	MOVE	4(A6),(A2)+
	MOVE	6(A6),(A2)+

	;MOVE	8(A6),(A2)+
	MOVE	10(A6),(A2)+
	MOVE	12(A6),(A2)+
	MOVE	14(A6),(A2)+

	MOVE	16(A6),(A2)+
	MOVE	18(A6),(A2)+
	MOVE	20(A6),(A2)+
	MOVE	22(A6),(A2)+

	;MOVE	24(A6),(A2)+
	MOVE	26(A6),(A2)+
	MOVE	28(A6),(A2)+
	MOVE	30(A6),(A2)+

	MOVE	32(A6),(A2)+
	MOVE	34(A6),(A2)+
	MOVE	36(A6),(A2)+
	MOVE	38(A6),(A2)+

	;MOVE	40(A6),(A2)+
	MOVE	42(A6),(A2)+
	MOVE	44(A6),(A2)+
	MOVE	46(A6),(A2)+

	MOVE	48(A6),(A2)+
	MOVE	50(A6),(A2)+
	MOVE	52(A6),(A2)+
	MOVE	54(A6),(A2)+

	;MOVE	56(A6),(A2)+
	MOVE	58(A6),(A2)+
	MOVE	60(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS

RT_14
	MOVE	(A6),(A2)+
	;MOVE	2(A6),(A2)+
	MOVE	4(A6),(A2)+
	MOVE	6(A6),(A2)+

	;MOVE	8(A6),(A2)+
	MOVE	10(A6),(A2)+
	MOVE	12(A6),(A2)+
	MOVE	14(A6),(A2)+

	;MOVE	16(A6),(A2)+
	MOVE	18(A6),(A2)+
	MOVE	20(A6),(A2)+
	MOVE	22(A6),(A2)+

	;MOVE	24(A6),(A2)+
	MOVE	26(A6),(A2)+
	MOVE	28(A6),(A2)+
	MOVE	30(A6),(A2)+

	;MOVE	32(A6),(A2)+
	MOVE	34(A6),(A2)+
	MOVE	36(A6),(A2)+
	MOVE	38(A6),(A2)+

	;MOVE	40(A6),(A2)+
	MOVE	42(A6),(A2)+
	MOVE	44(A6),(A2)+
	MOVE	46(A6),(A2)+

	;MOVE	48(A6),(A2)+
	MOVE	50(A6),(A2)+
	MOVE	52(A6),(A2)+
	MOVE	54(A6),(A2)+

	;MOVE	56(A6),(A2)+
	MOVE	58(A6),(A2)+
	MOVE	60(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS
RT_13
	MOVE	(A6),(A2)+
	;MOVE	2(A6),(A2)+
	MOVE	4(A6),(A2)+
	MOVE	6(A6),(A2)+

	;MOVE	8(A6),(A2)+
	MOVE	10(A6),(A2)+
	;MOVE	12(A6),(A2)+
	MOVE	14(A6),(A2)+

	;MOVE	16(A6),(A2)+
	MOVE	18(A6),(A2)+
	MOVE	20(A6),(A2)+
	MOVE	22(A6),(A2)+

	;MOVE	24(A6),(A2)+
	MOVE	26(A6),(A2)+
	;MOVE	28(A6),(A2)+
	MOVE	30(A6),(A2)+

	;MOVE	32(A6),(A2)+
	MOVE	34(A6),(A2)+
	MOVE	36(A6),(A2)+
	MOVE	38(A6),(A2)+

	;MOVE	40(A6),(A2)+
	MOVE	42(A6),(A2)+
	;MOVE	44(A6),(A2)+
	MOVE	46(A6),(A2)+

	;MOVE	48(A6),(A2)+
	MOVE	50(A6),(A2)+
	MOVE	52(A6),(A2)+
	MOVE	54(A6),(A2)+

	;MOVE	56(A6),(A2)+
	MOVE	58(A6),(A2)+
	;MOVE	60(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS
RT_12
	MOVE	(A6),(A2)+
	;MOVE	2(A6),(A2)+
	MOVE	4(A6),(A2)+
	MOVE	6(A6),(A2)+

	;MOVE	8(A6),(A2)+
	MOVE	10(A6),(A2)+
	;MOVE	12(A6),(A2)+
	MOVE	14(A6),(A2)+

	;MOVE	16(A6),(A2)+
	MOVE	18(A6),(A2)+
	;MOVE	20(A6),(A2)+
	MOVE	22(A6),(A2)+

	;MOVE	24(A6),(A2)+
	MOVE	26(A6),(A2)+
	;MOVE	28(A6),(A2)+
	MOVE	30(A6),(A2)+

	;MOVE	32(A6),(A2)+
	MOVE	34(A6),(A2)+
	;MOVE	36(A6),(A2)+
	MOVE	38(A6),(A2)+

	;MOVE	40(A6),(A2)+
	MOVE	42(A6),(A2)+
	;MOVE	44(A6),(A2)+
	MOVE	46(A6),(A2)+

	;MOVE	48(A6),(A2)+
	MOVE	50(A6),(A2)+
	;MOVE	52(A6),(A2)+
	MOVE	54(A6),(A2)+

	;MOVE	56(A6),(A2)+
	;MOVE	58(A6),(A2)+
	;MOVE	60(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS
RT_11
	MOVE	(A6),(A2)+
	;MOVE	2(A6),(A2)+
	MOVE	4(A6),(A2)+
	;MOVE	6(A6),(A2)+

	;MOVE	8(A6),(A2)+
	MOVE	10(A6),(A2)+
	;MOVE	12(A6),(A2)+
	MOVE	14(A6),(A2)+

	;MOVE	16(A6),(A2)+
	MOVE	18(A6),(A2)+
	;MOVE	20(A6),(A2)+
	;MOVE	22(A6),(A2)+

	;MOVE	24(A6),(A2)+
	MOVE	26(A6),(A2)+
	;MOVE	28(A6),(A2)+
	MOVE	30(A6),(A2)+

	;MOVE	32(A6),(A2)+
	;MOVE	34(A6),(A2)+
	;MOVE	36(A6),(A2)+
	MOVE	38(A6),(A2)+

	;MOVE	40(A6),(A2)+
	MOVE	42(A6),(A2)+
	;MOVE	44(A6),(A2)+
	MOVE	46(A6),(A2)+

	;MOVE	48(A6),(A2)+
	MOVE	50(A6),(A2)+
	;MOVE	52(A6),(A2)+
	;MOVE	54(A6),(A2)+

	;MOVE	56(A6),(A2)+
	;MOVE	58(A6),(A2)+
	;MOVE	60(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS
RT_10
	MOVE	(A6),(A2)+
	;MOVE	2(A6),(A2)+
	MOVE	4(A6),(A2)+
	;MOVE	6(A6),(A2)+

	;MOVE	8(A6),(A2)+
	MOVE	10(A6),(A2)+
	;MOVE	12(A6),(A2)+
	;MOVE	14(A6),(A2)+

	;MOVE	16(A6),(A2)+
	MOVE	18(A6),(A2)+
	;MOVE	20(A6),(A2)+
	;MOVE	22(A6),(A2)+

	;MOVE	24(A6),(A2)+
	MOVE	26(A6),(A2)+
	;MOVE	28(A6),(A2)+
	;MOVE	30(A6),(A2)+

	;MOVE	32(A6),(A2)+
	;MOVE	34(A6),(A2)+
	;MOVE	36(A6),(A2)+
	MOVE	38(A6),(A2)+

	;MOVE	40(A6),(A2)+
	;MOVE	42(A6),(A2)+
	;MOVE	44(A6),(A2)+
	MOVE	46(A6),(A2)+

	;MOVE	48(A6),(A2)+
	;MOVE	50(A6),(A2)+
	;MOVE	52(A6),(A2)+
	;MOVE	54(A6),(A2)+

	;MOVE	56(A6),(A2)+
	;MOVE	58(A6),(A2)+
	;MOVE	60(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS
RT_9
	MOVE	(A6),(A2)+
	;MOVE	2(A6),(A2)+
	MOVE	4(A6),(A2)+
	;MOVE	6(A6),(A2)+

	;MOVE	8(A6),(A2)+
	MOVE	10(A6),(A2)+
	;MOVE	12(A6),(A2)+
	;MOVE	14(A6),(A2)+

	;MOVE	16(A6),(A2)+
	MOVE	18(A6),(A2)+
	;MOVE	20(A6),(A2)+
	;MOVE	22(A6),(A2)+

	;MOVE	24(A6),(A2)+
	MOVE	26(A6),(A2)+
	;MOVE	28(A6),(A2)+
	;MOVE	30(A6),(A2)+

	;MOVE	32(A6),(A2)+
	;MOVE	34(A6),(A2)+
	;MOVE	36(A6),(A2)+
	MOVE	38(A6),(A2)+

	;MOVE	40(A6),(A2)+
	;MOVE	42(A6),(A2)+
	;MOVE	44(A6),(A2)+
	MOVE	46(A6),(A2)+

	;MOVE	48(A6),(A2)+
	;MOVE	50(A6),(A2)+
	;MOVE	52(A6),(A2)+
	;MOVE	54(A6),(A2)+

	;MOVE	56(A6),(A2)+
	;MOVE	58(A6),(A2)+
	;MOVE	60(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS
RT_8
	MOVE	(A6),(A2)+
	;MOVE	2(A6),(A2)+
	MOVE	4(A6),(A2)+
	;MOVE	6(A6),(A2)+

	;MOVE	8(A6),(A2)+
	MOVE	10(A6),(A2)+
	;MOVE	12(A6),(A2)+
	;MOVE	14(A6),(A2)+

	;MOVE	16(A6),(A2)+
	MOVE	18(A6),(A2)+
	;MOVE	20(A6),(A2)+
	;MOVE	22(A6),(A2)+

	;MOVE	24(A6),(A2)+
	MOVE	26(A6),(A2)+
	;MOVE	28(A6),(A2)+
	;MOVE	30(A6),(A2)+

	;MOVE	32(A6),(A2)+
	;MOVE	34(A6),(A2)+
	;MOVE	36(A6),(A2)+
	MOVE	38(A6),(A2)+

	;MOVE	40(A6),(A2)+
	;MOVE	42(A6),(A2)+
	;MOVE	44(A6),(A2)+
	MOVE	46(A6),(A2)+

	;MOVE	48(A6),(A2)+
	;MOVE	50(A6),(A2)+
	;MOVE	52(A6),(A2)+
	;MOVE	54(A6),(A2)+

	;MOVE	56(A6),(A2)+
	;MOVE	58(A6),(A2)+
	;MOVE	60(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS
RT_7
	MOVE	(A6),(A2)+
	;MOVE	2(A6),(A2)+
	;MOVE	4(A6),(A2)+
	;MOVE	6(A6),(A2)+

	;MOVE	8(A6),(A2)+
	MOVE	10(A6),(A2)+
	;MOVE	12(A6),(A2)+
	;MOVE	14(A6),(A2)+

	;MOVE	16(A6),(A2)+
	;MOVE	18(A6),(A2)+
	;MOVE	20(A6),(A2)+
	;MOVE	22(A6),(A2)+

	;MOVE	24(A6),(A2)+
	MOVE	26(A6),(A2)+
	;MOVE	28(A6),(A2)+
	;MOVE	30(A6),(A2)+

	;MOVE	32(A6),(A2)+
	;MOVE	34(A6),(A2)+
	;MOVE	36(A6),(A2)+
	;MOVE	38(A6),(A2)+

	;MOVE	40(A6),(A2)+
	;MOVE	42(A6),(A2)+
	;MOVE	44(A6),(A2)+
	;MOVE	46(A6),(A2)+

	;MOVE	48(A6),(A2)+
	;MOVE	50(A6),(A2)+
	;MOVE	52(A6),(A2)+
	;MOVE	54(A6),(A2)+

	;MOVE	56(A6),(A2)+
	;MOVE	58(A6),(A2)+
	;MOVE	60(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS
RT_6
RT_5
RT_4
RT_3
	MOVE	(A6),(A2)+
	;MOVE	2(A6),(A2)+
	;MOVE	4(A6),(A2)+
	;MOVE	6(A6),(A2)+

	;MOVE	8(A6),(A2)+
	;MOVE	10(A6),(A2)+
	;MOVE	12(A6),(A2)+
	;MOVE	14(A6),(A2)+

	;MOVE	16(A6),(A2)+
	;MOVE	18(A6),(A2)+
	;MOVE	20(A6),(A2)+
	;MOVE	22(A6),(A2)+

	;MOVE	24(A6),(A2)+
	;MOVE	26(A6),(A2)+
	;MOVE	28(A6),(A2)+
	;MOVE	30(A6),(A2)+

	;MOVE	32(A6),(A2)+
	;MOVE	34(A6),(A2)+
	;MOVE	36(A6),(A2)+
	;MOVE	38(A6),(A2)+

	;MOVE	40(A6),(A2)+
	;MOVE	42(A6),(A2)+
	;MOVE	44(A6),(A2)+
	;MOVE	46(A6),(A2)+

	;MOVE	48(A6),(A2)+
	;MOVE	50(A6),(A2)+
	;MOVE	52(A6),(A2)+
	;MOVE	54(A6),(A2)+

	;MOVE	56(A6),(A2)+
	;MOVE	58(A6),(A2)+
	;MOVE	60(A6),(A2)+
	MOVE	62(A6),(A2)+
	RTS

RT_16	REPT	32
	MOVE	(A6)+,(A2)+
	ENDR
	RTS

RAZTER	DC	$100,$200,$300,$400,$500,$600,$700
	DC	$701,$702,$703,$704,$705,$706,$707
	DC	$717,$727,$737,$747,$757,$767,$777
	DC	$767,$656,$545,$434,$323,$212,$101

	REPT	32
	DC.L	RT_1
	ENDR

TB
	REPT	32
	DC.L	RT_1
	ENDR
	REPT	32
	DC.L	RT_2
	ENDR
	REPT	32
	DC.L	RT_3
	ENDR
	REPT	32
	DC.L	RT_4
	ENDR
	REPT	32
	DC.L	RT_5
	ENDR
	REPT	32
	DC.L	RT_6
	ENDR
	REPT	32
	DC.L	RT_7
	ENDR
	REPT	32
	DC.L	RT_8
	ENDR
	REPT	32
	DC.L	RT_9
	ENDR
	REPT	32
	DC.L	RT_10
	ENDR
	REPT	32
	DC.L	RT_11
	ENDR
	REPT	32
	DC.L	RT_12
	ENDR
	REPT	32
	DC.L	RT_13
	ENDR
	REPT	32
	DC.L	RT_14
	ENDR
	REPT	32
	DC.L	RT_15
	ENDR
	REPT	32+32
	DC.L	RT_16
	ENDR
	
AFFICH	LEA	BUF_PTS,A0
	JSR	PROJETTE
	RTS
GERE_TABLO
	LEA	TABLO_COLORS,A0
	MOVE	#(240/2)-1,D7
.EFF_TABLO	CLR.L	(A0)+
	DBF	D7,.EFF_TABLO

	MOVE	#NB_PTS_-1,D7
	;MOVEQ	#4,D7
	LEA	COORD,A0
	LEA	TABLO_Z,A1
	LEA	FADING,A3
	LEA	TB,A4
LOOP_PT	LEA	RAZTER,A6
	LEA	TABLO_COLORS,A2
	MOVEQ	#0,D1
	MOVE	(A1)+,D1
	ADD	#375,D1
	MOVE	D1,D2
	LSL.L	#8,D1
	LSL.L	#1,D1
	DIVU	#723,D1
	ADD	D1,D1
	ADD	D1,D1
	MOVE.L	(A4,D1.W),A5

	MOVE	2(A0),D0
	ADD	D0,D0
	ADD	D0,A2

	MOVEQ	#0,D0
	MOVE	D2,D0
	DIVU	#144,D0
	ADD	D0,D0
	MOVE	(A3,D0.W),D0
	JSR	(A5)
	ADDQ	#4,A0
	DBF	D7,LOOP_PT
	RTS

PROJETTE	LEA	CORES_X,A2
	MOVE.L	SCREEN2,A3
	LEA	COEFF+380*2,A1
	LEA	COORD,A5
	LEA	TABLO_Z,A6
	MOVE.W	#NB_PTS_-1,D7
AFF_ALL	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	MOVE.W	(A0)+,D2
	ADD.W	Z_BASE,D2
	MOVE	D2,(A6)+
	ADD.W	D2,D2
	MOVE.W	(A1,D2.W),D2
	MULS.W	D2,D0
	ASR.L	#8,D0
	MULS.W	D2,D1
	ASR.L	#8,D1
MOD_X	ADDI.W	#160,D0
MOD_Y	ADDI.W	#100,D1
	MOVE	D0,(A5)+
	MOVE	D1,(A5)+
	DBRA	D7,AFF_ALL
	RTS
TABLO_Z	DCB	NB_PTS_,0
COORD	DCB	2*NB_PTS_,0

;ENTRêES : A0=TABLE DE POINTS ( .W:NB DE POINTS, ET LES POINTS)
;          A1=TABLE D'ANGLE ( ANGLE X, ANGLE Y, ANGLE Z)
;          A6=TABLE DE TRANSLATION ( U_X, U_Y, U_Z)
;          A2=TABLE DE DESTINATION POUR LES POINTS

;Formules de rotation 3 axes:
;X'=X[SIN(T)SIN(U)SIN(V)+COS(T)COS(V)]+Y[COS(T)SIN(U)SIN(V)-SIN(T)COS(V)]+Z[COS(U)SIN(V)]
;Y'=X[SIN(T)COS(U)]+Y[COS(T)COS(U)]-ZSIN(U)
;Z'=X[SIN(T)SIN(U)COS(V)-COS(T)SIN(V)]+Y[COS(T)SIN(U)COS(V)+SIN(T)SIN(V)]+Z[COS(U)COS(V)]

ROTATE	LEA	SINUS(PC),A4	;SINUS
	LEA	LONG_SINUS/4(A4),A3	;COSINUS

	MOVE.W	(A1)+,D0	;ANGLE_X
	ADD.W	D0,D0
	MOVE.W	(A3,D0.W),D1	;COSINUS TETA_X
	MOVE.W	(A4,D0.W),D0	;SINUS TETA_X

	MOVE.W	(A1)+,D2	;ANGLE_Y
	ADD.W	D2,D2
	MOVE.W	(A3,D2.W),D3	;COSINUS TETA_Y
	MOVE.W	(A4,D2.W),D2	;SINUS TETA_Y

	MOVE.W	(A1)+,D4	;ANGLE_Z
	ADD.W	D4,D4
	MOVE.W	(A3,D4.W),D5	;COSINUS TETA_Z
	MOVE.W	(A4,D4.W),D4	;SINUS TETA_Z

	LEA	MATRICE,A4
	LEA	LOGARITHMES+512*2(PC),A3
	;ON FABRIQUE SIN(T)*SIN(U)*SIN(V)+COS(T)*COS(V)
	MOVE.W	D0,D6	;D6=SIN(T)
	MULS.W	D2,D6	;D6=SIN(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D4,D6	;D6=SIN(T)*SIN(U)*SIN(V)
	MOVE.W	D1,D7	;D7=COS(T)
	MULS.W	D5,D7	;D7=COS(T)*COS(V)
	ADD.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(T)*SIN(U)*SIN(V)-SIN(T)*COS(V)
	MOVE.W	D1,D6	;D6=COS(T)
	MULS.W	D2,D6	;D6=COS(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D4,D6	;D6=COS(T)*SIN(U)*SIN(V)
	MOVE.W	D0,D7	;D7=SIN(T)
	MULS.W	D5,D7	;D7=SIN(T)*COS(V)
	SUB.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(U)*SIN(V)
	MOVE.W	D3,D6	;D6=COS(U)
	MULS.W	D4,D6	;D6=COS(U)*SIN(V)
	ASR.L	#8,D6
	ADD.W	D6,D6
	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE SIN(T)*COS(U)
	MOVE.W	D0,D6	;D6=SIN(T)
	MULS.W	D3,D6	;D6=SIN(T)*COS(U)
	ASR.L	#8,D6
	ADD.W	D6,D6
	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(T)*COS(U)
	MOVE.W	D1,D6	;D6=COS(T)
	MULS.W	D3,D6	;D6=COS(T)*COS(U)
	ASR.L	#8,D6
	ADD.W	D6,D6
	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE -SIN(U)
	MOVE.W	D2,D6
	NEG.W	D6
	ADD.W	D6,D6
	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE SIN(T)*SIN(U)*COS(V)-COS(T)*SIN(V)
	MOVE.W	D0,D6	;D6=SIN(T)
	MULS.W	D2,D6	;D6=SIN(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D5,D6	;D6=SIN(T)*SIN(U)*COS(V)
	MOVE.W	D1,D7	;D7=COS(T)
	MULS.W	D4,D7	;D7=COS(T)*SIN(V)
	SUB.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(T)*SIN(U)*COS(V)+SIN(T)*SIN(V)
	MOVE.W	D1,D6	;D6=COS(T)
	MULS.W	D2,D6	;D6=COS(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D5,D6	;D6=COS(T)*SIN(U)*COS(V)
	MOVE.W	D0,D7	;D7=SIN(T)
	MULS.W	D4,D7	;D7=SIN(T)*SIN(V)
	ADD.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(U)*COS(V)
	MULS.W	D3,D5	;D5=COS(U)*COS(V)
	ASR.L	#8,D5
	ADD.W	D5,D5
	MOVE.W	(A3,D5.W),(A4)+

	MOVE.W	(A6)+,D0
	ADD.W	D0,D0	;U_X*2
	MOVE.W	D0,TRANSLATE_X
	MOVE.W	(A6)+,D0
	ADD.W	D0,D0	;U_Y*2
	MOVE.W	D0,TRANSLATE_Y
	MOVE.W	(A6),D0
	ADD.W	D0,D0	;U_Z*2
	MOVE.W	D0,TRANSLATE_Z

	MOVE.W	(A0)+,D7	;D7=NB DE POINTS
	SUBQ.W	#1,D7
	MOVE.L	#MATRICE,D6
	LEA	BUF_EXP,A5

ROTATE_ALL	MOVE.L	D6,A4
	MOVE.W	(A0)+,D0	;X*2
TRANSLATE_X = *+2
	ADDI.W	#$1234,D0
	MOVE.W	(A0)+,D1	;Y*2
TRANSLATE_Y = *+2
	ADDI.W	#$1234,D1
	MOVE.W	(A0)+,D2	;Z*2
TRANSLATE_Z = *+2
	ADDI.W	#$1234,D2

	REPT	3
	MOVE.W	(A3,D0.W),D3	;LOG(X)
	ADD.W	(A4)+,D3	;LOG(X)+LOG(COEFF)
	MOVE.W	(A5,D3.W),D3	;D3=X*COEFF
	MOVE.W	(A3,D1.W),D4	;LOG(Y)
	ADD.W	(A4)+,D4	;LOG(Y)+LOG(COEFF)
	MOVE.W	(A5,D4.W),D4	;D4=Y*COEFF
	MOVE.W	(A3,D2.W),D5	;LOG(Z)
	ADD.W	(A4)+,D5	;LOG(Z)+LOG(COEFF)
	MOVE.W	(A5,D5.W),D5	;D5=Z*COEFF
	ADD.W	D4,D3
	ADD.W	D5,D3
	MOVE.W	D3,(A2)+
	ENDR

	DBRA	D7,ROTATE_ALL
	RTS

PREPA_LOG_EXP	;ON ARRANGE LA PARTIE NEGATIVE DE LA TABLE DES LOG.
	LEA	LOGARITHMES(PC),A0
	MOVE.W	#512-1,D7
LOG_NEGATIFS	MOVE.W	(A0),D0
	ADD.W	D0,D0
	ADDI.W	#LONG_EXP/2,D0
	MOVE.W	D0,(A0)+
	DBRA	D7,LOG_NEGATIFS
	MOVE.W	#LONG_EXP/2,D7
	MULU.W	#3,D7
	MOVE.W	D7,(A0)+	;D7=LONG_EXP*3 (ZERO)
	MOVE.W	#512-1,D7
LOG_POSITIFS	MOVE.W	(A0),D0
	ADD.W	D0,D0
	MOVE.W	D0,(A0)+	;LOG*2
	DBRA	D7,LOG_POSITIFS

;ON CREE LA PARTIE NEGATIVE ET LA 2EME PARTIE POSITIVE DES EXPONENTIELLES.
	LEA	EXPONENTIELLES(PC),A0
	LEA	BUF_EXP,A1
	LEA	LONG_EXP/2(A1),A2
	LEA	LONG_EXP/2(A2),A3
	MOVE.W	#LONG_EXP/4-1,D7
MAKE_EXP	MOVEQ	#0,D0
	MOVE.L	(A0)+,D0
	LSR.L	#8,D0	;VALEUR REELLE DU RESULTAT
	MOVE.W	D0,(A1)+
	MOVE.W	D0,(A3)+
	NEG.W	D0
	MOVE.W	D0,(A2)+
	DBRA	D7,MAKE_EXP

;ON CREE LES 2 TABLES DE ZERO LONGUES CHACUNES DE LONG_EXP.
	LEA	BUF_EXP+(LONG_EXP/2)*3,A0
	MOVE.W	#(LONG_EXP/4)*2-1,D7
MAKE_ZERO_TABLE	CLR.W	(A0)+
	DBRA	D7,MAKE_ZERO_TABLE
	RTS

POINTS
	DC	30

Y	SET	-50*15
	REPT	30
	DC	0,Y,0
Y	SET	Y+50
	ENDR

ANGLES	DC.W	0,256,0
TRANS	DC.W	0,0,0
SINUS	INCBIN	SINUSROT.DAT
LONG_SINUS = *-SINUS
	DCB.W	LONG_SINUS/4,0

LOGARITHMES	INCBIN	LOG512.LOG

EXPONENTIELLES	INCBIN	EXP512.EXP
LONG_EXP = *-EXPONENTIELLES

COEFF	INCBIN	COEFF2.3D

	DCB.W	500,0
N	SET	0
CORES_X	REPT	20
	DC.W	32768,N,16384,N,8192,N,4096,N,2048,N,1024,N,512,N,256,N,128,N,64,N,32,N,16,N,8,N,4,N,2,N,1,N
N	SET	N+8
	ENDR
	DCB.W	500,0

ADX	DC	0
ADY	DC	0
ADZ	DC	0

FADING	DC	$012,$112,$223,$334,$445,$556,$667,$777
	DCB	50,$777
	DC	$070
	SECTION	BSS
;BSS gÇnÇrale
DEB_BSS
	DS.W	2000
TABLO_COLORS	DS.W	200
	DS.W	2000

	DS.B	256
BUFFER	DS.B	32000*2
	DS.B	100
SCREEN1	DS.L	1
SCREEN2	DS.L	1
Z_BASE	DS.W	1
;Calcul des points
MATRICE	DS.W	9
BUF_EXP	DS.W	(LONG_EXP/4)*5
BUF_PTS	DS.W	500*3

END_BSS