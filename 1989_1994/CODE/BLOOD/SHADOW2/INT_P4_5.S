X1	EQU	0
X2	EQU	4
X3	EQU	8
X4	EQU	12
X5	EQU	16
X6	EQU	20
X7	EQU	24
X8	EQU	28

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	LEA	DEB_BSS,A0
	LEA	END_BSS,A1
KILL_IT	CLR.L	(A0)+
	CMP.L	A1,A0
	BLT.S	KILL_IT

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2

	MOVE.L	#BUF_BIDON,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN3

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	LEA	SHADOW+34,A2
	MOVE	#7999,D7
EFFS	MOVE.L	(A2),(A0)+
	MOVE.L	(A2)+,(A1)+
	DBF	D7,EFFS

	MOVE.L	SCREEN2,A0
	ADDQ	#6,A0
	MOVE.L	A0,A1
	MOVE	#199,D7
.HOP	MOVE.L	A1,A0
	LEA	6*8(A0),A0
	MOVE	#%0000000001111111,(A0)
	MOVE	#-1,8(A0)
	MOVE	#-1,16(A0)
	MOVE	#-1,24(A0)
	MOVE	#-1,32(A0)
	MOVE	#-1,40(A0)
	MOVE	#-1,48(A0)
	MOVE	#%1111111100000000,56(A0)
	LEA	160(A1),A1
	DBF	D7,.HOP

	MOVE.L	SCREEN1,A0
	ADDQ	#6,A0
	MOVE.L	A0,A1
	MOVE	#199,D7
.HOP2	MOVE.L	A1,A0
	LEA	6*8(A0),A0
	MOVE	#%0000000001111111,(A0)
	MOVE	#-1,8(A0)
	MOVE	#-1,16(A0)
	MOVE	#-1,24(A0)
	MOVE	#-1,32(A0)
	MOVE	#-1,40(A0)
	MOVE	#-1,48(A0)
	MOVE	#%1111111100000000,56(A0)
	LEA	160(A1),A1
	DBF	D7,.HOP2

	;MOVEM.L	SHADOW+2,D0-D3
	;MOVEM.L	D0-D3,$FFFF8240.W
	;MOVEM.L	D0-D3,$FFFF8250.W
	;MOVE	#$700,$FFFF8250.W

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	CLR	CHOICE
	MOVE	#1,CHOICE
	JSR	INIT_ROUT_POLY
	MOVE.L	#RT_COOL,TABLE_ADR_COD+40

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBL_FAD,$70.W
	CLR.W	NB_VBL
	MOVE.W	#$2300,SR

IT_VBL
	BSR	EFFAC
	BSR	AFFICH
	MOVE.L	SCREEN3,A0
	MOVE.L	A0,A1
	MOVE	#199,D7
HOP	MOVE.L	A1,A0
	LEA	6*8(A0),A0
	AND	#%1111111110000000,(A0)
	CLR	8(A0)
	CLR	16(A0)
	CLR	24(A0)
	CLR	32(A0)
	CLR	40(A0)
	CLR	48(A0)
	AND	#%0000000011111111,56(A0)
	LEA	160(A1),A1
	DBF	D7,HOP

	ADDQ	#1,TIME
	CMPI	#68,TIME
	BEQ	SUITE_DEM
	
	LEA	BUF,A0
MOD_BUF	EQU	*+2
	ADDA.L	#0,A0
	MOVE.L	SCREEN3,A1
	MOVE	#199,D7
COPI
	MOVE	(A1),(A0)+
	MOVE	8(A1),(A0)+
	MOVE	16(A1),(A0)+
	MOVE	24(A1),(A0)+
	MOVE	32(A1),(A0)+
	MOVE	40(A1),(A0)+
	MOVE	48(A1),(A0)+

	MOVE	104(A1),(A0)+
	MOVE	112(A1),(A0)+
	MOVE	120(A1),(A0)+
	MOVE	128(A1),(A0)+
	MOVE	136(A1),(A0)+
	MOVE	144(A1),(A0)+
	MOVE	152(A1),(A0)+
	LEA	160(A1),A1
	DBF	D7,COPI
	ADD.L	#5600,MOD_BUF
	BRA	IT_VBL
SUITE_DEM	ADDQ	#1,TIME
	BSR	EFFAC
	BSR	AFFICH2

	LEA	BUF2,A0
MODBUFX	EQU	*+2
	ADDA.L	#0,A0
	MOVE.L	SCREEN3,A1
	MOVE	#(20*200)-1,D7
.COP	MOVE	(A1),(A0)+
	ADDQ	#8,A1
	DBF	D7,.COP
	ADD.L	#8000,MODBUFX
	CMPI.L	#8000*30,MODBUFX
	BNE.S	.PREC
	BRA.S	SUITEX
.PREC	
	BRA	SUITE_DEM
DONNEE	DC	256
SUITEX
*********************
	CLR	TIME
	MOVE	#2,CNTFAD
IT_VBL2	BSR	VSYNC

	CMPI	#50,TIME
	BLT.S	.OUT
	LEA	FAD_2,A0
MODFAD2	EQU	*+2
	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BEQ.S	.OUT
	SUBQ	#1,CNTFAD
	BNE.S	.OUT
	MOVE	#2,CNTFAD
	MOVEM.L	(A0),D0-D3
	MOVEM.L	D0-D3,$FFFF8250.W
	ADD	#16,MODFAD2
.OUT	
	ADDQ	#1,TIME
	CMPI	#68,TIME
	BNE.S	.COOL
	;CLR.L	MOD_BUF1
	;CLR	TIME
	;CLR	MODFAD2
	;MOVE	#2,CNTFAD
	JMP	DO_XPLO
.COOL
	LEA	BUF,A0
MOD_BUF1	EQU	*+2
	ADDA.L	#0,A0
	MOVE.L	SCREEN2,A1
	LEA	SHADOW+34,A2
	MOVE	#199,D7
COPI1
	MOVE	(A0)+,(A1)
	MOVE	(A0)+,8(A1)
	MOVE	(A0)+,16(A1)
	MOVE	(A0)+,24(A1)
	MOVE	(A0)+,32(A1)
	MOVE	(A0)+,40(A1)
	MOVE	(A0)+,D0
	MOVE	48(A2),48(A1)
	OR	D0,48(A1)

	MOVE	(A0)+,D0
	MOVE	104(A2),104(A1)
	OR	D0,104(A1)
	MOVE	(A0)+,112(A1)
	MOVE	(A0)+,120(A1)
	MOVE	(A0)+,128(A1)
	MOVE	(A0)+,136(A1)
	MOVE	(A0)+,144(A1)
	MOVE	(A0)+,152(A1)
	LEA	160(A1),A1
	LEA	160(A2),A2
	DBF	D7,COPI1
	ADD.L	#5600,MOD_BUF1

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME
	BSR	SWAPEC

	BRA	IT_VBL2
DO_XPLO
	MOVE.L	SCREEN3,A1
	MOVEQ	#-1,D0
	MOVE	#199,D7
.C
N	SET	0
	REPT	20
	MOVE	D0,N(A1)
	CLR.L	N+2(A1)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	DBF	D7,.C

	MOVE.L	SCREEN3,A0
	ADDQ	#6,A0
	MOVE.L	A0,A1
	MOVE	#199,D7
.HOP	MOVE.L	A1,A0
	LEA	6*8(A0),A0
	MOVE	#%0000000001111111,(A0)
	MOVE	#-1,8(A0)
	MOVE	#-1,16(A0)
	MOVE	#-1,24(A0)
	MOVE	#-1,32(A0)
	MOVE	#-1,40(A0)
	MOVE	#-1,48(A0)
	MOVE	#%1111111100000000,56(A0)
	LEA	160(A1),A1
	DBF	D7,.HOP

;	MOVE	#$070,$FFFF8240.W
;.ZZ	CMPI.B	#$39,$FFFFFC02.W
;	BNE.S	.ZZ
;.ZZE	CMPI.B	#$39,$FFFFFC02.W
;	BEQ.S	.ZZE
;	MOVE	#$000,$FFFF8240.W

	MOVE.B	SCREEN3+1,$FFFF8201.W
	MOVE.B	SCREEN3+2,$FFFF8203.W
	JSR	VSYNC
	JSR	VSYNC
**********************************
	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A2
	MOVEQ	#-1,D0
	MOVE	#199,D7
.CZ
N	SET	0
	REPT	20
	MOVE	D0,N(A1)
	MOVE	D0,N(A2)
	CLR.L	N+2(A1)
	CLR.L	N+2(A2)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	LEA	160(A2),A2
	DBF	D7,.CZ

	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A3
	ADDQ	#6,A0
	ADDQ	#6,A3
	MOVE.L	A0,A1
	MOVE.L	A3,A4
	MOVE	#199,D7
.HOP_LA	MOVE.L	A1,A0
	MOVE.L	A4,A3
	LEA	6*8(A0),A0
	LEA	6*8(A3),A3
	MOVE	#%0000000001111111,(A0)
	MOVE	#%0000000001111111,(A3)
	MOVE	#-1,8(A0)
	MOVE	#-1,8(A3)
	MOVE	#-1,16(A0)
	MOVE	#-1,16(A3)
	MOVE	#-1,24(A0)
	MOVE	#-1,24(A3)
	MOVE	#-1,32(A0)
	MOVE	#-1,32(A3)
	MOVE	#-1,40(A0)
	MOVE	#-1,40(A3)
	MOVE	#-1,48(A0)
	MOVE	#-1,48(A3)
	MOVE	#%1111111100000000,56(A0)
	MOVE	#%1111111100000000,56(A3)
	LEA	160(A1),A1
	LEA	160(A4),A4
	DBF	D7,.HOP_LA
**********************************

;	MOVE	#$700,$FFFF8240.W
;.ZZZ	CMPI.B	#$39,$FFFFFC02.W
;	BNE.S	.ZZZ
;.ZZEZ	CMPI.B	#$39,$FFFFFC02.W
;	BEQ.S	.ZZEZ
;	MOVE	#$000,$FFFF8240.W

	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	JSR	VSYNC
	JSR	VSYNC

IT_VBL3
	BSR	VSYNC
	SF	$FFFF8240.W

	LEA	BUF2,A0
MODBUF2X	EQU	*+2
	ADDA.L	#0,A0
	MOVE.L	SCREEN2,A1

	MOVE	#200-1,D7
.COP	
N	SET	0
	REPT	20
	MOVE	(A0)+,N+6(A1)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	DBF	D7,.COP

	ADD.L	#8000,MODBUF2X
	CMPI.L	#8000*30,MODBUF2X
	BNE.S	.AFF_P
	MOVE	#$666,$FFFF8252.W
.STOP	BRA.S	.STOP
.AFF_P	BSR	SWAPEC
	LEA	COLORS,A0
MODC	EQU	*+2
	LEA	0(A0),A0
	MOVE	(A0),$FFFF8252.W
	ADDQ	#2,MODC
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME	
	BRA	IT_VBL3

COLORS	DCB	20,$556
	DCB	15,$667

TIME	DC	0
VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS
CNTFAD	DC	3
VBL_FAD	ADDQ.W	#1,NB_VBL
	MOVEM.L	D0-D3/A0,-(SP)
	LEA	FAD_INIT,A0
MODFAD	EQU	*+2
	LEA	0(A0),A0
	CMPI	#$1234,(A0)
	BNE.S	.E
	MOVE.L	#VBLR,$70.W
	MOVEM.L	(SP)+,D0-D3/A0
	JMP	VBLR
.E	SUBQ	#1,CNTFAD
	BNE.S	.OUT
	MOVE	#3,CNTFAD
	MOVEM.L	(A0),D0-D3
	MOVEM.L	D0-D3,$FFFF8240.W
	MOVEM.L	D0-D3,$FFFF8250.W
	ADD	#16,MODFAD
.OUT	
	MOVEM.L	(SP)+,D0-D3/A0
	RTE
VBLR	ADDQ.W	#1,NB_VBL
	RTE

FIN	MOVE.L	4.W,A0
	JMP	(A0)

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

EFFAC
	MOVE.L	SCREEN3,A0
	MOVEQ	#0,D0
	MOVE.W	#200-1,D7
.ALL
N	SET	0
	REPT	20
	MOVE.W	D0,N(A0)
N	SET	N+8
	ENDR
	LEA	160(A0),A0
	DBRA	D7,.ALL
	RTS

ADRESSES	
	DC.L	DATAS_LINES
	DC.L	DATAS_LINES+4*40
	DC.L	DATAS_LINES+4*80
	DC.L	DATAS_LINES+4*120
	DC.L	DATAS_LINES+4*160
	DC.L	DATAS_LINES+4*200
	DC.L	DATAS_LINES+4*240
	DC.L	DATAS_LINES+4*280
	DC.L	DATAS_LINES+4*320
	DC.L	DATAS_LINES+4*360

FLAGS	DC	1,1,1,1,1,1,1,1,1,1
TEMPOS	DC	5
	DC	10
	DC	30
	DC	20
	DC	32
	DC	15
	DC	29
	DC	34
	DC	21
	DC	36
AFFICH
	LEA	TEMPOS,A0
	LEA	FLAGS,A1
	MOVEQ	#9,D7
.GERE_TEMPOS	
	TST	(A0)
	BEQ.S	.NUL
	SUBQ	#1,(A0)
	BNE.S	.NUL
	CLR	(A1)
.NUL	ADDQ	#2,A1
	ADDQ	#2,A0
	DBF	D7,.GERE_TEMPOS

	;MOVE	#$5446,MOD_PLAN
	MOVE	#$4E71,MOD_PLAN

	LEA	ADRESSES,A1
	LEA	FLAGS,A3
	MOVEQ	#10-1,D7
GERE_ONE	
	MOVE.L	(A1),A0

	ADDQ	#4,A0
	CMP.L	#DATAS_LINES+1604,A0
	BLT.S	.OK
	LEA	DATAS_LINES,A0
.OK	MOVE.L	A0,(A1)+

	MOVE	(A3)+,D0
	BNE	SAUT
	MOVEM.L	D7/A1/A3,-(SP)

	lea	face_en_cours+2,a1
	move.l	(a0),(a1)
	ADD	#160,(A1)+
	ADD	#100,(A1)+

	MOVE	#160,(A1)+
	MOVE	#100,(A1)+
MOD_INC	EQU	*+2
	LEA	4*6(A0),A2
	CMP.L	#DATAS_LINES+1604,A2
	BLT.S	.OK2
	LEA	-1604(A2),A2
.OK2	move.l	(a2),(a1)
	ADD	#160,(A1)+
	ADD	#100,(A1)+

	lea	face_en_cours,a0
	JSR	RT_POLY

	MOVEM.L	(SP)+,D7/A1/A3
SAUT	DBF	D7,GERE_ONE
	
	CMPI	#4*45,MOD_INC
	BEQ.S	.NO_INC
	CMPI	#30,TIME
	BLT.S	.NO_INC
	ADDQ	#4,MOD_INC
.NO_INC
	RTS
face_en_cours	dC.w	3
	ds.l	4

AFFICH2	MOVE	#4,face_en_cours
	MOVE	#$4E71,MOD_PLAN

	lea	XPLO,a0
	LEA	VECTEURS,A2
	MOVEQ	#18-1,D6
.AFF_A_POLY	MOVE	DONNEE,D1
	lea	face_en_cours+2,a1

	REPT	4
	MOVE	(A0),D0
	SUB	#160,D0
	MULS	D1,D0
	LSR.L	#8,D0
	ADD	#160,D0
	ADD	(A2),D0
	MOVE	D0,(A0)+
	MOVE	D0,(A1)+

	MOVE	(A0),D0
	SUB	#100,D0
	MULS	D1,D0
	LSR.L	#8,D0
	ADD	#100,D0
	ADD	2(A2),D0
	MOVE	D0,(A0)+
	MOVE	D0,(A1)+
	ENDR
	ADDQ	#4,A2
	MOVEM.L	D6/A0/A2,-(SP)
	lea	face_en_cours,a0
	JSR	RT_POLY
	MOVEM.L	(SP)+,D6/A0/A2

	DBF	D6,.AFF_A_POLY
	ADD	#1,DONNEE
	RTS

*** Partie affichage
CHOICE	DC	0	;0=MOVE
			;1=OR
INIT_ROUT_POLY	LEA	BUF_COD_GEN,A0
	LEA	TABLE_ADR_COD,A1

	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D3
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D3,.GO

	MOVE.W	#$FFFF,D0	;1ER MOTIF
	MOVE.W	#$8000,D1	;DERNIER MOTIF
	MOVEQ	#0,D3	;DECALAGE

	MOVEQ	#16-1,D6
.DECAL	
	MOVEQ	#16+1,D5
	ADD.W	#18*16,D5	;D5=LONGUEUR DE LA LIGNE
	ADD.W	D3,D5

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
.COPY_MOVED7	BSR	POZ_ADRESSE
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	SUBI.W	#16,D5
	DBRA	D7,.COPY_MOVED7

	MOVE.W	#16+1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_1
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_1
.PAS_D7_1
	TST	CHOICE
	BNE.S	.OR1
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV1
.OR1	MOVE	#$0054,(A0)+	OR.W #$Imm,(A4)
.MOV1	MOVE.W	D0,(A0)+
.RETOUR_1	MOVE.W	#1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_2
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_2
.PAS_D7_2
	TST	CHOICE
	BNE.S	.OR2
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV2
.OR2	MOVE	#$0055,(A0)+	OR "             "
.MOV2	MOVE.W	D1,(A0)+
.RETOUR_2
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	ADDQ.W	#1,D3	;LONGUEUR SUIVANT
	DBRA	D6,.DECAL

	LEA	TABLE_ADR_COD+1320,A1

	MOVE.W	#$7FFF,D3	;1ER MOTIF
	MOVEQ	#14,D5

	MOVEQ	#14,D2
ALL_RT	
	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D0
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D0,.GO

	MOVE.W	#1,A3

	MOVEQ	#0,D0
	MOVE.W	D5,D6
LITTLE	BSR	POZ_ADRESSE2
	BSET	D6,D0
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_LITTLE
	MOVE.W	#$3887,(A0)+
	BRA.S	.RETOUR_LITTLE
.PAS_D7_LITTLE
	TST	CHOICE
	BNE.S	.OR3
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV3
.OR3	MOVE	#$0054,(A0)+
.MOV3	MOVE.W	D0,(A0)+

.RETOUR_LITTLE
	BSR	COPY_RT_COOL

	ADDQ.W	#1,A3
	DBRA	D6,LITTLE

	MOVE.W	D3,D0
	MOVE.W	#$8000,D1	;DERNIER MOTIF

	MOVEQ	#16-1,D6
DECAL
	MOVE.W	#18*16,A3
	ADD.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
COPY_MOVED7	BSR	POZ_ADRESSE2
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	LEA	-16(A3),A3
	DBRA	D7,COPY_MOVED7

	MOVE.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3
	BSR	POZ_ADRESSE2
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_3
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_3
.PAS_D7_3
	TST	CHOICE
	BNE.S	.OR4
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV4
.OR4	MOVE	#$0054,(A0)+
.MOV4	MOVE.W	D0,(A0)+
.RETOUR_3	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_4
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_4
.PAS_D7_4
	TST	CHOICE
	BNE.S	.OR5
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV5
.OR5	MOVE	#$0055,(A0)+
.MOV5	MOVE.W	D1,(A0)+
.RETOUR_4
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	DBRA	D6,DECAL

	SUBQ.W	#1,D5
	LSR.W	#1,D3
	LEA	1280(A1),A1
	DBRA	D2,ALL_RT

*******
TBL4:	LEA	TABLE_A4,A0
	MOVE	#639,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	D6,(A0)+
	MOVE	(A1),(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#639,D7
.FINT	MOVE	#319*4,(A0)+
	MOVE	#152,(A0)+
	;CLR	(A0)+
	DBF	D7,.FINT
TBL2:
	LEA	TABLE_A2,A0
	MOVE	#639,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	(A1),(A0)+
	MOVE	D6,(A0)
	MOVE	D6,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#639,D7
.FINT	;CLR	(A0)+
	MOVE	#152,(A0)+
	MOVE	#319*4,(A0)+
	DBF	D7,.FINT

	RTS

MINI_TABLE
N	SET	0
	REPT	20
	DC	N
N	SET	N+8
	ENDR


POZ_ADRESSE2	MOVEM.L	D5/A0,-(SP)
	MOVE.W	A3,D5
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS
	

POZ_ADRESSE	MOVEM.L	D5/A0,-(SP)
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS
	
COPY_RT_COOL	LEA	RT_COOL,A6
	MOVE.W	#LONG_RT_COOL,D7
.COPY	MOVE.W	(A6)+,(A0)+
	DBRA	D7,.COPY
	RTS

RT_COOL	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2

	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1

	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1

	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE... RUSê, HEIN?
	   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE, ZOU.
	MOVE.L	(A6,D3.W),A2	;5
	JMP	(A2)	;2

;TOTAL:31 NOPS. +58=89 NOPS.
LONG_RT_COOL = ((*-RT_COOL)/2)-1


RT_POLY	MOVE.L	A0,ADR_DEPART

	LEA	2(A0),A1	;A1 POINTE SUR LES POINTS
	MOVE.W	(A1),D3
	SUB.W	4(A1),D3
	MOVE.W	2(A1),D4
	SUB.W	6(A1),D4
	MOVE.W	8(A1),D6
	SUB.W	4(A1),D6
	MOVE.W	$A(A1),D5
	SUB.W	6(A1),D5
	MULS.W	D3,D5
	MULS.W	D6,D4
	SUB.L	D4,D5
	BGT.S	.VISIBLE
	RTS
.VISIBLE
;	LEA	REMP_GAUCHE,A1
;	MOVE	#(500/2)-1,D6
;.EFF_TBX	CLR.L	(A1)+
;	DBF	D6,.EFF_TBX

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7

	MOVE.W	(A0)+,D0	;NB DE POINTS COMPOSANT LA SURFACE
	LEA	BUF_EXAM,A1
	MOVE.L	A0,A2
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_PTS
.COPYING	MOVE.L	(A0)+,(A1)+
	DBRA	D0,.COPYING
	MOVE.L	(A2),(A1)+

	LEA	BUF_EXAM,A0
	LEA	BUF_GAUCHE,A2
	LEA	BUF_DROIT,A3
	ADDQ.W	#4,A0
	MOVE.W	-2(A0),D3	;Y DE REFERENCE
NB_PTS = *+2
	MOVE.W	#$1234,D0
	MOVEQ	#-1,D6
	MOVEQ	#-1,D7
ALL_PTS	MOVE.W	(A0),D1	;X
	MOVE.W	2(A0),D2	;Y
	CMP.W	D2,D3
	BGE.S	ON_MONTE
ON_DESCEND	MOVE.L	-4(A0),(A2)+
	MOVE.L	(A0)+,(A2)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D6
	DBRA	D0,ALL_PTS
	BRA.S	FINITO
ON_MONTE	MOVE.L	-4(A0),(A3)+
	MOVE.L	(A0)+,(A3)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D7
	DBRA	D0,ALL_PTS
FINITO	;ICI LES TABLEAU BUF_GAUCHE ET BUF_DROIT SONT CONSTITUêS
	;D6=NB DE PTS-1 DANS BUF_GAUCHE
	;D7=NB DE PTS-1 DANS BUF_DROIT
	LEA	BUF_GAUCHE,A0
MAKE_REMP_GAUCHE
	LEA	REMP_GAUCHE,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	SUB.W	D1,D3	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D1,D1
	ADDA.W	D1,A1

	ADDQ.W	#1,D3
	SUB.W	D0,D2
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#2,D3
	MOVE.W	D0,(A1)+
.PUT	MOVE.W	D0,(A1)+
	DBRA	D3,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D3,D2
	EXT.L	D2
	SWAP	D2
	ROL.L	#8,D2
	SUBQ.W	#2,D3
	MOVE.W	D0,(A1)+
.MK_X	ADDX.L	D2,D0
	MOVE.W	D0,(A1)+
	DBRA	D3,.MK_X
.NEXT	DBRA	D6,MAKE_REMP_GAUCHE

	LEA	BUF_DROIT,A0
MAKE_REMP_DROIT	LEA	REMP_DROIT,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	MOVE.W	D1,D4
	SUB.W	D3,D1	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D4,D4
	ADDA.W	D4,A1

	ADDQ.W	#1,D1
	SUB.W	D0,D2
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#2,D1
	MOVE.W	D0,(A1)
.PUT	MOVE.W	D0,-(A1)
	DBRA	D1,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D1,D2
	EXT.L	D2
	SWAP	D2
	ROL.L	#8,D2
	SUBQ.W	#2,D1
	MOVE.W	D0,(A1)
.MK_X	ADDX.L	D2,D0
	MOVE.W	D0,-(A1)
	DBRA	D1,.MK_X
.NEXT	DBRA	D7,MAKE_REMP_DROIT
	MOVEQ	#0,D1
ADR_DEPART= *+2
	LEA	$12345678,A0
	MOVE.W	(A0)+,D7
	SUBQ.W	#1,D7
	MOVE.W	#-1000,D0	;MAXIMUM
	MOVE.W	#20000,D1	;MINIMUM
.TEST_MAX	CMP.W	2(A0),D0
	BGE.S	.FUCK
	MOVE.W	2(A0),D0
.FUCK	CMP.W	2(A0),D1
	BLE.S	.FUCK_2
	MOVE.W	2(A0),D1
.FUCK_2	ADDQ.W	#4,A0
	DBRA	D7,.TEST_MAX
	;D0 = Y MAXIMUM
	;D1 = Y MINIMUM
	CMPI.W	#200,D0
	BLT.S	.OK_Y_MAX
	MOVE.W	#199,D0
.OK_Y_MAX	TST.W	D1
	BGE.S	.OK_Y_MIN
	MOVEQ	#0,D1
.OK_Y_MIN

INIT	LEA	REMP_GAUCHE,A0
	LEA	REMP_DROIT,A1
	ADDQ.W	#1,D0
	ADD.W	D0,D0
	MOVE.W	#15,(A1,D0.W)
	MOVE.W	#25,(A0,D0.W)
	ADD.W	D1,D1
	ADDA.W	D1,A0
	ADDA.W	D1,A1
	MULU.W	#80,D1       ;D1=Y_MIN*160 (POUR ADD ECRAN)

	LEA	TABLE_ADR_COD+40,A6
	MOVE	#-1,D7
	MOVE.L	SCREEN3,D6
MOD_PLAN	NOP
	ADD.L	D1,D6	;...+Y_MIN*160
	SUBI.L	#160,D6
	MOVE.L	#TABLE_A2+640*4,D4
	MOVE.L	#160,D5
	LEA	TABLE_A4+640*4,A3
	MOVEQ	#0,D2

	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2
	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1
	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1
	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE*1320... RUSê, HEIN?   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE*1320, ZOU.
	MOVE.L	(A6,D3.W),A2
	JMP	(A2)

RETOUR_POLY	RTS

CLIPPING_Y	MOVEM.L	D4/D6/A6,-(SP)
	MOVEQ	#0,D5
	LEA	TABLE_Y,A6
	ADD.W	D1,D1
	ADD.W	D3,D3
	MOVEQ	#0,D4
	ADD.W	(A6,D1.W),D4
	ADD.W	(A6,D3.W),D4
	ASR.W	#1,D1
	ASR.W	#1,D3
	TST.W	D4
	BEQ	FIN_NORMALE
	CMPI.W	#-2,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#4,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#-1,D4
	BEQ	CLIP_HAUT
	CMPI.W	#2,D4
	BEQ	CLIP_BAS

CLIP_HAUT_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	BRA.S	SUIT
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1

SUIT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_HAUT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

RIEN_DU_TOUT	MOVE.W	#-1,D5
FIN_NORMALE	MOVEM.L	(SP)+,D4/D6/A6
	RTS

	DCB.W	640,-1
TABLE_Y	DCB.W	200,0
	DCB.W	640,2

DATAS_LINES	;1604 BYTES
	INCBIN	DATA.LIG

SHADOW	INCBIN	SHADOW.PI1
FAD_INIT
	DC	$777,$777,$777,$777,$777,$777,$777,$777
	DC	$666,$777,$777,$777,$777,$777,$777,$777
	DC	$555,$777,$777,$777,$777,$777,$666,$777
	DC	$444,$777,$777,$777,$777,$666,$555,$777
	DC	$333,$777,$777,$777,$666,$555,$444,$777
	DC	$222,$777,$777,$666,$555,$444,$333,$777
	DC	$111,$777,$666,$555,$444,$333,$222,$777
	DC	$000,$666,$555,$444,$333,$222,$111,$777
	DC	$1234
FAD_2
	DC	$000,$666,$555,$444,$333,$222,$111,$777
	DC	$112,$556,$556,$445,$334,$223,$112,$667
	DC	$223,$556,$556,$556,$445,$334,$223,$556
	DC	$334,$556,$556,$556,$556,$445,$334,$556
	DC	$445,$556,$556,$556,$556,$556,$445,$556
	DC	$556,$556,$556,$556,$556,$556,$556,$556
	DC	$1234

XPLO	INCBIN	XPLO.DAT
VECTEURS	

	DC	1,2
	DC	3,-1
	DC	3,2
	DC	2,1
	DC	2,2
	DC	1,-1
	DC	-2,2
	DC	2,2
	DC	1,1
	DC	-1,-2
	DC	-3,-1
	DC	-3,-2
	DC	-2,-1
	DC	-2,-2
	DC	-1,-4
	DC	-3,-2
	DC	-2,-1
	DC	-1,-1

	SECTION	BSS
;BSS gÇnÇrale
DEB_BSS
	DS.B	256
BUFFER	DS.B	32000*2
	DS.B	256
BUF_BIDON	DS.B	32000
	DS.B	100
SCREEN1	DS.L	1
SCREEN2	DS.L	1
SCREEN3	DS.L	1
NB_VBL	DS.W	1
;Routine d'affichage
TABLE_A4	DS.L	2*320*3
TABLE_A2	DS.L	2*320*3
BUF_EXAM	DS.W	2*50
BUF_GAUCHE	DS.W	4*50
BUF_DROIT	DS.W	4*50
REMP_GAUCHE	DS.W	250
REMP_DROIT	DS.W	250
TABLE_ADR_COD	DS.B	(1280+40)*16
BUF_COD_GEN	DS.B	39000
BUF	DS.B	5600*70
BUF2	DS.B	8000	*30
END_BSS