MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	INIT_SYSTEME
	MOVEQ	#0,D0
	MOVEQ	#7,D7
	MOVE.W	#$8240,A0
.F	MOVE.L	D0,(A0)+
	DBRA	D7,.F
	MOVE.W	#$122,$FFFF8242.W
	LEA	TEXT_1,A0
	BSR	PUT_TEXT

	BSR	INIT_ROUT_POLY
	MOVE.L	#RT_COOL,TABLE_ADR_COD+40
	BSR	PREPARE_TILT
	BSR	PREPARE_FAIT
	CLR.W	PT

	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	ADDQ.W	#2,A0
	ADDQ.W	#2,A1
	MOVEQ	#-1,D0
	MOVEQ	#62-1,D6
.AFF2	ADDQ.W	#8,A0
	ADDQ.W	#8,A1
	MOVE.W	#18-1,D7
.AFF	MOVE.W	D0,(A0)
	ADDQ.W	#8,A0
	MOVE.W	D0,(A1)
	ADDQ.W	#8,A1
	DBRA	D7,.AFF
	ADDQ.W	#8,A0
	ADDQ.W	#8,A1
	DBRA	D6,.AFF2

	MOVE.W	#$2700,SR
	MOVE.L	#INTER_RTE,$68.W
	MOVE.L	#INTER_RTE,$120.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

IT_VBL	BSR	VSYNC

	MOVE.W	PT,D0
	CMPI.W	#25,D0
	BLT.S	.OKK
	MOVEQ	#24,D0
.OKK	MULU.W	#8000,D0
	LEA	BUF_SOV,A0
	ADDA.L	D0,A0
	MOVE.L	SCREEN2,A1
	LEA	160*60(A1),A1
	MOVE.W	#200-1,D7
N	SET	0
.PUT	REPT	20
	MOVE.W	(A0)+,N(A1)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	DBRA	D7,.PUT

	MOVE.W	PT,D0
	ADDQ.W	#1,D0
	CMPI.W	#28,D0
	BLT.S	.OK
	CLR.W	PT
	BRA.S	IT_VBL2
.OK	MOVE.W	D0,PT
	
	BSR	SWAPEC

	BRA	IT_VBL

IT_VBL2	BSR	VSYNC

	MOVE.W	PT,D0
	CMPI.W	#25,D0
	BLT.S	.GG
	MOVEQ	#24,D0
.GG	MULU.W	#4000,D0
	LEA	BUF_SOV+200000,A0
	ADDA.L	D0,A0
	MOVE.L	SCREEN2,A1
	MOVE.W	#100-1,D7
N	SET	0
.PUT	REPT	20
	MOVE.W	(A0)+,N(A1)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	DBRA	D7,.PUT

	MOVE.W	PT,D0
	ADDQ.W	#1,D0
	CMPI.W	#28,D0
	BLT.S	.OK
	CLR.W	PT
	MOVE.L	#FADE_FAIT,PT_FADE
	CLR.W	TEMPO
	BRA.S	IT_VBL3
.OK	MOVE.W	D0,PT
	
	BSR	SWAPEC

	BRA	IT_VBL2

IT_VBL3	BSR	VSYNC

	TST.W	TEMPO
	BGT.S	.NOT_NOW
	MOVE.W	#3,TEMPO
	MOVE.L	PT_FADE,A0
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	CMPI.W	#$1234,(A0)
	BEQ.S	.NO
	MOVE.L	A0,PT_FADE
.NO	MOVE.W	D0,$FFFF8248.W
	MOVE.W	D1,$FFFF824A.W
	MOVE.W	D0,$FFFF824C.W
	MOVE.W	D1,$FFFF824E.W
.NOT_NOW	SUBQ.W	#1,TEMPO

	MOVE.W	PT,D0
	MULU.W	#4000,D0
	LEA	BUF_FAIT,A0
	ADDA.L	D0,A0
	MOVE.L	SCREEN2,A1
	LEA	4+160*86(A1),A1
	MOVE.W	#100-1,D7
N	SET	0
.PUT	REPT	20
	MOVE.W	(A0)+,N(A1)
N	SET	N+8
	ENDR
	LEA	160(A1),A1
	DBRA	D7,.PUT

	MOVE.W	PT,D0
	ADDQ.W	#1,D0
	CMPI.W	#60,D0
	BLT.S	.OK
	MOVEQ	#0,D0
.OK	MOVE.W	D0,PT
	
	BSR	SWAPEC

	BRA	IT_VBL3

FADE_FAIT	DC.W	$002,$770
	DC.W	$012,$770
	DC.W	$122,$770
	DC.W	$232,$770
	DC.W	$342,$770
	DC.W	$452,$770
	DC.W	$562,$770
	DC.W	$762,$762
	DC.W	$1234

PT_FADE	DS.L	1
TEMPO	DS.W	1

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	MOVE.B	#0,$FFFFFA19.W
	MOVE.B	#99,$FFFFFA1F.W       ; BORDER HAUT
	MOVE.B	#4,$FFFFFA19.W
	BCLR	#3,$FFFFFA17.W
	MOVE.L	#INTER_TMA,$134.W
	ORI.B	#$20,$FFFFFA13.W
	ORI.B	#$20,$FFFFFA07.W
	;ICI 33 LIGNES DE LIBRES

	MOVE.W	#$02,$FFFF8240.W
	MOVE.W	#$770,$FFFF8242.W
	MOVE.W	#$756,$FFFF8244.W
	MOVE.W	#$770,$FFFF8246.W

INTER_RTE	RTE

INTER_TMB	DCB.W	120,$4E71
	CLR.B	$FFFF820A.W
	DCB.W	14,$4E71
	MOVE.B	#2,$FFFF820A.W
	CLR.B	$FFFFFA1B.W
	RTE

INTER_TMA:	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE	#$2100,SR

	MOVEQ	#16,D0
	LEA	$FFFF820A.W,A6
	LEA	$FFFF8260.W,A3

	MOVEQ	#2,D3
	MOVEQ	#0,D4

	STOP	#$2100

	MOVE	#$2700,SR
	MOVE.W	#$2300,(SP)

	MOVEQ	#29,D2
SYNCHROA:	DBF	D2,SYNCHROA
	NOP

	MOVE.B	D4,(A6)	;820A
	REPT	6
	NOP
	ENDR
	MOVE.B	D3,(A6)	;820A

	MOVE.L	#INTER_TMB,$120.W
	ORI.B	#1,$FFFFFA07.W
	ORI.B	#1,$FFFFFA13.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.B	#229,$FFFFFA21.W
	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREPARE_FAIT	LEA	FAIT,A0
	MOVE.L	A0,PT
	MOVE.L	#BUF_FAIT,ADR_SOV

	MOVEQ	#60-1,D7
.REPRISE	MOVE.W	D7,.SOV+2
	BSR	AFF_POLY_4
	BSR	AFF_POLY_4
	BSR	AFF_POLY_4
	BSR	AFF_POLY_5
	BSR	AFF_POLY_5
	BSR	AFF_POLY_4
	BSR	AFF_POLY_4
	BSR	AFF_POLY_4
	BSR	AFF_POLY_4
	BSR	SOV_SCREEN2
	BSR	EFFAC_TEMP
.SOV	MOVE.W	#$1234,D7
	DBRA	D7,.REPRISE
	RTS

AFF_POLY_4	MOVE.L	PT,A1
	LEA	PUT_COOR,A0
	MOVE.W	#4,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE.L	A1,PT
	LEA	PUT_COOR,A0
	MOVE.L	#$12345678,D0
	BSR	RT_POLY
	CMPI.L	#$12345678,D0
	BNE.S	.FINI
	LEA	PUT_COOR,A0
	MOVE.W	#4,(A0)+
	MOVEM.L	(A0),D0-D3
	MOVE.L	D3,(A0)+
	MOVE.L	D2,(A0)+
	MOVE.L	D1,(A0)+
	MOVE.L	D0,(A0)+
	LEA	PUT_COOR,A0
	BSR	RT_POLY
.FINI	RTS

AFF_POLY_5	MOVE.L	PT,A1
	LEA	PUT_COOR,A0
	MOVE.W	#5,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE.L	A1,PT
	LEA	PUT_COOR,A0
	MOVE.L	#$12345678,D0
	BSR	RT_POLY
	CMPI.L	#$12345678,D0
	BNE.S	.FINI
	LEA	PUT_COOR,A0
	MOVE.W	#5,(A0)+
	MOVEM.L	(A0),D0-D4
	MOVE.L	D4,(A0)+
	MOVE.L	D3,(A0)+
	MOVE.L	D2,(A0)+
	MOVE.L	D1,(A0)+
	MOVE.L	D0,(A0)+
	LEA	PUT_COOR,A0
	BSR	RT_POLY
.FINI	RTS

PREPARE_TILT
	LEA	PUT_COOR,A0
	MOVE.W	#4,(A0)+
	CLR.L	(A0)+
	CLR.L	(A0)+
	CLR.L	(A0)+
	CLR.L	(A0)+

	MOVE.L	#TILT_BAS,PT
	MOVE.L	#BUF_SOV,ADR_SOV

	MOVEQ	#24-1,D7
.REPRISE	MOVE.W	D7,SOV
	BSR	EFFAC_TEMP
	REPT	7
	MOVE.L	PT,A0
	LEA	PUT_COOR+2,A1
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.L	A0,PT
	LEA	PUT_COOR,A0
	BSR	RT_POLY
	ENDR
	BSR	SOV_SCREEN
SOV = *+2
	MOVE.W	#$1234,D7
	DBRA	D7,.REPRISE
	BSR	EFFAC_TEMP
	MOVEQ	#100,D0
	BSR	LAST_ONE
	BSR	SOV_SCREEN

	MOVE.L	#TILT_HAUT,PT
;	MOVE.L	#BUF_SOV+100000,ADR_SOV

	MOVEQ	#24-1,D7
.REPRISE2	MOVE.W	D7,SOV2
	BSR	EFFAC_TEMP
	REPT	7
	MOVE.L	PT,A0
	LEA	PUT_COOR+2,A1
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	MOVE.L	A0,PT
	LEA	PUT_COOR,A0
	BSR	RT_POLY
	ENDR
	BSR	SOV_SCREEN2
SOV2 = *+2
	MOVE.W	#$1234,D7
	DBRA	D7,.REPRISE2
	MOVEQ	#0,D0
	BSR	EFFAC_TEMP
	BSR	LAST_ONE
	BSR	SOV_SCREEN2
	BSR	EFFAC_TEMP

	RTS

SOV_SCREEN	MOVE.L	ADR_SOV,A0
	MOVE.L	SCREEN2,A1
	MOVE.W	#20*200-1,D7
.COPY_X	MOVE.W	(A1),(A0)+
	ADDQ.W	#8,A1
	DBRA	D7,.COPY_X
	MOVE.L	A0,ADR_SOV
	RTS

SOV_SCREEN2	MOVE.L	ADR_SOV,A0
	MOVE.L	SCREEN2,A1
	MOVE.W	#20*100-1,D7
.COPY_X	MOVE.W	(A1),(A0)+
	ADDQ.W	#8,A1
	DBRA	D7,.COPY_X
	MOVE.L	A0,ADR_SOV
	RTS

EFFAC_TEMP	MOVE.L	SCREEN2,A1
	MOVEQ	#0,D0
	MOVE.W	#20*200-1,D7
.COPY_X	MOVE.W	D0,(A1)
	ADDQ.W	#8,A1
	DBRA	D7,.COPY_X
	RTS

LAST_ONE	LEA	POLY_1+2,A0
	BSR	AFF_TEMP
	LEA	POLY_2+2,A0
	BSR	AFF_TEMP
	LEA	POLY_3+2,A0
	BSR	AFF_TEMP
	LEA	POLY_4+2,A0
	BSR	AFF_TEMP
	LEA	POLY_5+2,A0
	BSR	AFF_TEMP
	LEA	POLY_6+2,A0
	BSR	AFF_TEMP
	LEA	POLY_7+2,A0
	BSR	AFF_TEMP
	RTS

AFF_TEMP	LEA	PUT_COOR+2,A1
	REPT	4
	MOVE.W	(A0)+,(A1)+
	MOVE.W	(A0)+,D1
	ADD.W	D0,D1
	MOVE.W	D1,(A1)+
	ENDR
	MOVE.W	D0,-(SP)
	LEA	PUT_COOR,A0
	BSR	RT_POLY
	MOVE.W	(SP)+,D0
	RTS

	INCLUDE	TEXTER.S

INIT_ROUT_POLY	LEA	BUF_COD_GEN,A0
	LEA	TABLE_ADR_COD,A1

	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D3
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D3,.GO

	MOVE.W	#$FFFF,D0	;1ER MOTIF
	MOVE.W	#$8000,D1	;DERNIER MOTIF
	MOVEQ	#0,D3	;DECALAGE

	MOVEQ	#16-1,D6
.DECAL	
	MOVEQ	#16+1,D5
	ADD.W	#18*16,D5	;D5=LONGUEUR DE LA LIGNE
	ADD.W	D3,D5

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
.COPY_MOVED7	BSR	POZ_ADRESSE
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	SUBI.W	#16,D5
	DBRA	D7,.COPY_MOVED7

	MOVE.W	#16+1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_1
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_1
.PAS_D7_1	MOVE.W	#$54,(A0)+	;ORI.W #$Imm,(A4)
;	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	MOVE.W	D0,(A0)+
.RETOUR_1	MOVE.W	#1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_2
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_2
.PAS_D7_2	MOVE.W	#$55,(A0)+	;ORI.W #$Imm,(A5)
;	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	MOVE.W	D1,(A0)+
.RETOUR_2
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	ADDQ.W	#1,D3	;LONGUEUR SUIVANT
	DBRA	D6,.DECAL

	LEA	TABLE_ADR_COD+1320,A1

	MOVE.W	#$7FFF,D3	;1ER MOTIF
	MOVEQ	#14,D5

	MOVEQ	#14,D2
ALL_RT	
	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D0
.GO	MOVE.L	#RT_COOL,(A1)+
	DBRA	D0,.GO

	MOVE.W	#1,A3

	MOVEQ	#0,D0
	MOVE.W	D5,D6
LITTLE	BSR	POZ_ADRESSE2
	BSET	D6,D0
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_LITTLE
	MOVE.W	#$3887,(A0)+
	BRA.S	.RETOUR_LITTLE
.PAS_D7_LITTLE	MOVE.W	#$54,(A0)+	;ORI.W #$Imm,(A4)
;	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	MOVE.W	D0,(A0)+
.RETOUR_LITTLE
	BSR	COPY_RT_COOL

	ADDQ.W	#1,A3
	DBRA	D6,LITTLE

	MOVE.W	D3,D0
	MOVE.W	#$8000,D1	;DERNIER MOTIF

	MOVEQ	#16-1,D6
DECAL
	MOVE.W	#18*16,A3
	ADD.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
COPY_MOVED7	BSR	POZ_ADRESSE2
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	LEA	-16(A3),A3
	DBRA	D7,COPY_MOVED7

	MOVE.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3
	BSR	POZ_ADRESSE2
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_3
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_3
.PAS_D7_3	MOVE.W	#$54,(A0)+	;ORI.W #$Imm,(A4)
;	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	MOVE.W	D0,(A0)+
.RETOUR_3	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_4
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_4
.PAS_D7_4	MOVE.W	#$55,(A0)+	;MOVE.W #$Imm,(A5)
;	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	MOVE.W	D1,(A0)+
.RETOUR_4
	BSR	COPY_RT_COOL

	ASR.W	#1,D1
	DBRA	D6,DECAL

	SUBQ.W	#1,D5
	LSR.W	#1,D3
	LEA	1280(A1),A1
	DBRA	D2,ALL_RT

*******
TBL4:	LEA	TABLE_A4,A0
	MOVE	#319,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	D6,(A0)+
	MOVE	(A1),(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#319,D7
.FINT	MOVE	#319*4,(A0)+
	MOVE	#152,(A0)+
	;CLR	(A0)+
	DBF	D7,.FINT
TBL2:
	LEA	TABLE_A2,A0
	MOVE	#319,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	(A1),(A0)+
	MOVE	D6,(A0)
	MOVE	D6,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#319,D7
.FINT	;CLR	(A0)+
	MOVE	#152,(A0)+
	MOVE	#319*4,(A0)+
	DBF	D7,.FINT

	RTS

MINI_TABLE
N	SET	0
	REPT	20
	DC	N
N	SET	N+8
	ENDR


POZ_ADRESSE2	MOVEM.L	D5/A0,-(SP)
	MOVE.W	A3,D5
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS
	

POZ_ADRESSE	MOVEM.L	D5/A0,-(SP)
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS
	
COPY_RT_COOL	LEA	RT_COOL,A6
	MOVE.W	#LONG_RT_COOL,D7
.COPY	MOVE.W	(A6)+,(A0)+
	DBRA	D7,.COPY
	RTS

RT_COOL	ADD.W	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2

	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1

	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1

	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE... RUSê, HEIN?
	   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE, ZOU.
	MOVE.L	(A6,D3.W),A2	;5
	JMP	(A2)	;2

;TOTAL:31 NOPS. +58=89 NOPS.
LONG_RT_COOL = ((*-RT_COOL)/2)-1

INIT_SYSTEME	MOVE.W	#7,D7
	MOVE.W	#$2300,SR
.LOW	STOP	#$2100
	MOVE.B	#1,$FFFF8260.W
	STOP	#$2100
	CLR.B	$FFFF8260.W
	DBRA	D7,.LOW

	MOVE.L	#BUFFER,D0
	CLR.W	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#65536,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	RTS

TILT_BAS	INCBIN	TILT.BAS
	DC.W	$1234

TILT_HAUT	INCBIN	TILT.HOT
	DC.W	$1234

FAIT	INCBIN	FAIT.ROT

	;ON SPECIFIE LE NB DE POINTS, ET LES POINTS EUX-MEME
POLY_1	DC.W	4	;PREMIER 'T'
	DC.W	0,0
	DC.W	0,31
	DC.W	97,31
	DC.W	97,0

POLY_2	DC.W	4
	DC.W	25,31
	DC.W	25,99
	DC.W	71,99
	DC.W	71,31

POLY_3	DC.W	4	;'I'
	DC.W	103,0
	DC.W	103,99
	DC.W	149,99
	DC.W	149,0

POLY_4	DC.W	4	;'L'
	DC.W	156,0
	DC.W	156,99
	DC.W	202,99
	DC.W	202,0

POLY_5	DC.W	4
	DC.W	202,66
	DC.W	202,99
	DC.W	241,99
	DC.W	241,66

N	SET	222
POLY_6	DC.W	4	;DEUXIEME 'T'
	DC.W	N+0,0
	DC.W	N+0,31
	DC.W	N+97,31
	DC.W	N+97,0

POLY_7	DC.W	4
	DC.W	N+25,31
	DC.W	N+25,99
	DC.W	N+71,99
	DC.W	N+71,31

	;'FAIT'
N	SET	66
POLY2_1	DC.W	4	;'F'
	DC.W	N+0,0
	DC.W	N+0,99
	DC.W	N+21,99
	DC.W	N+21,0

POLY2_2	DC.W	4
	DC.W	N+21,0
	DC.W	N+21,23
	DC.W	N+37,23
	DC.W	N+37,0

POLY2_3	DC.W	4
	DC.W	N+21,37
	DC.W	N+21,61
	DC.W	N+37,61
	DC.W	N+37,37

POLY2_4	DC.W	5	;'A'
	DC.W	N+64,0
	DC.W	N+44,99
	DC.W	N+67,99
	DC.W	N+77,23
	DC.W	N+77,0

POLY2_5	DC.W	5
	DC.W	N+77,0
	DC.W	N+77,23
	DC.W	N+84,99
	DC.W	N+107,99
	DC.W	N+91,0

POLY2_6	DC.W	4
	DC.W	N+72,64
	DC.W	N+69,86
	DC.W	N+83,86
	DC.W	N+81,64

POLY2_7	DC.W	4	;'I'
	DC.W	N+113,0
	DC.W	N+113,99
	DC.W	N+136,99
	DC.W	N+136,0

POLY2_8	DC.W	4	'T'
	DC.W	N+145,0
	DC.W	N+145,23
	DC.W	N+188,23
	DC.W	N+188,0

POLY2_9	DC.W	4
	DC.W	N+157,23
	DC.W	N+157,99
	DC.W	N+178,99
	DC.W	N+178,23

RT_POLY	MOVE.L	A0,ADR_DEP

	LEA	2(A0),A1	;A1 POINTE SUR LES POINTS
	MOVE.W	(A1),D3
	SUB.W	4(A1),D3
	MOVE.W	2(A1),D4
	SUB.W	6(A1),D4
	MOVE.W	8(A1),D6
	SUB.W	4(A1),D6
	MOVE.W	$A(A1),D5
	SUB.W	6(A1),D5
	MULS.W	D3,D5
	MULS.W	D6,D4
	SUB.L	D4,D5
	BGT.S	.VISIBLE
	RTS
.VISIBLE
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7

	MOVE.W	#$4E75,SI_LE_POLY_CLIPPE

	MOVE.W	(A0)+,D0	;NB DE POINTS COMPOSANT LA SURFACE
	LEA	BUF_EXAM,A1
	MOVE.L	A0,A2
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_PTS
.COPYING	MOVE.L	(A0)+,(A1)+
	DBRA	D0,.COPYING
	MOVE.L	(A2),(A1)+

	LEA	BUF_EXAM,A0
	LEA	BUF_GAUCHE,A2
	LEA	BUF_DROIT,A3
	ADDQ.W	#4,A0
	MOVE.W	-2(A0),D3	;Y DE REFERENCE
NB_PTS = *+2
	MOVE.W	#$1234,D0
	MOVEQ	#-1,D6
	MOVEQ	#-1,D7
ALL_PTS	MOVE.W	(A0),D1	;X
	MOVE.W	2(A0),D2	;Y
	CMP.W	D2,D3
	BGE.S	ON_MONTE
ON_DESCEND	MOVE.L	-4(A0),(A2)+
	MOVE.L	(A0)+,(A2)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D6
	DBRA	D0,ALL_PTS
	BRA.S	FINITO
ON_MONTE	MOVE.L	-4(A0),(A3)+
	MOVE.L	(A0)+,(A3)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D7
	DBRA	D0,ALL_PTS
FINITO	;ICI LES TABLEAU BUF_GAUCHE ET BUF_DROIT SONT CONSTITUêS
	;D6=NB DE PTS-1 DANS BUF_GAUCHE
	;D7=NB DE PTS-1 DANS BUF_DROIT
	LEA	BUF_GAUCHE,A0
MAKE_REMP_GAUCHE
	LEA	REMP_GAUCHE,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT	.NEXT
	MOVE.W	#$4E71,SI_LE_POLY_CLIPPE
	SUB.W	D1,D3	;HAUTEUR-1 DU SEGMENT
	BEQ	.NEXT

	ADD.W	D1,D1
	ADDA.W	D1,A1

	ADDQ.W	#1,D3
	SUB.W	D0,D2
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#2,D3
	MOVE.W	D0,(A1)+
.PUT	MOVE.W	D0,(A1)+
	DBRA	D3,.PUT
	BRA	.NEXT
.PAS_VERTICALE	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D3,D2
	EXT.L	D2
	SWAP	D2
	ROL.L	#8,D2
	SUBQ.W	#1,D3
	MOVE.W	D0,(A1)+
	LEA	.NEXT(PC),A6
	ADD.W	D3,D3
	ADD.W	D3,D3
	SUBA.W	D3,A6
	JMP	(A6)
	REPT	200
	ADDX.L	D2,D0
	MOVE.W	D0,(A1)+
	ENDR
.NEXT	DBRA	D6,MAKE_REMP_GAUCHE

SI_LE_POLY_CLIPPE = *
	RTS

	LEA	BUF_DROIT,A0
MAKE_REMP_DROIT	LEA	REMP_DROIT,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT	.NEXT
	SUB.W	D3,D1	;HAUTEUR-1 DU SEGMENT
	BEQ	.NEXT

	ADD.W	D3,D3
	ADDA.W	D3,A1

	ADDQ.W	#1,D1
	SUB.W	D2,D0
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#1,D1
.PUT	MOVE.W	D2,(A1)+
	DBRA	D1,.PUT
	BRA	.NEXT
.PAS_VERTICALE	EXT.L	D0
	MOVEQ	#8,D4
	ASL.L	D4,D0
	DIVS.W	D1,D0
	EXT.L	D0
	SWAP	D0
	ROL.L	#8,D0
	MOVE.W	D2,(A1)+
	LEA	.NEXT(PC),A6
	SUBQ.W	#1,D1
	ADD.W	D1,D1
	ADD.W	D1,D1
	SUBA.W	D1,A6
	JMP	(A6)
	REPT	200
	ADDX.L	D0,D2
	MOVE.W	D2,(A1)+
	ENDR
.NEXT	DBRA	D7,MAKE_REMP_DROIT

ADR_DEP = *+2
	LEA	$12345678,A0
	MOVE.W	(A0)+,D7
	SUBQ.W	#1,D7
	MOVE.W	#-1000,D0	;MAXIMUM
	MOVE.W	#20000,D1	;MINIMUM
.TEST_MAX	CMP.W	2(A0),D0
	BGE.S	.FUCK
	MOVE.W	2(A0),D0
.FUCK	CMP.W	2(A0),D1
	BLE.S	.FUCK_2
	MOVE.W	2(A0),D1
.FUCK_2	ADDQ.W	#4,A0
	DBRA	D7,.TEST_MAX
	;D0 = Y MAXIMUM
	;D1 = Y MINIMUM
	CMPI.W	#200,D0
	BLT.S	.OK_Y_MAX
	MOVE.W	#199,D0
.OK_Y_MAX	TST.W	D1
	BGE.S	.OK_Y_MIN
	MOVEQ	#0,D1
.OK_Y_MIN

INIT	LEA	REMP_GAUCHE,A0
	LEA	REMP_DROIT,A1
	ADDQ.W	#1,D0
	ADD.W	D0,D0
	MOVE.W	#15,(A1,D0.W)
	MOVE.W	#25,(A0,D0.W)
	ADD.W	D1,D1
	ADDA.W	D1,A0
	ADDA.W	D1,A1
	MULU.W	#80,D1       ;D1=Y_MIN*160 (POUR ADD ECRAN)

	LEA	TABLE_ADR_COD+40,A6
	MOVE	#-1,D7
	MOVE.L	SCREEN2,D6
	ADD.W	D1,D6	;...+Y_MIN*160
	SUBI.W	#160,D6
	MOVE.L	#TABLE_A2+320*4,D4
	MOVE.W	#160,D5
	LEA	TABLE_A4+320*4,A3
	MOVEQ	#0,D2

	ADD.W	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2
	MOVE.W	(A1)+,D1	2  X2
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1
	MOVE.L	(A3,D1.W),D3	5
	ADD	D3,A5	2
	SWAP	D3	1
	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	ADD	(A2)+,A4	3  OFFSET. A3=>1ER MOT
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE*1320... RUSê, HEIN?   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE*1320, ZOU.
	MOVE.L	(A6,D3.W),A2
	JMP	(A2)

RETOUR_POLY	RTS

CLIPPING_Y	MOVEM.L	D4/D6/A6,-(SP)
	MOVEQ	#0,D5
	LEA	TABLE_Y,A6
	ADD.W	D1,D1
	ADD.W	D3,D3
	MOVEQ	#0,D4
	ADD.W	(A6,D1.W),D4
	ADD.W	(A6,D3.W),D4
	ASR.W	#1,D1
	ASR.W	#1,D3
	TST.W	D4
	BEQ	FIN_NORMALE
	CMPI.W	#-2,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#4,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#-1,D4
	BEQ	CLIP_HAUT
	CMPI.W	#2,D4
	BEQ	CLIP_BAS

CLIP_HAUT_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	BRA.S	SUIT
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1

SUIT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_HAUT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

RIEN_DU_TOUT	MOVE.W	#-1,D5
FIN_NORMALE	MOVEM.L	(SP)+,D4/D6/A6
	RTS

TEXT_1	DC.B	0,0,0,"...mi-dÇcembre...",0
	DC.B	7,"...j'ouvre mon courrier...",0
	DC.B	"      ...qu'est-ce que je lis ?..",-1

	DCB.W	400,-1
TABLE_Y	DCB.W	200,0
	DCB.W	400,2

	SECTION	BSS
	DS.B	65536
BUFFER	DS.B	32000+65536+32000+16000
SCREEN1	DS.L	1
SCREEN2	DS.L	1
NB_VBL	DS.W	1
PT	DS.L	1
;
PUT_COOR	DS.W	1+5*2
ADR_SOV	DS.L	1
BUF_SOV	DS.W	20*200*25+20*100*25
BUF_FAIT	DS.W	20*100*60
;
TABLE_A4	DS.L	320*3
TABLE_A2	DS.L	320*3
BUF_EXAM	DS.W	2*50
BUF_GAUCHE	DS.W	4*50
BUF_DROIT	DS.W	4*50
REMP_GAUCHE	DS.W	250
REMP_DROIT	DS.W	250
TABLE_ADR_COD	DS.B	(1280+40)*16
BUF_COD_GEN	DS.B	39000