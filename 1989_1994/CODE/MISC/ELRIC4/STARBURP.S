;OK C'EST BON
;LES CLIPS SONT FINIS !!!
;OPTIMISER : LES MULU, L'EFFACAGE, LE CLIP HAUT(MULU)

NB_STARS = 36+4

	RSRESET
PT	RS.L	1
FLAG	RS.W	1
TABLE_EFFAC	RS.W	(3*NB_STARS*2)
ADR_TABLE	RS.L	2
TABLE_ADR_STARS	RS.W	(2+1+1+3*8)*45*13
BUF_DEC_STARS	RS.B	353000
LONG_BSS_STARS	RS.B	1

LINK = 0	;1 POUR LINKER
DEBUG = 0	;1 POUR DEBUGGER SANS SE FAIRE CHIER, 0 POUR EXECUTER

WAIT	MACRO
	MOVE.W	#\1,D7
\@BCL	DBRA	D7,\@BCL
	ENDM

CPU	MACRO
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	\@PAS_TOUCHE
	MOVE.W	#\1,$FFFF8240.W
\@PAS_TOUCHE	
	ENDM

COUL	MACRO
	MOVE.W	#\2,$FFFF8240+(\1*2).W
	ENDM

	IFEQ	LINK
	 CLR.L	-(SP)
	 MOVE.W	#$20,-(SP)
	 TRAP	#1
	 ADDQ.L	#6,SP
	 MOVE.L	D0,SAUV_SP

	 IFEQ	DEBUG
	  CLR.W	-(SP)
	  MOVE.L	#-1,-(SP)
	  MOVE.L	#-1,-(SP)
	  MOVE.W	#5,-(SP)
	  TRAP	#14
	  LEA	12(SP),SP

	  LEA	FIN,A0
	  MOVE.W	#8,A1
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	  MOVE.L	A0,(A1)+
	 ENDC
	ENDC

	BSR	PREP_ECR
	BSR	PREPARE_STARS

	IFNE	DEBUG
	 BRA	IT_VBL
	ENDC

	CLR.B	DATA_JOY
	CLR.B	DATA2JOY

	IFEQ	LINK
	 ANDI.B	#%11111000,$484.W
	ENDC

	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	MOVE.W	#$413,$FFFF8242.W

	MOVE.W 	#$FA00,A0
	MOVE.B	7(A0),IERA
	MOVE.B	9(A0),IERB
	MOVE.B	$F(A0),ISRA
	MOVE.B	$11(A0),ISRB
	MOVE.B	$13(A0),IMRA
	MOVE.B	$15(A0),IMRB
	MOVE.B	$17(A0),VR
	MOVE.B	$19(A0),TACR
	MOVE.B	$1B(A0),TBCR
	MOVE.B	$1D(A0),TCDCR
	MOVE.B	$1F(A0),TADR
	MOVE.B	$21(A0),TBDR
	MOVE.B	$23(A0),TCDR
	MOVE.B	$25(A0),TDDR
	
	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE.L	$70.W,ANC_VBL
	MOVE.L	$120.W,ANC_TIM
	MOVE.L	$118.W,ANC_CLAV
	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	MOVE.L	#IT_CLAV,$118.W
	BSET	#0,$FFFFFA07.W		TB AUTORIS
;	BSET	#6,$FFFFFA09.W		IT CLAVIER OK
	MOVE.B	$FFFFFA07.W,$FFFFFA13.W
	MOVE.B	$FFFFFA09.W,$FFFFFA15.W
	MOVE.B	#$40,$FFFFFA17.W
	MOVE.W	#$2300,SR

	CLR.W	NB_VBL
	JMP	IT_VBL

FIN	MOVE.W	#$2700,SR
	MOVE.L	ANC_VBL,$70.W
	MOVE.L	ANC_TIM,$120.W
	MOVE.L	ANC_CLAV,$118.W
	
	MOVE.W	#$FA00,A0
	MOVE.B	IERA,7(A0)
	MOVE.B	IERB,9(A0)
	MOVE.B	ISRA,$F(A0)
	MOVE.B	ISRB,$11(A0)
	MOVE.B	IMRA,$13(A0)
	MOVE.B	IMRB,$15(A0)
	MOVE.B	VR,$17(A0)
	MOVE.B	TACR,$19(A0)
	MOVE.B	TBCR,$1B(A0)
	MOVE.B	TCDCR,$1D(A0)
	MOVE.B	TADR,$1F(A0)
	MOVE.B	TBDR,$21(A0)
	MOVE.B	#$B0,$23(A0)
	MOVE.B	TDDR,$25(A0)
	MOVE.W	#$2300,SR

	MOVE.B	SAUVEC,$FFFF8201.W
	MOVE.B	SAUVEC+1,$FFFF8203.W

	MOVEM.L	ANC_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	
	MOVE.L	#$8080000,$FFFF8800.W
	MOVE.L	#$9090000,$FFFF8800.W
	MOVE.L	#$A0A0000,$FFFF8800.W
	
	IFEQ	LINK
	 ORI.B	#%00000111,$484.W

	 MOVE.L	SAUV_SP,-(SP)
	 MOVE.W	#$20,-(SP)
	 TRAP	#1
	 ADDQ.L	#6,SP
	
	 CLR.W	-(SP)
	 TRAP	#1
	 ADDQ.L	#2,SP
	ELSE
	 ILLEGAL
	ENDC
	
	*******	V B L  &  P R O C E D U R E S    *******
VBLR	ADDQ.W	#1,NB_VBL
	RTE
	
TB0	RTE

IT_CLAV	MOVE.L	D0,-(SP)
	MOVEQ	#0,D0
	MOVE.B	$FFFFFC02.W,D0
	CMPI.B	#$FF,D0
	BNE.S	JOY_2
	MOVE.L	#CLAV_1,$118.W
	MOVE.L	(SP)+,D0
	RTE
JOY_2	CMPI.B	#$FE,D0
	BNE.S	PAS_JOY
	MOVE.L	#CLAV_2,$118.W
	MOVE.L	(SP)+,D0
	RTE
PAS_JOY	MOVE.L	(SP)+,D0
	RTE

CLAV_1	MOVE.B	$FFFFFC02.W,DATA_JOY
	MOVE.L	#IT_CLAV,$118.W
	RTE

CLAV_2	MOVE.B	$FFFFFC02.W,DATA2JOY
	MOVE.L	#IT_CLAV,$118.W
	RTE

IT_VBL	BSR	VSYNC
	CMPI.B	#1,$FFFFFC02.W
	BEQ	SS
	
	LEA	ADR_TABLE+BSS_DEMO,A1
	MOVE.L	(A1),A1
	MOVEQ	#0,D0
	MOVEQ	#NB_STARS-1,D7
.F	MOVE.W	(A1)+,D6
	MOVE.L	(A1)+,A0
.R	MOVE.W	D0,(A0)
	MOVE.W	D0,8(A0)
	MOVE.W	D0,16(A0)
	LEA	160(A0),A0
	DBRA	D6,.R
	DBRA	D7,.F
	BSR	ROTATING_STARS
	BSR	SWAPEC
	LEA	ADR_TABLE+BSS_DEMO,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)
SS	COUL	0,0
	COUL	1,$545	0001
	COUL	2,$434	0010
	COUL	3,$434	0011
	COUL	4,$323	0100
	COUL	5,$545	0101
	COUL	6,$434	0110
	COUL	7,$545	0111
	COUL	8,$212	1000
	COUL	9,$545	1001
	COUL	10,$434	1010
	COUL	11,$545	1011
	COUL	12,$323	1100
	COUL	13,$545	1101
	COUL	14,$434	1110
	COUL	15,$545	1111
	
	JMP	IT_VBL

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

ROTATING_STARS	CPU	$700
	LEA	BSS_DEMO,A6
	LEA	COOR_STARS(PC),A0
	LEA	ADR_TABLE(A6),A1
	MOVE.L	(A1),A1
	LEA	CORES_ZOOM(PC),A2
	LEA	TABLE_ADR_STARS(A6),A3
	LEA	MOVEMENT(PC),A6
	SUBQ.W	#1,FLAG+BSS_DEMO
	BGT.S	.PAS_NEXT
	MOVE.W	#2,FLAG+BSS_DEMO
	ADDQ.W	#6,PT+BSS_DEMO
.PAS_NEXT	ADDA.W	PT+BSS_DEMO,A6
	MOVE.L	A6,CHG_4
	CMPI.W	#$1234,6(A6)
	BNE.S	.NEW_FUCK
	CLR.W	PT+BSS_DEMO
.NEW_FUCK	MOVEQ	#NB_STARS-1,D7
REPEAT_STARS	MOVE.W	D7,-(SP)
CHG_4 = *+2
	LEA	$12345678,A6
	CLR	COLORZ
	MOVEQ	#0,D6	;D6 VA CONTENIR UN FLAG
	MOVEQ	#0,D5	;D5 VA CONTENIR LE NB DE LIGNES A CLIPPER EN HAUT
	MOVE.W	(A0),D0	;X
	ADD.W	(A6)+,D0
	ANDI.W	#511,D0
	MOVE.W	D0,(A0)+
	MOVE.W	(A0),D1	;Y
	ADD.W	(A6)+,D1
	ANDI.W	#255,D1
	MOVE.W	D1,(A0)+
	MOVE.W	(A0),D2	;Z
	ADD.W	(A6)+,D2
	ANDI.W	#511,D2
	MOVE.W	D2,(A0)+
***
	CMPI	#200,D2
	BLT.S	MOINS0
	ADDQ	#2,COLORZ
MOINS0	CMPI	#300,D2
	BLT.S	MOINS1
	ADDQ	#2,COLORZ
MOINS1	CMPI	#400,D2
	BLT.S	MOINS2
	ADDQ	#2,COLORZ
MOINS2
***
	MOVE.W	(A0),D3	;ETAPE DE ROTATION
	ADD.W	2(A0),D3
	CMPI.W	#45,D3
	BLT.S	.FUCK_4
	MOVEQ	#0,D3
.FUCK_4	CMPI.W	#-1,D3
	BNE.S	.FUCK_5
	MOVEQ	#44,D3
.FUCK_5	MOVE.W	D3,(A0)+
	ADDQ.W	#2,A0

	SUBI.W	#256,D0
	SUBI.W	#128,D1

	MOVE.L	SCREEN2,A6
	ADD	COLORZ,A6

	LEA	COEFF_3D(PC),A5
	ADD.W	D2,D2
	MOVE.W	(A5,D2.W),D4	;D4=COEFF 3D*(2^15)
	MOVE.W	(A2,D2.W),D2	;D2=No DU ZOOM
	ADD.W	D0,D0
	ADD.W	D1,D1
	MULS.W	D4,D0
	MULS.W	D4,D1
	SWAP	D0
	SWAP	D1
	ADDI.W	#160,D0	;X ECRAN
	ADDI.W	#100,D1	;Y ECRAN

	LEA	TABLE_56_13(PC),A5
	ADD.W	D3,D3
	MOVE.W	(A5,D3.W),D3
*	MULU.W	#56*13,D3
	LEA	TABLE_56(PC),A5
	ADD.W	D2,D2
	MOVE.W	(A5,D2.W),D2
*	MULU.W	#56,D2
	ADD.W	D2,D3
	LEA	(A3,D3.W),A4	;A4=ADRESSE DE LA TABLE DU SPRITE CONCERN

	MOVEQ	#0,D4	;D4 VA CONTENIR LE NB DE BLOCS DE 16 QUI SORTENT A GAUCHE
	ADD.W	(A4)+,D0	;X+OFFSET X=X DEF
	BGE.S	X_POSITIF
	CMPI.W	#-40,D0
	BLT	NEXT_STAR
	MOVE.W	D0,D4
	NEG.W	D4
	SUBQ.W	#1,D4
	LSR	#4,D4	;D4:NB DE BLOK16 QUI SORTENT A GAUCHE
	ADDQ.W	#1,D4
X_POSITIF	CMPI.W	#320,D0
	BGE	NEXT_STAR
	ADDQ.W	#2,A4
.PAS_CLIP_DROITE
	ADD.W	(A4)+,D1	;Y+OFFSET Y=Y DEF
	MOVE.W	(A4)+,D7	;HAUTEUR-1	
	TST.W	D1
	BGE.S	Y_POSITIF
	NEG.W	D1	;NB DE LIGNE QUI SORTENT
	SUB.W	D1,D7	;NOUVELLE HAUTEUR
	BLT	NEXT_STAR
	MOVE.W	D1,D5	;D5=NB DE LIGNES A VIRER
	MOVEQ	#0,D1
Y_POSITIF	MOVE.W	D1,D3
	ADD.W	D7,D3
	CMPI.W	#200,D3
	BLT.S	NO_CLIP_BAS
	SUBI.W	#199,D3
	SUB.W	D3,D7	;NOUVELLE HAUTEUR
	BLT	NEXT_STAR

NO_CLIP_BAS	MOVE.W	D0,D3
	ANDI.W	#$FFF0,D0
	BGE.S	.SUP
	MOVEQ	#0,D0
.SUP	LSR.W	#1,D0
	MOVE.W	#$A0,D2
	SUB.W	D0,D2
	LSR.W	#3,D2
	SUBQ.W	#1,D2
	LEA	TABLE_160(PC),A5
	ADD.W	D1,D1
	MOVE.W	(A5,D1.W),D1
*	MULU.W	#160,D1
	ADD.W	D0,D1
	ADDA.W	D1,A6	;A6=ADRESSE ECRAN DESTINATION
	ANDI.W	#15,D3
	BTST	#3,D3	;DECALAGE > 8 ?
	BEQ.S	.OK_COOL	;PARCE QUE J'EN AI PLUS EN RAYON.
	MOVEQ	#-1,D6	;SINON JE VOUS MET SUR LISTE D'ATTENTE
	BCLR	#3,D3	;VOUS VOYEZ, QUAND VOUS VOULEZ...
.OK_COOL	LEA	TABLE_6(PC),A5
	ADD.W	D3,D3
	MOVE.W	(A5,D3.W),D3
*	MULU.W	#6,D3
	ADDA.W	D3,A4	;A4=ADRESSE DE L'ADRESSE DU SPRITE(.L)+SA LONGUEUR(.W)
	MOVE.L	(A4)+,A5	;A5=ADRESSE DU SPRITE
	MOVE.W	(A4),D0	;D0=LONGUEUR DU SPRITE EN BLOK DE 16:0,1 OU 2
	MOVE.W	D7,(A1)+
	MOVE.L	A6,(A1)+
	CMP.W	D2,D0
	BGT	CLIPPING_DROIT
	TST.W	D4
	BNE	CLIPPING_GAUCHE
	ADDQ.W	#1,D0
	ADD.W	D0,D0
	MULU.W	D0,D5
	ADDA.W	D5,A5
	LSR.W	#1,D0
	CMPI.W	#1,D0
	BEQ	_16_PIXELS
	CMPI.W	#3,D0
	BEQ	_48_PIXELS
_32_PIXELS	MOVE.W	D6,D6	;ON TESTE D6...
	BEQ	.AFF_NORMAL
	ADDQ.W	#1,A6
.AFF_DECAL	MOVE.B	(A5)+,D0
	MOVE.B	(A5)+,D1
	MOVE.B	(A5)+,D2
	MOVE.B	(A5)+,D3
	OR.B	D0,(A6)
	OR.B	D1,7(A6)
	OR.B	D2,8(A6)
	OR.B	D3,1(A6)
	LEA	160(A6),A6
	DBRA	D7,.AFF_DECAL
	BRA	NEXT_STAR
.AFF_NORMAL	MOVE.W	(A5)+,D0
	MOVE.W	(A5)+,D1
	OR.W	D0,(A6)
	OR.W	D1,8(A6)
	LEA	160(A6),A6
	DBRA	D7,.AFF_NORMAL
	BRA	NEXT_STAR
_16_PIXELS	MOVE.W	D6,D6	;ON TESTE D6...
	BEQ	.AFF_NORMAL
	ADDQ.W	#1,A6
.RECOP_16	MOVE.B	(A5)+,D0
	MOVE.B	(A5)+,D1
	OR.B	D0,(A6)
	OR.B	D1,7(A6)
	LEA	160(A6),A6
	DBRA	D7,.RECOP_16
	BRA	NEXT_STAR
.AFF_NORMAL	MOVE.W	(A5)+,D0
	OR.W	D0,(A6)
	LEA	160(A6),A6
	DBRA	D7,.AFF_NORMAL
	BRA	NEXT_STAR
_48_PIXELS	MOVE.W	D6,D6	;ON TESTE D6...
	BEQ	.AFF_NORMAL
	ADDQ.W	#1,A6
.RECOP_48	MOVE.B	(A5)+,D0
	MOVE.B	(A5)+,D1
	MOVE.B	(A5)+,D2
	OR.B	D0,(A6)
	OR.B	D1,7(A6)
	OR.B	D2,8(A6)
	MOVE.B	(A5)+,D0
	MOVE.B	(A5)+,D1
	MOVE.B	(A5)+,D2
	OR.B	D0,15(A6)
	OR.B	D1,16(A6)
	OR.B	D2,23(A6)
	LEA	160(A6),A6
	DBRA	D7,.RECOP_48
	BRA	NEXT_STAR
.AFF_NORMAL	MOVE.W	(A5)+,D0
	MOVE.W	(A5)+,D1
	MOVE.W	(A5)+,D2
	OR.W	D0,(A6)
	OR.W	D1,8(A6)
	OR.W	D2,16(A6)
	LEA	160(A6),A6
	DBRA	D7,.AFF_NORMAL

NEXT_STAR	MOVE.W	(SP)+,D7
	DBRA	D7,REPEAT_STARS
	RTS

CLIPPING_GAUCHE	SUB.W	D4,D0
	BLT.S	NEXT_STAR
	BEQ	_16_PIXELS_CLIP_GAUCHE
_32_PIXELS_CLIP_GAUCHE
	ADD.W	D4,D4
	MULU.W	#6,D5
	ADDA.W	D5,A5
	MOVE.W	D6,D6	;ON TESTE D6...
	BEQ	.AFF_NORMAL
.RECOP_32	ADD.W	D4,A5
	SUBQ.W	#1,A5
	MOVE.B	(A5)+,D0
	MOVE.B	(A5)+,D1
	MOVE.B	(A5)+,D2
	MOVE.B	(A5)+,D3
	MOVE.B	(A5)+,D5
	OR.B	D0,(A6)
	OR.B	D1,1(A6)
	OR.B	D2,8(A6)
	OR.B	D3,9(A6)
	OR.B	D5,16(A6)
	LEA	160(A6),A6
	DBRA	D7,.RECOP_32
	BRA	NEXT_STAR
.AFF_NORMAL	ADD.W	D4,A5
	MOVE.W	(A5)+,D0
	MOVE.W	(A5)+,D1
	OR.W	D0,(A6)
	OR.W	D1,8(A6)
	LEA	160(A6),A6
	DBRA	D7,.AFF_NORMAL
	BRA	NEXT_STAR
_16_PIXELS_CLIP_GAUCHE
	ADD.W	D4,D4
	ADD.W	D5,D5
	ADD.W	D5,D5
	ADDA.W	D5,A5
	MOVE.W	D6,D6	;ON TESTE D6...
	BEQ	.AFF_NORMAL
.RECOP_16	ADD.W	D4,A5
	SUBQ.W	#1,A5
	MOVE.B	(A5)+,D0
	MOVE.B	(A5)+,D1
	MOVE.B	(A5)+,D2
	OR.B	D0,(A6)
	OR.B	D1,1(A6)
	OR.B	D2,8(A6)
	LEA	160(A6),A6
	DBRA	D7,.RECOP_16
	BRA	NEXT_STAR
.AFF_NORMAL	ADD.W	D4,A5
	MOVE.W	(A5)+,D0
	OR.W	D0,(A6)
	LEA	160(A6),A6
	DBRA	D7,.AFF_NORMAL
	BRA	NEXT_STAR

CLIPPING_DROIT	ADDQ.W	#1,D0
	ADD.W	D0,D0
	MULU.W	D0,D5
	ADDA.W	D5,A5
	LSR	#1,D0
	SUBQ.W	#1,D0

	SUB.W	D2,D0
	ADD.W	D0,D0
	CMPI.W	#0,D2
	BEQ	_16_PIXELS_CLIP_DROIT
_32_PIXELS_CLIP_DROIT
	MOVE.W	D6,D6	;ON TESTE D6...
	BEQ	.AFF_NORMAL
	ADDQ.W	#1,A6
	ADDQ.W	#1,D0
.RECOP_32	MOVE.B	(A5)+,D1
	MOVE.B	(A5)+,D2
	MOVE.B	(A5)+,D3
	OR.B	D1,(A6)
	OR.B	D2,7(A6)
	OR.B	D3,8(A6)
	ADD.W	D0,A5
	LEA	160(A6),A6
	DBRA	D7,.RECOP_32
	BRA	NEXT_STAR
.AFF_NORMAL	MOVE.W	(A5)+,D1
	MOVE.W	(A5)+,D2
	OR.W	D1,(A6)
	OR.W	D2,8(A6)
	ADD.W	D0,A5
	LEA	160(A6),A6
	DBRA	D7,.AFF_NORMAL
	BRA	NEXT_STAR
_16_PIXELS_CLIP_DROIT
	MOVE.W	D6,D6	;ON TESTE D6...
	BEQ	.AFF_NORMAL
	ADDQ.W	#1,A6
	ADDQ.W	#1,D0
.RECOP_16	MOVE.B	(A5)+,D1
	OR.B	D1,(A6)
	ADD.W	D0,A5
	LEA	160(A6),A6
	DBRA	D7,.RECOP_16
	BRA	NEXT_STAR
.AFF_NORMAL	MOVE.W	(A5)+,D1
	OR.W	D1,(A6)
	ADD.W	D0,A5
	LEA	160(A6),A6
	DBRA	D7,.AFF_NORMAL
	BRA	NEXT_STAR

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREP_ECR	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

PREPARE_STARS	LEA	BSS_DEMO,A6
	LEA	SPR(PC),A0
	LEA	INF,A1
	LEA	BUF_DEC_STARS+BSS_DEMO,A2
	LEA	TABLE_ADR_STARS(A6),A3

.NEXT_ROTATION	MOVE.W	(A1)+,D0	;LONGUEUR
	MOVE.W	(A1)+,D1	;HAUTEUR
	SUBQ.W	#1,D1	;HAUTEUR-1
	MOVE.W	(A1)+,(A3)+	;OFFSET X 
	MOVE.W	D0,(A3)
	SUBQ.W	#1,(A3)+	;LONGUEUR-1
	MOVE.W	(A1)+,(A3)+	;OFFSET Y
	MOVE.W	D1,(A3)+	;ON RANGE LA HAUTEUR -1
	MOVE.L	A2,(A3)+	;ADRESSE DECALAGE 0
	SF	FLAG(A6)
	SUBQ.W	#1,D0
	MOVE.W	D0,D2
	LSR.W	#4,D0	;LARGEUR EN MOTS -1
	ADDQ.W	#1,D0
	MOVE.W	D0,(A3)+	;LONGUEUR EN BLOCS DE 16-1
	MOVE.W	D0,D4

	MOVE.L	A2,A4
	MOVE.W	D1,D6
.PRE2	MOVE.W	D0,D7
	SUBQ.W	#1,D7
.PRE	MOVE.W	(A0)+,(A2)+
	DBRA	D7,.PRE
	CLR.W	(A2)+
	DBRA	D6,.PRE2
	MOVE.L	A0,-(SP)
	MOVE.L	A4,A0
	MOVEQ	#7,D3
	MOVE.W	D0,D4
	BSR	PROCEDURE_DEC
	MOVE.L	(SP)+,A0

	CMPI.W	#$1234,(A1)
	BNE.S	.NEXT_ROTATION

	LEA	COOR_STARS(PC),A0
.FUCK	ADDI.W	#256,(A0)+
	ADDI.W	#128,(A0)+
	ADDQ.W	#6,A0
	CMPI.W	#$1234,(A0)
	BNE.S	.FUCK
	CLR.L	PT(A6)
	MOVE.W	#1,FLAG(A6)

	LEA	TABLE_EFFAC(A6),A0
	LEA	6*NB_STARS(A0),A1
	LEA	ADR_TABLE(A6),A2
	MOVE.L	A0,(A2)+
	MOVE.L	A1,(A2)
	MOVE.L	SCREEN2,A2
	ADDQ.W	#2,A0
	MOVEQ	#(NB_STARS*2)-1,D7
.R	MOVE.L	A2,(A0)
	ADDQ.W	#6,A0
	DBRA	D7,.R
	RTS

	;D0:(NB DE MOTS DE LARGEUR)-1, MOTIF ORIGINAL
	;D1:HAUTEUR-1
	;D3:NB DE DECALAGES A EFFECTUER
	;D4:NB DE MOTS DU MOTIF DE DESTINATION
PROCEDURE_DEC	MOVEM.L	D0-D7,-(SP)
	MOVE.W	D0,CHG_1
	MOVE.W	D1,CHG_2
	MOVE.W	D0,CHG_3

	SUBQ.W	#1,D3
.DEC_GEN	MOVE.L	A2,(A3)+	;ADRESSE
	MOVE.W	D4,(A3)+	;LONGUEUR EN BLOCS DE 16-1
	MOVE.L	A2,A4
CHG_2 = *+2
	MOVE.W	#$1234,D6
.DEC_Y	MOVEQ	#0,D5
	LEA	POZ_LIG(PC),A5
CHG_1 = *+2
	MOVE.W	#$1234,D7
.DEC_X	MOVEQ	#0,D2
	MOVE.W	(A0)+,D2
	ROR.L	#1,D2
	OR.W	D5,D2
	MOVE.W	D2,(A5)+
	SWAP	D2
	MOVE.W	D2,D5
	DBRA	D7,.DEC_X
CHG_3 = *+2
	MOVE.W	#$1234,D7
	LEA	POZ_LIG(PC),A5
.AFF_X	MOVE.W	(A5)+,(A2)+
	DBRA	D7,.AFF_X

	DBRA	D6,.DEC_Y
	MOVE.L	A4,A0
	DBRA	D3,.DEC_GEN
	MOVEM.L	(SP)+,D0-D7
	RTS
COLORZ	DS	1
POZ_LIG	DS.W	4

MOVEMENT	INCBIN	MOVE_HC.DAT
	DC.W	$1234

COEFF_3D	INCBIN	COEFF.DAT

	;X,Y,Z,ETAPE DE ROTATION
COOR_STARS	INCBIN	ETOILES.3D
	DC.W	$1234

CORES_ZOOM	INCBIN	CORESZOM.3D

TABLE_6
N	SET	0
	REPT	100
	DC.W	N*6
N	SET	N+1
	ENDR

TABLE_56_13	
N	SET	0
	REPT	50
	DC.W	N*56*13
N	SET	N+1
	ENDR

TABLE_56
N	SET	0
	REPT	100
	DC.W	N*56
N	SET	N+1
	ENDR

TABLE_160
N	SET	0
	REPT	200
	DC.W	N*160
N	SET	N+1
	ENDR
	DCB.W	600,0

SPR	INCBIN	SPR.SPR
INF	INCBIN	INF.INF
	DC.W	$1234
	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
ANC_CLAV	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
IERA	DS.B	1
IERB	DS.B	1
ISRA	DS.B	1
ISRB	DS.B	1
IMRA	DS.B	1
IMRB	DS.B	1
VR	DS.B	1
TACR	DS.B	1
TBCR	DS.B	1
TCDCR	DS.B	1
TADR	DS.B	1
TBDR	DS.B	1
TCDR	DS.B	1
TDDR	DS.B	1
	DS.B	256
BUFFER	DS.L	16000
SCREEN1	DS.L	1
SCREEN2	DS.L	1
DATA_JOY	DS.B	1
DATA2JOY	DS.B	1
NB_VBL	DS.W	1
;
BSS_DEMO	DS.B	LONG_BSS_STARS