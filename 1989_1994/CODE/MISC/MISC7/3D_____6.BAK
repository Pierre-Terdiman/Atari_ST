; RESTE A FAIRE : ORDONNER LES BIPOINTS                             (FAIT)
;	  DENOMBRER LE NB DE BIPOINTS A GAUCHE ET A DROITE  (FAIT)
;                 GENERER LES TABLEAU GAUCHE ET DROITE	    (FAIT)
;	  FAIRE LES CLIPPINGS		    (FAIT)
;	  FAIRE LE TRUC DES ADRESSES MINIMALES ET MAXIMALES (FAIT)
;	  PREVOIR LES DX=0		   
;	  PREVOIR LES DY=0		    (FAIT)
;	  PREVOIR LE CAS OU LE POLYGONE N'EST PAS VISIBLE
X1	EQU	0
Y1	EQU	2
X2	EQU	4
Y2	EQU	6
X3	EQU	8
Y3	EQU	10
X4	EQU	12
Y4	EQU	14

Y_CLIP = 199

TEST_CLIP_FIN	MACRO
	CMP.W	D3,D\1
	BLT.S	.\@PROUT
	ADDQ.W	#1*4,D5
.\@PROUT	CMP.W	D4,D\1
	BLE.S	.\@PROUT2
	ADDQ.W	#2*4,D5
.\@PROUT2
	ENDM

REMPLI_COLONNE	MACRO
	CMP.W	D1,D2
	BLT.S	.\@D2_INF_D1
.\@D1_INF_D2	SUB.W	D1,D2
;	SUBQ.W	#1,D2
	ADD.W	D1,D1
	ADDA.W	D1,A4
.\@REMP_1	MOVE.W	D6,(A4)+
	DBRA	D2,.\@REMP_1
	BRA.S	.\@FINITO
.\@D2_INF_D1	SUB.W	D2,D1
;	SUBQ.W	#1,D1
	ADD.W	D2,D2
	ADDA.W	D2,A4
.\@REMP_2	MOVE.W	D6,(A4)+
	DBRA	D1,.\@REMP_2
.\@FINITO
	ENDM

	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR.W	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	BSR	PREP_ECR

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

	BSR	PREP_COD_GEN
	MOVE.L	#face_en_cours+0*16,PT_COOR

VBL	BSR	VSYNC
	BSR	EFF
	MOVE.W	#$333,$FFFF8240.W
	MOVE.L	PT_COOR,A0
	CMPI.W	#$1234,(A0)
	BNE.S	.PAS_LOOP
	LEA	face_en_cours,A0
	MOVE.L	#face_en_cours-16,PT_COOR
.PAS_LOOP	CMPI.B	#1,$FFFFFC02.W
	BEQ	FIN_RT
	ADDI.L	#16,PT_COOR
	BSR	GERE_FACE
 	ADDI.W	#123,$FFFF8240.W
	MOVEQ	#0,D1
	LEA	TABLE_OFFSET,A3
	LEA	TAB,A0
	LEA	TAB2,A1
	MOVE.W	OFFSET_Y,D0
	ADD.W	D0,D0
	ADDA.W	D0,A0
	ADDA.W	D0,A1
	MULU.W	#160/2,D0
.PROUT	MOVE.L	SCREEN2,D2
	ADD.L	D0,D2
	LEA	TABLE_ADR,A6
	LEA	TABLE_MULU,A2
	MOVE.L	#160,D5
	MOVEQ	#-1,D7	;MOTIF PLEIN
	MOVEQ	#0,D6
	MOVE.L	D2,A4	1
	MOVE.W	(A0)+,D0	2;X1
	MOVE.W	(A1)+,D1	2;X2
	SUB.W	D0,D1	1;DISTANCE
	ADD.W	D0,D0	1
	ADD.W	(A3,D0.W),A4	5  OFFSET
	ADD.W	D1,D1	1
	ADD.W	D1,D1	1
	ADD.W	(A2,D0.W),D1	4
	MOVE.L	(A6,D1.W),A5	5
	JMP	(A5)	2  TOTAL : 25 NOPS
FIN_RT	
	BSR	SWAPEC
	CLR.W	$FFFF8240.W

	BRA	VBL

VSYNC	CMPI.W	#1,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	RTE

EFF	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	MOVE.W	#200-1,D7
.EFFAC	MOVE.W	D0,(A0)
	MOVE.W	D0,8(A0)
	MOVE.W	D0,16(A0)
	MOVE.W	D0,24(A0)
	MOVE.W	D0,32(A0)
	MOVE.W	D0,40(A0)
	MOVE.W	D0,48(A0)
	MOVE.W	D0,56(A0)
	MOVE.W	D0,64(A0)
	MOVE.W	D0,72(A0)
	MOVE.W	D0,80(A0)
	MOVE.W	D0,88(A0)
	MOVE.W	D0,96(A0)
	MOVE.W	D0,104(A0)
	MOVE.W	D0,112(A0)
	MOVE.W	D0,120(A0)
	MOVE.W	D0,128(A0)
	MOVE.W	D0,136(A0)
	MOVE.W	D0,144(A0)
	MOVE.W	D0,152(A0)
	LEA	160(A0),A0
	DBRA	D7,.EFFAC
	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

SWAPEC	LEA	SCREEN1,A0
	MOVE.L	(A0)+,D0
	MOVE.L	(A0),-(A0)
	MOVE.L	D0,4(A0)
	MOVE.B	1(A0),$FFFF8201.W
	MOVE.B	2(A0),$FFFF8203.W
	RTS

GERE_FACE
;1) Teste si la face est visible
	MOVE.L	A0,A1
	MOVE.W	(A1),D3
	SUB	4(A1),D3
	MOVE.W	2(A1),D4
	SUB	6(A1),D4
	MOVE.W	4*2(A1),D0
	SUB	4(A1),D0
	MOVE.W	10(A1),D5
	SUB	6(A1),D5
	MULS	D3,D5	(X1-X2)*(Y3-Y2)
	MULS	D0,D4	(X3-X2)*(Y1-Y2)
	SUB.L	D4,D5	produit vectoriel
	BGT.S	visible
cachee	RTS
visible
;3) CrÇe les nouveaux tableaux
	LEA	TABLEAU_GAUCHE,A5
	LEA	TABLEAU_DROITE,A6
	MOVEQ	#0,D0     ;COMPTEUR DE BIPOINTS GAUCHE
	MOVEQ	#0,D1     ;COMPTEUR DE BIPOINTS DROITE

	LEA	ADR_RTS,A4
	MOVE.W	2(A0),D6	Y1
	CMP	2+4(A0),D6
	BGT	D6_WIN
;Y2>Y1	CAS 1 ET 2
	MOVE.W	(A0),(A5)+
	MOVE.W	2(A0),(A5)+
	MOVE.W	4(A0),(A5)+
	MOVE.W	6(A0),(A5)+
	ADDQ.W	#1,D0

	MOVE.W	Y2(A0),D6
	CMP	Y3(A0),D6
	BGT	D6_W
;Y3>Y2	CAS 1
	MOVE.W	X2(A0),(A5)+
	MOVE.W	Y2(A0),(A5)+
	MOVE.W	X3(A0),(A5)+
	MOVE.W	Y3(A0),(A5)+
	ADDQ.W	#1,D0

	MOVE.W	Y3(A0),D3
	CMP	Y4(A0),D3
	BGT.S	D3W

	MOVE.W	X3(A0),(A5)+
	MOVE.W	Y3(A0),(A5)+
	MOVE.W	X4(A0),(A5)+
	MOVE.W	Y4(A0),(A5)+
	ADDQ.W	#1,D0
	BRA.S	CONT0
D3W	
	MOVE.W	X4(A0),(A6)+
	MOVE.W	Y4(A0),(A6)+
	MOVE.W	X3(A0),(A6)+
	MOVE.W	Y3(A0),(A6)+
	ADDQ.W	#1,D1

CONT0	MOVE.W	Y4(A0),D3
	CMP	Y1(A0),D3
	BGT.S	D3Q
	MOVE.W	X4(A0),(A5)+
	MOVE.W	Y4(A0),(A5)+
	MOVE.W	X1(A0),(A5)+
	MOVE.W	Y1(A0),(A5)+
	ADDQ.W	#1,D0
	BRA	DONE
D3Q	MOVE.W	X1(A0),(A6)+
	MOVE.W	Y1(A0),(A6)+
	MOVE.W	X4(A0),(A6)+
	MOVE.W	Y4(A0),(A6)+
	ADDQ.W	#1,D1
CONT1	BRA	DONE

D6_W	;Y2>Y3 CAS 2
D3E	
	MOVE.W	X3(A0),(A6)+
	MOVE.W	Y3(A0),(A6)+
	MOVE.W	X2(A0),(A6)+
	MOVE.W	Y2(A0),(A6)+
	ADDQ.W	#1,D1
	
	MOVE.W	Y3(A0),D3
	CMP	Y4(A0),D3
	BGT.S	D3F
	MOVE.W	X3(A0),(A5)+
	MOVE.W	Y3(A0),(A5)+
	MOVE.W	X4(A0),(A5)+
	MOVE.W	Y4(A0),(A5)+
	ADDQ.W	#1,D0
	BRA.S	CONT4
D3F	
	MOVE.W	X4(A0),(A6)+
	MOVE.W	Y4(A0),(A6)+
	MOVE.W	X3(A0),(A6)+
	MOVE.W	Y3(A0),(A6)+
	ADDQ.W	#1,D1

CONT4	MOVE.W	Y1(A0),D3
	CMP	Y4(A0),D3
	BGT.S	D3D
	MOVE.W	X1(A0),(A6)+
	MOVE.W	Y1(A0),(A6)+
	MOVE.W	X4(A0),(A6)+
	MOVE.W	Y4(A0),(A6)+
	ADDQ.W	#1,D1
	BRA	DONE
D3D
	MOVE.W	X4(A0),(A5)+
	MOVE.W	Y4(A0),(A5)+
	MOVE.W	X1(A0),(A5)+
	MOVE.W	Y1(A0),(A5)+
	ADDQ.W	#1,D0
CONT2	BRA	DONE

D6_WIN	;Y1>Y2	CAS 3 ET 4
	MOVE.W	4(A0),(A6)+
	MOVE.W	6(A0),(A6)+
	MOVE.W	(A0),(A6)+
	MOVE.W	2(A0),(A6)+
	ADDQ.W	#1,D1

	MOVE.W	4+2(A0),D6	Y2
	CMP	8+2(A0),D6	Y3,Y2
	BGT	D6_WINB
;CAS 4	Y3>Y2
	MOVE.W	4(A0),(A5)+
	MOVE.W	6(A0),(A5)+
	MOVE.W	8(A0),(A5)+
	MOVE.W	10(A0),(A5)+
	ADDQ.W	#1,D0

	MOVE.W	Y4(A0),D3
	CMP	Y3(A0),D3
	BGT.S	D3R
	MOVE.W	X4(A0),(A6)+
	MOVE.W	Y4(A0),(A6)+
	MOVE.W	X3(A0),(A6)+
	MOVE.W	Y3(A0),(A6)+
	ADDQ.W	#1,D1
	BRA.S	CONT6

D3R	MOVE.W	X3(A0),(A5)+
	MOVE.W	Y3(A0),(A5)+
	MOVE.W	X4(A0),(A5)+
	MOVE.W	Y4(A0),(A5)+
	ADDQ.W	#1,D0

CONT6	MOVE.W	Y4(A0),D3
	CMP	Y1(A0),D3
	BGT.S	D3P
	MOVE.W	X4(A0),(A5)+
	MOVE.W	Y4(A0),(A5)+
	MOVE.W	X1(A0),(A5)+
	MOVE.W	Y1(A0),(A5)+
	ADDQ.W	#1,D0
	BRA	DONE
D3P	
	MOVE.W	X1(A0),(A6)+
	MOVE.W	Y1(A0),(A6)+
	MOVE.W	X4(A0),(A6)+
	MOVE.W	Y4(A0),(A6)+
	ADDQ.W	#1,D1
CONT7	BRA	DONE

D6_WINB	;Y2>Y3	CAS 3
	MOVE.W	8(A0),(A6)+
	MOVE.W	10(A0),(A6)+
	MOVE.W	4(A0),(A6)+
	MOVE.W	6(A0),(A6)+
	ADDQ.W	#1,D1

	MOVE.W	Y4(A0),D3
	CMP	Y3(A0),D3
	BGT.S	D3Y
	MOVE.W	X4(A0),(A6)+
	MOVE.W	Y4(A0),(A6)+
	MOVE.W	X3(A0),(A6)+
	MOVE.W	Y3(A0),(A6)+
	ADDQ.W	#1,D1
	BRA.S	CONT8

D3Y	MOVE.W	X3(A0),(A5)+
	MOVE.W	Y3(A0),(A5)+
	MOVE.W	X4(A0),(A5)+
	MOVE.W	Y4(A0),(A5)+
	ADDQ.W	#1,D0

CONT8	MOVE.W	Y1(A0),D3
	CMP	Y4(A0),D3
	BGT.S	D3Z
	MOVE.W	X1(A0),(A6)+
	MOVE.W	Y1(A0),(A6)+
	MOVE.W	X4(A0),(A6)+
	MOVE.W	Y4(A0),(A6)+
	ADDQ.W	#1,D1
	BRA	DONE

D3Z	MOVE.W	X4(A0),(A5)+
	MOVE.W	Y4(A0),(A5)+
	MOVE.W	X1(A0),(A5)+
	MOVE.W	Y1(A0),(A5)+
	ADDQ.W	#1,D0

DONE	LEA	COLONNE_0+2,A1
	LEA	COLONNE_319+2,A2
	LEA	BUF_YS+2,A4
	MOVE.W	D0,D6
	MOVE.W	D1,D7
	SUBQ.W	#1,D6
	SUBQ.W	#1,D7
	LEA	TABLEAU_GAUCHE,A5
	SF	FLAG_CLIP	;
CLIP_GAUCHE	LEA	TAB,A3
	MOVEM.W	(A5)+,D0-D3
	CMP.W	D1,D3
	BNE.S	.DY_DIFF_0
	MOVE.W	D1,(A4)+
	MOVE.W	D1,(A4)+
	BRA.S	.DY_0
.DY_DIFF_0	BSR	CLIPPING
.DY_0	DBRA	D6,CLIP_GAUCHE
	LEA	TABLEAU_DROITE,A5
	ST	FLAG_CLIP	;
CLIP_DROITE	LEA	TAB2,A3
	MOVEM.W	(A5)+,D0-D3
	CMP.W	D1,D3
	BNE.S	.DY_DIFF_0
	MOVE.W	D1,(A4)+
	MOVE.W	D1,(A4)+
	BRA.S	.DY_0
.DY_DIFF_0	BSR	CLIPPING
.DY_0	DBRA	D7,CLIP_DROITE
	LEA	COLONNE_319,A0
	SUB.L	A0,A2
	MOVE.W	A2,D7
	LSR.W	#2,D7
	SUBQ.W	#1,D7
	MOVE.W	D7,(A0)
	LEA	COLONNE_0,A0
	SUB.L	A0,A1
	MOVE.W	A1,D7
	LSR.W	#2,D7
	SUBQ.W	#1,D7
	MOVE.W	D7,(A0)
	LEA	BUF_YS,A0
	SUB.L	A0,A4
	MOVE.W	A4,D7
	LSR.W	#2,D7
	SUBQ.W	#1,D7
	MOVE.W	D7,(A0)
	BSR	MINIM_MAXIM
	LEA	ADRS_CLIP_FIN,A6
	LEA	COLONNE_0,A0
	LEA	TAB,A1
	MOVEQ	#0,D6
	BSR	CLIPPING_FIN
	LEA	COLONNE_319,A0
	LEA	TAB2,A1
	MOVE.W	#319,D6
	BSR	CLIPPING_FIN

	MOVE.W	D3,OFFSET_Y
	LEA	TAB,A0
	LEA	TAB2,A1
	ADD.W	D4,D4
	MOVE.W	#$FFFE,(A1,D4.W)
	MOVE.W	#$FFFF,(A0,D4.W)
	RTS

CLIPPING	MOVEQ	#0,D4
	MOVE.W	D0,D5
	BSR	TEST_CLIP
	MOVE.W	D2,D5
	BSR	TEST_CLIP
	LEA	TABLE_CLIP_X,A6
	MOVE.L	(A6,D4.W),A6	;ADRESSE DE LA TABLE DES Y
	MOVEQ	#0,D4
	MOVE.W	D1,D5
	BSR	TEST_CLIP2
	MOVE.W	D3,D5
	BSR	TEST_CLIP2
	MOVE.L	(A6,D4.W),A6
	JSR	(A6)
	RTS

MINIM_MAXIM	MOVE.W	(A0)+,D0
	MOVE.W	#600,D3
	MOVEQ	#-1,D4
.S_MIN_MAX	MOVE.W	(A0)+,D1
	MOVE.W	(A0)+,D2
	CMP.W	D1,D2
	BLT.S	.D2_PETIT
	CMP.W	D1,D3
	BLT.S	.NO_CHANGE1
	MOVE.W	D1,D3	;NOUVEAU MINIMUM
.NO_CHANGE1	CMP.W	D2,D4
	BGT.S	.NEXT_BIPOINT
	MOVE.W	D2,D4	;NOUVEAU MAXIMUM
	BRA.S	.NEXT_BIPOINT
.D2_PETIT	CMP.W	D2,D3
	BLT.S	.NO_CHANGE2
	MOVE.W	D2,D3	;NOUVEAU MINIMUM
.NO_CHANGE2	CMP.W	D1,D4
	BGT.S	.NEXT_BIPOINT
	MOVE.W	D1,D4	;NOUVEAU MAXIMUM
.NEXT_BIPOINT	DBRA	D0,.S_MIN_MAX
	TST.W	D3
	BGT.S	.VERIF_2
	MOVEQ	#0,D3
.VERIF_2	CMPI.W	#199,D4
	BLT.S	.OK_TOUT
	MOVE.W	#199,D4
.OK_TOUT	RTS

CLIPPING_FIN	MOVE.W	(A0)+,D0
	BGE.S	.FUCK
	RTS
.FUCK	MOVEQ	#0,D5
	MOVE.W	(A0)+,D1	;Y1
	TEST_CLIP_FIN	1
	MOVE.W	(A0)+,D2	;Y2
	TEST_CLIP_FIN	2
	MOVE.L	A1,A4
	MOVE.L	(A6,D5.W),A5
	JSR	(A5)
	DBRA	D0,.FUCK
	RTS

CLIP_FIN_1	CMP.W	D3,D1
	BLT.S	.C_EST_D1
	MOVE.W	D3,D2
	REMPLI_COLONNE
	RTS
.C_EST_D1	MOVE.W	D3,D1
	REMPLI_COLONNE
	RTS

CLIP_FIN_2	CMP.W	D4,D1
	BGT.S	.C_EST_D1
	MOVE.W	D4,D2
	REMPLI_COLONNE
	RTS
.C_EST_D1	MOVE.W	D4,D1
	REMPLI_COLONNE
	RTS

CLIP_FIN_3	MOVE.W	D3,D1
	MOVE.W	D4,D2
	REMPLI_COLONNE
	RTS

CLIP_FIN_4	REMPLI_COLONNE
	RTS
		
CLIP_FIN_5	RTS
CLIP_FIN_6	RTS

COLONNE_0	DS.W	50
COLONNE_319	DS.W	50
BUF_YS	DS.W	50

ADRS_CLIP_FIN	DC.L	CLIP_FIN_5
	DC.L	CLIP_FIN_1
	DC.L	CLIP_FIN_4
	DC.L	CLIP_FIN_3
	DC.L	CLIP_FIN_2
	DC.L	0
	DC.L	CLIP_FIN_6

TABLE_CLIP_X	DC.L	TABLE_CLIP_X_6
	DC.L	TABLE_CLIP_X_1
	DC.L	TABLE_CLIP_X_4
	DC.L	TABLE_CLIP_X_3
	DC.L	TABLE_CLIP_X_2
	DC.L	0
	DC.L	TABLE_CLIP_X_5	

TABLE_CLIP_X_1	DC.L	CLIP_X_1_Y_6
	DC.L	CLIP_X_1_Y_1
	DC.L	CLIP_X_1_Y_4
	DC.L	CLIP_X_1_Y_3
	DC.L	CLIP_X_1_Y_2
	DC.L	0
	DC.L	CLIP_X_1_Y_5
TABLE_CLIP_X_2	DC.L	CLIP_X_2_Y_6
	DC.L	CLIP_X_2_Y_1
	DC.L	CLIP_X_2_Y_4
	DC.L	CLIP_X_2_Y_3
	DC.L	CLIP_X_2_Y_2
	DC.L	0
	DC.L	CLIP_X_2_Y_5
TABLE_CLIP_X_3	DC.L	CLIP_X_3_Y_6
	DC.L	CLIP_X_3_Y_1
	DC.L	CLIP_X_3_Y_4
	DC.L	CLIP_X_3_Y_3
	DC.L	CLIP_X_3_Y_2
	DC.L	0
	DC.L	CLIP_X_3_Y_5
TABLE_CLIP_X_4	DC.L	CLIP_X_4_Y_6
	DC.L	CLIP_X_4_Y_1
	DC.L	CLIP_X_4_Y_4
	DC.L	CLIP_X_4_Y_3
	DC.L	CLIP_X_4_Y_2
	DC.L	0
	DC.L	CLIP_X_4_Y_5
TABLE_CLIP_X_5	DC.L	CLIP_X_5_Y_6
	DC.L	CLIP_X_5_Y_1
	DC.L	CLIP_X_5_Y_4
	DC.L	CLIP_X_5_Y_3
	DC.L	CLIP_X_5_Y_2
	DC.L	0
	DC.L	CLIP_X_5_Y_5
TABLE_CLIP_X_6	DC.L	CLIP_X_6_Y_6
	DC.L	CLIP_X_6_Y_1
	DC.L	CLIP_X_6_Y_4
	DC.L	CLIP_X_6_Y_3
	DC.L	CLIP_X_6_Y_2
	DC.L	0
	DC.L	CLIP_X_6_Y_5

TEST_CLIP	TST.W	D5
	BLT.S	.GO
.SUP_0	ADDQ.W	#1*4,D4	;LES *4 C'EST POUR LA TABLE
	CMPI.W	#319,D5	;...LA TABLE D'APRES!!!
	BLE.S	.GO
.SUP_319	ADD.W	#2*4,D4
.GO	RTS

TEST_CLIP2	TST.W	D5
	BLT.S	.GO
.SUP_0	ADDQ.W	#1*4,D4	;LES *4 C'EST POUR LA TABLE
	CMPI.W	#199,D5	;...LA TABLE D'APRES!!!
	BLE.S	.GO
.SUP_319	ADD.W	#2*4,D4
.GO	RTS

PREP_COD_GEN	LEA	BUF_COD,A6
	LEA	TABLE_ADR-40,A5
	REPT	10
	MOVE.L	#FIN_RT,(A5)+
	ENDR
	MOVEQ	#15,D6	;NB
	MOVEQ	#0,D4	;DECALAGE
.RECOM_GEN	MOVE.W	#319,D5
	MOVEQ	#0,D7	;LONGUEUR
.COD_DIST	MOVE.L	A6,(A5)+
	BSR	REMP_LINE
	BSR	DEC_LINE
	BSR	COD_LINE
	BSR	EFF_LINE
	ADDQ.W	#1,D7
	DBRA	D5,.COD_DIST
	ADDQ.W	#1,D4
	DBRA	D6,.RECOM_GEN
	RTS

	;ENTREE : D4 = DêCALAGE
DEC_LINE	MOVE.W	D7,-(SP)
	LEA	BUF_LINE,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#9,D7
.DEC	MOVE.W	(A0),D0
	ROR.L	D4,D0
	OR.W	D1,D0
	MOVE.W	D0,(A0)+
	CLR.W	D0
	SWAP	D0
	MOVE.W	(A0),D1
	ROR.L	D4,D1
	OR.W	D0,D1
	MOVE.W	D1,(A0)+
	CLR.W	D1
	SWAP	D1
	DBRA	D7,.DEC
	MOVE.W	(SP)+,D7
	RTS

REMP_LINE	MOVE.W	D7,-(SP)
	LEA	BUF_LINE,A0
	MOVEQ	#7,D0
.AFF_LINE_0	BSET	D0,(A0)
	SUBQ.W	#1,D0
	BGE.S	.OK
	MOVEQ	#7,D0
	ADDQ.W	#1,A0
.OK	DBRA	D7,.AFF_LINE_0
	MOVE.W	(SP)+,D7
	RTS

COD_LINE	LEA	BUF_LINE,A0
	MOVEQ	#-8,D1	;OFFSET
	MOVEQ	#0,D2
.CONT_COD	ADDQ.W	#8,D1
	MOVE.W	(A0)+,D0
	BEQ.S	.FIN_COD
	ADDQ.W	#1,D2
	CMPI.W	#-1,D0
	BEQ.S	.SPECIAL_1
	TST.W	D1
	BEQ.S	.NO_OFFSET_1
	MOVE.W	#$397C,(A6)+
	MOVE.W	D0,(A6)+
	MOVE.W	D1,(A6)+
	BRA.S	.CONT_COD
.NO_OFFSET_1	MOVE.W	#$38BC,(A6)+
	MOVE.W	D0,(A6)+
	BRA.S	.CONT_COD
.SPECIAL_1	TST.W	D1
	BEQ.S	.NO_OFFSET_SP_1
	MOVE.W	#$3947,(A6)+
	MOVE.W	D1,(A6)+
	BRA.S	.CONT_COD
.NO_OFFSET_SP_1
	MOVE.W	#$3887,(A6)+
	BRA.S	.CONT_COD
.FIN_COD	
;	CMPI.W	#19,D2
;	BEQ.S	.ONLY_1
;	BGT.S	.FUCK
;	MOVE.W	#$3946,(A6)+
;	MOVE.W	D1,(A6)
;	ADDQ.W	#8,(A6)+
;.ONLY_1	MOVE.W	#$3946,(A6)+
;	MOVE.W	D1,(A6)+
.FUCK	LEA	RT_COOL(PC),A0
	MOVE.W	#LONG_RT,D1
.COPY	MOVE.W	(A0)+,(A6)+
	DBRA	D1,.COPY
;	MOVE.W	#$4ED7,(A6)+	;JMP (A7) !!!
	RTS

EFF_LINE	MOVE.W	D7,-(SP)
	LEA	BUF_LINE,A0
	MOVEQ	#9,D7
	MOVEQ	#0,D0
.EFF	MOVE.L	D0,(A0)+
	DBRA	D7,.EFF
	MOVE.W	(SP)+,D7
	RTS

BUF_LINE	DS.B	42

	INCLUDE	36_RT.S

MOTIF_1	REPT	20
	DC.W	$FFFF,$7FFF,$3FFF,$1FFF,$FFF,$7FF,$3FF,$1FF
	DC.W	$FF,$7F,$3F,$1F,$F,7,3,1
	ENDR

MOTIF_2	REPT	20
	DC.W	$8000,$C000,$E000,$F000,$F800,$FC00,$FE00
	DC.W	$FF00,$FF80,$FFC0,$FFE0,$FFF0,$FFF8,$FFFC
	DC.W	$FFFE,$FFFF
	ENDR

RT_COOL	ADD.L	D5,D2	2
	MOVE.L	D2,A4	1
	MOVE.W	(A0)+,D0	2;X1
	MOVE.W	(A1)+,D1	2;X2
	SUB.W	D0,D1	1;DISTANCE
	ADD.W	D0,D0	1
	ADD.W	(A3,D0.W),A4	5  OFFSET
	ADD.W	D1,D1	1
	ADD.W	D1,D1	1
	ADD.W	(A2,D0.W),D1	4
	MOVE.L	(A6,D1.W),A5	5
	JMP	(A5)	2  TOTAL : 25 NOPS
LONG_RT = ((*-RT_COOL)/2)-1

face_en_cours	
	;OK
;	DC.W	170,99
;	DC.W	329,199
;	DC.W	489,99
;	DC.W	329,0

	;OK
;	INCBIN	A:\CARRE.DAT
	;OK
	INCBIN	A:\CARRE2.DAT
	DC.W	$1234

	;OK
;	DC.W	50,50
;	DC.W	50,150
;	DC.W	150,150
;	DC.W	150,50

	;OK
;	DC.W	0,99
;	DC.W	316,204
;	DC.W	323,4
;	DC.W	145,0

	;OK
;	DC.W	-20,120	;POLYGONE BIEN CLIPPê
;	DC.W	180,220
;	DC.W	310,170
;	DC.W	330,5

	;OK
;	DC.W	3,$FFFC
;	DC.W	$FFFD,$C2
;	DC.W	$13C,$CC
;	DC.W	$143,5

	;OK
;	DC.W	0,99
;	DC.W	159,169
;	DC.W	319,99
;	DC.W	159,30

	;OK
;	DC.W	10,-30	;POLYGONE BIEN CLIPPê
;	DC.W	30,250
;	DC.W	180,180
;	DC.W	300,-20

	;OK
;	DC.W	0,99
;	DC.W	159,299
;	DC.W	319,99
;	DC.W	119,-100

	;OK
;	DC.W	0,99
;	DC.W	159,199
;	DC.W	359,99
;	DC.W	159,0

N	SET	0
TABLE_OFFSET	REPT	20
	DCB.W	16,N
N	SET	N+8
	ENDR

	DS.B	100
TABLE_MULU	REPT	20
	DC.W	0
	DC.W	1280
	DC.W	2*1280
	DC.W	3*1280
	DC.W	4*1280
	DC.W	5*1280
	DC.W	6*1280
	DC.W	7*1280
	DC.W	8*1280
	DC.W	9*1280
	DC.W	10*1280
	DC.W	11*1280
	DC.W	12*1280
	DC.W	13*1280
	DC.W	14*1280
	DC.W	15*1280
	ENDR

TABLE_DECA	REPT	20
	DC.W	0,4,8,12,16,20,24,28,32,36,40,44,48,52,56,60
	ENDR
	SECTION	BSS
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	64000
NB_VBL	DS.W	1
PT_COOR	DS.L	1
OFFSET_Y	DS.W	1
BUF_TEST	DS.B	160
	DS.L	10
TABLE_ADR	DS.L	16*320
TABLEAU_GAUCHE	DS.B	30
TABLEAU_DROITE	DS.B	30
BUF_ADR_GAUCHE	DS.L	8*4
BUF_ADR_DROITE	DS.L	8*4
TAB	DS.W	400
TAB2	DS.W	400
BUF_COD	DS.B	386270