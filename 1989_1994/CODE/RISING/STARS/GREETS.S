X	EQU	5	;(COLONNES DE 16 PIXELS)+1
Y	EQU	63	;HAUTEUR
NB_DECAL	EQU	8	;8 OU 16
NB_DEC	EQU	1	;1 POUR 8 ET 0 POUR 16
PAS	EQU	15*6	;PRISE DE POS DANS LA COURBE

NB_SPR	EQU	4

;	opt	d+
;	opt	o+

	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	ST	$FFFF8240.W

;	MOVEQ	#1,D0
;	JSR	ZIK
		
	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,ANC_PAL
	CLR.L	$40C.W
	MOVE.B	$FFFF8201.W,SAUVEC
	MOVE.B	$FFFF8203.W,SAUVEC+1
	MOVE.L	#BUFFER_FULL,GOUZI
	BSR	PREP_ECR
	BSR	MAK_COD
	LEA	PIC+34,A0
	BSR	PREDECALET
	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVE.W	#7999,D7
RECOMEFF	CLR.L	(A0)+
	CLR.L	(A1)+
	DBRA	D7,RECOMEFF

	LEA	COURB,A0
	LEA	FIN_CRB,A1
	MOVE.L	-12(A1),-12(A0)
	MOVE.L	-8(A1),-8(A0)
	MOVE.L	-4(A1),-4(A0)	
	MOVE.W	#((PAS/6)*NB_SPR)-2,D7
RECOP1	MOVE.L	(A0)+,(A1)+
	MOVE.W	(A0)+,(A1)+
	DBRA	D7,RECOP1
	MOVE.L	#-1,(A1)

	MOVE.L	#COURB,PNT_CRB

	CLR.W	-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP

	MOVE.L	#FIN,$008.w
	MOVE.L	#FIN,$00C.w
	MOVE.L	#FIN,$010.w
	MOVE.L	#FIN,$014.w
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	MOVEM.L	PIC+2,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	
	MOVE.L	PNT_CRB,A5
	SUBA.L	#12,A5
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE.L	#0,A0
	MOVE.L	#0,A1
	MOVE.L	#0,A2
	MOVE.L	#0,A3

	MOVE.L	#VBLR,$70.W
	MOVE.L	#TB0,$120.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W

LOOP	BRA.S	LOOP

FIN	MOVE.L	4.W,A0
	JMP	(A0)
****************************************
VBLR	
	SF	$FFFF8240.W
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	SUITV0
	ST	$FFFF8240.W
SUITV0

NOVBL	MOVE.B	#1,VBF
	MOVE.L	PNT_CRB,A3
	SUBA.L	#12,A3
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7
	MOVE.L	#0,A0
	MOVE.L	#0,A1
	MOVE.L	#0,A2

	MOVEQ	#0,D0
	CLR.L	$140.W

EFF_	MOVE.L	SCREEN2,A4	5
	lea	160+230*50(a4),a4	2
	ADDA.W	4(A3),A4	4
N	SET	0
	REPT	Y
	MOVEM.L	D0-D6/A0-A2,N(A4)	23
N	SET	N+230
	ENDR
;	LEA	PAS(A3),A3	2
;	LEA	-PAS(A3),A3	2

;	MOVE.L	PNT_CRB,A3
;
;	LEA	PREDEC_TAB,A4
;	MOVE.W	(A3),D0
;	LSR	#1,D0
;	MOVE.L	(A4,D0.W),A0
;	MOVE.L	SCREEN2,A1
;	LEA	160+230*0(A1),A1
;	ADDA.W	4(A3),A1

;N	SET	0
;	REPT	Y
;	MOVEM.L	(A0)+,D0-D7/A2-A3
;	MOVEM.L	D0-D7/A2-A3,N(A1)
;N	SET	N+230
;	ENDR

	MOVE	#$8,$FFFF8240.W

full	
	MOVE.L	#$FFFF8209,A4
	MOVE.L	#$FFFF8260,A6
	MOVE.L	#$FFFF820A,A5
	MOVEQ	#0,D1		
	MOVEQ	#$10,D6
;	MOVE.L	#199+28,D0
;	MOVE	#$2700,SR

SYNCHRO	MOVE.B	(A4),D7     * SYNCHRO
	BEQ.S	SYNCHRO
	SUB.W	D7,D6		
	LSL.W	D6,D1
	MOVE	#8,$FFFF8240.W
	DCB	97-4-8-3,$4E71
	MOVEQ	#0,D0
	MOVE.L	D0,D1
	MOVE.L	D0,D2
	MOVE.L	D0,D3
	MOVE.L	D0,D4
	MOVE.L	D0,D5
	MOVE.L	D0,D6
	MOVE.L	D0,D7
	MOVE.L	D0,A0
	MOVE.L	D0,A1
	MOVE.L	D0,A2
	;DCB	97-5-3-4-5-4-5-4-3-3-4-4-4-2,$4E71
**EFFACAGE

;EFF1 FINI

	MOVE	A6,(A6)
	MOVE.B	#0,(A6)
	LEA	PAS(A3),A3	2
	MOVE.L	SCREEN2,A4	5
	lea	160+230*50(a4),a4	2
	ADDA.W	4(A3),A4	4
	MOVEM.L	D0-D6/A0-A2,(A4)	22 1
	MOVEM.L	D0-D6/A0-A2,230(A4)	23 2
	MOVEM.L	D0-D6/A0-A2,230*2(A4)	23 3
	MOVEM.L	D0-D1,230*3(A4)	9-2 4
	MOVE.B	#0,(A5)
	MOVE	A6,(A5)
	MOVEM.L	D0-D4,230*3+4*2(A4)	13 4
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D2,230*3+4*7(A4)	9 4
	NOP
	NOP
	NOP
	;FIN LIGNE 1
	
N	SET	0
	REPT	7-1
	;CETTE SERIE=9 LIGNES
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D6/A0-A2,230*4+N(A4)	23 5
	MOVEM.L	D0-D6/A0-A2,230*5+N(A4)	23 6
	MOVEM.L	D0-D6/A0-A2,230*6+N(A4)	23 7
	MOVEM.L	D0-D6/A0,230*7+N(A4)	19 8
	;DCB     88,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D0-D1,230*7+8*4+N(A4)	7 8
	MOVE.L	D0,230*8+N(A4)	4 9
	DCB     13-7-4,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D3,230*8+4+N(A4)	11 9
	DCB     12-11,$4E71

	;FIN LIGNE 2
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D4,230*8+4+4*4+N(A4)	13 9
	MOVEM.L	D0-D6/A0-A2,230*9+N(A4)	23 10
	MOVEM.L	D0-D6/A0-A2,230*10+N(A4)	23 11
	MOVEM.L	D0-D6/A0-A2,230*11+N(A4)	23 12
	MOVE.L	D0,230*12+N(A4)	4 13
	DCB     88-82-4,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D0-D4,230*12+4+N(A4)	13 13
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D3,230*12+4+4*5+N(A4)	11 13
	DCB     12-11,$4E71
	;FIN LIGNE 3

N	SET	N+230*(13-4)
	ENDR

	;FO ENCORE EN EFFACER 5
N	SET	230*58
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D6/A0-A2,N(A4)	23 5
	MOVEM.L	D0-D6/A0-A2,230+N(A4)	23 6
	MOVEM.L	D0-D6/A0-A2,230*2+N(A4)	23 7
	MOVEM.L	D0-D6/A0,230*3+N(A4)	19 8
	;DCB     88,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D0-D1,230*3+8*4+N(A4)	7 8
	MOVE.L	D0,230*4+N(A4)	4 9
	DCB     13-7-4,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D3,230*4+4+N(A4)	11 9
	DCB     12-11,$4E71

	;FIN LIGNE 2
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D4,230*4+4+4*4+N(A4)	13 9
	DCB     88-13,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	DCB	13,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	DCB     12,$4E71
	;OK

;EFF1 FINI

	MOVE	A6,(A6)
	MOVE.B	#0,(A6)
	LEA	PAS(A3),A3	2
	MOVE.L	SCREEN2,A4	5
	lea	160+230*50(a4),a4	2
	ADDA.W	4(A3),A4	4
	MOVEM.L	D0-D6/A0-A2,(A4)	22 1
	MOVEM.L	D0-D6/A0-A2,230(A4)	23 2
	MOVEM.L	D0-D6/A0-A2,230*2(A4)	23 3
	MOVEM.L	D0-D1,230*3(A4)	9-2 4
	MOVE.B	#0,(A5)
	MOVE	A6,(A5)
	MOVEM.L	D0-D4,230*3+4*2(A4)	13 4
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D2,230*3+4*7(A4)	9 4
	NOP
	NOP
	NOP
	;FIN LIGNE 1
	
N	SET	0
	REPT	7-1
	;CETTE SERIE=9 LIGNES
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D6/A0-A2,230*4+N(A4)	23 5
	MOVEM.L	D0-D6/A0-A2,230*5+N(A4)	23 6
	MOVEM.L	D0-D6/A0-A2,230*6+N(A4)	23 7
	MOVEM.L	D0-D6/A0,230*7+N(A4)	19 8
	;DCB     88,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D0-D1,230*7+8*4+N(A4)	7 8
	MOVE.L	D0,230*8+N(A4)	4 9
	DCB     13-7-4,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D3,230*8+4+N(A4)	11 9
	DCB     12-11,$4E71

	;FIN LIGNE 2
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D4,230*8+4+4*4+N(A4)	13 9
	MOVEM.L	D0-D6/A0-A2,230*9+N(A4)	23 10
	MOVEM.L	D0-D6/A0-A2,230*10+N(A4)	23 11
	MOVEM.L	D0-D6/A0-A2,230*11+N(A4)	23 12
	MOVE.L	D0,230*12+N(A4)	4 13
	DCB     88-82-4,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D0-D4,230*12+4+N(A4)	13 13
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D3,230*12+4+4*5+N(A4)	11 13
	DCB     12-11,$4E71
	;FIN LIGNE 3

N	SET	N+230*(13-4)
	ENDR

	;FO ENCORE EN EFFACER 5
N	SET	230*58
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D6/A0-A2,N(A4)	23 5
	MOVEM.L	D0-D6/A0-A2,230+N(A4)	23 6
	MOVEM.L	D0-D6/A0-A2,230*2+N(A4)	23 7
	MOVEM.L	D0-D6/A0,230*3+N(A4)	19 8
	;DCB     88,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D0-D1,230*3+8*4+N(A4)	7 8
	MOVE.L	D0,230*4+N(A4)	4 9
	DCB     13-7-4,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D3,230*4+4+N(A4)	11 9
	DCB     12-11,$4E71

	;FIN LIGNE 2
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D4,230*4+4+4*4+N(A4)	13 9
	DCB     88-13,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	DCB	13,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	DCB     12,$4E71
	;OK


;EFF2 FINI	
	MOVE	A6,(A6)
	MOVE.B	#0,(A6)
	LEA	PAS(A3),A3	2
	MOVE.L	SCREEN2,A4	5
	lea	160+230*50(a4),a4	2
	ADDA.W	4(A3),A4	4
	MOVEM.L	D0-D6/A0-A2,(A4)	22 1
	MOVEM.L	D0-D6/A0-A2,230(A4)	23 2
	MOVEM.L	D0-D6/A0-A2,230*2(A4)	23 3
	MOVEM.L	D0-D1,230*3(A4)	9-2 4
	MOVE.B	#0,(A5)
	MOVE	A6,(A5)
	MOVEM.L	D0-D4,230*3+4*2(A4)	13 4
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D2,230*3+4*7(A4)	9 4
	NOP
	NOP
	NOP
	;FIN LIGNE 1
	
N	SET	0
	REPT	7-1
	;CETTE SERIE=9 LIGNES
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D6/A0-A2,230*4+N(A4)	23 5
	MOVEM.L	D0-D6/A0-A2,230*5+N(A4)	23 6
	MOVEM.L	D0-D6/A0-A2,230*6+N(A4)	23 7
	MOVEM.L	D0-D6/A0,230*7+N(A4)	19 8
	;DCB     88,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D0-D1,230*7+8*4+N(A4)	7 8
	MOVE.L	D0,230*8+N(A4)	4 9
	DCB     13-7-4,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D3,230*8+4+N(A4)	11 9
	DCB     12-11,$4E71

	;FIN LIGNE 2
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D4,230*8+4+4*4+N(A4)	13 9
	MOVEM.L	D0-D6/A0-A2,230*9+N(A4)	23 10
	MOVEM.L	D0-D6/A0-A2,230*10+N(A4)	23 11
	MOVEM.L	D0-D6/A0-A2,230*11+N(A4)	23 12
	MOVE.L	D0,230*12+N(A4)	4 13
	DCB     88-82-4,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D0-D4,230*12+4+N(A4)	13 13
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D3,230*12+4+4*5+N(A4)	11 13
	DCB     12-11,$4E71
	;FIN LIGNE 3

N	SET	N+230*(13-4)
	ENDR
	
*ICI FIN FINALE POUR ENCHAINER SUR AFFI EN MOVEM
	;FO ENCORE EN EFFACER 5
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D6/A0-A2,230*4+N(A4)	23 5
	MOVEM.L	D0-D6/A0-A2,230*5+N(A4)	23 6
	MOVEM.L	D0-D6/A0-A2,230*6+N(A4)	23 7
	MOVEM.L	D0-D6/A0,230*7+N(A4)	19 8
	;DCB     88,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D0-D1,230*7+8*4+N(A4)	7 8
	MOVE.L	D0,230*8+N(A4)	4 9
	DCB     13-7-4,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D3,230*8+4+N(A4)	11 9
	DCB     12-11,$4E71

;	JMP	POPOPO

	;FIN LIGNE 2
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D4,230*8+4+4*4+N(A4)	13 9
*DEBUT AFF MOVEM
	MOVE.L	PNT_CRB,A3	5
	LEA	PREDEC_TAB,A4	3
	MOVE.W	(A3),D0	2
	LSR	#1,D0	2
	MOVE.L	(A4,D0.W),A0	5
	MOVE.L	SCREEN2,A1	5
	LEA	160+230*50(A1),A1	2
	ADDA.W	4(A3),A1	4

	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVEM.L	D0-D7/A2-A3,(A1)	22
	;NOP
	;NOP
	DCB     88-13-28-45,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	(A0)+,D0-D4	13
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	(A0)+,D5-D7/A2	11
	DCB     12-11,$4E71
;
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVE.L	(A0)+,A3	3
	MOVEM.L	D0-D7/A2-A3,230(A1)	23

	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVEM.L	D0-D7/A2-A3,230*2(A1)	23

	MOVEM.L	(A0)+,D0-D5	15
	DCB     88-72-15,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	(A0)+,D6-D7/A2-A3	11
	DCB     13-11,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D3,230*3(A1)	23
	DCB     12-11,$4E71
;
;LOOP	;4 LIGNES AFFICHENT 9 LIGNES

N	SET	0
	REPT	6
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D4-D7/A2-A3,230*3+4*4+N(A1)	15
	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVEM.L	D0-D7/A2-A3,230*4+N(A1)	23
	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVE.L	D0,230*5+N(A1)	4
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D1-D4,230*5+4+N(A1)	11
	DCB     13-11,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D5-D7/A2,230*5+4+4*4+N(A1)	11
	DCB     12-11,$4E71
;
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVE.L	A3,230*5+4+4*4+4*4+N(A1)	4

	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVEM.L	D0-D7/A2-A3,230*6+N(A1)	23
	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVEM.L	D0-D5,230*7+N(A1)	15
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D6-D7/A2-A3,230*7+4*6+N(A1)
	DCB     13-11,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	(A0)+,D0-D3	11
	DCB     12-11,$4E71
;
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	(A0)+,D4-D7/A2-A3	15
	MOVEM.L	D0-D7/A2-A3,230*8+N(A1)	23
	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVEM.L	D0-D7/A2-A3,230*9+N(A1)	23
	MOVE.L	(A0)+,D0	3
	DCB     88-84-3,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	(A0)+,D1-D5	13
	;DCB     13,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	(A0)+,D6-D7/A2-A3	11
	DCB     12-11,$4E71
;

	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D0-D7/A2-A3,230*10+N(A1)	23
	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVEM.L	D0-D7/A2-A3,230*11+N(A1)	23

	MOVEM.L	(A0)+,D0-D7	19

	;DCB     88,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	(A0)+,A2-A3	7
	MOVE.L	D0,230*12+N(A1)
	DCB     13-7-4,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D1-D3,230*12+4+N(A1)
	DCB     12-11+2,$4E71
;
N	SET	N+230*(10-1)
	ENDR

;FIN
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	; FIN LIGNE 58...+ENCORE 5
	MOVEM.L	D4-D7/A2-A3,230*3+4*4+N(A1)	15
	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVEM.L	D0-D7/A2-A3,230*4+N(A1)	23
	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVE.L	D0,230*5+N(A1)	4
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D1-D4,230*5+4+N(A1)	11
	DCB     13-11,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	D5-D7/A2,230*5+4+4*4+N(A1)	11
	DCB     12-11,$4E71
;
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVE.L	A3,230*5+4+4*4+4*4+N(A1)	4

	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVEM.L	D0-D7/A2-A3,230*6+N(A1)	23
	MOVEM.L	(A0)+,D0-D7/A2-A3	23
	MOVEM.L	D0-D5,230*7+N(A1)	15
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	MOVEM.L	D6-D7/A2-A3,230*7+4*6+N(A1)
	DCB     13-11,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	(A0)+,D0-D3	11
	DCB     12-11,$4E71
;
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVEM.L	(A0)+,D4-D7/A2-A3	15
	MOVEM.L	D0-D7/A2-A3,230*8+N(A1)	23
	;TOUT FINI
	DCB     88-15-23-42,$4E71
	MOVE.L	PNT_CRB,A3		5
	LEA	PAS(A3),A3		2
	LEA	TABLE_GENF+12*8,A4		3
	ADDA.W	2(A3),A4		4
	MOVE.L	SCREEN2,A0		5
	lea	160+230*50(a0),a0	2
	ADDA.W	4(A3),A0		4
	MOVE.L	SCREEN2,A1		5
	lea	160+230*50(a1),a1	2
	ADDA.W	4(A3),A1		4
	MOVE.L	(A4)+,A2	;MSK	3
	MOVE.L	(A4),A4		3

	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	DCB     13,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	DCB     12-4,$4E71
	;DCB	97-5-3-4-5-4-5-4-3-3-4-4-4-2,$4E71

	;JMP	POPOPO

**AFFICHAGE CODE GEN

;	MOVE.L	PNT_CRB,A3		5
;	LEA	PAS(A3),A3
;	LEA	TABLE_GENF,A4	3
;	ADDA.W	2(A3),A4		4
;	MOVE.L	SCREEN2,A0		5
;	lea	160+230*0(a0),a0
;	ADDA.W	4(A3),A0		4
;	MOVE.L	SCREEN2,A1		5
;	lea	160+230*0(a1),a1
;	ADDA.W	4(A3),A1		4
;	MOVE.L	(A4)+,A2	;MSK	3
;	MOVE.L	(A4),A4		3

	JSR	(A4)	4

	REPT	NB_SPR-2
	MOVE    a6,(A6)			2	2
	MOVE.B  #0,(A6)			3	5

;	MOVE.L	PNT_CRB,A3		5

	LEA	PAS(A3),A3
	LEA	TABLE_GENF,A4	3
	ADDA.W	2(A3),A4		4
	MOVE.L	SCREEN2,A0		5
	lea	160+230*50(a0),a0
	ADDA.W	4(A3),A0		4
	MOVE.L	SCREEN2,A1		5
	lea	160+230*50(a1),a1
	ADDA.W	4(A3),A1		4
	MOVE.L	(A4)+,A2	;MSK	3
	MOVE.L	(A4),A4		3

;	JSR	(A4)	4

	DCB     88-40+5-2,$4E71			88	93
	MOVE.b  #0,(A5)			3	96
	MOVE    a6,(A5)			2	98
	DCB     13,$4E71			13	111
	MOVE    a6,(A6)			2	113
	MOVE.B  #0,(A6)			3	116
	DCB     12-4,$4E71			12	128
	JSR	(A4)
	ENDR

	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	MOVE.L	$140.W,D0	4
	MOVE.L	#183-60-8,D1	3
	SUB.L	D0,D1	2

	DCB     88-4-3-2,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	DCB     13,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	DCB     12-4-1,$4E71
	MOVE	#$700,$FFFF8240.W
LAST_LIGNES	NOP
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	DCB     88,$4E71
	MOVE.b  #0,(A5)
	MOVE    a6,(A5)
	DCB     13,$4E71
	MOVE    a6,(A6)
	MOVE.B  #0,(A6)
	DCB     12-4,$4E71
	DBF	D1,LAST_LIGNES

	MOVE	A6,(A6)
	MOVE.B	#0,(A6)
	MOVE	#8,$FFFF8240.W
	DCB	88-4,$4E71
	MOVE.b	#0,(A5)
	MOVE	a6,(A5)
	DCB	13,$4E71
	MOVE	a6,(A6)
	MOVE.B	#0,(A6)
	;	ICI=116	NOPS

	CLR.B	$FFFF820A.W 
	DCB	8,$4E71

	MOVE	A6,(A6)
	MOVE.B	#0,(A6)
	MOVE	A6,(A5)
	DCB	88-2,$4E71
	MOVE.b	#0,(A5)
	MOVE	a6,(A5)
	DCB	13,$4E71
	MOVE	a6,(A6)
	MOVE.B	#0,(A6)
	DCB	12,$4E71
	
	REPT	30
	MOVE	A6,(A6)
	MOVE.B	#0,(A6)
	DCB	88,$4E71
	MOVE.b	#0,(A5)
	MOVE	a6,(A5)
	DCB	13,$4E71
	MOVE	a6,(A6)
	MOVE.B	#0,(A6)
	DCB	12,$4E71
	ENDR

*************************
POPOPO	
	CMPI.B	#$3B,$FFFFFC02.W
	BEQ.S	NOL
	ADDQ.L	#6,PNT_CRB
NOL
	MOVE.L	PNT_CRB,A0
	CMPI.W	#-1,(PAS*NB_SPR)-6(A0)
	BNE.S	FINF
;	BRA	FIN
	MOVE.L	#COURB,PNT_CRB
FINF	
	;JSR	ZIK+34

	BSR.S	SWAPEC

	;ST	$FFFF8240.W
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	RTE
	
TB0	BCLR	#0,$FFFFFA0F.W
	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS
	
MAK_COD	MOVE.L	#TABLE_SPR,ANNEX
	LEA	BUF_COD,A2
	LEA	TABLE_GEN,A6
	LEA	TABLE_GENF,A4
	LEA	FIC_OPT,A3	;OPT POUR LE SPRITE(MASK-SPRITE)
	CLR.W	FLAG_EFF
	BSR	CODING	;D

	LEA	BUF_COD,A2
	LEA	TABLE_GEN,A6
	ADDQ.L	#4,ANNEX
	LEA	FIC_OPT+512,A3
	CLR.W	FLAG_EFF
	BSR	CODING	;A

	RTS

CODING	
RECOMGENSPR	MOVE.L	ANNEX,A0
	MOVE.L	(A0),A0
	MOVE.L	SCREEN1,A1
	MOVE.L	SCREEN2,A5
	MOVE.W	#Y-1,D6
REAF2	MOVE.W	#X-2,D7
REAF1	MOVE.L	(A0),(A1)+
	MOVE.L	4(A0),(A1)+
	MOVE.W	(A0),D0
	MOVE.W	2(A0),D1
	MOVE.W	4(A0),D2
	OR.W	D2,D0
	OR.W	D1,D0
	NOT.W	D0
	MOVE.W	D0,(A5)+
	ADDQ.L	#8,A0
	DBRA	D7,REAF1
	CLR.L	(A1)+
	CLR.L	(A1)+
	MOVE.W	#-1,(A5)+
	LEA	160-((X-1)*8)(A0),A0
	DBRA	D6,REAF2

	MOVE.L	A3,(A6)+
	MOVE.L	A3,(A4)+
	MOVE.L	A2,(A6)+
	MOVE.L	GOUZI,(A4)+
	MOVE.L	GOUZI,(A4)+
	BSR	COD_GEN
	MOVE.L	A2,(A6)+
	BSR	MAK_EFF


	MOVEM.L	D0-A6,SAVE_ALL
	JSR	INCRUSTE
	MOVE.L	A2,GOUZI
	MOVEM.L	SAVE_ALL,D0-A6



	REPT	NB_DECAL-1
	LEA	BUF_COD,A2
	BSR	DECAL_SPR
	MOVE.L	A3,(A6)+
	MOVE.L	A3,(A4)+
	MOVE.L	A2,(A6)+
	MOVE.L	GOUZI,(A4)+
	MOVE.L	GOUZI,(A4)+
	BSR	COD_GEN
	MOVE.L	A2,(A6)+
	BSR	MAK_EFF


	MOVEM.L	D0-A6,SAVE_ALL
	JSR	INCRUSTE
	MOVE.L	A2,GOUZI
	MOVEM.L	SAVE_ALL,D0-A6

	ENDR
	RTS

DECAL_SPR	MOVE.L	SCREEN2,A1	;MSK
	MOVE.W	#NB_DEC,D4
RECOM_DEC	MOVE.L	SCREEN1,A0	;SPR
	MOVE.W	#Y-1,D5
DEC3	MOVE.W	#3,D6
DEC2	MOVE.W	#X-1,D7
DEC1	ROXR	(A0)
	ADDQ.L	#8,A0
	DBRA	D7,DEC1
	LEA	-((X*8)-2)(A0),A0
	DBRA	D6,DEC2
	LEA	(X-1)*8(A0),A0
	DBRA	D5,DEC3
	DBRA	D4,RECOM_DEC
	MOVE.L	SCREEN1,A0
	MOVE.W	#Y-1,D6
MSK2	MOVE.W	#X-1,D7
MSK1	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	MOVE.W	(A0)+,D2
	ADDQ.L	#2,A0
	OR.W	D1,D0
	OR.W	D2,D0
	NOT.W	D0
	MOVE.W	D0,(A1)+
	DBRA	D7,MSK1
	DBRA	D6,MSK2
	RTS

COD_GEN	MOVE.L	SCREEN1,A0	;SPR
	MOVE.L	SCREEN2,A1	;MSK
	;MOVE.L	#$4CDA00FF,(A2)+
	MOVE.L	#$201A221A,(A2)+
	MOVE.L	#$241A261A,(A2)+
	MOVE.L	#$281A2A1A,(A2)+
	MOVE.L	#$2C1A2E1A,(A2)+
	MOVE.W	#Y-1,D6
CDG2	MOVE.W	#X-1,D7
CDG1	BSR	ANDS
	DBRA	D7,CDG1
	MOVE.W	#$41E8,(A2)+
	MOVE.W	#230-(X*8),(A2)+
	BSR	OPTI_ADDQ_ANDS
 	DBRA	D6,CDG2
	LEA	32(A3),A3
	MOVE.L	SCREEN1,A0	;SPR
	MOVE.L	SCREEN2,A1	;MSK
	SUBQ.L	#4,A2
;	MOVE.L	#$4CDA00FF,(A2)+
	MOVE.L	#$201A221A,(A2)+
	MOVE.L	#$241A261A,(A2)+
	MOVE.L	#$281A2A1A,(A2)+
	MOVE.L	#$2C1A2E1A,(A2)+
	MOVE.W	#Y-1,D6
CDG4	MOVE.W	#X-1,D7
CDG3	BSR	ORS
	DBRA	D7,CDG3
	MOVE.W	#$43E9,(A2)+
	MOVE.W	#230-(X*8),(A2)+
	BSR	OPTI_ADDQ_ORS
	DBRA	D6,CDG4
	LEA	32(A3),A3
	SUBQ.L	#4,A2
	MOVE.W	#$4E75,(A2)+
	RTS

ANDS	TST.W	(A1)
	BEQ.S	NO_MASK
	CMPI.W	#-1,(A1)
	BEQ.S	NO_MASK
	;TST.L	0(A0)
	;BEQ	AND1
	MOVE.W	#$C158,D4
	MOVE.W	#$C198,D5
	CLR.W	ADD1
	MOVE.W	(A1),D0
	MOVE.W	#7,D3
CHERCHMSK	CMP.W     	(A3),D0
	BEQ.S	MSKTROUVê
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	ADDI.W	#$200,D5
	ADDQ.W	#4,ADD1
	DBRA	D3,CHERCHMSK
	LEA	-32(A3),A3
	MOVE.W	#$0298,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	#$0258,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	#$5448,(A2)+
;	ADDQ.W	#1,ADD
	BRA.S	FIN_MASK
MSKTROUVê	SUBA.W	ADD1,A3
	MOVE.W	D5,(A2)+
	MOVE.W	D4,(A2)+
	MOVE.W	#$5448,(A2)+
;	ADDQ.W	#1,ADD
	BRA.S	FIN_MASK
NO_MASK	MOVE.W	#$5048,(A2)+
	BSR	OPTI_ADDQ_ANDS
;	ADDQ.W	#1,ADD
FIN_MASK	ADDQ.L	#2,A1
	ADDQ.L	#8,A0
	RTS

AND1	MOVE.W	#$0258,(A2)+
	MOVE.W	(A1),(A2)+
	MOVE.W	#$5C48,(A2)+
	BRA	FIN_MASK

OPTI_ADDQ_ANDS:
	CLR.W	VARI
	MOVE.W	#$5048,D1	;ADDQ.W	#8,A0
	MOVE.W	#$5448,D2	;ADDQ.W	#2,A0
	MOVE.W	#$41E8,D3	;LEA	#IMM(A0),A0
CONTANDQ	CMP.W	-2(A2),D1
	BNE.S	ANDQ2
	ADDQ.W	#8,VARI
	SUBQ.L	#2,A2
	BRA	CONTANDQ
ANDQ2	CMP.W	-2(A2),D2
	BNE.S	ANDQ3
	ADDQ.W	#2,VARI
	SUBQ.L	#2,A2
	BRA	CONTANDQ
ANDQ3	CMP.W	-4(A2),D3
	BNE.S	FINANDQ
	MOVE.W	-2(A2),D4
	ADD.W	D4,VARI
	SUBQ.L	#4,A2
	BRA	CONTANDQ
FINANDQ	TST.W	VARI
	BEQ	SORTANDQ
	CMPI.W	#8,VARI
	BGT	CHANDQ2
	CMPI.W	#2,VARI
	BNE.S	CH1
	MOVE.W	#$5448,(A2)+
	BRA.S	SORTANDQ
CH1	CMPI.W	#4,VARI
	BNE.S	CH11
	MOVE.W	#$5848,(A2)+
	BRA.S	SORTANDQ
CH11	CMPI.W	#6,VARI
	BNE.S	CH111
	MOVE.W	#$5C48,(A2)+
	BRA.S	SORTANDQ
CH111	MOVE.W	#$5048,(A2)+
	BRA.S	SORTANDQ
CHANDQ2	MOVE.W	#$41E8,(A2)+
	MOVE.W	VARI,(A2)+
SORTANDQ	RTS

OPTI_ADDQ_ORS:
	CLR.W	VARI
	MOVE.W	#$5449,D0
	MOVE.W	#$5849,D1
	MOVE.W	#$5C49,D2
	MOVE.W	#$5049,D3
	MOVE.W	#$43E9,D4
CONTORQ	CMP.W	-2(A2),D0
	BNE.S	ORQ2
	ADDQ.W	#2,VARI
	SUBQ.L	#2,A2
	BRA.S	CONTORQ
ORQ2	CMP.W	-2(A2),D1
	BNE.S	ORQ3
	ADDQ.W	#4,VARI
	SUBQ.L	#2,A2
	BRA.S	CONTORQ
ORQ3	CMP.W	-2(A2),D2
	BNE.S	ORQ4
	ADDQ.W	#6,VARI
	SUBQ.L	#2,A2
	BRA.S	CONTORQ
ORQ4	CMP.W	-2(A2),D3
	BNE.S	ORQ5
	ADDQ.W	#8,VARI
	SUBQ.L	#2,A2
	BRA.S	CONTORQ
ORQ5	CMP.W	-4(A2),D4
	BNE.S	SORTORQ
	MOVE.W	-2(A2),D0
	ADD.W	D0,VARI
	MOVE.W	#$5449,D0
	SUBQ.L	#4,A2
	BRA.S	CONTORQ
SORTORQ	TST.W	VARI
	BEQ	FINORQ
	CMPI.W	#8,VARI
	BGT	CHORQ2
	CMPI.W	#2,VARI
	BNE.S	ORQ11
	MOVE.W	#$5449,(A2)+
	BRA	FINORQ
ORQ11	CMPI.W	#4,VARI
	BNE.S	ORQ111
	MOVE.W	#$5849,(A2)+
	BRA	FINORQ
ORQ111	CMPI.W	#6,VARI
	BNE.S	ORQ1111
	MOVE.W	#$5C49,(A2)+
	BRA	FINORQ
ORQ1111	MOVE.W	#$5049,(A2)+
	BRA	FINORQ
CHORQ2	MOVE.W	#$43E9,(A2)+
	MOVE.W	VARI,(A2)+
FINORQ	RTS

ORS	TST.W	(A1)
	BEQ	AFF_MOV
	CMPI.W	#-1,(A1)
	BEQ	NO_AFF
	TST.L	(A0)
	BEQ	AFF3
	TST.L	2(A0)
	BEQ	AFF1
	TST.W	(A0)
	BEQ	AFF23
	TST.W	2(A0)
	BEQ	AFF13
	TST.W	4(A0)
	BEQ	AFF12
	MOVEQ	#0,D0
	MOVE.W	(A0),D0
	CMP.W	2(A0),D0
	BEQ	COMPARE1
	BRA	NOT1SAME
APRES1COMP	MOVEQ	#0,D0
	MOVE.W	4(A0),D0
	CLR.W	ADD1
	MOVE.W	#$8159,D4
	MOVE.W	#7,D3
CHERCHSP1	CMP.L	(A3),D0
	BEQ.S	TROUVê1
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP1
	LEA	-32(A3),A3
	MOVE.W	#$0059,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVê1	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

COMPARE1	CLR.W	ADD1
	SWAP	D0
	MOVE.W	2(A0),D0
	MOVE.W	#$8199,D4
	MOVE.W	#7,D3
CHERCHCOMP1	CMP.L	(A3),D0
	BEQ.S	TROUVêC1
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHCOMP1
	LEA	-32(A3),A3
	MOVE.W	#$0099,(A2)+
	MOVE.L	(A0),(A2)+
	BRA	APRES1COMP
TROUVêC1	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	BRA	APRES1COMP

NOT1SAME	MOVE.W	(A0),D0
	CLR.W	ADD1
	MOVE.W	#$8159,D4
	MOVE.W	#7,D3
CHERCHSPA	CMP.L	(A3),D0
	BEQ.S	TROUVêA
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSPA
	LEA	-32(A3),A3
	MOVE.W	#$0099,(A2)+
	MOVE.L	(A0),(A2)+
	BRA	APRES1COMP
TROUVêA	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	2(A0),D0
	CLR.W	ADD1
	MOVE.W	#$8159,D4
	MOVE.W	#7,D3
CHERCHSPB	CMP.L	(A3),D0
	BEQ.S	TROUVêB
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSPB
	LEA	-32(A3),A3
	MOVE.W	#$0059,(A2)+
	MOVE.W	2(A0),(A2)+
	BRA	APRES1COMP
TROUVêB	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	BRA	APRES1COMP


AFF13	MOVEQ	#0,D0
	MOVE.W	(A0),D0
	CLR.W	ADD1
	MOVE.W	#$8159,D4
	MOVE.W	#7,D3
CHERCHSP13	CMP.L	(A3),D0
	BEQ.S	TROUVê13
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP13
	LEA	-32(A3),A3
	MOVE.W	#$0059,(A2)+
	MOVE.W	(A0),(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	CONT2
TROUVê13	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD

CONT2	MOVEQ	#0,D0
	MOVE.W	4(A0),D0
	CLR.W	ADD1
	MOVE.W	#$8159,D4
	MOVE.W	#7,D3
CHERCHSP1_3	CMP.L	(A3),D0
	BEQ.S	TROUVê1_3
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP1_3
	LEA	-32(A3),A3
	MOVE.W	#$0059,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVê1_3	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	
	BRA	FIN_AFF

AFF3	MOVE.W	#$5849,(A2)+
	BSR	OPTI_ADDQ_ORS
;	ADDQ.W	#1,ADD
	MOVEQ	#0,D0
	MOVE.W	4(A0),D0
	CLR.W	ADD1
	MOVE.W	#$8159,D4
	MOVE.W	#7,D3
CHERCHSP3	CMP.L	(A3),D0
	BEQ.S	TROUVê3
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP3
	LEA	-32(A3),A3
	MOVE.W	#$0059,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVê3	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

AFF1	MOVEQ	#0,D0
	MOVE.W	(A0),D0
	CLR.W	ADD1
	MOVE.W	#$8159,D4
	MOVE.W	#7,D3
CHERCHSP11	CMP.L	(A3),D0
	BEQ.S	TROUVê11
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP11
	LEA	-32(A3),A3
	MOVE.W	#$0059,(A2)+
	MOVE.W	(A0),(A2)+
	MOVE.W	#$5C49,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVê11	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$5C49,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

AFF12	MOVEQ	#0,D0
	MOVE.W	(A0),D0
	CMP.W	2(A0),D0
	BEQ	COMPARE2
	MOVE.W	#$0099,(A2)+
	MOVE.L	(A0),(A2)+
APRES2COMP	MOVE.W	#$5849,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

COMPARE2	CLR.W	ADD1
	MOVE.W	#$8159,D4
	MOVE.W	#7,D3
CHERCHCOMP2	CMP.L	(A3),D0
	BEQ.S	TROUVêC2
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHCOMP2
	LEA	-32(A3),A3
	MOVE.W	#$0099,(A2)+
	MOVE.L	(A0),(A2)+
	BRA	APRES2COMP
TROUVêC2	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	D4,(A2)+
	BRA	APRES2COMP

AFF23	MOVE.W	#$5449,(A2)+
	BSR	OPTI_ADDQ_ORS
;	ADDQ.W	#1,ADD
	TST.W	4(A0)
	BEQ	AFF2
	MOVE.W	#$0099,(A2)+
	MOVE.L	2(A0),(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
AFF2	MOVEQ	#0,D0
	MOVE.W	2(A0),D0
	CLR.W	ADD1
	MOVE.W	#$8159,D4
	MOVE.W	#7,D3
CHERCHSP2	CMP.L	(A3),D0
	BEQ.S	TROUVê2
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#$200,D4
	DBRA	D3,CHERCHSP2
	LEA	-32(A3),A3
	MOVE.W	#$0059,(A2)+
	MOVE.W	2(A0),(A2)+
	MOVE.W	#$5849,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVê2	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$5849,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

AFF_MOV	TST.L	(A0)
	BEQ	MOV3
	TST.L	2(A0)
	BEQ	MOV1
	TST.W	(A0)
	BEQ	MOV23
	TST.W	2(A0)
	BEQ	MOV13
	TST.W	4(A0)
	BEQ	MOV12
	MOVE.W	(A0),D0
	CMP.W	2(A0),D0
	BEQ	COMPARE3
	MOVE.W	(A0),D0
	CLR.W	ADD1
	MOVE.W	#$32C0,D4
	MOVE.W	#7,D3
CHERCHMV66	CMP.L	(A3),D0
	BEQ.S	TROUVêMV66
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV66
	LEA	-32(A3),A3
	BRA	CONT6
TROUVêMV66	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+

	MOVE.W	2(A0),D0
	CLR.W	ADD1
	MOVE.W	#$32C0,D4
	MOVE.W	#7,D3
CHERCHMV77	CMP.L	(A3),D0
	BEQ.S	TROUVêMV77
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV77
	LEA	-32(A3),A3
	MOVE.W	#$32FC,(A2)+
 	MOVE.W	2(A0),(A2)+
	BRA	APRES3COMP
TROUVêMV77	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	BRA	APRES3COMP

CONT6	MOVE.W	#$22FC,(A2)+
	MOVE.L	(A0),(A2)+
APRES3COMP	MOVEQ	#0,D0
	MOVE.W	4(A0),D0
	CLR.W	ADD1
	MOVE.W	#$32C0,D4
	MOVE.W	#7,D3
CHERCHMV33	CMP.L	(A3),D0
	BEQ.S	TROUVêMV33
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV33
	LEA	-32(A3),A3
	MOVE.W	#$32FC,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMV33	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

COMPARE3	SWAP	D0
	MOVE.W	2(A0),D0
	CLR.W	ADD1
	MOVE.W	#$22C0,D4
	MOVE.W	#7,D3
CHERCHCOMP3	CMP.L	(A3),D0
	BEQ.S	TROUVêC3
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#1,D4
	DBRA	D3,CHERCHCOMP3
	LEA	-32(A3),A3
	MOVE.W	#$22FC,(A2)+
	MOVE.L	(A0),(A2)+
	BRA	APRES3COMP
TROUVêC3	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	BRA	APRES3COMP


MOV13	MOVEQ	#0,D0
	MOVE.W	(A0),D0
	CLR.W	ADD1
	MOVE.W	#$32C0,D4
	MOVE.W	#7,D3
CHERCHMV13	CMP.L	(A3),D0
	BEQ.S	TROUVêMV13
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV13
	LEA	-32(A3),A3
	MOVE.W	#$22FC,(A2)+
	MOVE.W	(A0),(A2)+
	CLR.W	(A2)+
	;MOVE.W	#$32CC,(A2)+
	BRA	CONT3
TROUVêMV13	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$4259,(A2)+
CONT3	MOVEQ	#0,D0
	MOVE.W	4(A0),D0
	CLR.W	ADD1
	MOVE.W	#$32C0,D4
	MOVE.W	#7,D3
CHERCHMV1_3	CMP.L	(A3),D0
	BEQ.S	TROUVêMV1_3
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV1_3
	LEA	-32(A3),A3
	MOVE.W	#$32FC,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMV1_3	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
MOV3	MOVE.W	#$4299,(A2)+
	MOVEQ	#0,D0
	MOVE.W	4(A0),D0
	CLR.W	ADD1
	MOVE.W	#$32C0,D4
	MOVE.W	#7,D3
CHERCHMV3	CMP.L	(A3),D0
	BEQ.S	TROUVêMV3
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV3
	LEA	-32(A3),A3
	MOVE.W	#$32FC,(A2)+
	MOVE.W	4(A0),(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMV3	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

MOV1	MOVEQ	#0,D0
	MOVE.W	(A0),D0
	CLR.W	ADD1
	MOVE.W	#$32C0,D4
	MOVE.W	#7,D3
CHERCHMV1	CMP.L	(A3),D0
	BEQ.S	TROUVêMV1
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV1
	LEA	-32(A3),A3
	MOVE.W	#$32FC,(A2)+
	MOVE.W	(A0),(A2)+
	MOVE.W	#$4299,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMV1	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$4299,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
	MOVE.W	#$32FC,(A2)+
	MOVE.W	(A0),(A2)+
	MOVE.W	#$4299,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF


MOV23	MOVE.W	#$4259,(A2)+
	TST.W	4(A0)
	BEQ	MOV2

	MOVEQ	#0,D0
	MOVE.W	2(A0),D0
	CMP.W	4(A0),D0
	BEQ	COMPARE5
	MOVE.W	#$22FC,(A2)+
	MOVE.L	2(A0),(A2)+
APRES5COMP	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

COMPARE5	CLR.W	ADD1
	SWAP	D0
	MOVE.W	4(A0),D0
	MOVE.W	#$22C0,D4
	MOVE.W	#7,D3
CHERCHCOMP5	CMP.L	(A3),D0
	BEQ.S	TROUVêC5
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#1,D4
	DBRA	D3,CHERCHCOMP5
	LEA	-32(A3),A3
	MOVE.W	#$22FC,(A2)+
	MOVE.L	2(A0),(A2)+
	BRA	APRES5COMP
TROUVêC5	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	BRA	APRES5COMP

MOV2	MOVEQ	#0,D0
	MOVE.W	2(A0),D0
	CLR.W	ADD1
	MOVE.W	#$32C0,D4
	MOVE.W	#7,D3
CHERCHMV2	CMP.L	(A3),D0
	BEQ.S	TROUVêMV2
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMV2
	LEA	-32(A3),A3
	MOVE.W	#$22FC,(A2)+
	MOVE.W	2(A0),(A2)+
	CLR.W	(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMV2	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$4259,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF


MOV12	MOVEQ	#0,D0
	MOVE.W	(A0),D0
	CMP.W	2(A0),D0
	BEQ	COMPARE4
	MOVE.W	(A0),D0
	CLR.W	ADD1
	MOVE.W	#$32C0,D4
	MOVE.W	#7,D3
CHERCHMVD	CMP.L	(A3),D0
	BEQ.S	TROUVêMVD
	ADDQ.L	#4,A3
	ADDQ.W	#4,ADD1
	ADDQ.W	#1,D4
	DBRA	D3,CHERCHMVD
	LEA	-32(A3),A3
	MOVE.W	#$22FC,(A2)+
	MOVE.L	(A0),(A2)+
	MOVE.W	#$4259,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
TROUVêMVD	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	#$22FC,(A2)+
	MOVE.W	2(A0),(A2)+
	CLR.W	(A2)+
	;MOVE.W	#$32CC,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF
	
APRES4COMP	MOVE.W	#$4259,(A2)+
	MOVE.W	#$5449,(A2)+
;	ADDQ.W	#1,ADD
	BRA	FIN_AFF

COMPARE4	CLR.W	ADD1
	MOVE.W	#$32C0,D4
	MOVE.W	#7,D3
CHERCHCOMP4	CMP.L	(A3),D0
	BEQ.S	TROUVêC4
	ADDQ.W	#4,ADD1
	ADDQ.L	#4,A3
	ADDI.W	#1,D4
	DBRA	D3,CHERCHCOMP4
	LEA	-32(A3),A3
	MOVE.W	#$22FC,(A2)+
	MOVE.L	(A0),(A2)+
	BRA	APRES4COMP
TROUVêC4	SUBA.W	ADD1,A3
	MOVE.W	D4,(A2)+
	MOVE.W	D4,(A2)+
	BRA	APRES4COMP

NO_AFF	MOVE.W	#$5049,(A2)+	
	BSR	OPTI_ADDQ_ORS
;	ADDQ.W	#1,ADD
FIN_AFF	ADDQ.L	#8,A0
	ADDQ.L	#2,A1
	RTS

PREDECALET	LEA	BUF_DEC,A1
	LEA	PREDEC_TAB,A6
	MOVE.W	#Y-1,D6
RECOP2	MOVE.W	#X-2,D7
RECOP11	MOVE.L	(A0)+,(A1)+
	MOVE.W	(A0)+,(A1)+
	CLR.W	(A1)+
	ADDQ.L	#2,A0
	DBRA	D7,RECOP11
	CLR.L	(A1)+
	CLR.L	(A1)+
	LEA	160-((X-1)*8)(A0),A0
	DBRA	D6,RECOP2

	LEA	BUF_DEC,A0
	MOVE.L	A0,(A6)+
	MOVE.W	#6,D3
DECPGEN	LEA	X*8*Y(A0),A1
	MOVE.L	A1,(A6)+
	MOVE.W	#(X*Y)-1,D7
RECOP3	MOVE.L	(A0)+,(A1)+
	MOVE.L	(A0)+,(A1)+
	DBRA	D7,RECOP3

	MOVE.W	#1,D4
DECPG1	MOVE.W	#Y-1,D5
DECP3	MOVE.W	#3,D6
DECP2	MOVE.W	#X-1,D7
DECP1	ROXR	(A0)
	ADDQ.L	#8,A0
	DBRA	D7,DECP1
	LEA	-((X*8)-2)(A0),A0
	DBRA	D6,DECP2
	LEA	(X-1)*8(A0),A0
	DBRA	D5,DECP3
	LEA	-(X*8*Y)(A0),A0
	DBRA	D4,DECPG1
	DBRA	D3,DECPGEN
	RTS

MAK_EFF	TST.W	FLAG_EFF
	BEQ	FLAGEFF_OFF
	MOVE.L	SCREEN2,A0	;MSK
	MOVE.L	SCREEN1,A1	;SPR
	MOVE.W	#Y-1,D6
RECOMEF2	MOVE.W	#X-1,D7
RECOMEF1	CMPI.W	#-1,(A0)
	BEQ	QQQ
	TST.L	(A1)
	BEQ	EFF3
	TST.L	2(A1)
	BEQ	EFF1
	TST.W	(A1)
	BEQ	EFF23
	TST.W	2(A1)
	BEQ	EFF13
	TST.W	4(A1)
	BEQ	EFF12
EFF_ALL	MOVE.W	#$20C0,(A2)+	;6 NOPS
	MOVE.W	#$30C0,(A2)+
	MOVE.W	#$5448,(A2)+
	BRA.S	NO_EFF
QQQ	MOVE.W	#$5048,(A2)+
	BSR	OPTI_ADDQ_EFF
NO_EFF	ADDQ.L	#8,A1
	ADDQ.L	#2,A0
	DBRA	D7,RECOMEF1
	MOVE.W	#$41E8,(A2)+
	MOVE.W	#230-(X*8),(A2)+
	BSR	OPTI_ADDQ_EFF
	DBRA	D6,RECOMEF2
	MOVE.W	#$4E75,(A2)+
FLAGEFF_OFF	RTS

EFF3	MOVE.W	#$5848,(A2)+
	BSR	OPTI_ADDQ_EFF
	MOVE.W	#$30C0,(A2)+	;4 NOPS
	MOVE.W	#$5448,(A2)+
	BRA	NO_EFF

EFF1	MOVE.W	#$30C0,(A2)+	;3 NOPS
	MOVE.W	#$5C48,(A2)+
	BRA	NO_EFF

EFF23	TST.W	4(A1)
	BEQ	EFF2
	MOVE.W	#$5448,(A2)+	;5 NOPS
	BSR	OPTI_ADDQ_EFF
	MOVE.W	#$20C0,(A2)+
	MOVE.W	#$5448,(A2)+
	BRA	NO_EFF
EFF2	MOVE.W	#$5448,(A2)+	;4 NOPS
	BSR	OPTI_ADDQ_EFF
	MOVE.W	#$30C0,(A2)+
	MOVE.W	#$5848,(A2)+
	BRA	NO_EFF

EFF13	MOVE.W	#$30C0,(A2)+	;6 NOPS
	MOVE.W	#$5448,(A2)+
	MOVE.W	#$30C0,(A2)+
	MOVE.W	#$5448,(A2)+
	BRA	NO_EFF

EFF12	MOVE.W	#$20C0,(A2)+	;4 NOPS
	MOVE.W	#$5848,(A2)+
	BRA	NO_EFF

OPTI_ADDQ_EFF
	MOVE.W	#$5448,D0
	MOVE.W	#$5848,D1
	MOVE.W	#$5C48,D2
	MOVE.W	#$5048,D3
	MOVE.W	#$41E8,D4
	CLR.W	ADD1
CONT_OPTEF	CMP.W	-2(A2),D0
	BNE.S	ADEF2
	ADDQ.W	#2,ADD1
	SUBQ.L	#2,A2
	BRA	CONT_OPTEF
ADEF2	CMP.W	-2(A2),D1
	BNE.S	ADEF3
	ADDQ.W	#4,ADD1
	SUBQ.L	#2,A2
	BRA	CONT_OPTEF
ADEF3	CMP.W	-2(A2),D2
	BNE.S	ADEF4
	ADDQ.W	#6,ADD1
	SUBQ.L	#2,A2
	BRA	CONT_OPTEF
ADEF4	CMP.W	-2(A2),D3
	BNE.S	ADEF5
	ADDQ.W	#8,ADD1
	SUBQ.L	#2,A2
	BRA	CONT_OPTEF
ADEF5	CMP.W	-4(A2),D4
	BNE.S	FIN_OPTEF
	MOVE.W	-2(A2),D0
	ADD.W	D0,ADD1
	MOVE.W	#$5448,D0
	SUBQ.L	#4,A2
	BRA	CONT_OPTEF
FIN_OPTEF	TST.W	ADD1
	BEQ	FINI_OPTEF
	CMPI.W	#8,ADD1
	BGT	MAK_LEAEF
	CMPI.W	#2,ADD1
	BNE.S	TESTEF2
	MOVE.W	#$5448,(A2)+
	BRA	FINI_OPTEF
TESTEF2	CMPI.W	#4,ADD1
	BNE.S	TESTEF3
	MOVE.W	#$5848,(A2)+
	BRA	FINI_OPTEF
TESTEF3	CMPI.W	#6,ADD1
	BNE.S	TESTEF4
	MOVE.W	#$5C48,(A2)+
	BRA	FINI_OPTEF
TESTEF4	MOVE.W	#$5048,(A2)+
	BRA	FINI_OPTEF
MAK_LEAEF	MOVE.W	#$41E8,(A2)+
	MOVE.W	ADD1,(A2)+
FINI_OPTEF	RTS

PREP_ECR	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#64000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

INCRUSTE	CLR.L	LIGNES
	CLR.L	TM
	CLR.L	D0
	CLR.L	D1
	
	LEA	BUF_COD,A0
	LEA	INSTRUCTIONS,A1
	LEA	BUFFER_FULL,A2
	ADDA.L	PNT_F,A2
BOUCLE	MOVE	(A1),D2
	MOVE	2(A1),D1
	MOVE	4(A1),D0
	CMP	(A0),D0
	BEQ.S	THATS_IT
	CMPI	#$4E75,(A0)
	BEQ	THE_END
	CMPI	#$ABCD,D0
	BEQ	PROBLEME_NUMERO_UN
	LEA	6*2(A1),A1
	BRA.S	BOUCLE
THATS_IT
	CMPI.L	#0,TM
	BNE.S	PAS_OVER_GAUCHE
	MOVE.L	#$3C8E1CBC,(A2)+
	MOVE	#$0000,(A2)+
	MOVE.L	#5,TM
	ADDq.L	#6,PNT_F
PAS_OVER_GAUCHE
	CMPI.L	#93,TM
	BLE	PARTIE_UN
	CMPI.L	#111,TM
	BLE.S	PARTIE_DEUX
	CMPI.L	#128,TM
	BLE	PARTIE_TROIS


PARTIE_TROIS
	MOVE.L	TM,D4
	ADD.L	D1,D4
	CMPI.L	#128,D4
	BGT.S	TOUJOURS_TROP_GRAND	SI=ALORS CA RENTRE
	BRA	CA_RENTRE
TOUJOURS_TROP_GRAND
	MOVE.L	#128,D4
	MOVE.L	TM,D5
	SUB.L	D5,D4
	CMPI.L	#0,D4
	BEQ.S	DECIDEMENT_PAS_DE_NOP
	SUBQ.L	#1,D4
NOPZ	MOVE	#$4E71,(A2)+
	ADDq.L	#2,PNT_F
	DBF	D4,NOPZ
DECIDEMENT_PAS_DE_NOP	
	MOVE.L	#$3C8E1CBC,(A2)+
	MOVE	#$0000,(A2)+
	MOVE.L	#5,TM
	ADDq.L	#6,PNT_F
	ADD.L	#1,LIGNES
	BRA	CA_RENTRE

PARTIE_DEUX
	MOVE.L	TM,D4
	ADD.L	D1,D4
	CMPI.L	#111,D4
	BGT.S	ENCORE_TROP_GRAND	SI=ALORS CA RENTRE
	BRA	CA_RENTRE
ENCORE_TROP_GRAND
	MOVE.L	#111,D4
	MOVE.L	TM,D5
	SUB.L	D5,D4
	CMPI.L	#0,D4
	BEQ.S	TOUJOURS_PAS_DE_NOP
	SUBQ.L	#1,D4
NOPX	MOVE	#$4E71,(A2)+
	ADDq.L	#2,PNT_F
	DBF	D4,NOPX
TOUJOURS_PAS_DE_NOP	
	MOVE.L	#$3C8E1CBC,(A2)+
	MOVE	#$0000,(A2)+
	ADDq.L	#6,PNT_F
	MOVE.L	#116,TM
;	BRA.S	CA_RENTRE
	BRA	PAS_OVER_GAUCHE
	
PARTIE_UN
	MOVE.L	TM,D4
	ADD.L	D1,D4
	CMPI.L	#93,D4
	BGT.S	TROP_GRAND	SI=ALORS CA RENTRE
	BRA	CA_RENTRE
TROP_GRAND
	MOVE.L	#93,D4
	MOVE.L	TM,D5
	SUB.L	D5,D4
	CMPI.L	#0,D4
	BEQ.S	PAS_DE_NOP
	SUBQ.L	#1,D4
NOPY	MOVE	#$4E71,(A2)+
	ADDq.L	#2,PNT_F
	DBF	D4,NOPY
PAS_DE_NOP	
	MOVE.L	#$1ABC0000,(A2)+
	MOVE	#$3A8E,(A2)+
	ADDq.L	#6,PNT_F
	MOVE.L	#98,TM
CA_RENTRE	
	SUBQ	#1,D2
ICI	MOVE	(A0)+,(A2)+
	ADDq.L	#2,PNT_F
	DBF	D2,ICI
	ADD.L	D1,TM
	LEA	INSTRUCTIONS,A1
	BRA	BOUCLE

THE_END
	JSR	NOPS_QUI_RESTENT
	MOVE	#$4E75,(A2)
	
	MOVE.L	#TEXTE2,-(SP)
	MOVE	#$9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
;	MOVE	#$7,-(SP)
;	TRAP	#1
;	ADDQ.L	#2,SP
	RTS

PROBLEME_NUMERO_UN
	MOVE	#$070,$FFFF8240.W
	MOVE.L	#TEXTE1,-(SP)
	MOVE	#$9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE	#$7,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	CLR.L	-(SP)
	TRAP	#1	
	

PROBLEME_NUMBER_TWO
	MOVE	#$700,$FFFF8240.W
	MOVEM.L	D0-A6,-(SP)
	MOVE.L	#TEXTE3,-(SP)
	MOVE	#$9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
;	MOVE	#$7,-(SP)
;	TRAP	#1
;	ADDQ.L	#2,SP
	MOVEM.L	(SP)+,D0-A6

	MOVE.L	TM,D0
	MOVE.L	#128,D1
	SUB.L	D0,D1
	CMPI.L	#0,D1
	BEQ.S	PADNOP
	SUBQ.L	#1,D1
LMK	MOVE	#$4E71,(A2)+
	ADDQ.L	#2,PNT_F
	DBF	D1,LMK
PADNOP	
	MOVE	#$3C8E,(A2)+
	MOVE.L	#$1CBC0000,(A2)+
	ADDQ.L	#6,PNT_F
	REPT	44
	MOVE.L	#$4E714E71,(A2)+
	ADDQ.L	#4,PNT_F
	ENDR
	MOVE.L	#$1ABC0000,(A2)+
	MOVE	#$3A8E,(A2)+
	ADDQ.L	#6,PNT_F
	REPT	6
	MOVE.L	#$4E714E71,(A2)+
	ADDQ.L	#4,PNT_F
	ENDR
	MOVE.L	#$4E713C8E,(A2)+
	MOVE.L	#$1CBC0000,(A2)+
	ADDQ.L	#8,PNT_F
;	REPT	4
;	MOVE.L	#$4E714E71,(A2)+
;	ADDQ.L	#4,PNT_F
;	ENDR

	MOVE	#$06B8,(A2)+
	MOVE.L	LIGNES,(A2)
	ADD.L	#0,(A2)+
	MOVE	#$0140,(A2)+
	ADDQ.L	#8,PNT_F
	
	MOVE	#$777,$FFFF8240.W
;	CLR.L	-(SP)
;	TRAP	#1
	BRA	OUT
NOPS_QUI_RESTENT
	CMPI.L	#124,TM
	BGT	PROBLEME_NUMBER_TWO

	CMPI.L	#93,TM
	BEQ.S	MILIEU
	CMPI.L	#111,TM
	BEQ.S	OUT_DR
	CMPI.L	#124,TM
	BEQ	PROBLEME_NUMBER_TWO
	MOVE	#$4E71,(A2)+
	ADDq.L	#2,PNT_F
	ADD.L	#1,TM
	BRA.S	NOPS_QUI_RESTENT
MILIEU	MOVE.L	#$1ABC0000,(A2)+
	MOVE	#$3A8E,(A2)+
	ADDq.L	#6,PNT_F
	ADD.L	#5,TM
	BRA.S	NOPS_QUI_RESTENT
OUT_DR	MOVE.L	#$3C8E1CBC,(A2)+
	MOVE	#$0000,(A2)+
	ADDq.L	#6,PNT_F
	ADD.L	#5,TM
	BRA	NOPS_QUI_RESTENT
OUT	MOVE	#$4E75,(A2)+
	ADDq.L	#2,PNT_F
	RTS
	
TEXTE1	DC.B	'INSTRUCTION NON RECONNUE.',0
TEXTE2	DC.B	'CA ROULE.',0
TEXTE3	DC.B	'PROB DS LES NOPS DE FIN.',0
	EVEN
	
INSTRUCTIONS
; DC 2,3,$32FC,$0000,$0000,$0000	MOVE #0,(A1)+
; DC 1,2,$32CC,$0000,$0000,$0000	MOVE A4,(A1)+

 DC 1,3,$4259,$0000,$0000,$0000	CLR	(A1)+
 DC 1,5,$4299,$0000,$0000,$0000	CLR.L	(A1)+


; DC 2,19,$4CDA,$00FF,$0,$0                     MOVEM.L (A2)+,D0-D7
; DC 2,19,$4CDB,$00FF,$0,$0                     MOVEM.L (A3)+,D0-D7
 DC 2,2,$41E8,$00A0,$0,$0                     LEA     160(A0),A0
 DC 2,2,$43E9,$00A0,$0,$0                     LEA     160(A1),A1
 DC 1,5,$C198,$0,$0,$0                         AND.L   D0,(A0)+
 DC 1,5,$C398,$0,$0,$0                         AND.L   D1,(A0)+
 DC 1,5,$C598,$0,$0,$0                         AND.L   D2,(A0)+
 DC 1,5,$C798,$0,$0,$0                         AND.L   D3,(A0)+
 DC 1,5,$C998,$0,$0,$0                         AND.L   D4,(A0)+
 DC 1,5,$CB98,$0,$0,$0                         AND.L   D5,(A0)+
 DC 1,5,$CD98,$0,$0,$0                         AND.L   D6,(A0)+
 DC 1,5,$CF98,$0,$0,$0                         AND.L   D7,(A0)+

 DC 1,3,$C158,$0,$0,$0                         AND     D0,(A0)+
 DC 1,3,$C358,$0,$0,$0                         AND     D1,(A0)+
 DC 1,3,$C558,$0,$0,$0                         AND     D2,(A0)+
 DC 1,3,$C758,$0,$0,$0                         AND     D3,(A0)+
 DC 1,3,$C958,$0,$0,$0                         AND     D4,(A0)+
 DC 1,3,$CB58,$0,$0,$0                         AND     D5,(A0)+
 DC 1,3,$CD58,$0,$0,$0                         AND     D6,(A0)+
 DC 1,3,$CF58,$0,$0,$0                         AND     D7,(A0)+


 DC 3,7,$0298,$0000,$00A0,$0                 ANDI.L  #160,(A0)+
 DC 2,4,$0258,$00A0,$0,$0                     ANDI    #160,(A0)+
 DC 3,5,$22FC,$0000,$00A0,$0                 MOVE.L  #160,(A1)+
 DC 2,3,$32FC,$00A0,$0,$0                     MOVE    #160,(A1)+

 DC 1,2,$32C0,$0,$0,$0                         MOVE    d0,(A1)+
 DC 1,2,$32C1,$0,$0,$0                         MOVE    d1,(A1)+
 DC 1,2,$32C2,$0,$0,$0                         MOVE    d2,(A1)+
 DC 1,2,$32C3,$0,$0,$0                         MOVE    d3,(A1)+
 DC 1,2,$32C4,$0,$0,$0                         MOVE    d4,(A1)+
 DC 1,2,$32C5,$0,$0,$0                         MOVE    d5,(A1)+
 DC 1,2,$32C6,$0,$0,$0                         MOVE    d6,(A1)+
 DC 1,2,$32C7,$0,$0,$0                         MOVE    d7,(A1)+

 DC 1,3,$22C0,$0,$0,$0                                        MOVE.L    d0,(A1)+
 DC 1,3,$22C1,$0,$0,$0                                        MOVE.L    d1,(A1)+
 DC 1,3,$22C2,$0,$0,$0                                          MOVE.L    d2,(A1)+
 DC 1,3,$22C3,$0,$0,$0                                          MOVE.L    d3,(A1)+
 DC 1,3,$22C4,$0,$0,$0                                        MOVE.L    d4,(A1)+
 DC 1,3,$22C5,$0,$0,$0                                         MOVE.L    d5,(A1)+
 DC 1,3,$22C6,$0,$0,$0                                       MOVE.L    d6,(A1)+
 DC 1,3,$22C7,$0,$0,$0                                         MOVE.L    d7,(A1)+


 DC 1,2,$5449,$0,$0,$0                         ADDQ    #2,A1
 DC 1,2,$5849,$0,$0,$0                         ADDQ    #4,A1
 DC 1,2,$5C49,$0,$0,$0                         ADDQ    #6,A1
 DC 1,2,$5049,$0,$0,$0                         ADDQ    #8,A1

 DC 1,2,$5448,$0,$0,$0                         ADDQ    #2,A0   
 DC 1,2,$5848,$0,$0,$0                         ADDQ    #4,A0   
 DC 1,2,$5C48,$0,$0,$0                         ADDQ    #6,A0   
 DC 1,2,$5048,$0,$0,$0                         ADDQ    #8,A0   


 DC 3,7,$0099,$0000,$00A0,$0                 ORI.L   #160,(A1)+
 DC 2,4,$0059,$00A0,$0,$0                     ORI     #160,(A1)+
 DC 1,3,$8159,$0,$0,$0                         OR      D0,(A1)+
 DC 1,3,$8359,$0,$0,$0                         OR      D1,(A1)+
 DC 1,3,$8559,$0,$0,$0                         OR      D2,(A1)+
 DC 1,3,$8759,$0,$0,$0                         OR      D3,(A1)+
 DC 1,3,$8959,$0,$0,$0                         OR      D4,(A1)+
 DC 1,3,$8B59,$0,$0,$0                         OR      D5,(A1)+
 DC 1,3,$8D59,$0,$0,$0                         OR      D6,(A1)+
 DC 1,3,$8F59,$0,$0,$0                         OR      D7,(A1)+

 DC 1,5,$8199,$0,$0,$0                         OR.L    D0,(A1)+
 DC 1,5,$8399,$0,$0,$0                         OR.L    D1,(A1)+
 DC 1,5,$8599,$0,$0,$0                         OR.L    D2,(A1)+
 DC 1,5,$8799,$0,$0,$0                         OR.L    D3,(A1)+
 DC 1,5,$8999,$0,$0,$0                         OR.L    D4,(A1)+
 DC 1,5,$8B99,$0,$0,$0                         OR.L    D5,(A1)+
 DC 1,5,$8D99,$0,$0,$0                         OR.L    D6,(A1)+
 DC 1,5,$8F99,$0,$0,$0                         OR.L    D7,(A1)+


 DC 1,3,$201A,$0,$0,$0	MOVE.L	(A2)+,D0
 DC 1,3,$221A,$0,$0,$0	MOVE.L	(A2)+,D1
 DC 1,3,$241A,$0,$0,$0	MOVE.L	(A2)+,D2
 DC 1,3,$261A,$0,$0,$0	MOVE.L	(A2)+,D3
 DC 1,3,$281A,$0,$0,$0	MOVE.L	(A2)+,D4
 DC 1,3,$2A1A,$0,$0,$0	MOVE.L	(A2)+,D5
 DC 1,3,$2C1A,$0,$0,$0	MOVE.L	(A2)+,D6
 DC 1,3,$2E1A,$0,$0,$0	MOVE.L	(A2)+,D7

; DC 1,3,$201B,$0,$0,$0	MOVE.L	(A3)+,D0
; DC 1,3,$221B,$0,$0,$0	MOVE.L	(A3)+,D1
; DC 1,3,$241B,$0,$0,$0	MOVE.L	(A3)+,D2
; DC 1,3,$261B,$0,$0,$0	MOVE.L	(A3)+,D3
; DC 1,3,$281B,$0,$0,$0	MOVE.L	(A3)+,D4
; DC 1,3,$2A1B,$0,$0,$0	MOVE.L	(A3)+,D5
; DC 1,3,$2C1B,$0,$0,$0	MOVE.L	(A3)+,D6
; DC 1,3,$2E1B,$0,$0,$0	MOVE.L	(A3)+,D7



 DC	9,9,$ABCD			;FIN

;FULLSCREEN
;1283	MOVE.B	D3,(A1)			2
;	NOP				1	3
;1284	MOVE.B	D4,(A1)			2	5
;	DCB	89,$4E71			89	94
;1084	MOVE.B	D4,(A0)			2	96
;1083	MOVE.B	D3,(A0)			2	98
;	DCB	13,$4E71			13	111
;1283	MOVE.B	D3,(A1)			2	113
;	NOP				1	114
;1284	MOVE.B	D4,(A1)			2	116
;	DCB	12,$4E71			12	128

; 3C8E     MOVE    a6,(A6)			2	2
; 1CBC0000 MOVE.B  #0,(A6)			3	5
;          DCB     88,$4E71			88	93
; 1ABC0000 MOVE.b  #0,(A5)			3	96
; 3A8E     MOVE    a6,(A5)			2	98
;          DCB     13,$4E71			13	111
; 3C8E     MOVE    a6,(A6)			2	113
; 1CBC0000 MOVE.B  #0,(A6)			3	116
;          DCB     12,$4E71			12	128

	
	SECTION	DATA
;ZIK	INCBIN	a:\NEXT2.SND

	DS.L	1000
COURB	INCBIN	BIGGIE5B.CRB
FIN_CRB	DS.L	1120
	DC.W	-1

PIC	INCBIN	BULLES.PI1
FIC_OPT	INCBIN	BULLES.OPT

TABLE_SPR	DC.L	PIC+34	E
	DC.L	PIC+34+63*160	L

	SECTION	BSS
ANC_PAL	DS.L	8
ANC_VBL	DS.L	1
ANC_TIM	DS.L	1
SAUV_SP	DS.L	1
SAUVEC	DS.W	1
MFP	DS.B	8
	DS.B	256
BUFFER	DS.B	64000*2
VBF	DS.B	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
PREDEC_TAB	DS.L	8
BUF_DEC	DS.L	(12*Y)*8
PNT_CRB	DS.L	1
VARI	DS.W	1
ADD1	DS.W	1
ANNEX	DS.L	1
SAVE_ALL	DS.L	16
FLAG_EFF	DS.W	1
PNT_F	DS.L	1
GOUZI	DS.L	1
TABLE_GEN	DS.L	25
TABLE_GENF	DS.L	96

LIGNES	DS.L	1
TM	DS.L	1	

BUF_COD	DS.B	20000
END_COD
BUFFER_FULL
	DS.B	192000
	EVEN
