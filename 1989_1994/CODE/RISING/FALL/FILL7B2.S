;FILL - Version 7: MOVEs/une routine par Dx et par d‚calage.
;88+3 nops/ligne pleine => 440 lignes/VBL.
;455 lignes sans le DBF. ( je le laisse pour tester, c'est + simple )

	CLR.L	-(SP)
	MOVE	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP	

	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W

	move.l	#BUFFER,d0
	CLR.B	D0
	move.l	d0,SCREEN1		
	add.l	#32000,d0		
	move.l	d0,SCREEN2
	
	LEA	CODE,A0
	LEA	TABLE,A1
	MOVE.L	A0,(A1)+
	LEA	TABLO,A6
	MOVE	#%1000000000000000,(A6)
	MOVE	#%0100000000000000,D6

ANEW	LEA	TABLO,A6
	LEA	TABLO_COPIE,A5
	REPT	10
	MOVE.L	(A6)+,(A5)+
	ENDR

AGAIN	LEA	TABLO,A6
	MOVEQ	#0,D7
	
VIDE	MOVE	(A6)+,D0
	BEQ.S	VIDE
	CMPI	#$9999,D0
	BEQ.S	GNR

	CMPI	#$FFFF,D0
	BNE.S	NON_PLEIN

	TST	D7
	BEQ.S	DEBUT_DE_LIGNE
	MOVE	#$3141,(A0)+
	MOVE	D7,(A0)+
	ADDQ	#8,D7
	BRA.S	SUIT0
DEBUT_DE_LIGNE	MOVE	#$3081,(A0)+
	ADDQ	#8,D7
	BRA.S	SUIT0

NON_PLEIN	TST	D7
	BEQ.S	DEB_LINE

	MOVE	#$317C,(A0)+
	MOVE	D0,(A0)+
	MOVE	D7,(A0)+
	ADDQ	#8,D7
	BRA.S	SUIT0
DEB_LINE	MOVE	#$30BC,(A0)+
	MOVE	D0,(A0)+
	ADDQ	#8,D7

SUIT0	BRA.S	VIDE
	
GNR	MOVE	#$4ED5,(A0)+
	MOVE.L	A0,(A1)+
	LEA	TABLO,A6
N	SET	0
	REPT	20
	ROXR	N(A6)
N	SET	N+2
	ENDR

	ADDQ.L	#1,CNT
	CMPI.L	#16,CNT
	BNE	AGAIN
	CLR.L	CNT
	LEA	TABLO,A5
	LEA	TABLO_COPIE,A6
	REPT	10
	MOVE.L	(A6)+,(A5)+
	ENDR
	LEA	TABLO,A6
MODX	EQU	*+2
	LEA	0(A6),A6
	OR	D6,(A6)
	LSR	#1,D6
	BNE	ANEW
	MOVE	#%1000000000000000,D6
	ADDQ	#2,MODX
	CMPI	#40,MODX
	BEQ.S	FINITO
	BRA	ANEW
	
FINITO	NOP

	LEA	TABLE_,A1
	LEA	TABLE,A0
	MOVE.L	A0,A2
	MOVE	#16-1,D1
TA2	MOVE.L	A2,A0
	MOVE	#320-1,D0
TA	MOVE.L	(A0),(A1)+
	LEA	16*4(A0),A0
	DBF	D0,TA
	ADDQ	#4,A2
	DBF	D1,TA2

	MOVE.L	#VBL,$70.W
H	BRA.S	H
FIN	MOVE.L	4.W,A0
	JMP	(A0)
VBL	
	SF	$FFFF8240.W
;	BRA	T
	MOVE.L	SCREEN2,A6
	moveq	#0,d0
	MOVE	#199,D1
EFF
N	SET	0
	REPT	20
	MOVE	D0,N(A6)
N	SET	N+8
	ENDR
	LEA	160(A6),A6
	DBF	D1,EFF

T	MOVE	#$001,$FFFF8240.W

	MOVE.L	SCREEN2,A6	6? 7?
	MOVE.L	#$FFFFFFFF,D1	3?

	LEA	TABL,A3	3?
	LEA	TABL2,A2	3?
	LEA	TABLE_,A4	3?
	LEA	RETOUR,A5
	MOVE	#%1111111111111000,D5

	MOVE	#200-1,D3	2

ICI	
	MOVE.L	A6,A0	1
MOD2	EQU	*+2
	MOVE	#1,D6	2
MOD	EQU	*+2
	MOVE	#1,D7	2

	SUB	D6,D7	1
;	LSL	#6,D7	5
	ADD	D7,D7	1
	ADD	D7,D7	1

;	AND	#$F,D6
;	ADD	D6,D6	1
;	ADD	D6,D6	1	
;	ADD	D6,D7	1

	ADD	D6,D6	1
	ADD	(A3,D6.W),D7	4 (DC.B = OPT...)

;	ADD	D6,D6	1
	ADDA	(A2,D6.W),A0	5

;	SUBQ	#1,D6	1
;	LSR	#2,D6	3
;	AND	D5,D6	1
;	ADDA	D6,A0	2

	MOVE.L	(A4,D7.W),A1	5
	JMP	(A1)	2+61
RETOUR
	LEA	160(A6),A6	2
	DBF	D3,ICI	3
;total=88+3 nops
;On peut virer le DBF en mettant RETOUR en ICI...on monte alors …
;455 lignes. Mais on ne sait plus quand arreter!!
;Le pb est r‚solu en tps normal coz on aura MOVE (An)+,Dn pour les
;coords. Il suffit de faire une routine sp‚ciale pour des coords
;sp‚ciales. Ex: DeltaX=321 => dans la table de 320*16, rajouter
;une adresse: l'adresse du retour final...

;MOVE D1,(A0) = 2
;MOVE D1,0(A0) = 3
;1 ligne pleine = un Move sans offset+19 Moves avec offsets + JMP (A5)
;               = 2 + 19*3 + 2 = 61 nops
	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	NO_T
	MOVE	#$002,$FFFF8240.W
NO_T	NOP
	CMPI.B	#$3B,$FFFFFC02.W
	BNE.S	NO_T2
	ADDQ	#1,MOD
NO_T2	NOP
	CMPI.B	#$3c,$FFFFFC02.W
	BNE.S	NO_T2c
	ADDQ	#1,MOD2
NO_T2c	NOP
	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	move.l	SCREEN1,d0	
	lsr.l	#8,d0		
	move.l	#$ffff8201,a0
	movep	d0,(a0)

	RTE

	DATA
TABLE1
N	SET	0
	REPT	20
	DC	%1111111111111111,N
	DC	%0111111111111111,N
	DC	%0011111111111111,N
	DC	%0001111111111111,N
	DC	%0000111111111111,N
	DC	%0000011111111111,N
	DC	%0000001111111111,N
	DC	%0000000111111111,N
	DC	%0000000011111111,N
	DC	%0000000001111111,N
	DC	%0000000000111111,N
	DC	%0000000000011111,N
	DC	%0000000000001111,N
	DC	%0000000000000111,N
	DC	%0000000000000011,N
	DC	%0000000000000001,N
N	SET	N+8
	ENDR

TABLE2
N	SET	0
	REPT	20
;	DC	%0000000000000000,N
	DC	%1000000000000000,N
	DC	%1100000000000000,N
	DC	%1110000000000000,N
	DC	%1111000000000000,N
	DC	%1111100000000000,N
	DC	%1111110000000000,N
	DC	%1111111000000000,N
	DC	%1111111100000000,N
	DC	%1111111110000000,N
	DC	%1111111111000000,N
	DC	%1111111111100000,N
	DC	%1111111111110000,N
	DC	%1111111111111000,N
	DC	%1111111111111100,N
	DC	%1111111111111110,N
	DC	%1111111111111111,N
N	SET	N+8
	ENDR


TABL	
	REPT	20
;	DC.B	4*14
	DC	4*15*320	0
	DC	0	1
	DC	4*320	2
	DC	4*2*320	3
	DC	4*3*320	4
	DC	4*4*320
	DC	4*5*320
	DC	4*6*320
	DC	4*7*320
	DC	4*8*320
	DC	4*9*320
	DC	4*10*320
	DC	4*11*320
	DC	4*12*320
	DC	4*13*320
	DC	4*14*320
;	DC	4*15*320
	ENDR


TABL2
	REPT	17
	DC	0
	ENDR

N	SET	8
	REPT	19
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
	DC	N
N	SET	N+8
	ENDR
COORD	
;MOD2	EQU	*+2
	DC	1
;MOD	EQU	*+2
	DC	31

CNT	DC.L	0
TABLO	DCB	20,0
	DC	$9999
TABLO_COPIE	DCB	20,0
	BSS
CODE	DS.B	250000
TABLE	DS.L	20*16*16
TABLE_	DS.L	20*16*16
GOGO
SCREEN1	DS.L	1
SCREEN2	DS.L	1

	DS.B	256
BUFFER	DS.B	64000
